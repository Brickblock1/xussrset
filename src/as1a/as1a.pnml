 // as1a

// Спрайты

#define IMAGEFILE  "src/as1a/as1a.png"

// покупка
spriteset (as1a_purchase_spriteset, IMAGEFILE)
{
  s5_purchase_template(160, 5, -1)
}

// головной: погрузка/перемещение
spriteset (as1a_spriteset, IMAGEFILE)
{
  s5_template(0, 5, 0)
} 

spriteset (as1a_2_spriteset, IMAGEFILE)
{
  s5_template(0, 51, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, as1a_get_spriteset,
  cargo_subtype)
{
  0: as1a_spriteset;
  as1a_2_spriteset;
}

// Присоединяем не более одной
switch (FEAT_TRAINS, SELF, as1a_can_attach_wagon1,
  (vehicle_type_id == as1a) && (last_computed_result <= 1))
{
  1: return CB_RESULT_ATTACH_ALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, as1a_can_attach_wagon,
  num_vehs_in_consist)
{
  as1a_can_attach_wagon1;
}

switch (FEAT_TRAINS, SELF, as1a_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_AS1A_V1);
  1: string(STR_AS1A_V2);
  return 0xFF;
}

item (FEAT_TRAINS, as1a) 
{
  property
  {
    name: string(STR_AS1A_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1964, 1, 1);
    model_life: VEHICLE_NEVER_EXPIRES;
    vehicle_life: 18; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 80 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 70 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 24; // пассажиров
    weight: 9 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.12;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_5_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: as1a_get_spriteset;
    purchase: as1a_purchase_spriteset;
    can_attach_wagon: as1a_can_attach_wagon;
    cargo_subtype_text: as1a_cargo_subtype_text;
  }
}

