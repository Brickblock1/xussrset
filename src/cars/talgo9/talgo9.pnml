// talgo9

#define IMAGEFILE  "src/cars/talgo9/talgo_A.png"
purchase_sprites(talgo9, 0, 0)
MU_car_nd_sprites(7, talgo9_A, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/cars/talgo9/talgo_BC.png"
MU_car_sprites(8, talgo9_BC, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/cars/talgo9/talgo_E.png"
MU_car_nd_sprites(8, talgo9_E, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/cars/talgo9/talgo_D.png"
MU_car_nd_sprites(8, talgo9_D, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/cars/talgo9/talgo_H.png"
MU_car_sprites(8, talgo9_H, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/cars/talgo9/talgo_H2.png"
MU_car_sprites(8, talgo9_H2, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, talgo9_A_sprites_check,
  position_in_articulated_veh == 0)
{
  0: talgo9_A_sprites_left;
     talgo9_A_sprites_right;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_right,
   position_in_articulated_veh)
{
      0: talgo9_A_sprites_check;
  1..11: talgo9_BC_sprites_right;
     12: talgo9_D_sprites_right;
     13: talgo9_E_sprites_right;
     14: talgo9_H2_sprites_right;
     15: talgo9_H_sprites_right;
     16: talgo9_H2_sprites_right;
     17: talgo9_H_sprites_right;
     18: talgo9_H2_sprites_right;
     19: talgo9_A_sprites_check;
         align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_left,
   position_in_articulated_veh)
{
      0: talgo9_A_sprites_check;
      1: talgo9_H2_sprites_left;
      2: talgo9_H_sprites_left;
      3: talgo9_H2_sprites_left;
      4: talgo9_H_sprites_left;
      5: talgo9_H2_sprites_left;
      6: talgo9_E_sprites_left;
      7: talgo9_D_sprites_left;
  8..18: talgo9_BC_sprites_left;
     19: talgo9_A_sprites_check;
         align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01) ])
{
  1: talgo9_sprites_left;
     talgo9_sprites_right;
}

long_vehicle(talgo9)

switch (FEAT_TRAINS, SELF, talgo9_length, position_in_articulated_veh)
{
   0: return 7;
  19: return 7;
      return 8;
}

switch (FEAT_TRAINS, SELF, talgo9_articulated_part,
  extra_callback_info1)
{
      1: return talgo9_part2;
  2..19: return talgo9_part3;
         return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, talgo9_create_effect2,
  diesel_create_effect(0, 10)) { return 1; }

switch (FEAT_TRAINS, SELF, talgo9_create_effect,
  position_in_articulated_veh)
{
   0: talgo9_create_effect2;
  19: talgo9_create_effect2;
      return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, talgo9_running_cost_factor,
[  STORE_TEMP(0, 0),     // Моторы
   STORE_TEMP(1333, 1),  // Бригада
   STORE_TEMP(3618, 2),  // Износ
   STORE_TEMP(317, 3),   // Сопровождение
   STORE_TEMP(952, 4),   // ТО
   STORE_TEMP(127, 5),   // Сертификация

   STORE_TEMP(200, 6),                 // Скорость
   STORE_TEMP(359, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 80, 8)])  // Максимальная масса
{ all_running_cost_factor; }           // 6347

hint_wagon_coach(talgo9,
   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
   STR_PURCHASE_HINT_CAP_001_NORMAL,
   STR_PURCHASE_HINT_COACH_TYPE_C_WITH_COND,
   2014, 2015,
   string(STR_PURCHASE_HINT_FACTORY_PATENTES_TALGO))

item (FEAT_TRAINS, talgo9, 607) {
  property {
    name: string(STR_NAME_TALGO9);
    vehicle_dates(2014, 2015, 40, 10, 8, 10159)
    vehicle_wagon(359, 200)
    vehicle_tourists()
    vehicle_group(group_class4)
  }
  graphics {
        additional_text: talgo9_additional_text;
       articulated_part: talgo9_articulated_part;
       cargo_age_period: return CAP_002_SLIGHTLY_SLOW;
         cargo_capacity: return 50;
         colour_mapping: any_cc_colour;
            cost_factor: return 10159;
                default: talgo9_sprites;
                 length: talgo9_length;
          loading_speed: return round(9 * 5 / 2);
               purchase: talgo9_purchase_sprites;
         purchase_speed: return 200;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    running_cost_factor: talgo9_running_cost_factor;
  }
}

item (FEAT_TRAINS, talgo9_part2, 608) {
  property {
    vehicle_no_dates(2014, 2015, 40, 10, 8, 0)
    vehicle_wagon(0, 200)
    vehicle_passengers()
  }
  graphics {
    cargo_age_period: return CAP_001_NORMAL;
      cargo_capacity: return 364;
       create_effect: talgo9_create_effect;
             default: talgo9_sprites;
              length: talgo9_length;
       loading_speed: return round(9 * 5);
  }
}

long_name_template(talgo9, STR_LONGNAME_TALGO9)

item (FEAT_TRAINS, talgo9_part3, 609) {
  property {
    vehicle_no_dates(2014, 2015, 40, 10, 8, 0)
    vehicle_wagon(0, 200)
  }
  graphics {
    cargo_capacity: return 0;
     create_effect: talgo9_create_effect;
           default: talgo9_sprites;
            length: talgo9_length;
  }
}
