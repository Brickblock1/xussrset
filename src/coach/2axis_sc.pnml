 // 2axis_ic

/// Вагон
// Двухосный, с длиной кузова 14 м, жесткий пригородный вагон.
// Советский вариант 1925-1933 гг. (когда-нибудь сделаем
// дореволюционный вариант).
/// 2-осный 14 м пригородный (1924-1933)
// 2 axis intercity

#define IMAGEFILE  "src/coach/2axis-suburban-1924-1933.png"

// покупка
spriteset (_2axis_sc_purchase_spriteset, IMAGEFILE)
{
  s9c_purchase_template(0, 23, -2)
}

// вид 1
spriteset (_2axis_sc_spriteset, IMAGEFILE)
{
  s9_template(0, 23, 0)
}

// Реверс: (440, y, 0)

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2axis_sc_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2axis_sc_spriteset;
}

item (FEAT_TRAINS, _2axis_sc) 
{
  property
  {
    name: string(STR_2AXIS_SC_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1924), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    // Разрешить пассажиры (и туристы)
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    non_refittable_cargo_classes: NO_CARGO_CLASS; // Запретить другие
    refittable_cargo_types: 0;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 110 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 18;
    cargo_capacity: 72;
    weight: 23 ton;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered: 
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _2axis_sc_get_spriteset;
    purchase: _2axis_sc_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_2AXIS_SC_PURCHASE_HINT);
  }
}
