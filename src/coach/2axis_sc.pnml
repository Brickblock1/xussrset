 // 2axis_ic

/// Вагон
// Двухосный, с длиной кузова 14 м, жесткий пригородный вагон.
// Советский вариант 1925-1933 гг. (когда-нибудь сделаем
// дореволюционный вариант).
/// 2-осный 14 м пригородный (1924-1933)
// 2 axis intercity

#define IMAGEFILE  "src/coach/2axis-suburban-1924-1933.png"

// покупка
purchase_spriteset(_2axis_sc, 0, 0)

// вид 1
spriteset (_2axis_sc_spriteset, IMAGEFILE)
{
  s9_template(0, 23, 0)
}

// Реверс: (440, y, 0)

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2axis_sc_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2axis_sc_spriteset;
}

item (FEAT_TRAINS, _2axis_sc) 
{
  property
  {
    name: string(STR_2AXIS_SC_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1924), 1, 1);
    model_life: get_model_life(1924,1933);
    retire_early: get_retire_early(1924,1933);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 110 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 18;
    cargo_capacity: 72;
    weight: 23 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _2axis_sc_get_spriteset;
    purchase: _2axis_sc_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_2AXIS_SC_PURCHASE_HINT);
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, _2axis_sc)
  {
    property
    { name: string(STR_2AXIS_SC_NAME_LONG); }
  }
}

