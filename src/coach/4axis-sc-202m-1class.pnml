 // _4axis_soft_compartment_202m_1class

/// Вагон
// 4-осный мягкий купейный 20 м I класса
// Тверской завод	1912-1917-1920

#define IMAGEFILE  "src/coach/4axis-sc-202m-1class.png"
purchase_sprites(_4axis_soft_compartment_202m_1class, 0, 0)

pass_wagon_sprites(12, _4axis_soft_compartment_202m_1class_v1, 32, 40)
pass_wagon_sprites(12, _4axis_soft_compartment_202m_1class_v2, 32, 40+268  )
pass_wagon_sprites(12, _4axis_soft_compartment_202m_1class_v3, 32, 40+268*2)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_left_b1921,
  cargo_subtype)
{
  0: _4axis_soft_compartment_202m_1class_v1_sprites_left;
  1: _4axis_soft_compartment_202m_1class_v2_sprites_left;
  2: _4axis_soft_compartment_202m_1class_v3_sprites_left;
     align_12_sprites;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_left_a1921,
  cargo_subtype)
{
  0: _4axis_soft_compartment_202m_1class_v2_sprites_left;
  1: _4axis_soft_compartment_202m_1class_v3_sprites_left;
     align_12_sprites;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_left,
  build_year >=1921)
{
  0: _4axis_soft_compartment_202m_1class_sprites_left_b1921;
     _4axis_soft_compartment_202m_1class_sprites_left_a1921;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_right_b1921,
  cargo_subtype)
{
  0: _4axis_soft_compartment_202m_1class_v1_sprites_right;
  1: _4axis_soft_compartment_202m_1class_v2_sprites_right;
  2: _4axis_soft_compartment_202m_1class_v3_sprites_right;
     align_12_sprites;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_right_a1921,
  cargo_subtype)
{
  0: _4axis_soft_compartment_202m_1class_v2_sprites_right;
  1: _4axis_soft_compartment_202m_1class_v3_sprites_right;
     align_12_sprites;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_right,
  build_year >=1921)
{
  0: _4axis_soft_compartment_202m_1class_sprites_right_b1921;
     _4axis_soft_compartment_202m_1class_sprites_right_a1921;
}

random_switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: _4axis_soft_compartment_202m_1class_sprites_left;
  1: _4axis_soft_compartment_202m_1class_sprites_right;
}

long_vehicle_RND(_4axis_soft_compartment_202m_1class, _4axis_soft_compartment_202m_1class_sprites_start)

wagon_running_cost(_4axis_soft_compartment_202m_1class, 105)

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text_3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text_a1921,
  (current_year >= 1931) || (build_year >= 1931) )
{
  0: return CB_RESULT_NO_TEXT;
     _4axis_soft_compartment_202m_1class_cargo_subtype_text_3;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text_2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_93);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text_1,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_93);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text_b1921,
  (current_year >= 1921) + 
  (current_year >= 1931) )
{
  0: return CB_RESULT_NO_TEXT;
  1: _4axis_soft_compartment_202m_1class_cargo_subtype_text_1;
     _4axis_soft_compartment_202m_1class_cargo_subtype_text_2;
}

switch (FEAT_TRAINS, SELF, _4axis_soft_compartment_202m_1class_cargo_subtype_text,
  build_year >= 1921)
{
  0: _4axis_soft_compartment_202m_1class_cargo_subtype_text_b1921;
     _4axis_soft_compartment_202m_1class_cargo_subtype_text_a1921;
}

item (FEAT_TRAINS, _4axis_soft_compartment_202m_1class) 
{
  property
  {
    name: string(STR_NAME_4AXIS_SOFT_COMPARTMENT_202M_1CLASS);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1912), 1, 1);
    model_life: get_model_life(1912,1920);
    retire_early: get_retire_early(1912,1920);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS, TOUR];
    default_cargo_type: PASS; 
    cost_factor: 11;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 18;
    weight: 40 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _4axis_soft_compartment_202m_1class_sprites;
    purchase: _4axis_soft_compartment_202m_1class_purchase_sprites;
    cargo_age_period: return 10000;
    running_cost_factor: _4axis_soft_compartment_202m_1class_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_PURCHASE_HINT_COACH,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                                   string(STR_PURCHASE_HINT_COACH_TYPE_SL_4AXLE),
                                   string(STR_PURCHASE_HINT_FROMTO, 1912, 1920),
                                   string(STR_PURCHASE_HINT_FACTORY_TVER_CW));
    cargo_subtype_text: _4axis_soft_compartment_202m_1class_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: return ttdall_cc + 0xC9;
  }
}

long_name_template(_4axis_soft_compartment_202m_1class, STR_LONGNAME_4AXIS_SOFT_COMPARTMENT_202M_1CLASS)
