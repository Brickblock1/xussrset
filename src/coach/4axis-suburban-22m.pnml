 // 4axis_suburban_22m

/// Вагон
// Трехтамбурный пригородный 22 м II класса 
// Русско-Балтийский завод (1912-1917)
// 4 axis suburban

#define IMAGEFILE  "src/coach/4axis-suburban-22m.png"
purchase_sprites(_4axis_suburban_22m, 0, 0)

pass_wagon_sprites(13, _4axis_suburban_22m_v1, 32, 40)
pass_wagon_sprites(13, _4axis_suburban_22m_v2, 32, 40+268)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_sprites_left,
  (build_year >= 1918) || (cargo_subtype == 1))
{
  0: _4axis_suburban_22m_v1_sprites_left;
  1: _4axis_suburban_22m_v2_sprites_left;
     align_13_sprites;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_sprites_right,
  (build_year >= 1918) || (cargo_subtype == 1))
{
  0: _4axis_suburban_22m_v1_sprites_right;
  1: _4axis_suburban_22m_v2_sprites_right;
     align_13_sprites;
}

random_switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: _4axis_suburban_22m_sprites_left;
  1: _4axis_suburban_22m_sprites_right;
}

long_vehicle_RND(_4axis_suburban_22m, _4axis_suburban_22m_sprites_start)

wagon_running_cost(_4axis_suburban_22m, 48)

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_capacity,
  (cargo_subtype == 1) || (build_year >= 1918))
{
  1: return 96;
     return 78;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_subtype_text1,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_40);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_subtype_text,
  (build_year < 1918) && (current_year >= 1918))
{
  1: _4axis_suburban_22m_cargo_subtype_text1;
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, _4axis_suburban_22m) 
{
  property
  {
    name: string(STR_NAME_4AXIS_SUBURBAN_22M);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1912), 1, 1);
    model_life: get_model_life(1912,1917);
    retire_early: get_retire_early(1912,1917);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS; 
    cost_factor: 6;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 45;
    cargo_capacity: 78;
    weight: 43 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _4axis_suburban_22m_sprites;
    purchase: _4axis_suburban_22m_purchase_sprites;
    cargo_age_period: return 85;
    running_cost_factor: _4axis_suburban_22m_running_cost_factor;
    cargo_capacity: _4axis_suburban_22m_cargo_capacity;
    cargo_subtype_text: _4axis_suburban_22m_cargo_subtype_text;
    articulated_part: articulated_part_dummy5;
    additional_text: return string(STR_PURCHASE_HINT_COACH,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_3),
                                   string(STR_PURCHASE_HINT_COACH_TYPE_S_4AXLE),
                                   string(STR_PURCHASE_HINT_FROMTO, 1912, 1917),
                                   string(STR_PURCHASE_HINT_FACTORY_RBW));
    colour_mapping: return ttdall_cc + 0xC6;
  }
}

long_name_template(_4axis_suburban_22m, STR_LONGNAME_4AXIS_SUBURBAN_22M)
