 // 4axis_suburban_22m

/// Вагон
// Трехтамбурный пригородный 22 м II класса 
// Русско-Балтийский завод (1912-1917)
// 4 axis suburban

#define IMAGEFILE  "src/coach/4axis-suburban-22m-1912-1917.png"

// покупка
purchase_spriteset(_4axis_suburban_22m, 0, 0)

// вид 
spriteset (_4axis_suburban_22m_spriteset, IMAGEFILE)
{
  s13_template(0, 45, 0)
}

spriteset (_4axis_suburban_22m_v2_spriteset, IMAGEFILE)
{
  s13_template(0, 117, 0)
}

spriteset (_4axis_suburban_22m_tail_spriteset, IMAGEFILE)
{
  s13_template(0, 209, 0)
}

spriteset (_4axis_suburban_22m_tail_v2_spriteset, IMAGEFILE)
{
  s13_template(0, 281, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (((cargo_subtype == 1) || (build_year >= 1918)) * 2)
)
{
  0: _4axis_suburban_22m_spriteset;
  1: _4axis_suburban_22m_tail_spriteset;
  2: _4axis_suburban_22m_v2_spriteset;
  3: _4axis_suburban_22m_tail_v2_spriteset;
     align_13_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _4axis_suburban_22m_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_capacity,
  (cargo_subtype == 1) || (build_year >= 1918))
{
  1: return 96;
     return 78;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_subtype_text1,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_40);
  1: string(STR_REFIT_TTD_5A);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _4axis_suburban_22m_cargo_subtype_text,
  (build_year < 1918) && (current_year >= 1918))
{
  1: _4axis_suburban_22m_cargo_subtype_text1;
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, _4axis_suburban_22m) 
{
  property
  {
    name: string(STR_4AXIS_SUBURBAN_22M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1912), 1, 1);
    model_life: get_model_life(1912,1917);
    retire_early: get_retire_early(1912,1917);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 18;
    cargo_capacity: 78;
    weight: 43 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _4axis_suburban_22m_get_spriteset;
    purchase: _4axis_suburban_22m_purchase_spriteset;
    cargo_capacity: _4axis_suburban_22m_cargo_capacity;
    cargo_subtype_text: _4axis_suburban_22m_cargo_subtype_text;
    articulated_part: articulated_part_dummy5;
    additional_text: return string(STR_4AXIS_SUBURBAN_22M_PURCHASE_HINT);
  }
}
