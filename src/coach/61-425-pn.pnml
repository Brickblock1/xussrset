 // 61_425_pn

/// Вагон
// 61-425. Плацкартный вагон Калининского завода, первая серия
// выпуска 1975-1980 гг.
/// 61-425 ПН (1975-1980)
//
// 61-425. Плацкартный вагон Калининского завода. У вагонов поздней серии
// (1980-1983) окно служебного купе смещено ближе к туалетному.
/// 61-425 ПН (1980-1983)

#define IMAGEFILE  "src/coach/61-425-pn.png"
purchase_sprites(_61_425_pn_v1, 0, 0)

pass_wagon_sprites(14, _61_425_pn_v1, 32, 40)
pass_wagon_sprites(14, _61_425_pn_v2, 32, 40+268  )

#undef IMAGEFILE
#define IMAGEFILE  "src/coach/61-425-pn-s1980.png"
purchase_sprites(_61_425_pn_v3, 0, 0)

pass_wagon_sprites(14, _61_425_pn_v3, 32, 40)
pass_wagon_sprites(14, _61_425_pn_v4, 32, 40+268  )

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _61_425_pn_sprites_left,
  ((build_year >= 1980) * 2) +
  cargo_subtype )
{
  0: _61_425_pn_v1_sprites_left;
  1: _61_425_pn_v2_sprites_left;
  2: _61_425_pn_v3_sprites_left;
  3: _61_425_pn_v4_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, _61_425_pn_sprites_right,
  ((build_year >= 1980) * 2) +
  cargo_subtype )
{
  0: _61_425_pn_v1_sprites_right;
  1: _61_425_pn_v2_sprites_right;
  2: _61_425_pn_v3_sprites_right;
  3: _61_425_pn_v4_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, _61_425_pn_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: _61_425_pn_sprites_left;
  1: _61_425_pn_sprites_right;
}

long_vehicle(_61_425_pn)

wagon_running_cost(_61_425_pn, 156)

// покупка
switch (FEAT_TRAINS, SELF, _61_425_pn_purchase_sprites,
  build_year >= 1980
)
{
  0: _61_425_pn_v1_purchase_sprites;
     _61_425_pn_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _61_425_pn_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}
   
item (FEAT_TRAINS, _61_425_pn) 
{
  property
  {
    name: string(STR_61_425_PN_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1975), 1, 1);
    model_life: get_model_life(1975,1983);
    retire_early: get_retire_early(1975,1983);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: 16;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 54;
    weight: 51 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_425_pn_sprites;
    purchase: _61_425_pn_purchase_sprites;
    cargo_age_period: return 65535;
    running_cost_factor: _61_425_pn_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_61_425_PN_PURCHASE_HINT);
    cargo_subtype_text: _61_425_pn_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, _61_425_pn)
  {
    property
    { name: string(STR_61_425_PN_NAME_LONG); }
  }
}

