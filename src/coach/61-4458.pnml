 // 61_4458

/// Вагон
// 61-4458. сидячий вагон Тверского завода.
/// 61-4458 (2009)

#define IMAGEFILE  "src/coach/61-4458.png"
purchase_sprites(_61_4458, 0, 0)

pass_wagon_sprites(15, _61_4458_v1, 32, 40)
pass_wagon_sprites(15, _61_4458_v2, 32, 40+268  )
pass_wagon_sprites(15, _61_4458_v3, 32, 40+268*2)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _61_4458_sprites_left,
  cargo_subtype)
{
  0: _61_4458_v1_sprites_left;
  1: _61_4458_v2_sprites_left;
  2: _61_4458_v3_sprites_left;
     align_15_sprites;
}

switch (FEAT_TRAINS, SELF, _61_4458_sprites_right,
  cargo_subtype)
{
  0: _61_4458_v1_sprites_right;
  1: _61_4458_v2_sprites_right;
  2: _61_4458_v3_sprites_right;
     align_15_sprites;
}

random_switch (FEAT_TRAINS, SELF, _61_4458_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: _61_4458_sprites_left;
  1: _61_4458_sprites_right;
}

long_vehicle_RND(_61_4458, _61_4458_sprites_start)

wagon_running_cost(_61_4458, 218)

switch (FEAT_TRAINS, SELF, _61_4458_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_RZD_PID);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, _61_4458) 
{
  property
  {
    name: string(STR_NAME_61_4458);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(2009), 1, 1);
    model_life: get_model_life(2009,2050);
    retire_early: get_retire_early(2009,2050);
    vehicle_life: 40;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: 22;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 56.8 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_4458_sprites;
    purchase: _61_4458_purchase_sprites;
    cargo_age_period: return 95;
    running_cost_factor: _61_4458_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    additional_text: return string(STR_PURCHASE_HINT_COACH,
                                   string(STR_PURCHASE_HINT_COACH_TYPE_4AXLE_PREMIUM_WITH_COND_TSMW),
                                   string(STR_PURCHASE_HINT_FROM, 2009),
                                   string(STR_PURCHASE_HINT_FACTORY_TVER_CW));
    cargo_subtype_text: _61_4458_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, _61_4458)
  {
    property
    { name: string(STR_LONGNAME_61_4458); }
  }
}

