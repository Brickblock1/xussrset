 // 61_185

/// Вагон
// 61-185. Скоростной вагон типа РТ-200 для поезда "Русская тройка".
/// 61-185 (1972-1985)

#define IMAGEFILE  "src/coach/61-185-sedentary-fast-rt-200.png"

// покупка
spriteset (_61_185_purchase_spriteset, IMAGEFILE)
{
  s16_purchase_template(0, 32, -16)
}

// вид 1
spriteset (_61_185_spriteset, IMAGEFILE)
{
  s16_template(0, 32, 0)
}

// Реверс: (440, y, 0)

// Хвост: вид 1
spriteset (_61_185_tail_spriteset, IMAGEFILE)
{
  s16_template(0, 109, 0)
}

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, _61_185_get_spriteset1,
  (position_in_consist_from_end == 1) // последний вагон?
)
{
  0: _61_185_spriteset;
  1: _61_185_tail_spriteset;
  align_16_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _61_185_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _61_185_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _61_185_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RUSSIAN_TROIKA);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _61_185) 
{
  property
  {
    name: string(STR_61_185_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1972), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS, TOUR];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 180 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 19;
    cargo_capacity: 76;
    weight: 42 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_185_get_spriteset;
    purchase: _61_185_purchase_spriteset;
    articulated_part: articulated_part_dummy8;
    additional_text: return string(STR_61_185_PURCHASE_HINT);
    cargo_subtype_text: _61_185_cargo_subtype_text;
  }
}
