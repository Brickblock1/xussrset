 // 61_425_pn

/// Вагон
// 61-425. Плацкартный вагон Калининского завода, первая серия
// выпуска 1975-1980 гг.
/// 61-425 ПН (1975-1980)
//
// 61-425. Плацкартный вагон Калининского завода. У вагонов поздней серии
// (1980-1983) окно служебного купе смещено ближе к туалетному.
/// 61-425 ПН (1980-1983)

#define IMAGEFILE  "src/coach/61-425-pn-1975-1980.png"

// покупка
spriteset (_61_425_pn_purchase_spriteset, IMAGEFILE)
{
  s14_purchase_template(10, 75, -12)
}

// вид 1
spriteset (_61_425_pn_spriteset, IMAGEFILE)
{
  s14_template(10, 75, 0)
}

// вид 2
spriteset (_61_425_pn_v2_spriteset, IMAGEFILE)
{
  s14_template(10, 152, 0)
}

// Реверс: (469, y, 0)

// Хвост: вид 1
spriteset (_61_425_pn_tail_spriteset, IMAGEFILE)
{
  s14_template(10, 258, 0)
}

// вид 2
spriteset (_61_425_pn_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(10, 335, 0)
}

#undef IMAGEFILE
#define IMAGEFILE  "src/coach/61-425-pn-1980-1983.png"

// покупка
spriteset (_61_425_pn_v3_purchase_spriteset, IMAGEFILE)
{
  s14_purchase_template(10, 75, -12)
}

// вид 1
spriteset (_61_425_pn_v3_spriteset, IMAGEFILE)
{
  s14_template(10, 75, 0)
}

// вид 2
spriteset (_61_425_pn_v4_spriteset, IMAGEFILE)
{
  s14_template(10, 152, 0)
}

// Реверс: (469, y, 0)

// Хвост: вид 1
spriteset (_61_425_pn_tail_v3_spriteset, IMAGEFILE)
{
  s14_template(10, 258, 0)
}

// вид 2
spriteset (_61_425_pn_tail_v4_spriteset, IMAGEFILE)
{
  s14_template(10, 335, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _61_425_pn_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  ((build_year >= 1980) * 4) +
  (cargo_subtype * 2)
)
{
  0: _61_425_pn_spriteset;
  1: _61_425_pn_tail_spriteset;
  2: _61_425_pn_v2_spriteset;
  3: _61_425_pn_tail_v2_spriteset;
  4: _61_425_pn_v3_spriteset;
  5: _61_425_pn_tail_v3_spriteset;
  6: _61_425_pn_v4_spriteset;
  7: _61_425_pn_tail_v4_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _61_425_pn_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _61_425_pn_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, _61_425_pn_get_purchase_spriteset,
  build_year >= 1980
)
{
  0: _61_425_pn_purchase_spriteset;
  _61_425_pn_v3_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, _61_425_pn_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_61_181_CMVO_66_REFIT_V1);
  1: string(STR_61_181_CMVO_66_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _61_425_pn) 
{
  property
  {
    name: string(STR_61_425_PN_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1975), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 14;
    cargo_capacity: 54;
    weight: 51 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_425_pn_get_spriteset;
    purchase: _61_425_pn_get_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_61_425_PN_PURCHASE_HINT);
    cargo_subtype_text: _61_425_pn_cargo_subtype_text;
  }
}
