 // 61_4458

/// Вагон
// 61-4458. Ныне выпускаемый сидячий вагон Тверского завода.
/// 61-4458 (2009)

#define IMAGEFILE  "src/coach/61-4458-2009.png"

// покупка
spriteset (_61_4458_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(110, 65, -14)
}

// вид 1
spriteset (_61_4458_spriteset, IMAGEFILE)
{
  s15_template(110, 65, 0)
}

// вид 2
spriteset (_61_4458_v2_spriteset, IMAGEFILE)
{
  s15_template(110, 135, 0)
}

// вид 3
spriteset (_61_4458_v3_spriteset, IMAGEFILE)
{
  s15_template(110, 205, 0)
}

// Реверс: (567, y, 0)

// Хвост: вид 1
spriteset (_61_4458_tail_spriteset, IMAGEFILE)
{
  s15_template(110, 322, 0)
}

// вид 2
spriteset (_61_4458_tail_v2_spriteset, IMAGEFILE)
{
  s15_template(110, 392, 0)
}

// вид 3
spriteset (_61_4458_tail_v3_spriteset, IMAGEFILE)
{
  s15_template(110, 462, 0)
}

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, _61_4458_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: _61_4458_spriteset;
  1: _61_4458_tail_spriteset;
  2: _61_4458_v2_spriteset;
  3: _61_4458_tail_v2_spriteset;
  4: _61_4458_v3_spriteset;
  5: _61_4458_tail_v3_spriteset;
  align_15_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _61_4458_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _61_4458_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _61_4458_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_61_817_REFIT_V1);
  1: string(STR_AMMENDORF_K_KIZ_REFIT_V1);
  2: string(STR_61_817_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _61_4458) 
{
  property
  {
    name: string(STR_61_4458_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(2009), 1, 1);
    model_life: 40;
    vehicle_life: 40;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 57 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_4458_get_spriteset;
    purchase: _61_4458_purchase_spriteset;
    articulated_part: articulated_part_dummy7;
    additional_text: return string(STR_61_4458_PURCHASE_HINT);
    cargo_subtype_text: _61_4458_cargo_subtype_text;
  }
}
