 // ammendorf_d_hard

/// Вагон
// Ammendorf Typ 47 D. Ранняя версия жесткого купейного вагона, с линкрустовой
// отделкой, тележками с базой 2700 мм и форточками в окнах. Вагон выпускался
// народным предприятием VEB Waggonbau Ammendorf.
// Ammendorf Typ 47 D. Ранняя версия жесткого купейного вагона, с линкрустовой
// отделкой, тележками с базой 2400 мм и опускными рамами в окнах. Вагон
// выпускался народным предприятием VEB Waggonbau Ammendorf.

#define IMAGEFILE  "src/coach/ammendorf-d-hard.png"
purchase_sprites(ammendorf_d_hard_v1, 0, 0)

pass_wagon_sprites(14, ammendorf_d_hard_v1, 32, 40)
pass_wagon_sprites(14, ammendorf_d_hard_v2, 32, 40+268  )
pass_wagon_sprites(14, ammendorf_d_hard_v3, 32, 40+268*2)
pass_wagon_sprites(14, ammendorf_d_hard_v4, 32, 40+268*3)
pass_wagon_sprites(14, ammendorf_d_hard_v5, 32, 40+268*4)
#undef IMAGEFILE

#define IMAGEFILE  "src/coach/ammendorf-d-hard-s1960.png"
purchase_sprites(ammendorf_d_hard_v2, 0, 0)

pass_wagon_sprites(14, ammendorf_d_hard_v6, 32, 40)
pass_wagon_sprites(14, ammendorf_d_hard_v7, 32, 40+268  )
pass_wagon_sprites(14, ammendorf_d_hard_v8, 32, 40+268*2)
pass_wagon_sprites(14, ammendorf_d_hard_v9, 32, 40+268*3)
pass_wagon_sprites(14, ammendorf_d_hard_v10, 32, 40+268*4)
pass_wagon_sprites(14, ammendorf_d_hard_v11, 32, 40+268*5)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_purchase_sprites,
  current_year >= 1957) 
{
  0: ammendorf_d_hard_v1_purchase_sprites;
     ammendorf_d_hard_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_sprites_left,
  (cargo_subtype ) +
  ((build_year >= 1957) * 5) +
  (((cargo_subtype == 4) && (build_year >= 1957) && (LOAD_TEMP(2) >= date(1962,1,1))) * 41) )
{
   0: ammendorf_d_hard_v1_sprites_left;
   1: ammendorf_d_hard_v2_sprites_left;
   2: ammendorf_d_hard_v4_sprites_left;
   3: ammendorf_d_hard_v5_sprites_left;
   4: ammendorf_d_hard_v3_sprites_left;
  
   5: ammendorf_d_hard_v6_sprites_left;
   6: ammendorf_d_hard_v7_sprites_left;
   7: ammendorf_d_hard_v10_sprites_left;
   8: ammendorf_d_hard_v11_sprites_left;
   9: ammendorf_d_hard_v8_sprites_left;

  50: ammendorf_d_hard_v9_sprites_left;
      align_14_sprites;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_sprites_right,
  (cargo_subtype ) +
  ((build_year >= 1957) * 5) +
  (((cargo_subtype == 4) && (build_year >= 1957) && (LOAD_TEMP(2) >= date(1962,1,1))) * 41) )
{
   0: ammendorf_d_hard_v1_sprites_right;
   1: ammendorf_d_hard_v2_sprites_right;
   2: ammendorf_d_hard_v4_sprites_right;
   3: ammendorf_d_hard_v5_sprites_right;
   4: ammendorf_d_hard_v3_sprites_right;
  
   5: ammendorf_d_hard_v6_sprites_right;
   6: ammendorf_d_hard_v7_sprites_right;
   7: ammendorf_d_hard_v10_sprites_right;
   8: ammendorf_d_hard_v11_sprites_right;
   9: ammendorf_d_hard_v8_sprites_right;

  50: ammendorf_d_hard_v9_sprites_right;
      align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, ammendorf_d_hard_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: ammendorf_d_hard_sprites_left;
  1: ammendorf_d_hard_sprites_right;
}

long_vehicle_RND(ammendorf_d_hard, ammendorf_d_hard_sprites_start)

wagon_running_cost(ammendorf_d_hard, 140)

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_RUSSIA);
  3: return string(STR_REFIT_BALTIC_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_RUSSIA);
  3: return string(STR_REFIT_BALTIC_RAILWAY);
  4: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_cargo_subtype_text,
  (build_year >= 1954) || (current_year >= 1954))
{
  0: ammendorf_d_hard_cargo_subtype_text3;
     ammendorf_d_hard_cargo_subtype_text2;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_hard_additional_text,
  current_year >= 1957)
{
  1: return string(STR_AMMENDORF_D_HARD_PURCHASE_HINT_1957);
     return string(STR_AMMENDORF_D_HARD_PURCHASE_HINT);
}

engine_speed_change_after2(ammendorf_d_hard, 120, 140, 1957, 160, 1962)           

item (FEAT_TRAINS, ammendorf_d_hard) 
{
  property
  {
    name: string(STR_AMMENDORF_D_HARD_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1950), 1, 1);
    model_life: get_model_life(1950,1963);
    retire_early: get_retire_early(1950,1963);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: 14;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 38;
    weight: 57 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ammendorf_d_hard_sprites;
    purchase: ammendorf_d_hard_purchase_sprites;
    cargo_age_period: return 225;
    running_cost_factor: ammendorf_d_hard_running_cost_factor;
    purchase_speed: ammendorf_d_hard_purchase_speed;
    articulated_part: articulated_part_dummy6;
    additional_text: ammendorf_d_hard_additional_text;
    cargo_subtype_text: ammendorf_d_hard_cargo_subtype_text;
    speed: ammendorf_d_hard_speed;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ammendorf_d_hard)
  {
    property
    { name: string(STR_AMMENDORF_D_HARD_NAME_LONG); }
  }
}

