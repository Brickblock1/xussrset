 // ammendorf_d_k_ew7n

/// Вагон
// "Шоколадный" Ammendorf Typ 47 D/k-EW7N - мягкий 19-местный вагон СВ
// без кондиционера с отделкой салона под дерево темно-коричневым пластиком
// (самая первая "шоколадка" 1972 года).
/// Ammendorf D-k-EW7N
#define IMAGEFILE  "src/coach/ammendorf-d-k-ew7n.png"
purchase_sprites(ammendorf_d_k_ew7n, 0, 0)

pass_wagon_sprites(14, ammendorf_d_k_ew7n_v1,  32, 40)
pass_wagon_sprites(14, ammendorf_d_k_ew7n_v2,  32, 40+268  )
pass_wagon_sprites(14, ammendorf_d_k_ew7n_v3,  32, 40+268*2)
pass_wagon_sprites(14, ammendorf_d_k_ew7n_v4,  32, 40+268*3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ammendorf_d_k_ew7n_sprites_left,
  cargo_subtype)
{
  0: ammendorf_d_k_ew7n_v1_sprites_left;
  1: ammendorf_d_k_ew7n_v2_sprites_left;
  2: ammendorf_d_k_ew7n_v3_sprites_left;
  3: ammendorf_d_k_ew7n_v4_sprites_left;
      align_14_sprites;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_k_ew7n_sprites_right,
  cargo_subtype)
{
  0: ammendorf_d_k_ew7n_v1_sprites_right;
  1: ammendorf_d_k_ew7n_v2_sprites_right;
  2: ammendorf_d_k_ew7n_v3_sprites_right;
  3: ammendorf_d_k_ew7n_v4_sprites_right;
      align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, ammendorf_d_k_ew7n_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: ammendorf_d_k_ew7n_sprites_left;
  1: ammendorf_d_k_ew7n_sprites_right;
}

long_vehicle_RND(ammendorf_d_k_ew7n, ammendorf_d_k_ew7n_sprites_start)

switch (FEAT_TRAINS, SELF, ammendorf_d_k_ew7n_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_WAGGONBAU_AMMENDORF_EXHIBITION);
  3: return string(STR_REFIT_BALTIC_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_k_ew7n_additional_text,
  enable_long_names)
{
  0: return string(STR_PURCHASE_HINT_COACH,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_COACH_TYPE_SL_4AXLE_TSMW),
                   string(STR_PURCHASE_HINT_FROMTO, 1972, 1993),
                   string(STR_PURCHASE_HINT_FACTORY_AMMENDORF));
     return string(STR_PURCHASE_HINT_COACH_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_COACH_TYPE_SL_4AXLE_TSMW),
                   string(STR_PURCHASE_HINT_FROMTO, 1972, 1993),
                   string(STR_PURCHASE_HINT_FACTORY_AMMENDORF),
                   string(STR_PURCHASE_HINT_INTERIOR_CHOCOLATE));
}

wagon_running_cost(ammendorf_d_k_ew7n, 250)

item (FEAT_TRAINS, ammendorf_d_k_ew7n) 
{
  property
  {
    name: string(STR_NAME_AMMENDORF_D_K_EW7N);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1972), 1, 1);
    model_life: get_model_life(1972,1993);
    retire_early: get_retire_early(1972,1993);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS, TOUR];
    default_cargo_type: PASS; 
    cost_factor: 25;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 19;
    weight: 52 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ammendorf_d_k_ew7n_sprites;
    purchase: ammendorf_d_k_ew7n_purchase_sprites;
    cargo_age_period: return 10000;
    running_cost_factor: ammendorf_d_k_ew7n_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: ammendorf_d_k_ew7n_additional_text;
    cargo_subtype_text: ammendorf_d_k_ew7n_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: return ttdall_cc + 0xC9;
  }
}

long_name_template(ammendorf_d_k_ew7n, STR_LONGNAME_AMMENDORF_D_K_EW7N)
