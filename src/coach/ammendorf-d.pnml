 // ammendorf_d

/// Вагон
// "Белый" Ammendorf Typ 47 D - купейный вагон без кондиционера с отделкой
// салона белым пластиком. Выпускался народным предприятием VEB
// Waggonbau Ammendorf.
/// Ammendorf D (1964-1973)

#define IMAGEFILE  "src/coach/ammendorf-d.png"
purchase_sprites(ammendorf_d, 0, 0)

pass_wagon_sprites(14, ammendorf_d_v1, 32, 40)
pass_wagon_sprites(14, ammendorf_d_v2, 32, 40+268  )
pass_wagon_sprites(14, ammendorf_d_v3, 32, 40+268*2)
pass_wagon_sprites(14, ammendorf_d_v4, 32, 40+268*3)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ammendorf_d_sprites_left,
  (build_year >= 1967) +
  (cargo_subtype * 2))
{
  0: ammendorf_d_v1_sprites_left;
  1: ammendorf_d_v2_sprites_left;
  2: ammendorf_d_v3_sprites_left;
  3: ammendorf_d_v4_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_sprites_right,
  (build_year >= 1967) +
  (cargo_subtype * 2))
{
  0: ammendorf_d_v1_sprites_right;
  1: ammendorf_d_v2_sprites_right;
  2: ammendorf_d_v3_sprites_right;
  3: ammendorf_d_v4_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, ammendorf_d_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: ammendorf_d_sprites_left;
  1: ammendorf_d_sprites_right;
}

long_vehicle(ammendorf_d)

switch (FEAT_TRAINS, SELF, ammendorf_d_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, ammendorf_d) 
{
  property
  {
    name: string(STR_AMMENDORF_D_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1964), 1, 1);
    model_life: get_model_life(1964,1973);
    retire_early: get_retire_early(1964,1973);
    vehicle_life: 28;
    reliability_decay: 1;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 38;
    weight: 52 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ammendorf_d_sprites;
    purchase: ammendorf_d_purchase_sprites;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_AMMENDORF_D_PURCHASE_HINT);
    cargo_subtype_text: ammendorf_d_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ammendorf_d)
  {
    property
    { name: string(STR_AMMENDORF_D_NAME_LONG); }
  }
}

