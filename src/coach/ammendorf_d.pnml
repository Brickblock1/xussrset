 // ammendorf_d

/// Вагон
// "Белый" Ammendorf Typ 47 D - купейный вагон без кондиционера с отделкой
// салона белым пластиком. Выпускался народным предприятием VEB
// Waggonbau Ammendorf.
/// Ammendorf D (1964-1973)

#define IMAGEFILE  "src/coach/ammendorf-d-1964-1973.png"

// покупка
spriteset (ammendorf_d_purchase_spriteset, IMAGEFILE)
{
  s14_purchase_template(0, 44, -12)
}

// вид 1
spriteset (ammendorf_d_spriteset, IMAGEFILE)
{
  s14_template(0, 44, 0)
}

// вид 2
spriteset (ammendorf_d_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 116, 0)
}

// вид 3
spriteset (ammendorf_d_v3_spriteset, IMAGEFILE)
{
  s14_template(0, 374, 0)
}

// вид 4
spriteset (ammendorf_d_v4_spriteset, IMAGEFILE)
{
  s14_template(0, 446, 0)
}

// Реверс: (458, y, 0)

// Хвост: вид 1
spriteset (ammendorf_d_tail_spriteset, IMAGEFILE)
{
  s14_template(0, 188, 0)
}

// вид 2
spriteset (ammendorf_d_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 260, 0)
}

// вид 3
spriteset (ammendorf_d_tail_v3_spriteset, IMAGEFILE)
{
  s14_template(0, 518, 0)
}

// вид 4
spriteset (ammendorf_d_tail_v4_spriteset, IMAGEFILE)
{
  s14_template(0, 590, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ammendorf_d_get_spriteset1,
  ((build_year >= 1967) * 4) +
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: ammendorf_d_spriteset;
  1: ammendorf_d_tail_spriteset;
  2: ammendorf_d_v2_spriteset;
  3: ammendorf_d_tail_v2_spriteset;
  4: ammendorf_d_v3_spriteset;
  5: ammendorf_d_tail_v3_spriteset;
  6: ammendorf_d_v4_spriteset;
  7: ammendorf_d_tail_v4_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, ammendorf_d_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  ammendorf_d_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, ammendorf_d_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RAL_6020);
  1: string(STR_REFIT_GREEN_GRASS);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, ammendorf_d) 
{
  property
  {
    name: string(STR_AMMENDORF_D_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1964), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 38;
    weight: 52 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ammendorf_d_get_spriteset;
    purchase: ammendorf_d_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_AMMENDORF_D_PURCHASE_HINT);
    cargo_subtype_text: ammendorf_d_cargo_subtype_text;
  }
}
