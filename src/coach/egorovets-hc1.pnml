 // egorovets_sc_c1

/// Вагон
// 32-местный жесткий 8-купейный вагон дальнего сообщения с длиной
// кузова 20,2 метра. Так называемый "Егоровский тип" (первые вагоны
// этой серии выпускал Ленинградский завод им. Егорова).
// Раскраски - стандартная зеленая и "Красная стрела".
/// Егоровец 20,2 м жесткий купейный (1928-1941)
// hard compartment

#define IMAGEFILE  "src/coach/egorovets-hc1.png"
purchase_sprites(egorovets_hc1, 0, 0)

spriteset (egorovets_hc1_spriteset, IMAGEFILE)
{
  s12_template(7, 30, 0)
}

spriteset (egorovets_hc1_v2_spriteset, IMAGEFILE)
{
  s12_template(7, 100, 0)
}
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, egorovets_hc1_sprites_start,
  cargo_subtype)
{
  0: egorovets_hc1_spriteset;
  1: egorovets_hc1_v2_spriteset;
     align_12_sprites;
}

long_vehicle(egorovets_hc1)

wagon_running_cost(egorovets_hc1, 103)

switch (FEAT_TRAINS, SELF, egorovets_hc1_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, egorovets_hc1) 
{
  property
  {
    name: string(STR_NAME_EGOROVETS_HC1);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1928), 1, 1);
    model_life: get_model_life(1928,1941);
    retire_early: get_retire_early(1928,1941);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS, TOUR];
    default_cargo_type: PASS; 
    cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 32;
    weight: 44 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: egorovets_hc1_sprites;
    purchase: egorovets_hc1_purchase_sprites;
    cargo_age_period: return 225;
    running_cost_factor: egorovets_hc1_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_PURCHASE_HINT_COACH,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_2_1),
                                   string(STR_PURCHASE_HINT_COACH_TYPE_C_4AXLE),
                                   string(STR_PURCHASE_HINT_FROMTO, 1928, 1941),
                                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD_CW));
    cargo_subtype_text: egorovets_hc1_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: return ttdall_cc + 0xC6;
  }
}

long_name_template(egorovets_hc1, STR_LONGNAME_EGOROVETS_HC1)
