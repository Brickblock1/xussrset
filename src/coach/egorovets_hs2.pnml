 // egorovets_hs2

/// Вагон

#define IMAGEFILE  "src/coach/egorovets-soft-2class.png"

// покупка
purchase_spriteset(egorovets_hs2, 0, 0)

purchase_spriteset(egorovets_hs2_v2, 0, 20)

// вид 
spriteset (egorovets_hs2_spriteset, IMAGEFILE)
{
  s11_template(0, 46, 0)
}

spriteset (egorovets_hs2_v2_spriteset, IMAGEFILE)
{
  s11_template(0, 121, 0)
}

spriteset (egorovets_hs2_tail_spriteset, IMAGEFILE)
{
  s11_template(0, 207, 0)
}

spriteset (egorovets_hs2_tail_v2_spriteset, IMAGEFILE)
{
  s11_template(0, 282, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, egorovets_hs2_get_purchase_spriteset,
  (current_year >= 1918))
{
  0: egorovets_hs2_purchase_spriteset;
     egorovets_hs2_v2_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, egorovets_hs2_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: egorovets_hs2_spriteset;
  1: egorovets_hs2_tail_spriteset;
  2: egorovets_hs2_v2_spriteset;
  3: egorovets_hs2_tail_v2_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, egorovets_hs2_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
     egorovets_hs2_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, egorovets_hs2_cargo_subtype_text1,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_40);
  1: string(STR_REFIT_TTD_5A);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, egorovets_hs2_cargo_subtype_text,
  (build_year < 1918) && (current_year >= 1918))
{
  1: egorovets_hs2_cargo_subtype_text1;
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, egorovets_hs2) 
{
  property
  {
    name: string(STR_EGOROVETS_HS2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1901), 1, 1);
    model_life: get_model_life(1901,1919);
    retire_early: get_retire_early(1901,1919);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 12;
    cargo_capacity: 20;
    weight: 40 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: egorovets_hs2_get_spriteset;
    purchase: egorovets_hs2_get_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    additional_text: return string(STR_EGOROVETS_HS2_PURCHASE_HINT);
    cargo_subtype_text: egorovets_hs2_cargo_subtype_text;
  }
}
