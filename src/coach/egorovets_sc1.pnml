 // egorovets_sc_c1

/// Вагон
// 28-местный мягкий 7-купейный вагон дальнего сообщения с длиной
// кузова 20,2 метра. Так называемый "Егоровский тип" (первые вагоны
// этой серии выпускал Ленинградский завод им. Егорова).
// Раскраски - стандартная зеленая RAL6020 и "Красная стрела".
/// Егоровец 20,2 м мягкий купейный (1928-1941)
// soft compartment

#define IMAGEFILE  "src/coach/egorovets-soft-compartment-1928-1941.png"

// покупка
spriteset (egorovets_sc1_purchase_spriteset, IMAGEFILE)
{
  s12_purchase_template(7, 30, -8)
}

// вид 1
spriteset (egorovets_sc1_spriteset, IMAGEFILE)
{
  s12_template(7, 30, 0)
}

// вид 2
spriteset (egorovets_sc1_v2_spriteset, IMAGEFILE)
{
  s12_template(7, 100, 0)
}

// Реверс: (438, y, 0)

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, egorovets_sc1_get_spriteset1,
  cargo_subtype
)
{
  0: egorovets_sc1_spriteset;
  1: egorovets_sc1_v2_spriteset;
  align_12_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, egorovets_sc1_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  egorovets_sc1_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, egorovets_sc1_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RAL_6020);
  1: string(STR_REFIT_RED_ARROW);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, egorovets_sc1) 
{
  property
  {
    name: string(STR_EGOROVETS_SC1_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1928), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS, TOUR];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 180 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 14;
    cargo_capacity: 28;
    weight: 44 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: egorovets_sc1_get_spriteset;
    purchase: egorovets_sc1_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_EGOROVETS_SC1_PURCHASE_HINT);
    cargo_subtype_text: egorovets_sc1_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
