 // hnc_issue1_blue_express

/// Вагон
// Жесткий некупейный 1 выпуска (1946-1949). Первый серийный советский
// цельнометаллический вагон (ЦМВ) - так называемый "Голубой экспресс",
// гладкобортный плацкартник. Название модели неизвестно. Выпускался
// Ленинградским ВСЗ им. Егорова и Лианозовским заводом в Москве.
/// Голубой экспресс, жесткий некупейный 1 выпуска (1946-1949)

#define IMAGEFILE  "src/coach/hnc-issue1-blue-express.png"
purchase_sprites(hnc_issue1_blue_express, 0, 0)

pass_wagon_sprites(14, hnc_issue1_blue_express_v1, 32, 40)
pass_wagon_sprites(14, hnc_issue1_blue_express_v2, 32, 40+268  )
pass_wagon_sprites(14, hnc_issue1_blue_express_v3, 32, 40+268*2)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_sprites_left,
  cargo_subtype)
{
  0: hnc_issue1_blue_express_v1_sprites_left;
  1: hnc_issue1_blue_express_v2_sprites_left;
  2: hnc_issue1_blue_express_v3_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_sprites_right,
  cargo_subtype)
{
  0: hnc_issue1_blue_express_v1_sprites_right;
  1: hnc_issue1_blue_express_v2_sprites_right;
  2: hnc_issue1_blue_express_v3_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: hnc_issue1_blue_express_sprites_left;
  1: hnc_issue1_blue_express_sprites_right;
}

long_vehicle_RND(hnc_issue1_blue_express, hnc_issue1_blue_express_sprites_start)

wagon_running_cost(hnc_issue1_blue_express, 90)

switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_BLUE_EXPRESS);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, hnc_issue1_blue_express) 
{
  property
  {
    name: string(STR_NAME_HNC_ISSUE1_BLUE_EXPRESS);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1946), 1, 1);
    model_life: get_model_life(1946,1950);
    retire_early: get_retire_early(1946,1950);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS; 
    cost_factor: 9;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 54 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: hnc_issue1_blue_express_sprites;
    purchase: hnc_issue1_blue_express_purchase_sprites;
    cargo_age_period: return 105;
    running_cost_factor: hnc_issue1_blue_express_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_PURCHASE_HINT_COACH,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_2),
                                   string(STR_PURCHASE_HINT_COACH_TYPE_NC_4AXLE_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1946, 1950),
                                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_LENINGRAD_CW) , string(STR_PURCHASE_HINT_FACTORY_LIANOZOVSK)));
    cargo_subtype_text: hnc_issue1_blue_express_cargo_subtype_text;
    colour_mapping: return ttdall_cc + 0xC9;
  }
}

long_name_template(hnc_issue1_blue_express, STR_LONGNAME_HNC_ISSUE1_BLUE_EXPRESS)
