 // hnc_issue1_blue_express

/// Вагон
// Жесткий некупейный 1 выпуска (1946-1949). Первый серийный советский
// цельнометаллический вагон (ЦМВ) - так называемый "Голубой экспресс",
// гладкобортный плацкартник. Название модели неизвестно. Выпускался
// Ленинградским ВСЗ им. Егорова и Лианозовским заводом в Москве.
/// Голубой экспресс, жесткий некупейный 1 выпуска (1946-1949)

#define IMAGEFILE  "src/coach/hnc-issue1-blue-express-1946-1949.png"

// покупка
spriteset (hnc_issue1_blue_express_purchase_spriteset, IMAGEFILE)
{
  s14c_purchase_template(0, 42, -12)
}

// вид 1
spriteset (hnc_issue1_blue_express_spriteset, IMAGEFILE)
{
  s14_template(0, 42, 0)
}

// вид 2
spriteset (hnc_issue1_blue_express_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 114, 0)
}

// Реверс: (459, y, 0)

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_get_spriteset1,
  cargo_subtype
)
{
  0: hnc_issue1_blue_express_spriteset;
  1: hnc_issue1_blue_express_v2_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  hnc_issue1_blue_express_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, hnc_issue1_blue_express_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_AMMENDORF_K_KIZ_REFIT_V1);
  1: string(STR_HNC_ISSUE1_BLUE_EXPRESS_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, hnc_issue1_blue_express) 
{
  property
  {
    name: string(STR_HNC_ISSUE1_BLUE_EXPRESS_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1946), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    // Разрешить пассажиры (и туристы)
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    non_refittable_cargo_classes: NO_CARGO_CLASS; // Запретить другие
    refittable_cargo_types: 0;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 54 ton;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: hnc_issue1_blue_express_get_spriteset;
    purchase: hnc_issue1_blue_express_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_HNC_ISSUE1_BLUE_EXPRESS_PURCHASE_HINT);
    cargo_subtype_text: hnc_issue1_blue_express_cargo_subtype_text;
  }
}
