 // hnc_issue2

/// Вагон
// Жесткий некупейный 2 выпуска (1950-1965). Цельнометаллический
// плацкартный вагон (ЦМВ) Калининского завода. Точное название
// модели неизвестно.
/// Жесткий некупейный 2 выпуска, КВЗ (1950-1965)

#define IMAGEFILE  "src/coach/hnc-issue2-1950-1965.png"

// покупка
spriteset (hnc_issue2_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(0, 0) }
spriteset (hnc_issue2_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(0, 0) }
switch (FEAT_TRAINS, SELF, hnc_issue2_purchase_spriteset, 
  enable_icons)
{  1: hnc_issue2_purchase_spriteset_ico;
      hnc_issue2_purchase_spriteset_noico;}

// вид 1
spriteset (hnc_issue2_spriteset, IMAGEFILE)
{
  s14_template(0, 42, 0)
}

// вид 2
spriteset (hnc_issue2_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 114, 0)
}

// Реверс: (459, y, 0)

// Хвост: вид 1
spriteset (hnc_issue2_tail_spriteset, IMAGEFILE)
{
  s14_template(0, 186, 0)
}

// вид 2
spriteset (hnc_issue2_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 258, 0)
}

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, hnc_issue2_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: hnc_issue2_spriteset;
  1: hnc_issue2_tail_spriteset;
  2: hnc_issue2_v2_spriteset;
  3: hnc_issue2_tail_v2_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, hnc_issue2_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  hnc_issue2_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, hnc_issue2_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_REFIT_RAL_6020);
  return CB_RESULT_NO_TEXT;
}

// 120 км/ч, с 1960 г. скорость 140, с 1965 - 160.
switch (FEAT_TRAINS, SELF, hnc_issue2_speed,
  (current_year >= 1960) +
  (current_year >= 1965))
{
  1: return 140;
  2: return 160;
  return 120;
}

item (FEAT_TRAINS, hnc_issue2) 
{
  property
  {
    name: string(STR_HNC_ISSUE2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1950), 1, 1);
    model_life: get_model_life(1950,1965);
    retire_early: get_retire_early(1950,1965);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 58;
    weight: 56 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: hnc_issue2_get_spriteset;
    purchase: hnc_issue2_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_HNC_ISSUE2_PURCHASE_HINT);
    cargo_subtype_text: hnc_issue2_cargo_subtype_text;
    speed: hnc_issue2_speed;
  }
}
