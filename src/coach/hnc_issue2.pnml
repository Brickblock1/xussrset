 // hnc_issue2

/// Вагон
// Жесткий некупейный 2 выпуска (1950-1965). Цельнометаллический
// плацкартный вагон (ЦМВ) Калининского завода. Точное название
// модели неизвестно.
/// Жесткий некупейный 2 выпуска, КВЗ (1950-1965)

#define IMAGEFILE  "src/coach/hnc-issue2-1950-1965.png"
purchase_sprites(hnc_issue2, 0, 0)

pass_wagon_sprites(14, hnc_issue2_v1, 32, 40)
pass_wagon_sprites(14, hnc_issue2_v2, 32, 40+268)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, hnc_issue2_sprites_left,
  cargo_subtype)
{
  0: hnc_issue2_v1_sprites_left;
  1: hnc_issue2_v2_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, hnc_issue2_sprites_right,
  cargo_subtype)
{
  0: hnc_issue2_v1_sprites_right;
  1: hnc_issue2_v2_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, hnc_issue2_sprites_2, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: hnc_issue2_sprites_left;
  1: hnc_issue2_sprites_right;
}

long_vehicle(hnc_issue2)

switch (FEAT_TRAINS, SELF, hnc_issue2_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_53);
  1: string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

engine_speed_change_after2(hnc_issue2, 120, 140, 1960, 160, 1965)    

item (FEAT_TRAINS, hnc_issue2) 
{
  property
  {
    name: string(STR_HNC_ISSUE2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1950), 1, 1);
    model_life: get_model_life(1950,1965);
    retire_early: get_retire_early(1950,1965);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 58;
    weight: 56 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: hnc_issue2_sprites;
    purchase: hnc_issue2_purchase_sprites;
    purchase_speed: hnc_issue2_purchase_speed;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_HNC_ISSUE2_PURCHASE_HINT);
    cargo_subtype_text: hnc_issue2_cargo_subtype_text;
    speed: hnc_issue2_speed;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, hnc_issue2)
  {
    property
    { name: string(STR_HNC_ISSUE2_NAME_LONG); }
  }
}

