 // d

// Спрайты

#define IMAGEFILE  "src/d/d-1960-1961.png"

// head-trailer-reverse_head
// для сокрытия с карты, в меню видны
spriteset (d_h_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 57, 0)
} 

spriteset (d_rh_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 144, 1)
} 

spriteset (d_t_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 231, 0)
} 

// покупка
spriteset (d_h_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 57, 4)
}

spriteset (d_t_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 231, 4)
}

// головной: погрузка/перемещение
spriteset (d_h_loading_spriteset, IMAGEFILE)
{
  s15_template(4, 57, 0)
} 

spriteset (d_h_traveling_spriteset, IMAGEFILE)
{
  s15_template(458, 57, 0)
} 

spritegroup d_h_spritegroup
{
  loading: d_h_loading_spriteset;
  loaded: d_h_traveling_spriteset;
}


// развёрнутая голова
spriteset (d_rh_spriteset, IMAGEFILE)
{
  s15r_template(4, 144, 0)
} 

// прицепной
spriteset (d_t_spriteset, IMAGEFILE)
{
  s15_template(4, 231, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE "src/d/d-1962-1964.png"

// То же с метельником

spriteset (d_h_m_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 57, 0)
} 

spriteset (d_rh_m_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 144, 1)
} 

spriteset (d_h_m_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 57, 4)
}

spriteset (d_h_m_loading_spriteset, IMAGEFILE)
{
  s15a_template(4, 57, 0)
} 

spriteset (d_h_m_traveling_spriteset, IMAGEFILE)
{
  s15a_template(458, 57, 0)
} 

spritegroup d_h_m_spritegroup
{
  loading: d_h_m_loading_spriteset;
  loaded: d_h_m_traveling_spriteset;
}

spriteset (d_rh_m_spriteset, IMAGEFILE)
{
  s15r_template(4, 144, 0)
} 

#undef IMAGEFILE

//
switch (FEAT_TRAINS, SELF, d_t_get_spriteset,
  var[0x62, 7, 1, 1])
{
  0: d_t_spriteset;
  d_t_hidden_spriteset;
}

// Если год постройки >= 1962, используем метельник
switch (FEAT_TRAINS, SELF, d_h_get_spriteset1,
  ((last_computed_result - count_veh_id(d_h)) % 2) +
  ((var[0x62, 7, 1, 1] != 0) ? 2 : 0) +
  ((build_year >= 1962) * 4)
)
{
  0: d_h_spritegroup;
  1: d_rh_spriteset;
  2: d_h_hidden_spriteset;
  3: d_rh_hidden_spriteset;

  4: d_h_m_spritegroup;
  5: d_rh_m_spriteset;
  6: d_h_m_hidden_spriteset;
  d_rh_m_hidden_spriteset;
}

switch (FEAT_TRAINS, PARENT, d_h_get_spriteset,
  count_veh_id(d_h))
{
  d_h_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, d_h_articulated_part,
  extra_callback_info1)
{
  1: return dummy;
  return 0xFF;
}

switch (FEAT_TRAINS, SELF, d_h_shorten_vehicle,
  position_in_consist % 2)
{
  0: return SHORTEN_TO_8_8;
  return SHORTEN_TO_7_8;
}

switch (FEAT_TRAINS, PARENT, d_h_can_attach_wagon4,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  d_h: return CB_RESULT_ATTACH_ALLOW;
  d_t: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, d_h_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  d_h_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, d_h_can_attach_wagon1,
  count_veh_id(d_h) % 2)
{
  1: d_h_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, SELF, d_h_can_attach_wagon,
  vehicle_type_id)
{
  d_h: return CB_RESULT_ATTACH_ALLOW;
  d_t: d_h_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, d_h_start_stop,
  count_veh_id(d_h) % 2)
{
  0: return 0xFF;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, d_h_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(d_h)) % 2)
{
  0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -2,
    DISABLE_WAGON_POWER);
  return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 7,
    DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, PARENT, d_h_visual_effect_and_powered,
  count_veh_id(d_h))
{
  d_h_visual_effect_and_powered1;
}

// ограничиваем скорость через 20 лет работы
switch (FEAT_TRAINS, SELF, d_speed,
  age_in_days >= (20*365+5))
{
  1: return 100;
  return 120;
}

switch (FEAT_TRAINS, SELF, d_purchase_speed,
  0)
{
  return 120;
}

item (FEAT_TRAINS, d_h) 
{
  property
  {
    name: string(STR_D_H_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1960, 1, 1);
    model_life: VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 500 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 77;
    weight: 65 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.11;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: d_h_get_spriteset;
    purchase: d_h_purchase_spriteset;
    articulated_part: d_h_articulated_part;
    shorten_vehicle: d_h_shorten_vehicle;
    can_attach_wagon: d_h_can_attach_wagon;
    start_stop: d_h_start_stop;
    visual_effect_and_powered: d_h_visual_effect_and_powered;
    speed: d_speed;
    purchase_speed: d_purchase_speed;
  }
}

item (FEAT_TRAINS, d_t) 
{
  property
  {
    name: string(STR_D_T_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1960, 1, 1);
    model_life: VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 128;
    weight: 37 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: d_t_get_spriteset;
    purchase: d_t_purchase_spriteset;
    articulated_part: d_h_articulated_part;
    shorten_vehicle: d_h_shorten_vehicle;
    speed: d_speed;
    purchase_speed: d_purchase_speed;
  }
}
