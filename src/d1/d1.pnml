 // d1

// Спрайты

#define IMAGEFILE  "src/d1/d1-1963-1967.png"

// head-trailer-reverse_head
// для сокрытия с карты, в меню видны
spriteset (d1_h_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 57, 0)
} 

spriteset (d1_rh_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 144, 1)
} 

spriteset (d1_c_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 231, 0)
} 

// покупка
spriteset (d1_h_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 57, 4)
}

spriteset (d1_c_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 231, 4)
}

// головной: погрузка/перемещение
spriteset (d1_h_loading_spriteset, IMAGEFILE)
{
  s15a_template(4, 57, 0)
} 

spriteset (d1_h_traveling_spriteset, IMAGEFILE)
{
  s15a_template(458, 57, 0)
} 

spritegroup d1_h_spritegroup
{
  loading: d1_h_loading_spriteset;
  loaded: d1_h_traveling_spriteset;
}


// развёрнутая голова
spriteset (d1_rh_spriteset, IMAGEFILE)
{
  s15r_template(4, 144, 0)
} 

// прицепной
spriteset (d1_c_spriteset, IMAGEFILE)
{
  s15_template(4, 231, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/d1/d1-1969-1988.png"

spriteset (d1_h_2_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 57, 0)
} 

spriteset (d1_rh_2_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 144, 1)
} 

spriteset (d1_c_2_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(4, 231, 0)
} 

// покупка
spriteset (d1_h_2_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 57, 4)
}

spriteset (d1_c_2_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(292, 231, 4)
}

// головной: погрузка/перемещение
spriteset (d1_h_2_loading_spriteset, IMAGEFILE)
{
  s15_template(4, 57, 0)
} 

spriteset (d1_h_2_traveling_spriteset, IMAGEFILE)
{
  s15_template(458, 57, 0)
} 

spritegroup d1_h_2_spritegroup
{
  loading: d1_h_2_loading_spriteset;
  loaded: d1_h_2_traveling_spriteset;
}


// развёрнутая голова
spriteset (d1_rh_2_spriteset, IMAGEFILE)
{
  s15r_template(4, 144, 0)
} 

// прицепной
spriteset (d1_c_2_spriteset, IMAGEFILE)
{
  s15_template(4, 231, 0)
} 

#undef IMAGEFILE

//
switch (FEAT_TRAINS, SELF, d1_c_get_spriteset,
  var[0x62, 7, 1, 1] +
  ((build_year >= 1967) * 2)
)
{
  0: d1_c_spriteset;
  1: d1_c_hidden_spriteset;
  2: d1_c_2_spriteset;
  d1_c_2_hidden_spriteset;
}

switch (FEAT_TRAINS, SELF, d1_h_get_spriteset1,
  ((last_computed_result - count_veh_id(d1_h)) % 2) +
  ((var[0x62, 7, 1, 1] != 0) ? 2 : 0) +
  ((build_year >= 1967) * 4)
)
{
  0: d1_h_spritegroup;
  1: d1_rh_spriteset;
  2: d1_h_hidden_spriteset;
  3: d1_rh_hidden_spriteset;

  4: d1_h_2_spritegroup;
  5: d1_rh_2_spriteset;
  6: d1_h_2_hidden_spriteset;
  d1_rh_2_hidden_spriteset;
}

switch (FEAT_TRAINS, PARENT, d1_h_get_spriteset,
  count_veh_id(d1_h))
{
  d1_h_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, d1_h_get_purchase_spriteset,
  build_year >= 1967)
{
  1: d1_h_2_purchase_spriteset;
  d1_h_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, d1_c_get_purchase_spriteset,
  build_year >= 1967)
{
  1: d1_c_2_purchase_spriteset;
  d1_c_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, d1_h_articulated_part,
  extra_callback_info1)
{
  1: return dummy;
  return 0xFF;
}

switch (FEAT_TRAINS, PARENT, d1_h_can_attach_wagon6,
[
  STORE_TEMP(num_vehs_in_consist-4, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  d1_h: return CB_RESULT_ATTACH_ALLOW;
  d1_c: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, d1_h_can_attach_wagon5,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  d1_h_can_attach_wagon6;
}

switch (FEAT_TRAINS, PARENT, d1_h_can_attach_wagon4,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  d1_h: return CB_RESULT_ATTACH_ALLOW;
  d1_c: d1_h_can_attach_wagon5;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, d1_h_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  d1_h_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, d1_h_can_attach_wagon1,
  count_veh_id(d1_h) % 2)
{
  1: d1_h_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, SELF, d1_h_can_attach_wagon,
  vehicle_type_id)
{
  d1_h: return CB_RESULT_ATTACH_ALLOW;
  d1_c: d1_h_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, d1_h_start_stop,
  count_veh_id(d1_h) % 2)
{
  0: return 0xFF;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, d1_h_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(d1_h)) % 2)
{
  0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -2,
    DISABLE_WAGON_POWER);
  return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 7,
    DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, PARENT, d1_h_visual_effect_and_powered,
  count_veh_id(d1_h))
{
  d1_h_visual_effect_and_powered1;
}

// ограничиваем скорость через 20 лет работы
switch (FEAT_TRAINS, SELF, d1_speed,
  age_in_days >= (20*365+5))
{
  1: return 110;
  return 120;
}

switch (FEAT_TRAINS, SELF, d1_purchase_speed,
  0)
{
  return 120;
}

switch (FEAT_TRAINS, SELF, d1_cargo_capacity,
  build_year >= 1967)
{
  1: return 72;
  return 77;
}

item (FEAT_TRAINS, d1_h) 
{
  property
  {
    name: string(STR_D1_H_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1963, 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 615 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 77;
    weight: 65 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.15;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: d1_h_get_spriteset;
    purchase: d1_h_get_purchase_spriteset;
    articulated_part: d1_h_articulated_part;
    can_attach_wagon: d1_h_can_attach_wagon;
    start_stop: d1_h_start_stop;
    visual_effect_and_powered: d1_h_visual_effect_and_powered;
    speed: d1_speed;
    purchase_speed: d1_purchase_speed;
    cargo_capacity: d1_cargo_capacity;
  }
}

item (FEAT_TRAINS, d1_c) 
{
  property
  {
    name: string(STR_D1_C_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 128;
    weight: 37 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: d1_c_get_spriteset;
    purchase: d1_c_get_purchase_spriteset;
    articulated_part: d1_h_articulated_part;
  }
}
