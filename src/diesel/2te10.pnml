 // 2te10

#define IMAGEFILE  "src/diesel/2te10.png"
purchase_sprites(_2te10_v1,   0, 0)
purchase_sprites(_2te10_v2, 110, 0)

diesel2_sprites(11, _2te10_v1, 32,   40)
diesel2_sprites(11, _2te10_v2, 32,  241)
diesel2_sprites(11, _2te10_v3, 32,  442)
diesel2_sprites(11, _2te10_v4, 32,  643)
#undef IMAGEFILE

// покупка
switch (FEAT_TRAINS, SELF, _2te10_purchase_sprites,
  (current_year >= 1972))
{
  0: _2te10_v1_purchase_sprites;
     _2te10_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _2te10_sprites_left,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 3) +
  (cargo_subtype * ((LOAD_TEMP(2) < date(1972,1,1)))))
{
  0: _2te10_v1_sprites_left;
  1: _2te10_v2_sprites_left;
  2: _2te10_v3_sprites_left;
  3: _2te10_v4_sprites_left;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, _2te10_sprites_right,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 3) +
  (cargo_subtype * ((LOAD_TEMP(2) < date(1972,1,1)))))
{
  0: _2te10_v1_sprites_right;
  1: _2te10_v2_sprites_right;
  2: _2te10_v3_sprites_right;
  3: _2te10_v4_sprites_right;
     align_11_sprites;
}

engine_direction_template(_2te10, ) 

long_vehicle(_2te10)

engine_diesel_1_effect(_2te10, 2, 0, 11)

engine_direction_template(_2te10, _create_effect) 

switch (FEAT_TRAINS, SELF, _2te10_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_93);
  1: return string(STR_REFIT_TTD_63);
  2: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2te10_cargo_subtype_text,
  build_year >= 1972)
{
  0: _2te10_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

engine_speed(_2te10, 100)

engine_penalise_speed(_2te10, 2)

general_running_cost(_2te10, 427)

item (FEAT_TRAINS, _2te10) {
  property {
    name: string(STR_2TE10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1960), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1960,1963);
    retire_early: get_retire_early(1960,1963);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 50;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2300 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(265.0, 129);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: _2te10_sprites;
    purchase: _2te10_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2TE10_PURCHASE_HINT);
    cargo_subtype_text: _2te10_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2te10_speed;
    purchase_speed: _2te10_purchase_speed;
    running_cost_factor: _2te10_running_cost_factor;
    bitmask_vehicle_info: _2te10_bitmask_vehicle_info;
    create_effect: _2te10_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _2te10) {
    property { name: string(STR_2TE10_NAME_LONG); }
  }
}

