 // 2te10m

// Спрайты

#define IMAGEFILE  "src/diesel/2te10m-1981.png"

// покупка
spriteset (_2te10m_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(455, 72)
}

// обычный: погрузка/перемещение
spriteset (_2te10m_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_2te10m_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _2te10m_spritegroup
{
  loading: _2te10m_loading_spriteset;
  loaded: _2te10m_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10m_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2te10m_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (_2te10m_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup _2te10m_v2_spritegroup
{
  loading: _2te10m_v2_loading_spriteset;
  loaded: _2te10m_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10m_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/2te10m-1985.png"

// покупка
spriteset (_2te10m_v3_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(455, 72)
}

// обычный: погрузка/перемещение
spriteset (_2te10m_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_2te10m_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _2te10m_v3_spritegroup
{
  loading: _2te10m_v3_loading_spriteset;
  loaded: _2te10m_v3_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10m_r_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2te10m_v4_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (_2te10m_v4_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup _2te10m_v4_spritegroup
{
  loading: _2te10m_v4_loading_spriteset;
  loaded: _2te10m_v4_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10m_r_v4_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2te10m_v5_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 290, 0)
} 

spriteset (_2te10m_v5_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 356, 0)
} 

spritegroup _2te10m_v5_spritegroup
{
  loading: _2te10m_v5_loading_spriteset;
  loaded: _2te10m_v5_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10m_r_v5_spriteset, IMAGEFILE)
{
  s10_template(423, 290, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te10m_get_spriteset2,
  ((last_computed_result - count_veh_id(_2te10m)) % 2) +
  ((build_year >= 1985) * 4) +
  cargo_subtype * 2
)
{
  0: _2te10m_spritegroup;
  1: _2te10m_r_spriteset;
  2: _2te10m_v2_spritegroup;
  3: _2te10m_r_v2_spriteset;
  4: _2te10m_v3_spritegroup;
  5: _2te10m_r_v3_spriteset;
  6: _2te10m_v4_spritegroup;
  7: _2te10m_r_v4_spriteset;
  8: _2te10m_v5_spritegroup;
  _2te10m_r_v5_spriteset;
}

switch (FEAT_TRAINS, PARENT, _2te10m_get_spriteset1,
  count_veh_id(_2te10m))
{
  _2te10m_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te10m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te10m_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, _2te10m_get_purchase_spriteset,
  (current_year >= 1985)
)
{
  0: _2te10m_purchase_spriteset;
  _2te10m_v3_purchase_spriteset;
}


switch (FEAT_TRAINS, PARENT, _2te10m_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2te10m_cargo_subtype_text2,
  current_year >= 1985)
{
  1: string(STR_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2te10m_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  2: _2te10m_cargo_subtype_text2;
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _2te10m_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2te10m_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _2te10m_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_2te10m) - count_veh_id(_2te10u) -
    count_veh_id(_2te116) - count_veh_id(_2te116u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 4)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _2te10m_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_2te10m) + count_veh_id(_2te10u) +
    count_veh_id(_2te116) + count_veh_id(_2te116u), 0),
  (vehicle_type_id == _2te10m) || (vehicle_type_id == _2te10u) ||
    (vehicle_type_id == _2te116) || (vehicle_type_id == _2te116u)
])
{
  _2te10m_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _2te10m_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _2te10m_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, _2te10m_cost_factor,
  (current_year >= 1985) +
  (current_year >= 1990))
{
  1: return 88;
  2: return 94;
  return 83;
}

item (FEAT_TRAINS, _2te10m) 
{
  property
  {
    name: string(STR_2TE10M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1981), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1981,1990);
    retire_early: get_retire_early(1981,1990); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2270 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(245.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _2te10m_get_spriteset;
    purchase: _2te10m_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: _2te10m_start_stop;
    additional_text: return string(STR_2TE10M_PURCHASE_HINT);
    cargo_subtype_text: _2te10m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2te10m_speed;
    purchase_speed: _2te10m_purchase_speed;
    bitmask_vehicle_info: _2te10m_bitmask_vehicle_info;
    cost_factor: _2te10m_cost_factor;
  }
}
