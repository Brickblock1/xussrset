 // 2te10ut

// Спрайты

#define IMAGEFILE  "src/diesel/2te10ut-1989.png"

// покупка
purchase_spriteset(_2te10ut, 455, 72)

// обычный: погрузка/перемещение
spriteset (_2te10ut_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_2te10ut_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _2te10ut_spritegroup
{
  loading: _2te10ut_loading_spriteset;
  loaded: _2te10ut_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10ut_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2te10ut_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (_2te10ut_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup _2te10ut_v2_spritegroup
{
  loading: _2te10ut_v2_loading_spriteset;
  loaded: _2te10ut_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10ut_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2te10ut_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 290, 0)
} 

spriteset (_2te10ut_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 356, 0)
} 

spritegroup _2te10ut_v3_spritegroup
{
  loading: _2te10ut_v3_loading_spriteset;
  loaded: _2te10ut_v3_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10ut_r_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 290, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te10ut_get_spriteset_l,
  cargo_subtype)
{
  0: _2te10ut_spritegroup;
  1: _2te10ut_v2_spritegroup;
     _2te10ut_v3_spritegroup;
}

switch (FEAT_TRAINS, SELF, _2te10ut_get_spriteset_r,
  cargo_subtype)
{
  0: _2te10ut_r_spriteset;
  1: _2te10ut_r_v2_spriteset;
     _2te10ut_r_v3_spriteset;
}

engine_direction_template(_2te10ut) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te10ut_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te10ut_get_spriteset_t;
}

switch (FEAT_TRAINS, SELF, _2te10ut_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  2: string(STR_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 120 км/ч
#define MAX_SPEED 120

switch (FEAT_TRAINS, SELF, _2te10ut_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2te10ut_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

engine_penalise_speed(_2te10ut, 2) 

item (FEAT_TRAINS, _2te10ut) 
{
  property
  {
    name: string(STR_2TE10UT_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1989), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1989,1995);
    retire_early: get_retire_early(1989,1995); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2280 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(213.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _2te10ut_get_spriteset;
    purchase: _2te10ut_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2TE10UT_PURCHASE_HINT);
    cargo_subtype_text: _2te10ut_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2te10ut_speed;
    purchase_speed: _2te10ut_purchase_speed;
    bitmask_vehicle_info: _2te10ut_bitmask_vehicle_info;
    cost_factor: return 112;
  }
}
