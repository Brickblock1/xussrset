 // 2te10v

// Спрайты

#define IMAGEFILE  "src/diesel/2te10v.png"

// 1975: покупка
purchase_sprites(_2te10v_v1, 455, 72)

// обычный: погрузка/перемещение
spriteset (_2te10v_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_2te10v_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _2te10v_spritegroup
{
  loading: _2te10v_loading_spriteset;
  loaded: _2te10v_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10v_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/2te10v-s1978.png"

// 1978: покупка
purchase_sprites(_2te10v_v2, 455, 72)

// обычный: погрузка/перемещение
spriteset (_2te10v_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_2te10v_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _2te10v_v2_spritegroup
{
  loading: _2te10v_v2_loading_spriteset;
  loaded: _2te10v_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2te10v_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

#undef IMAGEFILE

// покупка
switch (FEAT_TRAINS, SELF, _2te10v_purchase_sprites,
  (build_year >= 1978) )
{
  0: _2te10v_v1_purchase_sprites;
     _2te10v_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _2te10v_sprites_l,
  (build_year >= 1978) )
{
  0: _2te10v_spritegroup;
     _2te10v_v2_spritegroup;
}

switch (FEAT_TRAINS, SELF, _2te10v_sprites_r,
  (build_year >= 1978) )
{
  0: _2te10v_r_spriteset;
     _2te10v_r_v2_spriteset;
}

engine_direction_template(_2te10v, ) 

long_vehicle(_2te10v)

engine_speed(_2te10v, 100)

engine_penalise_speed(_2te10v, 2)

item (FEAT_TRAINS, _2te10v)
{
  property
  {
    name: string(STR_2TE10V_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1975), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1975,1981);
    retire_early: get_retire_early(1975,1981);
    reliability_decay: 20;
    cost_factor: 35;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2310 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(248.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _2te10v_sprites;
    purchase: _2te10v_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2TE10V_PURCHASE_HINT);
    speed: _2te10v_speed;
    purchase_speed: _2te10v_purchase_speed;
    bitmask_vehicle_info: _2te10v_bitmask_vehicle_info;
    cargo_capacity: return 0;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, _2te10v)
  {
    property
    { name: string(STR_2TE10V_NAME_LONG); }
  }
}

