 // 2te116u

// Спрайты
// До 2009 года первый набор, с 2009 года второй набор

#define IMAGEFILE  "src/diesel/2te116u-2007.png"

// покупка
spriteset (_2te116u_purchase_spriteset, IMAGEFILE)
{
  s11_purchase_template(4, 14, -6)
}

// обычный: погрузка/перемещение
spriteset (_2te116u_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (_2te116u_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup _2te116u_spritegroup
{
  loading: _2te116u_loading_spriteset;
  loaded: _2te116u_traveling_spriteset;
}

// развёрнутый
spriteset (_2te116u_r_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/2te116u-2009.png"

// покупка
spriteset (_2te116u_v2_purchase_spriteset, IMAGEFILE)
{
  s11_purchase_template(4, 14, -6)
}

// обычный: погрузка/перемещение
spriteset (_2te116u_v2_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (_2te116u_v2_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup _2te116u_v2_spritegroup
{
  loading: _2te116u_v2_loading_spriteset;
  loaded: _2te116u_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2te116u_r_v2_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te116u_get_spriteset2,
  ((last_computed_result - count_veh_id(_2te116u)) % 2) +
  ((build_year >= 2009) * 2)
)
{
  0: _2te116u_spritegroup;
  1: _2te116u_r_spriteset;
  2: _2te116u_v2_spritegroup;
  _2te116u_r_v2_spriteset;
}

switch (FEAT_TRAINS, PARENT, _2te116u_get_spriteset1,
  count_veh_id(_2te116u))
{
  _2te116u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te116u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te116u_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, _2te116u_get_purchase_spriteset,
  (current_year >= 2009)
)
{
  0: _2te116u_purchase_spriteset;
  _2te116u_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _2te116u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, _2te116u_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, _2te116u)
{
  property
  {
    name: string(STR_2TE116U_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(2007, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
//    refittable_cargo_classes: bitmask(CC_PASSENGERS);
//    cargo_allow_refit: [GEAR];
//    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2700 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
//    cargo_capacity: 1;
    weight: 138 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.2388346643;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
  }
  graphics {
    default: _2te116u_get_spriteset;
    purchase: _2te116u_get_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: _2te116u_can_attach_wagon;
    start_stop: _2te116u_start_stop;
    additional_text: return string(STR_2TE116U_PURCHASE_HINT);
//    cargo_capacity: return 0;
  }
}
