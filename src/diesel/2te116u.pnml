 // 2te116u

// Спрайты
// До 2009 года первый набор, с 2009 года второй набор

#define IMAGEFILE  "src/diesel/2te116u-2007.png"

// покупка
spriteset (_2te116u_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(452, 91) }
spriteset (_2te116u_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(452, 91) }
switch (FEAT_TRAINS, SELF, _2te116u_purchase_spriteset, 
  enable_icons)
{  1: _2te116u_purchase_spriteset_ico;
      _2te116u_purchase_spriteset_noico;
}

// обычный: погрузка/перемещение
spriteset (_2te116u_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (_2te116u_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup _2te116u_spritegroup
{
  loading: _2te116u_loading_spriteset;
  loaded: _2te116u_traveling_spriteset;
}

// развёрнутый
spriteset (_2te116u_r_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/2te116u-2009.png"

// покупка
spriteset (_2te116u_v2_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(500, 100) }
spriteset (_2te116u_v2_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(500, 100) }
switch (FEAT_TRAINS, SELF, _2te116u_v2_purchase_spriteset, 
  enable_icons)
{  1: _2te116u_v2_purchase_spriteset_ico;
      _2te116u_v2_purchase_spriteset_noico;
}

// обычный: погрузка/перемещение
spriteset (_2te116u_v2_loading_spriteset, IMAGEFILE)
{
  s11_template(86, 14, 0)
} 

spriteset (_2te116u_v2_traveling_spriteset, IMAGEFILE)
{
  s11_template(86, 80, 0)
} 

spritegroup _2te116u_v2_spritegroup
{
  loading: _2te116u_v2_loading_spriteset;
  loaded: _2te116u_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2te116u_r_v2_spriteset, IMAGEFILE)
{
  s11_template(505, 14, 0)
} 

// покупка
spriteset (_2te116u_v3_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(500, 120) }
spriteset (_2te116u_v3_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(500, 120) }
switch (FEAT_TRAINS, SELF, _2te116u_v3_purchase_spriteset, 
  enable_icons)
{  1: _2te116u_v3_purchase_spriteset_ico;
      _2te116u_v3_purchase_spriteset_noico;
}

// обычный: погрузка/перемещение
spriteset (_2te116u_v3_loading_spriteset, IMAGEFILE)
{
  s11_template(86, 166, 0)
} 

spriteset (_2te116u_v3_traveling_spriteset, IMAGEFILE)
{
  s11_template(86, 232, 0)
} 

spritegroup _2te116u_v3_spritegroup
{
  loading: _2te116u_v3_loading_spriteset;
  loaded: _2te116u_v3_traveling_spriteset;
}

// развёрнутый
spriteset (_2te116u_r_v3_spriteset, IMAGEFILE)
{
  s11_template(505, 166, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te116u_get_spriteset_l,
  (LOAD_TEMP(2) >= date(2009,1,1)) +
  (build_year >= 2011)
)
{
  0: _2te116u_spritegroup;
  1: _2te116u_v2_spritegroup;
  2: _2te116u_v3_spritegroup;
     align_11_spriteset;
}

switch (FEAT_TRAINS, SELF, _2te116u_get_spriteset_r,
  (LOAD_TEMP(2) >= date(2009,1,1)) +
  (build_year >= 2011)
)
{
  0: _2te116u_r_spriteset;
  1: _2te116u_r_v2_spriteset;
  2: _2te116u_r_v3_spriteset;
     align_11_spriteset;
}

engine_direction_template2(_2te116u, _2te116) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te116u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te116u_get_spriteset_t;
}

// покупка
switch (FEAT_TRAINS, SELF, _2te116u_get_purchase_spriteset,
  (current_year >= 2009) +
  (current_year >= 2011)
)
{
  0: _2te116u_purchase_spriteset;
  1: _2te116u_v2_purchase_spriteset;
  _2te116u_v3_purchase_spriteset;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _2te116u_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2te116u_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _2te116u_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_2te116u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 4)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _2te116u_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_2te116u), 0),
  vehicle_type_id == _2te116u
])
{
  _2te116u_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _2te116u_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _2te116u_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, _2te116u_cargo_subtype_text,
  (build_year >= 2011) +
  (cargo_subtype * 2))
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_RZD_PID);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, _2te116u)
{
  property
  {
    name: string(STR_2TE116U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2007), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2007,2030);
    retire_early: get_retire_early(2007,2030); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2700 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(324.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _2te116u_get_spriteset;
    purchase: _2te116u_get_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2TE116U_PURCHASE_HINT);
    speed: _2te116u_speed;
    purchase_speed: _2te116u_purchase_speed;
    bitmask_vehicle_info: _2te116u_bitmask_vehicle_info;
    cargo_subtype_text: _2te116u_cargo_subtype_text;
    cargo_capacity: return 0;
    cost_factor: return 490;
  }
}
