 // 2te25a

// Спрайты
// 2ТЭ25А "Витязь" (с 2006 г.) - новый тепловоз с асинхронным приводом
// производства Брянского завода (БМЗ).

#define IMAGEFILE  "src/diesel/2te25a.png"

// покупка
purchase_spriteset(_2te25a, 0, 0)

// секция А: погрузка/перемещение
spriteset (_2te25a_loading_spriteset, IMAGEFILE)
{
  s12_template(124, 42, 0)
} 

spriteset (_2te25a_traveling_spriteset, IMAGEFILE)
{
  s12_template(124, 113, 0)
} 

spritegroup _2te25a_spritegroup
{
  loading: _2te25a_loading_spriteset;
  loaded: _2te25a_traveling_spriteset;
}

// секция Б
spriteset (_2te25a_b_spriteset, IMAGEFILE)
{
  s12_template(567, 42, 0)
} 

// покупка
purchase_spriteset(_2te25a_v2, 0, 20)

// секция А, вид 2: погрузка/перемещение
spriteset (_2te25a_loading_v2_spriteset, IMAGEFILE)
{
  s12_template(124, 222, 0)
} 

spriteset (_2te25a_traveling_v2_spriteset, IMAGEFILE)
{
  s12_template(124, 290, 0)
} 

spritegroup _2te25a_v2_spritegroup
{
  loading: _2te25a_loading_v2_spriteset;
  loaded: _2te25a_traveling_v2_spriteset;
}

// секция Б, вид 2
spriteset (_2te25a_b_v2_spriteset, IMAGEFILE)
{
  s12_template(567, 222, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te25a_get_spriteset_l,
  (build_year >= 2010) )
{
  0: _2te25a_spritegroup;
  1: _2te25a_v2_spritegroup;
     align_12_spriteset;
}

switch (FEAT_TRAINS, SELF, _2te25a_get_spriteset_r,
  (build_year >= 2010) )
{
  0: _2te25a_b_spriteset;
  1: _2te25a_b_v2_spriteset;
     align_12_spriteset;
}

engine_direction_template(_2te25a) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te25a_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te25a_get_spriteset_t;
}

switch (FEAT_TRAINS, SELF, _2te25a_get_purchase_spriteset,
  build_year >= 2010)
{
  1: _2te25a_v2_purchase_spriteset;
  _2te25a_purchase_spriteset;
}

// Макс. скорость: 120 км/ч
#define MAX_SPEED 120

switch (FEAT_TRAINS, SELF, _2te25a_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2te25a_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

engine_penalise_speed(_2te25a, 4)

item (FEAT_TRAINS, _2te25a)
{
  property
  {
    name: string(STR_2TE25A_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2006,2030);
    retire_early: get_retire_early(2006,2030); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2725 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 144 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(390.0, 144);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _2te25a_get_spriteset;
    purchase: _2te25a_get_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2TE25A_PURCHASE_HINT);
    speed: _2te25a_speed;
    purchase_speed: _2te25a_purchase_speed;
    bitmask_vehicle_info: _2te25a_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    cost_factor: return 400;
  }
}
