 // 2te70

// Спрайты

#define IMAGEFILE  "src/diesel/2te70.png"

// покупка
spriteset (_2te70_purchase_spriteset, IMAGEFILE)
{
  s13_purchase_template(0, 31, -10)
}

// обычный: погрузка/перемещение
spriteset (_2te70_loading_spriteset, IMAGEFILE)
{
  s13_template(0, 31, 0)
} 

spriteset (_2te70_traveling_spriteset, IMAGEFILE)
{
  s13_template(0, 98, 0)
} 

spritegroup _2te70_spritegroup
{
  loading: _2te70_loading_spriteset;
  loaded: _2te70_traveling_spriteset;
}

// развёрнутый
spriteset (_2te70_r_spriteset, IMAGEFILE)
{
  s13_template(455, 31, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2te70_get_spriteset2,
  ((last_computed_result - count_veh_id(_2te70)) % 2)
)
{
  0: _2te70_spritegroup;
  _2te70_r_spriteset;
}

switch (FEAT_TRAINS, PARENT, _2te70_get_spriteset1,
  count_veh_id(_2te70))
{
  _2te70_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2te70_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2te70_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _2te70_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, _2te70_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 110 км/ч
#define MAX_SPEED 110

switch (FEAT_TRAINS, SELF, _2te70_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2te70_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _2te70_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_2te70), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 4)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _2te70_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_2te70), 0),
  vehicle_type_id == _2te70
])
{
  _2te70_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _2te70_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _2te70_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, _2te70)
{
  property
  {
    name: string(STR_2TE70_NAME);
    climates_available: get_climates_available();
    introduction_date: date(2004, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 110 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    ai_special_flag: AI_FLAG_CARGO;
    power: 3200 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 141 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.220002894774;
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _2te70_get_spriteset;
    purchase: _2te70_purchase_spriteset;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: _2te70_can_attach_wagon;
    start_stop: _2te70_start_stop;
    additional_text: return string(STR_2TE70_PURCHASE_HINT);
    speed: _2te70_speed;
    purchase_speed: _2te70_purchase_speed;
    bitmask_vehicle_info: _2te70_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    cost_factor: return 466;
  }
}
