 // 2tep60

// Спрайты

#define IMAGEFILE  "src/diesel/2tep60.png"

// 1965
// покупка
purchase_spriteset(_2tep60, 0, 0)

// обычный: погрузка/перемещение
spriteset (_2tep60_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 47, 0)
} 

spriteset (_2tep60_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 112, 0)
} 

spritegroup _2tep60_spritegroup
{
  loading: _2tep60_loading_spriteset;
  loaded: _2tep60_traveling_spriteset;
}

// развёрнутый
spriteset (_2tep60_r_spriteset, IMAGEFILE)
{
  s11_template(440, 47, 0)
} 

// 1966-69, 2 варианта
// покупка
purchase_spriteset(_2tep60_v2, 120, 0)

// обычный: погрузка/перемещение
spriteset (_2tep60_v2_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 184, 0)
} 

spriteset (_2tep60_v2_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 249, 0)
} 

spritegroup _2tep60_v2_spritegroup
{
  loading: _2tep60_v2_loading_spriteset;
  loaded: _2tep60_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_2tep60_r_v2_spriteset, IMAGEFILE)
{
  s11_template(440, 184, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2tep60_v3_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 321, 0)
} 

spriteset (_2tep60_v3_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 386, 0)
} 

spritegroup _2tep60_v3_spritegroup
{
  loading: _2tep60_v3_loading_spriteset;
  loaded: _2tep60_v3_traveling_spriteset;
}

// развёрнутый
spriteset (_2tep60_r_v3_spriteset, IMAGEFILE)
{
  s11_template(440, 321, 0)
} 

// с 1970, 2 варианта
// покупка
purchase_spriteset(_2tep60_v4, 240, 0)

// обычный: погрузка/перемещение
spriteset (_2tep60_v4_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 458, 0)
} 

spriteset (_2tep60_v4_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 523, 0)
} 

spritegroup _2tep60_v4_spritegroup
{
  loading: _2tep60_v4_loading_spriteset;
  loaded: _2tep60_v4_traveling_spriteset;
}

// развёрнутый
spriteset (_2tep60_r_v4_spriteset, IMAGEFILE)
{
  s11_template(440, 458, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2tep60_v5_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 595, 0)
} 

spriteset (_2tep60_v5_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 660, 0)
} 

spritegroup _2tep60_v5_spritegroup
{
  loading: _2tep60_v5_loading_spriteset;
  loaded: _2tep60_v5_traveling_spriteset;
}

// развёрнутый
spriteset (_2tep60_r_v5_spriteset, IMAGEFILE)
{
  s11_template(440, 595, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2tep60_get_spriteset_l,
  ((build_year >= 1966)) +
  ((build_year >= 1970) * 2) +
  cargo_subtype 
)
{
  0: _2tep60_spritegroup;
  1: _2tep60_v2_spritegroup;          
  2: _2tep60_v3_spritegroup;
  3: _2tep60_v4_spritegroup;        
     _2tep60_v5_spritegroup;    
}

switch (FEAT_TRAINS, SELF, _2tep60_get_spriteset_r,
  ((build_year >= 1966)) +
  ((build_year >= 1970) * 2) +
  cargo_subtype 
)
{
  0: _2tep60_r_spriteset;
  1: _2tep60_r_v2_spriteset;
  2: _2tep60_r_v3_spriteset;
  3: _2tep60_r_v4_spriteset;
     _2tep60_r_v5_spriteset;
}

engine_direction_template(_2tep60) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2tep60_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2tep60_get_spriteset_t;
}

// покупка
switch (FEAT_TRAINS, SELF, _2tep60_get_purchase_spriteset,
  (current_year >= 1966) +
  (current_year >= 1970)
)
{
  0: _2tep60_purchase_spriteset;
  1: _2tep60_v2_purchase_spriteset;
  _2tep60_v4_purchase_spriteset;
}

// Если хотя бы 2 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _2tep60_start_stop2,
[
  set_offset_to(2),
  prev_vehicle_type_id() == _2tep60
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_2_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше двух, то не выпускать
// Если год >= 1990, то можно по одному
switch (FEAT_TRAINS, PARENT, _2tep60_start_stop,
  current_year >= 1990 ? 0 : count_veh_id(_2tep60) >= 2 ? 1 : 2)
{
  0: any_start_stop;
  1: _2tep60_start_stop2;
  return string(STR_START_STOP_NEED_2_OR_MORE_ENGINE);
}

switch (FEAT_TRAINS, SELF, _2tep60_cargo_subtype_text2,
  (build_year >= 1966)
)
{
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2tep60_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: _2tep60_cargo_subtype_text2;
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 160 км/ч, после 20 лет - 130 км/ч
#define MAX_SPEED 160
switch (FEAT_TRAINS, SELF, _2tep60_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2tep60_speed,
[
  STORE_TEMP(get_max_speed(20, 130, MAX_SPEED), 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _2tep60_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_2tep60), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _2tep60_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_2tep60), 0),
  vehicle_type_id == _2tep60
])
{
  _2tep60_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _2tep60_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _2tep60_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, _2tep60) 
{
  property
  {
    name: string(STR_2TEP60_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1965), 1, 1);
    vehicle_life: 20; // срок службы
    model_life: get_model_life(1965,1987);
    retire_early: get_retire_early(1965,1987); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2310 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 127 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(123.0, 127);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _2tep60_get_spriteset;
    purchase: _2tep60_get_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: _2tep60_start_stop;
    additional_text: return string(STR_2TEP60_PURCHASE_HINT);
    cargo_subtype_text: _2tep60_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2tep60_speed;
    purchase_speed: _2tep60_purchase_speed;
    bitmask_vehicle_info: _2tep60_bitmask_vehicle_info;
    cost_factor: return 99;
  }
}
