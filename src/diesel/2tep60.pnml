 // 2tep60

// 1965
// 1966-69, 2 варианта
// с 1970, 2 варианта
#define IMAGEFILE  "src/diesel/2tep60.png"
purchase_sprites(_2tep60_v1,   0,  0)
purchase_sprites(_2tep60_v2, 110,  0)
purchase_sprites(_2tep60_v3, 220,  0)

diesel2_sprites(11, _2tep60_v1,  32,   40)
diesel2_sprites(11, _2tep60_v2,  32,  241)
diesel2_sprites(11, _2tep60_v3,  32,  442)
diesel2_sprites(11, _2tep60_v4,  32,  643)
diesel2_sprites(11, _2tep60_v5,  32,  844)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2tep60_purchase_sprites,
  (current_year >= 1966) +
  (current_year >= 1970))
{
  0: _2tep60_v1_purchase_sprites;
  1: _2tep60_v2_purchase_sprites;
     _2tep60_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _2tep60_sprites_left,
  ((build_year >= 1966)) +
  ((build_year >= 1970) * 2) +
  cargo_subtype)
{
  0: _2tep60_v1_sprites_left;
  1: _2tep60_v2_sprites_left;          
  2: _2tep60_v3_sprites_left;
  3: _2tep60_v4_sprites_left;        
  4: _2tep60_v5_sprites_left;    
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, _2tep60_sprites_right,
  ((build_year >= 1966)) +
  ((build_year >= 1970) * 2) +
  cargo_subtype)
{
  0: _2tep60_v1_sprites_right;
  1: _2tep60_v2_sprites_right;
  2: _2tep60_v3_sprites_right;
  3: _2tep60_v4_sprites_right;
  4: _2tep60_v5_sprites_right;
     align_11_sprites;
}

engine_direction_template(_2tep60, ) 
long_vehicle(_2tep60)

switch (FEAT_TRAINS, SELF, _2tep60_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_B5);
  1: return string(STR_REFIT_TTD_7C);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2tep60_cargo_subtype_text,
  (build_year >= 1966))
{
  1: _2tep60_cargo_subtype_text2;
     return string(STR_REFIT_PREPRODUCTION);
}

switch (FEAT_TRAINS, SELF, _2tep60_additional_text,
  current_year >= 1966)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1965), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1966, 1987), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA));
}

engine_diesel_1r_effect(_2tep60, 2, 0, 11)
engine_direction_template(_2tep60, _create_effect) 
engine_speed_with_reduce(_2tep60, 160, 130, 25)
engine_penalise_speed(_2tep60, 2)
general_running_cost(_2tep60, 379)

item (FEAT_TRAINS, _2tep60) {
  property {
    name: string(STR_NAME_2TEP60);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1964,1987);
    retire_early: get_retire_early(1964,1987);
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 43;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2330 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 127 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(201.0, 127);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: _2tep60_sprites;
    purchase: _2tep60_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: _2tep60_additional_text;
    cargo_subtype_text: _2tep60_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2tep60_speed;
    purchase_speed: _2tep60_purchase_speed;
    running_cost_factor: _2tep60_running_cost_factor;
    bitmask_vehicle_info: _2tep60_bitmask_vehicle_info;
    create_effect: _2tep60_create_effect_sprites_start;
  }
}

long_name_template(_2tep60, STR_LONGNAME_2TEP60)
models_default_cargo_template(_2tep60)
