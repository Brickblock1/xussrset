 // 3m62_all
// 471

switch (FEAT_TRAINS, SELF, _3m62_all_purchase_sprites,
  current_year >= 1986)
{
  0: _2m62_purchase_sprites;
     _3m62u_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _3m62_all_cargo_subtype_text,
  build_year >= 1986)
{
  0: _2m62_cargo_subtype_text;
     _2m62u_cargo_subtype_text;
}

hint_engine_with_subtype(_3m62_all_b1986,
                         STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, STR_NAME_2M62,
                         STR_PURCHASE_HINT_SECTIONS_1_2,
                         1976, 1986,
                         STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)
hint_engine_with_subtype(_3m62_all_a1986,
                         STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, STR_NAME_2M62U,
                         STR_PURCHASE_HINT_SECTIONS_1_4,
                         1986, 2000,
                         STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)

switch (FEAT_TRAINS, SELF, _3m62_all_additional_text,
  (current_year >= 1986) +
  (current_year >= USSREND))
{
  0: _3m62_all_b1986_additional_text;
  1: _3m62_all_a1986_additional_text;
// не переведено на шаблон
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_NAME_2M62U)),
                   string(STR_PURCHASE_HINT_SECTIONS_1_4),
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000),
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_AUSSR),
                   string(STR_PURCHASE_HINT_DRB1, string(STR_NAME_2M62)));
}

switch (FEAT_TRAINS, SELF, _3m62_all_refit_cost,
  build_year >= 1986)
{
  0: _2m62u_refit_cost;
     return 0;
}

switch (FEAT_TRAINS, SELF, _3m62_all_running_cost_factor,
  build_year >= 1986)
{
  0: _2m62_running_cost_factor;
     _3m62u_running_cost_factor;
}

// отключить пенальти для ДРБ1
engine_speed(_3m62_all, 100)
engine_penalise_speed4(_3m62_all, _2m62u, _3m62u, _3m62u_m, 4)

switch (FEAT_TRAINS, SELF, _3m62_all_bitmask_vehicle_info_drb1_2,
  (position_in_consist_from_end == 1) && (cargo_subtype == LV_BELARUSSIAN_RAILWAY_DRB))
{
  1: return bitmask(FLAG_DRB1_SH);
     return 0;
}

switch (FEAT_TRAINS, PARENT, _3m62_all_bitmask_vehicle_info_drb1,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(_3m62_all) * 2) == 0) &&
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1))
{
  1: _3m62_all_bitmask_vehicle_info_drb1_2;
     _3m62_all_bitmask_vehicle_info;
}

switch (FEAT_TRAINS, PARENT, _3m62_all_bitmask_vehicle_info_short_b1986,
  (build_year < 1986) *
  (vehicle_type_id == _3m62_all))
{
  0: return bitmask(FLAG_PENALISE_SPEED);
     return 0;
}

switch (FEAT_TRAINS, PARENT, _3m62_all_bitmask_vehicle_info_short_a1986,
  (build_year < 1986) && (vehicle_type_id == _3m62_all))
{
  1: return bitmask(FLAG_PENALISE_SPEED);
     _3m62_all_bitmask_vehicle_info_drb1;
}

switch (FEAT_TRAINS, SELF, _3m62_all_bitmask_vehicle_info_advanced,
  (build_year >= 1986) +
  ((position_in_consist > 2) * 2))
{
  0: _3m62_all_bitmask_vehicle_info_short_b1986;
  2: return bitmask(FLAG_PENALISE_SPEED);
     _3m62_all_bitmask_vehicle_info_short_a1986;
}

engine_weight_change_build_after(_3m62_all, 120,
                                            126, 1986)
engine_cost_change_build_after(_3m62_all, 34,
                                          35, 1986)

item (FEAT_TRAINS, _3m62_all, 471) {
  property {
    name: string(STR_NAME_2M62);
    vehicle_no_dates(1976, 2000, 30, 10, 8, 35)
    vehicle_cargo()
    vehicle_diesel(1546, 126, 350.0)
  }
  graphics {
    default: _2m62u_sprites;
    purchase: _3m62_all_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: _3m62_all_additional_text;
    cargo_subtype_text: _3m62_all_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3m62_all_speed;
    weight: _3m62_all_weight;
    cost_factor: _3m62_all_cost_factor;
    running_cost_factor: _3m62_all_running_cost_factor;
    bitmask_vehicle_info: _3m62_all_bitmask_vehicle_info_advanced;
    create_effect: _2m62u_create_effect_sprites_start;
    refit_cost: _3m62_all_refit_cost;
    colour_mapping: any_cc_colour;
    purchase_speed: return 100;
  }
}

long_name_template(_3m62_all, STR_LONGNAME_2M62)
models_default_cargo_template(_3m62_all)

if (models_details_level == 0) {
  item (FEAT_TRAINS, _2m62) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, _2m62u) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, _3m62u) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, _3m62_all) {
    property { climates_available: get_climates_available(); }
  }
}

allow_diesel(_3m62_all)
