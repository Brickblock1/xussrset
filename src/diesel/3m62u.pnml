 // 2m62u 

#define IMAGEFILE  "src/diesel/3m62u.png"
purchase_sprites(_2m62u, 0, 0)
purchase_sprites(_3m62u, 0, 19)
purchase_sprites(_3m62u_m, 150, 0)

diesel2_sprites(10, _2m62u_v1,  32,   40)
diesel2_sprites(10, _2m62u_v2,  32,  241)
diesel2_sprites(10, _2m62u_v3,  32,  442)
diesel2_sprites(10, _2m62u_v4,  32,  643)
diesel2_sprites(10, _2m62u_v5,  32,  844)
diesel2_sprites(10, _2m62u_v6,  32, 1045)
diesel2_sprites(10, _2m62u_v7,  32, 1246)
diesel2_sprites(10, _2m62u_v8,  32, 1447)
diesel2_sprites(10, _2m62u_v9,  32, 1648)
diesel2_sprites(10, _2m62u_v10, 32, 1849)
buster_sprites(10, _3m62u_m_v1, 32, 2050) 
buster_sprites(10, _3m62u_m_v2, 32, 2117) 
buster_sprites(10, _3m62u_m_v3, 32, 2184) 
buster_sprites(10, _3m62u_m_v4, 32, 2251) 
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2m62u_sprites_left,
  cargo_subtype)
{
  0: _2m62u_v1_sprites_left;
  1: _2m62u_v2_sprites_left;
  2: _2m62u_v3_sprites_left;
  3: _2m62u_v4_sprites_left;
  4: _2m62u_v5_sprites_left;
  5: _2m62u_v6_sprites_left;
  6: _2m62u_v7_sprites_left;
  7: _2m62u_v8_sprites_left;
  8: _2m62u_v9_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, _2m62u_sprites_right,
  cargo_subtype)
{
  0: _2m62u_v1_sprites_right;
  1: _2m62u_v2_sprites_right;
  2: _2m62u_v3_sprites_right;
  3: _2m62u_v4_sprites_right;
  4: _2m62u_v5_sprites_right;
  5: _2m62u_v6_sprites_right;
  6: _2m62u_v7_sprites_right;
  7: _2m62u_v8_sprites_right;
  8: _2m62u_v9_sprites_right;
     align_10_sprites;
}  

engine_direction_template3m(_2m62u, _3m62u, _3m62u_m, ) 

switch (FEAT_TRAINS, SELF, _2m62u_sprites_DRB1,
  (position_in_consist != 0) +
  (cargo_subtype * 2) )
{
  0: _2m62u_v1_sprites_left;
  1: _2m62u_v1_sprites_right;
  2: _2m62u_v2_sprites_left;
  3: _2m62u_v2_sprites_right;
  4: _2m62u_v3_sprites_left;
  5: _2m62u_v3_sprites_right;
  6: _2m62u_v4_sprites_left;
  7: _2m62u_v4_sprites_right;
  8: _2m62u_v5_sprites_left;
  9: _2m62u_v5_sprites_right;
  10: _2m62u_v6_sprites_left;
  11: _2m62u_v6_sprites_right;
  12: _2m62u_v7_sprites_left;
  13: _2m62u_v7_sprites_right;
  14: _2m62u_v8_sprites_left;
  15: _2m62u_v8_sprites_right;
  16: _2m62u_v9_sprites_left;
  17: _2m62u_v9_sprites_right;
      align_10_sprites;
}
  
switch (FEAT_TRAINS, PARENT, _2m62u_sprites_start_sp,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(_3m62u) * 2) == 0) && 
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1) )
{
  1: _2m62u_sprites_DRB1;
     _2m62u_sprites_start;
}
 
long_vehicle_sp(_2m62u)

// прицепить 2М62у можно только к составу, не имеющему вагонов ДР1А или имеющему только прицепные вагоны
switch (FEAT_TRAINS, PARENT, _2m62u_can_attach_wagon3,
  ((count_veh_id(dr1a_c) + count_veh_id(dr1a_h) + count_veh_id(dr1a_n)) == 0) ||
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - 2) == 0) )
{
  1: return any_can_attach_wagon;
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

// прицепить ДР1А прицепной можно только к составу, состоящему из 2М62у и не более чем 9 ДР1А прицепных
switch (FEAT_TRAINS, PARENT, _2m62u_can_attach_wagon2,
  (count_veh_id(dr1a_c) <= 9) && ( (num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - 2) == 0) )
{
  1: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_CAN_ATTACH_WRONG_DRB1);
}

// прицепить ДР1А головной немоторный можно только к составу, состоящему из 2М62у и ДР1А прицепных
switch (FEAT_TRAINS, PARENT, _2m62u_can_attach_wagon1,
  (num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - 2) == 0 )
{
  1: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_CAN_ATTACH_WRONG_DRB1);
}

// нельзя прицеплять ничего к ДР1Б кроме частей ДР1А
switch (FEAT_TRAINS, PARENT, _2m62u_can_attach_wagon0,
  (count_veh_id(dr1a_c)+count_veh_id(dr1a_n) >= 1) )
{
  1: return string(STR_CAN_ATTACH_WRONG_DRB1);
     any_can_attach_wagon;
}

switch (FEAT_TRAINS, SELF, _2m62u_can_attach_wagon,
  vehicle_type_id)
{
  dr1a_h: string(STR_CAN_ATTACH_WRONG_DRB1);
  dr1a_n: _2m62u_can_attach_wagon1;
  dr1a_c: _2m62u_can_attach_wagon2;
  _2m62u: _2m62u_can_attach_wagon3;
  _3m62u: _2m62u_can_attach_wagon3;
          _2m62u_can_attach_wagon0;
}

switch (FEAT_TRAINS, SELF, _2m62u_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2m62u_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_BELARUSSIAN_RAILWAY);
  3: return string(STR_REFIT_BELARUSSIAN_RAILWAY_DDB1_DRB1);
  4: return string(STR_REFIT_LG);
  5: return string(STR_REFIT_LDZ, string(STR_REFIT_TTD_53));
  6: return string(STR_REFIT_LDZ, string(STR_REFIT_TTD_B5));
  7: return string(STR_REFIT_KALININGRAD_RAILWAY);
  8: return string(STR_REFIT_OCTOBER_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2m62u_cargo_subtype_text,
  ((current_year >= 1991) || (build_year >= 1991)) )
{
  1: _2m62u_cargo_subtype_text2;
     _2m62u_cargo_subtype_text3;
}

switch (FEAT_TRAINS, SELF, _2m62u_additional_text,
  current_year >= 1991)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4_WITH_ATTACH, string(STR_3M62U_NAME)), 
                   string(STR_PURCHASE_HINT_FROMTO, 1987, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD), 
                   2);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4_WITH_ATTACH, string(STR_3M62U_NAME)), 
                   string(STR_PURCHASE_HINT_FROMTO, 1987, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK), 
                   9);
}

engine_diesel_1_effect(_2m62u, 3, -1, 11)
engine_direction_template3m(_2m62u, _3m62u, _3m62u_m, _create_effect) 
engine_speed(_2m62u, 100)
engine_penalise_speed3(_2m62u, _3m62u, _3m62u_m, 8)
general_running_cost(_2m62u, 341) 
// отключить пенальти для ДРБ1
switch (FEAT_TRAINS, PARENT, _2m62u_bitmask_vehicle_info_drb1,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(_3m62u) * 2) == 0) && 
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1) )
{
  1: return 0;
     _2m62u_bitmask_vehicle_info;
}

item (FEAT_TRAINS, _2m62u) {
  property {
    name: string(STR_2M62U_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1987), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1987,2000);
    retire_early: get_retire_early(1987,2000);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 35;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1545 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(196.0, 126);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: _2m62u_sprites;
    purchase: _2m62u_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: _2m62u_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: _2m62u_additional_text;
    cargo_subtype_text: _2m62u_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2m62u_speed;
    purchase_speed: _2m62u_purchase_speed;
    running_cost_factor: _2m62u_running_cost_factor;
    bitmask_vehicle_info: _2m62u_bitmask_vehicle_info_drb1;
    create_effect: _2m62u_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _2m62u) {
    property { name: string(STR_2M62U_NAME_LONG); }
  }
}

if (models_details_level == 1) {
  item (FEAT_TRAINS, _2m62u) {
    property { climates_available: get_climates_available(); }
  }
}

 // 3m62u

switch (FEAT_TRAINS, SELF, _3m62u_sprites_left,
  cargo_subtype)
{
  0: _2m62u_v1_sprites_left;
  1: _2m62u_v2_sprites_left;
  2: _2m62u_v10_sprites_left;
  3: _2m62u_v8_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, _3m62u_sprites_right,
  cargo_subtype)
{
  0: _2m62u_v1_sprites_right;
  1: _2m62u_v2_sprites_right;
  2: _2m62u_v10_sprites_right;
  3: _2m62u_v8_sprites_right;
     align_10_sprites;
}  

engine_direction_template3m(_3m62u, _2m62u, _3m62u_m, ) 
long_vehicle(_3m62u)

switch (FEAT_TRAINS, SELF, _3m62u_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3m62u_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_MOSCOW_RAILWAY, "");
  3: return string(STR_REFIT_KALININGRAD_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3m62u_cargo_subtype_text,
  ((current_year >= 1991) || (build_year >= 1991)) )
{
  1: _3m62u_cargo_subtype_text2;
     _3m62u_cargo_subtype_text3;
}

switch (FEAT_TRAINS, SELF, _3m62u_additional_text,
  current_year >= 1991)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4_WITH_ATTACH, string(STR_2M62U_NAME)), 
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 1992), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD), 
                   2);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4_WITH_ATTACH, string(STR_2M62U_NAME)), 
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 1992), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK), 
                   4);
}

new_3x_train_start_stop(_3m62u)
engine_diesel_1_effect(_3m62u, 3, -1, 11)
engine_direction_template3m(_3m62u, _2m62u, _3m62u_m, _create_effect) 
engine_speed(_3m62u, 100)
engine_penalise_speed3(_3m62u, _2m62u, _3m62u_m, 8)
general_running_cost(_3m62u, 341) 

// отключить пенальти для ДРБ1
switch (FEAT_TRAINS, PARENT, _3m62u_bitmask_vehicle_info_drb1,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(_3m62u) * 2) == 0) && 
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1) )
{
  1: return 0;
     _3m62u_bitmask_vehicle_info;
}
item (FEAT_TRAINS, _3m62u) {
  property {
    name: string(STR_3M62U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1986), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1986,1992);
    retire_early: get_retire_early(1986,1992);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 35;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1546 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: (378/3) ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(196.0, 378/3);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: _3m62u_sprites;
    purchase: _3m62u_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: _3m62u_start_stop;
    additional_text: _3m62u_additional_text;
    cargo_subtype_text: _3m62u_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3m62u_speed;
    purchase_speed: _3m62u_purchase_speed;
    running_cost_factor: _3m62u_running_cost_factor;
    bitmask_vehicle_info: _3m62u_bitmask_vehicle_info_drb1;
    create_effect: _3m62u_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _3m62u) {
    property { name: string(STR_3M62U_NAME_LONG); }
  }
}

 // 3m62u_m

switch (FEAT_TRAINS, SELF, _3m62u_m_sprites_left,
  cargo_subtype)
{
  0: _3m62u_m_v1_sprites_left;
  1: _3m62u_m_v2_sprites_left;
  2: _3m62u_m_v3_sprites_left;
  3: _3m62u_m_v4_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_sprites_right,
  cargo_subtype)
{
  0: _3m62u_m_v1_sprites_right;
  1: _3m62u_m_v2_sprites_right;
  2: _3m62u_m_v3_sprites_right;
  3: _3m62u_m_v4_sprites_right;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_sprites_start,
[  set_offset_to(-2),
   prev_vehicle_randombits() % 2 ] )
{ 
  1: _3m62u_m_sprites_left;
     _3m62u_m_sprites_right;
}

long_vehicle(_3m62u_m)

engine_diesel_1_effect(_3m62u_m, 3, -1, 11)

switch (FEAT_TRAINS, SELF, _3m62u_m_create_effect_sprites_start,
[  set_offset_to(-2),
   prev_vehicle_randombits() % 2 ] )
{ 
  1: _3m62u_m_create_effect_sprites_left;
     _3m62u_m_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_59);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_MOSCOW_RAILWAY, "");
  3: return string(STR_REFIT_KALININGRAD_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_cargo_subtype_text,
  ((current_year >= 1991) || (build_year >= 1991)) )
{
  1: _3m62u_m_cargo_subtype_text2;
     _3m62u_m_cargo_subtype_text3;
}

switch (FEAT_TRAINS, SELF, _3m62u_m_additional_text,
  current_year >= 1991)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_MS_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_MS, string(STR_PURCHASE_HINT_LIST2, string(STR_2M62U_NAME), string(STR_3M62U_NAME))), 	
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 1992), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD), 
                   2);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_MS_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_MS, string(STR_PURCHASE_HINT_LIST2, string(STR_2M62U_NAME), string(STR_3M62U_NAME))), 	
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 1992), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK), 
                   4);
}

engine_speed(_3m62u_m, 100)
engine_penalise_speed3(_3m62u_m, _2m62u, _3m62u, 8)
general_running_cost(_3m62u_m, 307) 

item (FEAT_TRAINS, _3m62u_m) {
  property {
    name: string(STR_3M62U_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1986), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1986,1992);
    retire_early: get_retire_early(1986,1992);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 34;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1546 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: (378/3) ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(196.0, 378/3);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: _3m62u_m_sprites;
    purchase: _3m62u_m_purchase_sprites;
    running_cost_factor: _3m62u_m_running_cost_factor;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: _3m62u_m_additional_text;
    cargo_subtype_text: _3m62u_m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3m62u_m_speed;
    purchase_speed: _3m62u_m_purchase_speed; 
    bitmask_vehicle_info: _3m62u_m_bitmask_vehicle_info;
    create_effect: _3m62u_m_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _3m62u_m) {
    property { name: string(STR_3M62U_M_NAME_LONG); }
  }
}

switch (FEAT_TRAINS, SELF, _3m62u_additional_text_short,
  current_year >= 1991)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4), 
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD), 
                   2);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_4), 
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK), 
                   4);
}
switch (FEAT_TRAINS, SELF, _3m62u_m_additional_text_short,
  current_year >= 1991)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_MS_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_MS, string(STR_3M62U_NAME)), 	
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD), 
                   2);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_MS_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_MS, string(STR_3M62U_NAME)), 	
                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK), 
                   4);
}

if (models_details_level == 0) {
  item (FEAT_TRAINS, _3m62u) {
    property {
      introduction_date: date(get_loc_year(1986), 1, 1);
      model_life: get_model_life(1986,2000);
      retire_early: get_retire_early(1986,2000);
    }
    graphics {
      default: _3m62u_sprites;
      purchase: _3m62u_purchase_sprites;
      articulated_part: articulated_part_dummy2;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: any_start_stop;
      additional_text: _3m62u_additional_text_short;
      cargo_subtype_text: _3m62u_cargo_subtype_text;
      cargo_capacity: return 0;
      speed: _3m62u_speed;
      purchase_speed: _3m62u_purchase_speed;
      running_cost_factor: _3m62u_running_cost_factor;
      bitmask_vehicle_info: _3m62u_bitmask_vehicle_info_drb1;
      create_effect: _3m62u_create_effect_sprites_start;
    }
  }
  item (FEAT_TRAINS, _3m62u_m) {
    property {
      introduction_date: date(get_loc_year(1986), 1, 1);
      model_life: get_model_life(1986,2000);
      retire_early: get_retire_early(1986,2000);
    }
    graphics {
      default: _3m62u_m_sprites;
      purchase: _3m62u_m_purchase_sprites;
      running_cost_factor: _3m62u_m_running_cost_factor;
      articulated_part: articulated_part_dummy2;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
      additional_text: _3m62u_m_additional_text_short;
      cargo_subtype_text: _3m62u_m_cargo_subtype_text;
      cargo_capacity: return 0;
      speed: _3m62u_m_speed;
      purchase_speed: _3m62u_m_purchase_speed; 
      bitmask_vehicle_info: _3m62u_m_bitmask_vehicle_info;
      create_effect: _3m62u_m_create_effect_sprites_start;
    }
  }
}
                             	