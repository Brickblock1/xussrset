 // 3m62u

// Спрайты

#define IMAGEFILE  "src/diesel/3m62u.png"

// покупка
spriteset (_3m62u_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(133, 80, -4)
}

// обычный: погрузка/перемещение
spriteset (_3m62u_loading_spriteset, IMAGEFILE)
{
  s10_template(133, 80, 0)
} 

spriteset (_3m62u_traveling_spriteset, IMAGEFILE)
{
  s10_template(133, 153, 0)
} 

spritegroup _3m62u_spritegroup
{
  loading: _3m62u_loading_spriteset;
  loaded: _3m62u_traveling_spriteset;
}

// секция Б
spriteset (_3m62u_b_spriteset, IMAGEFILE)
{
  s10_template(594, 80, 0)
} 

// секция В
spriteset (_3m62u_c_spriteset, IMAGEFILE)
{
  s10_template(1055, 80, 0)
} 

// секция В (реверс)
spriteset (_3m62u_cr_spriteset, IMAGEFILE)
{
  s10_template(1516, 80, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3m62u_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(133, 237, 0)
} 

spriteset (_3m62u_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(133, 310, 0)
} 

spritegroup _3m62u_v2_spritegroup
{
  loading: _3m62u_v2_loading_spriteset;
  loaded: _3m62u_v2_traveling_spriteset;
}

// секция Б
spriteset (_3m62u_b_v2_spriteset, IMAGEFILE)
{
  s10_template(594, 237, 0)
} 

// секция В
spriteset (_3m62u_c_v2_spriteset, IMAGEFILE)
{
  s10_template(1055, 237, 0)
} 

// секция В (реверс)
spriteset (_3m62u_cr_v2_spriteset, IMAGEFILE)
{
  s10_template(1516, 237, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3m62u_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(133, 394, 0)
} 

spriteset (_3m62u_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(133, 467, 0)
} 

spritegroup _3m62u_v3_spritegroup
{
  loading: _3m62u_v3_loading_spriteset;
  loaded: _3m62u_v3_traveling_spriteset;
}

// секция Б
spriteset (_3m62u_b_v3_spriteset, IMAGEFILE)
{
  s10_template(594, 394, 0)
} 

// секция В
spriteset (_3m62u_c_v3_spriteset, IMAGEFILE)
{
  s10_template(1055, 394, 0)
} 

// секция В (реверс)
spriteset (_3m62u_cr_v3_spriteset, IMAGEFILE)
{
  s10_template(1516, 394, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3m62u_v4_loading_spriteset, IMAGEFILE)
{
  s10_template(133, 551, 0)
} 

spriteset (_3m62u_v4_traveling_spriteset, IMAGEFILE)
{
  s10_template(133, 624, 0)
} 

spritegroup _3m62u_v4_spritegroup
{
  loading: _3m62u_v4_loading_spriteset;
  loaded: _3m62u_v4_traveling_spriteset;
}

// секция Б
spriteset (_3m62u_b_v4_spriteset, IMAGEFILE)
{
  s10_template(594, 551, 0)
} 

// секция В
spriteset (_3m62u_c_v4_spriteset, IMAGEFILE)
{
  s10_template(1055, 551, 0)
} 

// секция В (реверс)
spriteset (_3m62u_cr_v4_spriteset, IMAGEFILE)
{
  s10_template(1516, 551, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3m62u_get_spriteset2,
  ((last_computed_result - count_veh_id(_3m62u)) % 3) +
  (cargo_subtype * 3)
)
{
  0: _3m62u_spritegroup;
  1: _3m62u_c_spriteset;
  2: _3m62u_b_spriteset;
  3: _3m62u_v2_spritegroup;
  4: _3m62u_c_v2_spriteset;
  5: _3m62u_b_v2_spriteset;
  6: _3m62u_v3_spritegroup;
  7: _3m62u_c_v3_spriteset;
  8: _3m62u_b_v3_spriteset;
  9: _3m62u_v4_spritegroup;
  10: _3m62u_c_v4_spriteset;
  _3m62u_b_v4_spriteset;
}

switch (FEAT_TRAINS, PARENT, _3m62u_get_spriteset1,
  count_veh_id(_3m62u))
{
  _3m62u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3m62u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3m62u_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _3m62u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Если хотя бы 3 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _3m62u_start_stop2,
[
  set_offset_to(2),
  STORE_TEMP(prev_vehicle_type_id() == _3m62u, 0),
  set_offset_to(4),
  (prev_vehicle_type_id() == _3m62u) && LOAD_TEMP(0)
])
{
  1: return CB_RESULT_NO_TEXT;
  return string(STR_START_STOP_NEED_AT_LEAST_3_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 3, то не выпускать
switch (FEAT_TRAINS, PARENT, _3m62u_start_stop,
  count_veh_id(_3m62u) >= 3)
{
  1: _3m62u_start_stop2;
  return string(STR_START_STOP_NEED_3_OR_MORE_ENGINE);
}

switch (FEAT_TRAINS, SELF, _3m62u_cargo_subtype_text2,
  current_year >= 1990)
{
  1: string(STR_REFIT_MOSCOW_RAILWAY);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3m62u_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RAL_6020);
  1: string(STR_REFIT_GREEN_GRASS);
  2: _3m62u_cargo_subtype_text2;
  3: string(STR_REFIT_KALININGRAD_RAILWAY);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _3m62u_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _3m62u_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _3m62u_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_3m62u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 6)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _3m62u_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_3m62u), 0),
  vehicle_type_id == _3m62u
])
{
  _3m62u_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _3m62u_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _3m62u_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, _3m62u) 
{
  property
  {
    name: string(STR_3M62U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1986, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_CARGO;
    power: (4640/3) hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: (378/3) ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.15873;
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _3m62u_get_spriteset;
    purchase: _3m62u_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: _3m62u_can_attach_wagon;
    start_stop: _3m62u_start_stop;
    additional_text: return string(STR_3M62U_PURCHASE_HINT);
    cargo_subtype_text: _3m62u_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3m62u_speed;
    purchase_speed: _3m62u_purchase_speed;
    bitmask_vehicle_info: _3m62u_bitmask_vehicle_info;
  }
}
