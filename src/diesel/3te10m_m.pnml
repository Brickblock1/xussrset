 // 3te10m_m

// Спрайты

#define IMAGEFILE  "src/diesel/3te10m-1979.png"

// покупка
spriteset (_3te10m_m_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(459, 113) }
spriteset (_3te10m_m_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(459, 113) }
switch (FEAT_TRAINS, SELF, _3te10m_m_purchase_spriteset, 
  enable_icons)
{  1: _3te10m_m_purchase_spriteset_ico;
      _3te10m_m_purchase_spriteset_noico;
}

spriteset (_3te10m_m_rc_spriteset, IMAGEFILE)
{
  s10_template(842, 16, 0)
} 

spriteset (_3te10m_m_rc_v2_spriteset, IMAGEFILE)
{
  s10_template(842, 153, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/3te10m-1985.png"

// покупка
spriteset (_3te10m_m_v3_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(459, 113) }
spriteset (_3te10m_m_v3_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(459, 113) }
switch (FEAT_TRAINS, SELF, _3te10m_m_v3_purchase_spriteset, 
  enable_icons)
{  1: _3te10m_m_v3_purchase_spriteset_ico;
      _3te10m_m_v3_purchase_spriteset_noico;
}

spriteset (_3te10m_m_rc_v3_spriteset, IMAGEFILE)
{
  s10_template(842, 16, 0)
} 

spriteset (_3te10m_m_rc_v4_spriteset, IMAGEFILE)
{
  s10_template(842, 153, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3te10m_m_get_spriteset2,
  ((LOAD_TEMP(2) >= date(1985,1,1)) * 2) +
  cargo_subtype 
)
{
  0: _3te10m_m_rc_spriteset;
  1: _3te10m_m_rc_v2_spriteset;
  2: _3te10m_m_rc_v3_spriteset;
     _3te10m_m_rc_v4_spriteset;
}

switch (FEAT_TRAINS, PARENT, _3te10m_m_get_spriteset1,
[  STORE_TEMP(date_of_last_service, 2) ])
{
  _3te10m_m_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3te10m_m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3te10m_m_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, _3te10m_m_get_purchase_spriteset,
  (current_year >= 1985)
)
{
  0: _3te10m_m_purchase_spriteset;
  _3te10m_m_v3_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, _3te10m_m_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _3te10m_m_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _3te10m_m_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _3te10m_m_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_3te10m_m) - count_veh_id(_3te10m) - count_veh_id(_3te10u) -
    count_veh_id(_2te10m) - count_veh_id(_2te10u) -
    count_veh_id(_2te116) - count_veh_id(_2te116u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 6)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _3te10m_m_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_3te10m_m) + count_veh_id(_3te10m) + count_veh_id(_3te10u) +
    count_veh_id(_2te10m) + count_veh_id(_2te10u) +
    count_veh_id(_2te116) + count_veh_id(_2te116u), 0),
  (vehicle_type_id == _3te10m_m) || (vehicle_type_id == _3te10m) || (vehicle_type_id == _3te10u) ||
    (vehicle_type_id == _2te10m) || (vehicle_type_id == _2te10u) ||
    (vehicle_type_id == _2te116) || (vehicle_type_id == _2te116u)
])
{
  _3te10m_m_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _3te10m_m_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _3te10m_m_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, _3te10m_m)
{
  property
  {
    name: string(STR_3TE10M_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1979), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1979,1990);
    retire_early: get_retire_early(1979,1990); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_m_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2273 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(245.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _3te10m_m_get_spriteset;
    purchase: _3te10m_m_get_purchase_spriteset;
    purchase_speed: _3te10m_m_purchase_speed;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: return string(STR_CANNOT_START_HEAD);
    additional_text: return string(STR_3TE10M_M_PURCHASE_HINT);
    cargo_subtype_text: _3te10m_m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3te10m_m_speed; 
    bitmask_vehicle_info: _3te10m_m_bitmask_vehicle_info;
    cost_factor: return 72;
  }
}
