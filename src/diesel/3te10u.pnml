 // 3te10u

// Спрайты

#define IMAGEFILE  "src/diesel/3te10u-1990.png"

// покупка
spriteset (_3te10u_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(4, 16, -4)
}

// обычный: погрузка/перемещение
spriteset (_3te10u_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (_3te10u_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup _3te10u_spritegroup
{
  loading: _3te10u_loading_spriteset;
  loaded: _3te10u_traveling_spriteset;
}

// развёрнутый
spriteset (_3te10u_rb_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

spriteset (_3te10u_rc_spriteset, IMAGEFILE)
{
  s10_template(842, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3te10u_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (_3te10u_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup _3te10u_v2_spritegroup
{
  loading: _3te10u_v2_loading_spriteset;
  loaded: _3te10u_v2_traveling_spriteset;
}

// развёрнутый
spriteset (_3te10u_rb_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

spriteset (_3te10u_rc_v2_spriteset, IMAGEFILE)
{
  s10_template(842, 153, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3te10u_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 290, 0)
} 

spriteset (_3te10u_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 356, 0)
} 

spritegroup _3te10u_v3_spritegroup
{
  loading: _3te10u_v3_loading_spriteset;
  loaded: _3te10u_v3_traveling_spriteset;
}

// развёрнутый
spriteset (_3te10u_rb_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 290, 0)
} 

spriteset (_3te10u_rc_v3_spriteset, IMAGEFILE)
{
  s10_template(842, 290, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3te10u_get_spriteset2,
  ((last_computed_result - count_veh_id(_3te10u)) % 3) +
  cargo_subtype * 3
)
{
  0: _3te10u_spritegroup;
  1: _3te10u_rc_spriteset;
  2: _3te10u_rb_spriteset;
  3: _3te10u_v2_spritegroup;
  4: _3te10u_rc_v2_spriteset;
  5: _3te10u_rb_v2_spriteset;
  6: _3te10u_v3_spritegroup;
  7: _3te10u_rc_v3_spriteset;
  _3te10u_rb_v3_spriteset;
}

switch (FEAT_TRAINS, PARENT, _3te10u_get_spriteset1,
  count_veh_id(_3te10u))
{
  _3te10u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3te10u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3te10u_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _3te10u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, _3te10u_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _3te10u_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_3TE10U_REFIT_V1);
  1: string(STR_3TE10U_REFIT_V2);
  2: string(STR_3TE10U_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, _3te10u)
{
  property
  {
    name: string(STR_3TE10U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1990, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_CARGO;
    power: 2260 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.188553682;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _3te10u_get_spriteset;
    purchase: _3te10u_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: _3te10u_can_attach_wagon;
    start_stop: _3te10u_start_stop;
    additional_text: return string(STR_3TE10U_PURCHASE_HINT);
    cargo_subtype_text: _3te10u_cargo_subtype_text;
    cargo_capacity: return 0;
  }
}
