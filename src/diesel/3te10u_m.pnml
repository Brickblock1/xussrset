 // 3te10u_m

// Спрайты

#define IMAGEFILE  "src/diesel/3te10u-1990.png"

// покупка
purchase_spriteset(_3te10u_m, 486, 112)

// обычный: погрузка/перемещение
spriteset (_3te10u_m_rc_spriteset, IMAGEFILE)
{
  s10_template(842, 16, 0)
} 

spriteset (_3te10u_m_rc_v2_spriteset, IMAGEFILE)
{
  s10_template(842, 153, 0)
} 

spriteset (_3te10u_m_rc_v3_spriteset, IMAGEFILE)
{
  s10_template(842, 290, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3te10u_m_get_spriteset1,
  cargo_subtype)
{
  0: _3te10u_m_rc_spriteset;
  1: _3te10u_m_rc_v2_spriteset;
     _3te10u_m_rc_v3_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3te10u_m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3te10u_m_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _3te10u_m_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_63);
  1: string(STR_REFIT_TTD_96);
  2: string(STR_REFIT_TTD_14);
  return CB_RESULT_NO_TEXT;
}


engine_speed(_3te10u_m, 100)

engine_penalise_speed7(_3te10u_m, _3te10u, _3te10m, _2te10m, _2te10u, _2te116, _2te116u, 8) 

item (FEAT_TRAINS, _3te10u_m)
{
  property
  {
    name: string(STR_3TE10U_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1990), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1990,1992);
    retire_early: get_retire_early(1990,1992); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_m_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2260 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(255.0, 138);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: _3te10u_m_get_spriteset;
    purchase: _3te10u_m_purchase_spriteset;
    purchase_speed: _3te10u_m_purchase_speed;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: return string(STR_3TE10U_M_PURCHASE_HINT);
    cargo_subtype_text: _3te10u_m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _3te10u_m_speed; 
    bitmask_vehicle_info: _3te10u_m_bitmask_vehicle_info;
    cost_factor: return 84;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, _3te10u_m)
  {
    property
    { name: string(STR_3TE10U_M_NAME_LONG); }
  }
}

