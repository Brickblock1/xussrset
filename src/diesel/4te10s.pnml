 // 4te10s
// 350 (15Eh)

#define IMAGEFILE  "src/diesel/4te10s.png"
purchase_sprites(_4te10s, 0, 19)
#undef IMAGEFILE

// Если хотя бы 4 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop5,
[
  set_offset_to(2),
  STORE_TEMP((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m), 0),
  set_offset_to(4),
  STORE_TEMP(((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0), 0),
  set_offset_to(6),
  ((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0)
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_4_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 4, то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop_b1993,
  (count_veh_id(_4te10s) >= 2) && (count_veh_id(_4te10s_m) >= 2))
{
  1: _4te10s_start_stop5;
  return string(STR_START_STOP_NEED_2AND2_OR_MORE_ENGINE);
}

// Если хотя бы 3 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop3,
[
  set_offset_to(2),
  STORE_TEMP((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m), 0),
  set_offset_to(4),
  ((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0)
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_3_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 3, то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop_a1993,
  (count_veh_id(_4te10s) >= 2) && (count_veh_id(_4te10s_m) >= 1))
{
  1: _4te10s_start_stop3;
  return string(STR_START_STOP_NEED_2AND1_OR_MORE_ENGINE);
}

// Если год >= 1993, можно в 3 секции
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop,
  current_year >= 1993)
{
  1: _4te10s_start_stop_a1993;
     _4te10s_start_stop_b1993;
}

engine_speed(_4te10s, 100)
engine_penalise_speed4(_4te10s, _4te10s_m, _4te10s_all, _2te10s, 4)
general_running_cost(_4te10s, 478)

item (FEAT_TRAINS, _4te10s, 350) {
  property {
    name: string(STR_NAME_4TE10S);
    vehicle_dates(1983, 1987, 30, 10, 8, 49)
    vehicle_cargo()
    vehicle_diesel(2273, 138, 398.6)
  }
  graphics {
    default: _2te10s_sprites;
    purchase: _4te10s_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: _4te10s_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE,
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL),
                                   string(STR_PURCHASE_HINT_SECTIONS_4_WITH_ATTACH, string(STR_NAME_2TE10S)),
                                   string(STR_PURCHASE_HINT_FROMTO, 1983, 1987),
                                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
    speed: _4te10s_speed;
    running_cost_factor: _4te10s_running_cost_factor;
    bitmask_vehicle_info: _4te10s_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: _2te10s_create_effect_sprites_start;
    colour_mapping: any_cc_colour;
    cargo_subtype_text: return string(STR_REFIT_FACTORY);
    purchase_speed: return 100;
  }
}

long_name_template(_4te10s, STR_LONGNAME_4TE10S)
models_default_cargo_template(_4te10s)
allow_diesel(_4te10s)
