 // da

// Спрайты

#define IMAGEFILE  "src/diesel/da-te1.png"

// покупка
spriteset (da_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(283, 44)
}

// обычный
spriteset (da_spriteset, IMAGEFILE)
{
  s10_template(0, 0, 0)
} 

// развёрнутый
spriteset (da_r_spriteset, IMAGEFILE)
{
  s10_template(440, 0, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, da_get_spriteset1, 
  vehicle_is_reversed)
{
  0: da_spriteset;
  da_r_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, da_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  da_get_spriteset1;
}

// Кол-во моторных чётное?
switch (FEAT_TRAINS, PARENT, da_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 96 км/ч
#define MAX_SPEED 96

switch (FEAT_TRAINS, SELF, da_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, da_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, da_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(da), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, da_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(da), 0),
  vehicle_type_id == da
])
{
  da_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, da_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  da_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, da) 
{
  property
  {
    name: string(STR_DA_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1945), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1945,1945);
    retire_early: get_retire_early(1945,1945); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 15;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 800 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 121 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(160.0, 121);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(5);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: da_get_spriteset;
    purchase: da_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: da_start_stop;
    additional_text: return string(STR_DA_PURCHASE_HINT);
    speed: da_speed;
    purchase_speed: da_purchase_speed;
    bitmask_vehicle_info: da_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
