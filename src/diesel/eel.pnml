 // eel
// 361 (167h)

#define IMAGEFILE  "src/diesel/eel.png"
purchase_sprites(eel, 0, 0)

diesel2_sprites(8, eel_v1, 32, 40)
diesel2_sprites(8, eel_v2, 32, 241)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, eel_sprites_left,
  date_of_last_service >= date(1960,1,1))
{
  0: eel_v1_sprites_left;
  1: eel_v2_sprites_left;
     align_8_sprites; 
}

switch (FEAT_TRAINS, SELF, eel_sprites_right,
  date_of_last_service >= date(1960,1,1))
{
  0: eel_v1_sprites_right;
  1: eel_v2_sprites_right;
     align_8_sprites; 
}

engine_direction_template(eel, )
short_vehicle(eel)

switch (FEAT_TRAINS, SELF, eel_create_effect_sprites_left, 
  [diesel_create_effect_dy(-1, -1, 11), diesel_create_effect_dy2(1, -1, 11)]) 
{ 
  return 2; 
}    

switch (FEAT_TRAINS, SELF, eel_create_effect_sprites_right, 
  [diesel_create_effect_dy(-1, 1, 11), diesel_create_effect_dy2(1, 1, 11)]) 
{ 
  return 2; 
}    

engine_direction_template(eel, _create_effect) 
engine_speed(eel, 55)
engine_penalise_speed(eel, 2)
general_running_cost(eel, 185)

item (FEAT_TRAINS, eel, 361) {
  property {
    name: string(STR_NAME_EEL);
    vehicle_dates(1932, 1937, 30, 10, 7, 19)
    vehicle_passcargo()
    vehicle_diesel(1050, 138, 206.0)
  }
  graphics {
    default: eel_sprites;
    purchase: eel_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1937), 
                                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA));
    cargo_capacity: return 0;
    speed: eel_speed;
    running_cost_factor: eel_running_cost_factor;
    bitmask_vehicle_info: eel_bitmask_vehicle_info;
    cost_factor: 19;
    create_effect: eel_create_effect_sprites_start;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(eel, STR_LONGNAME_EEL)
models_default_cargo_template(eel)
allow_diesel(eel)
