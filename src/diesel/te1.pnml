 // te1

// Спрайты

#define IMAGEFILE  "src/diesel/da-te1.png"

// покупка
spriteset (te1_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(283, 113)
}

// обычный
spriteset (te1_spriteset, IMAGEFILE)
{
  s10_template(0, 69, 0)
} 

// развёрнутый
spriteset (te1_r_spriteset, IMAGEFILE)
{
  s10_template(440, 69, 0)
} 

// обычный 2
spriteset (te1_2_spriteset, IMAGEFILE)
{
  s10_template(0, 138, 0)
} 

// развёрнутый 2
spriteset (te1_r2_spriteset, IMAGEFILE)
{
  s10_template(440, 138, 0)
} 

// обычный 3
spriteset (te1_3_spriteset, IMAGEFILE)
{
  s10_template(0, 207, 0)
} 

// развёрнутый 3
spriteset (te1_r3_spriteset, IMAGEFILE)
{
  s10_template(440, 207, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te1_get_spriteset1,
  cargo_subtype
)
{
  0: te1_spriteset;
  1: te1_2_spriteset;
  te1_3_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, te1_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  te1_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, te1_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_GREEN_GRASS_WITH_YELLOW_STRIPE);
  2: string(STR_BLUE_WITH_WHITE_STRIPE);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 93 км/ч до 1956, 95 км/ч после
#define MAX_SPEED current_year >= 1956 ? 95 : 93

switch (FEAT_TRAINS, SELF, te1_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, te1_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, te1_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(te1), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, te1_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(te1), 0),
  vehicle_type_id == te1
])
{
  te1_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, te1_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  te1_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, te1) 
{
  property
  {
    name: string(STR_TE1_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1947), 1, 1);
    vehicle_life: 35; // срок службы
    model_life: get_model_life(1947,1950);
    retire_early: get_retire_early(1947,1950); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 13;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 710 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 124 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(157.0, 124);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(5);
  }
  graphics {
    default: te1_get_spriteset;
    purchase: te1_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TE1_PURCHASE_HINT);
    cargo_subtype_text: te1_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: te1_speed;
    purchase_speed: te1_purchase_speed;
    bitmask_vehicle_info: te1_bitmask_vehicle_info;
  }
}
