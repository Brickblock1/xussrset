 // te10

#define IMAGEFILE  "src/diesel/te10.png"
purchase_sprites(te10,   0, 0)

diesel1r_sprites(11, te10_v1, 32,   40)
diesel1r_sprites(11, te10_v2, 32,  241)
diesel1r_sprites(11, te10_v3, 32,  442)
diesel1r_sprites(11, te10_v4, 32,  643)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te10_sprites_left,
  build_year >= 1972 ? cargo_subtype + 2 :
  LOAD_TEMP(2) >= date(1972,1,1) ? 3 - cargo_subtype : cargo_subtype)
{
  0: te10_v1_sprites_left;
  1: te10_v2_sprites_left;
  2: te10_v3_sprites_left;
  3: te10_v4_sprites_left;
     align_11_sprites; 
}

switch (FEAT_TRAINS, SELF, te10_sprites_right,
  build_year >= 1972 ? cargo_subtype + 2 :
  LOAD_TEMP(2) >= date(1972,1,1) ? 3 - cargo_subtype : cargo_subtype)
{
  0: te10_v1_sprites_right;
  1: te10_v2_sprites_right;
  2: te10_v3_sprites_right;
  3: te10_v4_sprites_right;
     align_11_sprites; 
}

engine_RNDany_direction_template(te10, 5, 3) 
long_vehicle_sp(te10)

switch (FEAT_TRAINS, SELF, te10_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_93);
  1: return string(STR_REFIT_TTD_63);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te10_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_63);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te10_cargo_subtype_text,
  build_year >= 1972)
{
  1: te10_cargo_subtype_text2;
     te10_cargo_subtype_text3;
}

engine_diesel_1r_effect(te10, 3, 0, 12)
engine_RNDany_effect_direction_template(te10, 5, 3) 
engine_speed(te10, 100)
engine_penalise_speed(te10, 2)
general_running_cost(te10, 482)

item (FEAT_TRAINS, te10) {
  property {
    name: string(STR_NAME_TE10);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1958), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1958,1961);
    retire_early: get_retire_early(1958,1961);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 49;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2310 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(245.0, 138);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: te10_sprites;
    purchase: te10_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1968), 
                                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
    cargo_subtype_text: te10_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: te10_speed;
    purchase_speed: te10_purchase_speed;
    running_cost_factor: te10_running_cost_factor;
    bitmask_vehicle_info: te10_bitmask_vehicle_info;
    create_effect: te10_create_effect;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, te10) {
    property { name: string(STR_LONGNAME_TE10); }
  }
}

if (models_default_cargo == 1) {
  item (FEAT_TRAINS, te10) {
    property {
      cargo_allow_refit: [GOOD];
      default_cargo_type: GOOD;
    }
  }
}

if (models_default_cargo == 2) {
  item (FEAT_TRAINS, te10) {
    property {
      cargo_allow_refit: [PASS];
      default_cargo_type: PASS;
    }
  }
}

if (models_default_cargo == 3) {
  item (FEAT_TRAINS, te10) {
    property {
      cargo_allow_refit: [MAIL];
      default_cargo_type: MAIL;
    }
  }
}

