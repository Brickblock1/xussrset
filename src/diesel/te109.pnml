 // te109

// Спрайты

#define IMAGEFILE  "src/diesel/te109.png"

// покупка
spriteset (te109_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// обычный: погрузка/перемещение
spriteset (te109_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 31, 0)
} 

spriteset (te109_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 98, 0)
} 

spritegroup te109_spritegroup
{
  loading: te109_loading_spriteset;
  loaded: te109_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (te109_r_loading_spriteset, IMAGEFILE)
{
  s12_template(450, 31, 0)
} 

spriteset (te109_r_traveling_spriteset, IMAGEFILE)
{
  s12_template(450, 98, 0)
} 

spritegroup te109_r_spritegroup
{
  loading: te109_r_loading_spriteset;
  loaded: te109_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te109_get_spriteset2,
  ((last_computed_result - count_veh_id(te109)) % 2)
)
{
  0: te109_spritegroup;
  te109_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, te109_get_spriteset1,
  count_veh_id(te109))
{
  te109_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, te109_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  te109_get_spriteset1;
}

switch (FEAT_TRAINS, PARENT, te109_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, te109_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, te109_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, te109_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(te109), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, te109_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(te109), 0),
  vehicle_type_id == te109
])
{
  te109_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, te109_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  te109_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, te109) 
{
  property
  {
    name: string(STR_TE109_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1977), 1, 1);
    vehicle_life: 20; // срок службы
    model_life: get_model_life(1977,1978);
    retire_early: get_retire_early(1977,1978); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 79;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2200 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 120 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(241.0, 120);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: te109_get_spriteset;
    purchase: te109_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: te109_start_stop;
    additional_text: return string(STR_TE109_PURCHASE_HINT);
    speed: te109_speed;
    purchase_speed: te109_purchase_speed;
    bitmask_vehicle_info: te109_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
