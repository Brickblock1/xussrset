 // te109

// Спрайты

#define IMAGEFILE  "src/diesel/te109.png"

// покупка
purchase_spriteset(te109, 0, 0)

// обычный: погрузка/перемещение
spriteset (te109_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 31, 0)
} 

spriteset (te109_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 98, 0)
} 

spritegroup te109_spritegroup
{
  loading: te109_loading_spriteset;
  loaded: te109_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (te109_r_loading_spriteset, IMAGEFILE)
{
  s12_template(450, 31, 0)
} 

spriteset (te109_r_traveling_spriteset, IMAGEFILE)
{
  s12_template(450, 98, 0)
} 

spritegroup te109_r_spritegroup
{
  loading: te109_r_loading_spriteset;
  loaded: te109_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te109_get_spriteset2,
  ((LOAD_TEMP(0) - count_veh_id(te109)) % 2)
)
{
  0: te109_spritegroup;
     te109_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, te109_get_spriteset1,
[ STORE_TEMP(count_veh_id(te109), 0),                         
  STORE_TEMP(current_speed, 1),                              
  STORE_TEMP(date_of_last_service, 2) ])                     
{
  te109_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, te109_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  te109_get_spriteset1;
}


engine_speed(te109, 100)

engine_penalise_speed(te109, 2)

item (FEAT_TRAINS, te109) 
{
  property
  {
    name: string(STR_TE109_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1977), 1, 1);
    vehicle_life: 20; // срок службы
    model_life: get_model_life(1977,1978);
    retire_early: get_retire_early(1977,1978); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 79;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 2200 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 120 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(241.0, 120);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: te109_get_spriteset;
    purchase: te109_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TE109_PURCHASE_HINT);
    speed: te109_speed;
    purchase_speed: te109_purchase_speed;
    bitmask_vehicle_info: te109_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
