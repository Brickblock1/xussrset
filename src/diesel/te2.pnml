 // te2
 // 146 (92h)

#define IMAGEFILE  "src/diesel/te2.png"
purchase_sprites(te2_v1, 0, 0)
purchase_sprites(te2_v2, 102, 0)

diesel2_sprites(7, te2_v1, 32,   40)
diesel2_sprites(7, te2_v2, 32,  241)
diesel2_sprites(7, te2_v3, 32,  442)
diesel2_sprites(7, te2_v4, 32,  643)
diesel2_sprites(7, te2_v5, 32,  844)
diesel2_sprites(7, te2_v6, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te2_purchase_sprites,
  current_year >= 1950)
{
  0: te2_v1_purchase_sprites;
     te2_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, te2_sprites_left,
  (build_year >= 1950) +
  (build_year >= 1960) +
  ((build_year >= 1972) * 2) +
  cargo_subtype)
{
  0: te2_v1_sprites_left;
  1: te2_v2_sprites_left;
  2: te2_v3_sprites_left;
  3: te2_v4_sprites_left;
  4: te2_v5_sprites_left;
  5: te2_v6_sprites_left;
     align_7_sprites;
}

switch (FEAT_TRAINS, SELF, te2_sprites_right,
  (build_year >= 1950) +
  (build_year >= 1960) +
  ((build_year >= 1972) * 2) +
  cargo_subtype)
{
  0: te2_v1_sprites_right;
  1: te2_v2_sprites_right;
  2: te2_v3_sprites_right;
  3: te2_v4_sprites_right;
  4: te2_v5_sprites_right;
  5: te2_v6_sprites_right;
     align_7_sprites;
}

engine_direction_template(te2, ) 
short_vehicle(te2)

switch (FEAT_TRAINS, SELF, te2_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_63);
  1: return string(STR_REFIT_TTD_64);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te2_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_5B);
  1: return string(STR_REFIT_TTD_93);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te2_cargo_subtype_text,
  (build_year >= 1950) +
  (build_year >= 1960) +
  (build_year >= 1970))
{

  0: return string(STR_REFIT_PREPRODUCTION);
  1: return CB_RESULT_NO_TEXT;
  2: te2_cargo_subtype_text2;
  3: te2_cargo_subtype_text3;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te2_additional_text,
  current_year >= 1950)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_IN, 1949), 
                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1949, 1955), 
                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
}

engine_diesel_1r_effect(te2, -1, 0, 11)
engine_direction_template(te2, _create_effect) 
engine_speed_change_after(te2, 93, 
                               95, 1956)
engine_penalise_speed(te2, 2)
general_running_cost(te2, 149)

item (FEAT_TRAINS, te2, 146) {
  property {
    name: string(STR_NAME_TE2);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1948), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1948,1955);
    retire_early: get_retire_early(1948,1955);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 15;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT); 
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 754 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 85 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: get_tec(245.6, 85);
    length: 6;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: te2_sprites;
    purchase: te2_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: te2_additional_text;
    cargo_subtype_text: te2_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: te2_speed;
    purchase_speed: te2_purchase_speed;
    running_cost_factor: te2_running_cost_factor;
    bitmask_vehicle_info: te2_bitmask_vehicle_info;
    cost_factor: 15;
    create_effect: te2_create_effect_sprites_start;
  }
}

long_name_template(te2, STR_LONGNAME_TE2)
models_default_cargo_template(te2)
allow_diesel(te2)
