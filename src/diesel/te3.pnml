 // te3

// Спрайты

#define IMAGEFILE  "src/diesel/te3.png"

// 1953-1954 (предсерийный): покупка
spriteset (te3_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(882, 16)
}

// обычный: погрузка/перемещение
spriteset (te3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (te3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup te3_spritegroup
{
  loading: te3_loading_spriteset;
  loaded: te3_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/te3-1955-1957.png"

// 1955: покупка
spriteset (te3_v2_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(883, 16)
}

// обычный: погрузка/перемещение
spriteset (te3_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (te3_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup te3_v2_spritegroup
{
  loading: te3_v2_loading_spriteset;
  loaded: te3_v2_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// 1956-57: Харьковский завод (покупка)
spriteset (te3_v3_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(883, 153)
}

// обычный: погрузка/перемещение
spriteset (te3_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (te3_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup te3_v3_spritegroup
{
  loading: te3_v3_loading_spriteset;
  loaded: te3_v3_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

// 1956: Ворошиловоградский завод

// обычный: погрузка/перемещение
spriteset (te3_v4_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 290, 0)
} 

spriteset (te3_v4_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 356, 0)
} 

spritegroup te3_v4_spritegroup
{
  loading: te3_v4_loading_spriteset;
  loaded: te3_v4_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v4_spriteset, IMAGEFILE)
{
  s10_template(423, 290, 0)
} 

// 1956: Коломенский завод

// обычный: погрузка/перемещение
spriteset (te3_v5_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 427, 0)
} 

spriteset (te3_v5_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 493, 0)
} 

spritegroup te3_v5_spritegroup
{
  loading: te3_v5_loading_spriteset;
  loaded: te3_v5_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v5_spriteset, IMAGEFILE)
{
  s10_template(423, 427, 0)
} 

// 1972-: покупка
spriteset (te3_v6_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(883, 564)
}

// обычный: погрузка/перемещение
spriteset (te3_v6_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 564, 0)
} 

spriteset (te3_v6_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 630, 0)
} 

spritegroup te3_v6_spritegroup
{
  loading: te3_v6_loading_spriteset;
  loaded: te3_v6_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v6_spriteset, IMAGEFILE)
{
  s10_template(423, 564, 0)
} 

// 1972-: 

// обычный: погрузка/перемещение
spriteset (te3_v7_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 701, 0)
} 

spriteset (te3_v7_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 767, 0)
} 

spritegroup te3_v7_spritegroup
{
  loading: te3_v7_loading_spriteset;
  loaded: te3_v7_traveling_spriteset;
}

// развёрнутый
spriteset (te3_r_v7_spriteset, IMAGEFILE)
{
  s10_template(423, 701, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te3_get_spriteset2,
  ((last_computed_result - count_veh_id(te3)) % 2) +
  (((build_year >= 1955) +
   (build_year >= 1956) +
   (build_year >= 1957)) * 2 ) +
  ((current_year >= 1972) * 8 ) +
  cargo_subtype * 16
)
// 1 - 1953-1954
// 2 - 1955
// 3 - 1956-1957
// 4 - 1956 L2
// 5 - 1956 L3
// 6 - 1972- L1
// 7 - 1972- L2, L3

{
  0: te3_spritegroup;
  1: te3_r_spriteset;
  2: te3_v2_spritegroup;
  3: te3_r_v2_spriteset;
  4: te3_v3_spritegroup;
  5: te3_r_v3_spriteset;
  6: te3_v3_spritegroup;
  7: te3_r_v3_spriteset;

  8: te3_v6_spritegroup;
  9: te3_r_v6_spriteset;
 10: te3_v6_spritegroup;
 11: te3_r_v6_spriteset;
 12: te3_v6_spritegroup;
 13: te3_r_v6_spriteset;
 14: te3_v6_spritegroup;
 15: te3_r_v6_spriteset;

 16: te3_spritegroup;
 17: te3_r_spriteset;
 18: te3_v2_spritegroup;
 19: te3_r_v2_spriteset;
 20: te3_v4_spritegroup;
 21: te3_r_v4_spriteset;
 22: te3_v3_spritegroup;
 23: te3_r_v3_spriteset;

 24: te3_v7_spritegroup;
 25: te3_r_v7_spriteset;
 26: te3_v7_spritegroup;
 27: te3_r_v7_spriteset;
 28: te3_v7_spritegroup;
 29: te3_r_v7_spriteset;
 30: te3_v7_spritegroup;
 31: te3_r_v7_spriteset;

 32: te3_spritegroup;
 33: te3_r_spriteset;
 34: te3_v2_spritegroup;
 35: te3_r_v2_spriteset;
 36: te3_v5_spritegroup;
 37: te3_r_v5_spriteset;
 38: te3_v3_spritegroup;
 39: te3_r_v3_spriteset;

 40: te3_v7_spritegroup;
 41: te3_r_v7_spriteset;
 42: te3_v7_spritegroup;
 43: te3_r_v7_spriteset;
 44: te3_v7_spritegroup;
 45: te3_r_v7_spriteset;
 46: te3_v7_spritegroup;
     te3_r_v7_spriteset;

}

switch (FEAT_TRAINS, PARENT, te3_get_spriteset1,
  count_veh_id(te3))
{
  te3_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, te3_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  te3_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, te3_get_purchase_spriteset,
  (build_year >= 1955) +
  (build_year >= 1956) +
  (build_year >= 1972)
)
{
  0: te3_purchase_spriteset;
  1: te3_v2_purchase_spriteset;
  2: te3_v3_purchase_spriteset;
  te3_v6_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, te3_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, te3_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED (age_in_days >= (25*365+7)) ? 90 : 100

switch (FEAT_TRAINS, SELF, te3_purchase_speed,
  100)
{
  return;
}

switch (FEAT_TRAINS, SELF, te3_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, te3_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(te3), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, te3_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(te3), 0),
  vehicle_type_id == te3
])
{
  te3_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, te3_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  te3_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, te3_additional_text,
  (current_year >= 1955) +
  (current_year >= 1958)
)
{
  1: return string(STR_TE3_PURCHASE_HINT_MONGOLCA);
  2: return string(STR_TE3_PURCHASE_HINT_LASTOCHKA);
  return string(STR_TE3_PURCHASE_HINT);
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1956,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_HARKOV);
  1: string(STR_REFIT_FACTORY_VOROSHILOV);
  2: string(STR_REFIT_FACTORY_RKOLOMNA);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1972,
  cargo_subtype)
{
  0: string(STR_REFIT_DEPOT_V1);
  1: string(STR_REFIT_DEPOT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text,
  (current_year >= 1956) +
  (current_year >= 1957) +
  (current_year >= 1972) 
)
{
  1: te3_cargo_subtype_text_1956;
  3: te3_cargo_subtype_text_1972;
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, te3)
{
  property
  {
    name: string(STR_TE3_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1953), 1, 1);
    vehicle_life: 25; // срок службы
    model_life: get_model_life(1953,1973);
    retire_early: get_retire_early(1953,1973); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1495 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(198.0, 126);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: te3_get_spriteset;
    purchase: te3_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: te3_can_attach_wagon;
    start_stop: te3_start_stop;
    additional_text: te3_additional_text;
    speed: te3_speed;
    purchase_speed: te3_purchase_speed;
    bitmask_vehicle_info: te3_bitmask_vehicle_info;
    cargo_subtype_text: te3_cargo_subtype_text;
    cargo_capacity: return 0;
    cost_factor: return 24;
  }
}
