 // te3

#define IMAGEFILE  "src/diesel/te3.png"
purchase_sprites(te3_v1, 0, 0)
purchase_sprites(te3_v2, 102, 0)
purchase_sprites(te3_v3, 204, 0)
purchase_sprites(te3_v4, 306, 0)

diesel2_sprites(10, te3_v1, 32,   40)
diesel2_sprites(10, te3_v2, 32,  241)
diesel2_sprites(10, te3_v3, 32,  442)
diesel2_sprites(10, te3_v4, 32,  643)
diesel2_sprites(10, te3_v5, 32,  844)
diesel2_sprites(10, te3_v6, 32, 1045)
diesel2_sprites(10, te3_v7, 32, 1246)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te3_purchase_sprites,
  (build_year >= 1955) +
  (build_year >= 1956) +
  (build_year >= 1972) )
{
  0: te3_v1_purchase_sprites;
  1: te3_v2_purchase_sprites;
  2: te3_v3_purchase_sprites;
     te3_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_l,
  ((build_year >= 1955) +
   (build_year >= 1956) +
   (build_year >= 1957)) +
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 4 ) +
  cargo_subtype * 8)
// 1 - 1953-1954
// 2 - 1955
// 3 - 1956-1957
// 4 - 1956 L2
// 5 - 1956 L3
// 6 - 1972- L1
// 7 - 1972- L2, L3
{
  0: te3_v1_sprites_left;
  1: te3_v2_sprites_left;
  2: te3_v3_sprites_left;
  3: te3_v3_sprites_left;
  4: te3_v6_sprites_left;
  5: te3_v6_sprites_left;
  6: te3_v6_sprites_left;
  7: te3_v6_sprites_left;
  8: te3_v1_sprites_left;
  9: te3_v2_sprites_left;
 10: te3_v4_sprites_left;
 11: te3_v3_sprites_left;
 12: te3_v7_sprites_left;
 13: te3_v7_sprites_left;
 14: te3_v7_sprites_left;
 15: te3_v7_sprites_left;
 16: te3_v1_sprites_left;
 17: te3_v2_sprites_left;
 18: te3_v5_sprites_left;
 19: te3_v3_sprites_left;
 20: te3_v7_sprites_left;
 21: te3_v7_sprites_left;
 22: te3_v7_sprites_left;
 23: te3_v7_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_r,
  ((build_year >= 1955) +
   (build_year >= 1956) +
   (build_year >= 1957)) +
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 4 ) +
  cargo_subtype * 8)
// 1 - 1953-1954
// 2 - 1955
// 3 - 1956-1957
// 4 - 1956 L2
// 5 - 1956 L3
// 6 - 1972- L1
// 7 - 1972- L2, L3
{
  0: te3_v1_sprites_right;
  1: te3_v2_sprites_right;
  2: te3_v3_sprites_right;
  3: te3_v3_sprites_right;
  4: te3_v6_sprites_right;
  5: te3_v6_sprites_right;
  6: te3_v6_sprites_right;
  7: te3_v6_sprites_right;
  8: te3_v1_sprites_right;
  9: te3_v2_sprites_right;
 10: te3_v4_sprites_right;
 11: te3_v3_sprites_right;
 12: te3_v7_sprites_right;
 13: te3_v7_sprites_right;
 14: te3_v7_sprites_right;
 15: te3_v7_sprites_right;
 16: te3_v1_sprites_right;
 17: te3_v2_sprites_right;
 18: te3_v5_sprites_right;
 19: te3_v3_sprites_right;
 20: te3_v7_sprites_right;
 21: te3_v7_sprites_right;
 22: te3_v7_sprites_right;
 23: te3_v7_sprites_right;
     align_10_sprites;
}

engine_direction_template(te3, ) 

long_vehicle(te3)

engine_diesel2_1_effect(te3, 3, -1, 11)

engine_direction_template(te3, _create_effect) 

switch (FEAT_TRAINS, SELF, te3_additional_text,
  (current_year >= 1955) +
  (current_year >= 1958) +
  (current_year >= 1972) )
{
  1: return string(STR_TE3_PURCHASE_HINT_MONGOLCA);
  2: return string(STR_TE3_PURCHASE_HINT_LASTOCHKA);
  3: return string(STR_TE3_PURCHASE_HINT_LASTOCHKA_1972);
     return string(STR_TE3_PURCHASE_HINT);
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1956,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_MONGOLKA, string(STR_REFIT_FACTORY_HARKOV));
  1: return string(STR_REFIT_FACTORY_MONGOLKA, string(STR_REFIT_FACTORY_VOROSHILOV));
  2: return string(STR_REFIT_FACTORY_MONGOLKA, string(STR_REFIT_FACTORY_KOLOMNA));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1958,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_LASTOCHKA, string(STR_REFIT_FACTORY_HARKOV));
  1: return string(STR_REFIT_FACTORY_LASTOCHKA, string(STR_REFIT_FACTORY_VOROSHILOV));
  2: return string(STR_REFIT_FACTORY_LASTOCHKA, string(STR_REFIT_FACTORY_KOLOMNA));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1972,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_LASTOCHKA, string(STR_REFIT_TTD_63));
  1: return string(STR_REFIT_FACTORY_LASTOCHKA, string(STR_REFIT_TTD_53));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text,
  (build_year >= 1955) +
  (build_year >= 1956) +
  (build_year >= 1958) +
  (build_year >= 1972) )
{
  0: return string(STR_REFIT_PREPRODUCTION);
  1: return string(STR_REFIT_FACTORY_MONGOLKA, string(STR_REFIT_FACTORY_HARKOV));
  2: te3_cargo_subtype_text_1956;
  3: te3_cargo_subtype_text_1958;
  4: te3_cargo_subtype_text_1972;  
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(te3, 100, 90, 25)

engine_penalise_speed(te3, 2)

item (FEAT_TRAINS, te3)
{
  property
  {
    name: string(STR_TE3_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1953), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1953,1973);
    retire_early: get_retire_early(1953,1973);
    reliability_decay: 20;
    cost_factor: 24;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1495 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(198.0, 126);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: te3_sprites;
    purchase: te3_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: te3_additional_text;
    speed: te3_speed;
    purchase_speed: te3_purchase_speed;
    bitmask_vehicle_info: te3_bitmask_vehicle_info;
    cargo_subtype_text: te3_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: te3_create_effect_sprites_start;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, te3)
  {
    property
    { name: string(STR_TE3_NAME_LONG); }
  }
}

