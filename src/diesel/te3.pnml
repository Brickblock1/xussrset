 // te3
// 347 (15Bh)

#define IMAGEFILE  "src/diesel/te3-pre.png"
purchase_sprites(te3_v1,   0, 0)
purchase_sprites(te3_v2, 102, 0)
diesel2_sprites(10, te3_v1, 32,  40)
diesel2_sprites(10, te3_v2, 32, 241)
diesel2_sprites(10, te3_v4, 32, 442)
diesel2_sprites(10, te3_v5, 32, 643)
#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/te3.png"
purchase_sprites(te3_v3, 0, 0)
diesel2_sprites(10, te3_v3, 32,  40)
diesel2_sprites(10, te3_v6, 32, 241)
diesel2_sprites(10, te3_v7, 32, 442)
#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/te3-lastochka.png"
purchase_sprites(te3_v4,   0, 0)
purchase_sprites(te3_v5, 102, 0)
diesel2_sprites(10, te3_v8,  32,   40)
diesel2_sprites(10, te3_v9,  32,  241)
diesel2_sprites(10, te3_v10, 32,  442)
diesel2_sprites(10, te3_v11, 32,  643)
diesel2_sprites(10, te3_v12, 32,  844)
diesel2_sprites(10, te3_v13, 32, 1045)
#undef IMAGEFILE                 
purchase_sprites_select4pre(te3, 1955, 1956, 1957, 1963)

switch (FEAT_TRAINS, SELF, te3_sprites_left_lastochka2, 
  date_of_last_service >= LV_GREAT_CHANGE_DATE)
{
  0: te3_v10_sprites_left;
     te3_v12_sprites_left;
}

switch (FEAT_TRAINS, SELF, te3_sprites_left_lastochka1, 
  (date_of_last_service >= date(1963,1,1)) + 
  (date_of_last_service >= LV_GREAT_CHANGE_DATE))
{
  0: te3_v8_sprites_left;
  1: te3_v9_sprites_left;
     te3_v11_sprites_left;
}

switch (FEAT_TRAINS, SELF, te3_sprites_left_kharkiv2, 
  cargo_subtype)
{
  0: te3_v6_sprites_left;
  LV_RAL6002: te3_v7_sprites_left;
  align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_left_kharkiv1, 
  date_of_last_service >= LV_GREAT_CHANGE_DATE)
{
  0: te3_v3_sprites_left;
     te3_sprites_left_kharkiv2;
}
    
random_switch (FEAT_TRAINS, SELF, te3_sprites_left_a1956, 0)
{
  6: te3_sprites_left_kharkiv1;
  5: te3_v4_sprites_left;
  5: te3_v5_sprites_left;
}
    
switch (FEAT_TRAINS, SELF, te3_sprites_left_a1957,
  cargo_subtype)
{
  0: te3_sprites_left_lastochka1;
  1: te3_v13_sprites_left;
  LV_RAL6002: te3_sprites_left_lastochka2;
  align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_left_a1955,
  (build_year >= 1956) +
  (build_year >= 1957))
{
  0: te3_v2_sprites_left;
  1: te3_sprites_left_a1956;
     te3_sprites_left_a1957;
}

engine_pre_template(te3, 1955, left)

switch (FEAT_TRAINS, SELF, te3_sprites_right_lastochka2, 
  date_of_last_service >= LV_GREAT_CHANGE_DATE)
{
  0: te3_v10_sprites_right;
     te3_v12_sprites_right;
}

switch (FEAT_TRAINS, SELF, te3_sprites_right_lastochka1, 
  (date_of_last_service >= date(1963,1,1)) + 
  (date_of_last_service >= LV_GREAT_CHANGE_DATE))
{
  0: te3_v8_sprites_right;
  1: te3_v9_sprites_right;
     te3_v11_sprites_right;
}

switch (FEAT_TRAINS, SELF, te3_sprites_right_kharkiv2, 
  cargo_subtype)
{
  0: te3_v6_sprites_right;
  LV_RAL6002: te3_v7_sprites_right;
  align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_right_kharkiv1, 
  date_of_last_service >= LV_GREAT_CHANGE_DATE)
{
  0: te3_v3_sprites_right;
     te3_sprites_right_kharkiv2;
}
    
random_switch (FEAT_TRAINS, SELF, te3_sprites_right_a1956, 0)
{
  6: te3_sprites_right_kharkiv1;
  5: te3_v4_sprites_right;
  5: te3_v5_sprites_right;
}
    
switch (FEAT_TRAINS, SELF, te3_sprites_right_a1957,
  cargo_subtype)
{
  0: te3_sprites_right_lastochka1;
  1: te3_v13_sprites_right;
  LV_RAL6002: te3_sprites_right_lastochka2;
  align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te3_sprites_right_a1955,
  (build_year >= 1956) +
  (build_year >= 1957))
{
  0: te3_v2_sprites_right;
  1: te3_sprites_right_a1956;
     te3_sprites_right_a1957;
}

engine_pre_template(te3, 1955, right)

engine_direction_template(te3, ) 
long_vehicle(te3)

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1956_a1972_2,
  cargo_subtype)
{
  LV_RAL6002: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_REFIT_TTD_53)));
              return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_REFIT_TTD_63)));
}  

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1956_a1972,
  (current_year >= LV_GREAT_CHANGE_YEAR) || (build_year >= LV_GREAT_CHANGE_YEAR))
{
  1: te3_cargo_subtype_text_1956_a1972_2;
     return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_REFIT_FACTORY_NAME, string(STR_PURCHASE_HINT_FACTORY_KHARKIV)));
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_1956,
  random_bits % 16)
{
  0..5: te3_cargo_subtype_text_1956_a1972;
  6..10: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_REFIT_FACTORY_NAME, string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)));
  11..15: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_REFIT_FACTORY_NAME, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA)));
          return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_lastochka3,
  cargo_subtype)
{
  1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_96));
  LV_RAL6002: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_53));
              return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_63));
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_lastochka2,
  cargo_subtype)
{
  LV_RAL6002: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_53));
              return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_63));
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_lastochka,
  ((current_year >= 1963) || (build_year >= 1963)) +
  ((current_year >= 1972) || (build_year >= 1972)))
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_LASTOCHKA), string(STR_REFIT_TTD_63));
  1: te3_cargo_subtype_text_lastochka2; 
     te3_cargo_subtype_text_lastochka3; 
}

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text_long,
  ((built_during_preview == 1) || (build_year < 1955)) * 10 +
  (build_year >= 1956) +
  (build_year >= 1957))
{
 10..12: return string(STR_REFIT_PREPRODUCTION);
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_MONGOLKA), string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
  1: te3_cargo_subtype_text_1956;
     te3_cargo_subtype_text_lastochka;  
}

livery_template(te3_a1963, TTD_63, LV_RAL6002, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template(te3_a1972, TTD_63, LV_RAL6002, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(te3_a1972, TTD_96)

switch (FEAT_TRAINS, SELF, te3_a1957_cargo_subtype_text_pre,
  ((current_year >= 1963) || (build_year >= 1963)) +
  ((current_year >= 1972) || (build_year >= 1972)))
{
  0: return CB_RESULT_NO_TEXT;
  1: te3_a1963_cargo_subtype_text; 
     te3_a1972_cargo_subtype_text_pre; 
}

switch (FEAT_TRAINS, SELF, te3_a1955_cargo_subtype_text_pre,
  build_year >= 1957)
{
  0: return CB_RESULT_NO_TEXT;
     te3_a1957_cargo_subtype_text_pre;  
}

livery_template_prealt(te3, 1955)

switch (FEAT_TRAINS, SELF, te3_cargo_subtype_text,
  enable_long_names)
{
  1: te3_cargo_subtype_text_long;
     te3_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, SELF, te3_additional_text,
  (current_year >= 1955) +
  (current_year >= 1956) +
  (current_year >= 1957) +
  (current_year >= 1959) +
  (current_year >= 1963) +
  (current_year >= 1970) +
  (current_year >= 1972))
{
// до 1955
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1953, 1954), 
                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
// 1955
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_MONGOLKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1955, 1956), 
                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV));
// 1956
  2: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_MONGOLKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1955, 1956), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)));
// 1957 - 1958
  3: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_LASTOCHKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1973), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)));
// 1959 - 1962
  4: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_LASTOCHKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1973), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK)));
// 1963 - 1969
  5: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_LASTOCHKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1973), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK)));
// 1970 - 1971
  6: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_LASTOCHKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1973), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)));
// c 1972
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL_WITH_SUBTYPE, string(STR_PURCHASE_HINT_LASTOCHKA)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1973), 
                   string(STR_PURCHASE_HINT_LIST3, string(STR_PURCHASE_HINT_FACTORY_KHARKIV), string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD)));
}

engine_diesel_1r_effect(te3, 3, -1, 11)
engine_direction_template(te3, _create_effect) 
engine_penalise_speed(te3, 2)
general_running_cost(te3, 349)
engine_speed(te3, 100)

item (FEAT_TRAINS, te3, 347) {
  property {
    name: string(STR_NAME_TE3);
    vehicle_dates(1953, 1973, 25, 10, 8, 36)
    vehicle_cargo()
    vehicle_diesel(1600, 126, 285.3)
  }
  graphics {
    default: te3_sprites;
    purchase: te3_purchase_sprites;
    speed: te3_speed;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: te3_additional_text;
    running_cost_factor: te3_running_cost_factor;
    bitmask_vehicle_info: te3_bitmask_vehicle_info;
    cargo_subtype_text: te3_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: te3_create_effect_sprites_start;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(te3, STR_LONGNAME_TE3)
models_default_cargo_template(te3)
allow_diesel(te3)
