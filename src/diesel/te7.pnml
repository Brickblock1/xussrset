 // te7

// Спрайты

#define IMAGEFILE  "src/diesel/te7.png"
purchase_sprites(te7_v1, 0, 0)
purchase_sprites(te7_v2, 102, 0)

diesel2_sprites(10, te7_v1, 32,   40)
diesel2_sprites(10, te7_v5, 32,  241)
diesel2_sprites(10, te7_v6, 32,  442)
diesel2_sprites(10, te7_v3, 32,  643)
diesel2_sprites(10, te7_v2, 32,  844)
diesel2_sprites(10, te7_v4, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, te7_purchase_sprites,
  current_year >= 1958)
{
  0: te7_v1_purchase_sprites;
     te7_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, te7_sprites_l,
  (build_year >= 1958) * (cargo_subtype + 1) )
{
  0: te7_v1_sprites_left;
  1: te7_v2_sprites_left;
  2: te7_v3_sprites_left;
  3: te7_v4_sprites_left;
  4: te7_v5_sprites_left;
  5: te7_v6_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, te7_sprites_r,
  (build_year >= 1958) * (cargo_subtype + 1) )
{
  0: te7_v1_sprites_right;
  1: te7_v2_sprites_right;
  2: te7_v3_sprites_right;
  3: te7_v4_sprites_right;
  4: te7_v5_sprites_right;
  5: te7_v6_sprites_right;
     align_10_sprites;
}

engine_direction_template(te7, ) 

long_vehicle(te7)

switch (FEAT_TRAINS, SELF, te7_create_effect_sprites_l, 
  diesel_create_effect(3, 11)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, te7_create_effect_sprites_r, 
  diesel_create_effect(-1, 11)) 
{ 
  return 1; 
}    

engine_direction_template(te7, _create_effect) 

switch (FEAT_TRAINS, SELF, te7_additional_text,
  current_year >= 1958)
{
  1: return string(STR_TE7_PURCHASE_HINT);
     return string(STR_TE7_PURCHASE_HINT_PRE);
}

switch (FEAT_TRAINS, SELF, te7_cargo_subtype_text5,
  cargo_subtype)
{
  0: return string(STR_REFIT_PREPRODUCTION);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te7_cargo_subtype_text4,
  cargo_subtype )
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_TTD_96);
  3: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te7_cargo_subtype_text3,
  cargo_subtype )
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_TTD_96);
  3: return string(STR_REFIT_RED_ARROW);
  4: return string(STR_REFIT_AURORA);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, te7_cargo_subtype_text2,
  (current_year >= 1961) || (build_year >= 1961))
{
  1: te7_cargo_subtype_text3;
     te7_cargo_subtype_text4;
}

switch (FEAT_TRAINS, SELF, te7_cargo_subtype_text,
  build_year >= 1958)
{
  1: te7_cargo_subtype_text2;
     te7_cargo_subtype_text5;
}

engine_speed_with_reduce_and_pre_model_no_reduce(te7, 100, 110, 25, 140, 1962)

engine_penalise_speed(te7, 2)

item (FEAT_TRAINS, te7) 
{
  property
  {
    name: string(STR_TE7_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1956), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1956,1964);
    retire_early: get_retire_early(1956,1964);
    reliability_decay: 20;
    cost_factor: 24;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1500 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(114.0, 126);
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: te7_sprites;
    purchase: te7_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: te7_additional_text;
    speed: te7_speed;
    purchase_speed: te7_purchase_speed;
    bitmask_vehicle_info: te7_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: te7_cargo_subtype_text;
    create_effect: te7_create_effect_sprites_start;
    colour_mapping: return ttdall_cc + 0xC6;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, te7)
  {
    property
    { name: string(STR_TE7_NAME_LONG); }
  }
}

