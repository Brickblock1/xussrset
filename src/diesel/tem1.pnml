 // tem1

// Спрайты

// 2 окраса в зависимости от года выпуска

#define IMAGEFILE  "src/diesel/tem1.png"

// покупка
spriteset (tem1_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(135, 29, -4)
}

// обычный: погрузка/перемещение
spriteset (tem1_loading_spriteset, IMAGEFILE)
{
  s10_template(135, 29, 0)
} 

spriteset (tem1_traveling_spriteset, IMAGEFILE)
{
  s10_template(135, 96, 0)
} 

spritegroup tem1_spritegroup
{
  loading: tem1_loading_spriteset;
  loaded: tem1_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem1_r_loading_spriteset, IMAGEFILE)
{
  s10_template(570, 29, 0)
} 

spriteset (tem1_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(570, 96, 0)
} 

spritegroup tem1_r_spritegroup
{
  loading: tem1_r_loading_spriteset;
  loaded: tem1_r_traveling_spriteset;
}

// покупка
spriteset (tem1_v2_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(135, 163, -4)
}

// обычный: погрузка/перемещение
spriteset (tem1_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(135, 163, 0)
} 

spriteset (tem1_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(135, 230, 0)
} 

spritegroup tem1_v2_spritegroup
{
  loading: tem1_v2_loading_spriteset;
  loaded: tem1_v2_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem1_r_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(570, 163, 0)
} 

spriteset (tem1_r_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(570, 230, 0)
} 

spritegroup tem1_r_v2_spritegroup
{
  loading: tem1_r_v2_loading_spriteset;
  loaded: tem1_r_v2_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem1_get_spriteset2,
  ((last_computed_result - count_veh_id(tem1)) % 2) +
  (build_year >= 1960) * 2
)
{
  0: tem1_spritegroup;
  1: tem1_r_spritegroup;
  2: tem1_v2_spritegroup;
  tem1_r_v2_spritegroup;
}

switch (FEAT_TRAINS, PARENT, tem1_get_spriteset1,
  count_veh_id(tem1))
{
  tem1_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tem1_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tem1_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, tem1_get_purchase_spriteset,
  (build_year >= 1960)
)
{
  0: tem1_purchase_spriteset;
  tem1_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tem1_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tem1_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// увеличиваем скорость до 100 выпущенным после 1965 года
switch (FEAT_TRAINS, SELF, tem1_speed,
  build_year >= 1960)
{
  1: return 100;
  return 90;
}

// уменьшаем массу до 120 выпущенным после 1965 года
switch (FEAT_TRAINS, SELF, tem1_weight,
  build_year >= 1960)
{
  1: return 120;
  return 124;
}

// меняем tec выпущенным после 1965 года из-за смены массы
switch (FEAT_TRAINS, SELF, tem1_tractive_effort_coefficient,
  build_year >= 1960)
{
  1: return 26;
  return 25;
}

// Маневровый всегда штрафует других по скорости
switch (FEAT_TRAINS, SELF, tem1_bitmask_vehicle_info,
  0)
{
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, tem1)
{
  property
  {
    name: string(STR_TEM1_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1958, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 90 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 670 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 124 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.098749177;
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tem1_get_spriteset;
    purchase: tem1_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: tem1_can_attach_wagon;
    start_stop: tem1_start_stop;
    additional_text: return string(STR_TEM1_PURCHASE_HINT);
    speed: tem1_speed;
    weight: tem1_weight;
    tractive_effort_coefficient: tem1_tractive_effort_coefficient;
    bitmask_vehicle_info: tem1_bitmask_vehicle_info;
  }
}
