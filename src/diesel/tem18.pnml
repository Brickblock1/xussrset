 // tem18

#define IMAGEFILE  "src/diesel/tem18.png"
purchase_sprites(tem18, 0, 0)

diesel1r_sprites(10, tem18_v1, 32,  40)
diesel1r_sprites(10, tem18_v2, 32, 241)
diesel1r_sprites(10, tem18_v3, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem18_sprites_left,
  cargo_subtype)
{
  0: tem18_v1_sprites_left;
  1: tem18_v2_sprites_left;
  2: tem18_v3_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, tem18_sprites_right,
  cargo_subtype)
{
  0: tem18_v1_sprites_right;
  1: tem18_v2_sprites_right;
  2: tem18_v3_sprites_right;
     align_10_sprites; 
}

engine_direction_template(tem18, ) 
engine_RND_direction_template(tem18, 1, 1) 
long_vehicle_sp(tem18)

switch (FEAT_TRAINS, SELF, tem18_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_0A);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_TTD_93);
     return CB_RESULT_NO_TEXT;
}

engine_diesel_1r_effect(tem18, 0, 2, 11)
engine_direction_template(tem18, _create_effect) 
engine_RND_effect_direction_template(tem18, 1, 1) 
engine_speed(tem18, 100)
engine_penalise_speed(tem18, 2)
general_running_cost(tem18, 198)

item (FEAT_TRAINS, tem18) {
  property {
    name: string(STR_TEM18_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1992), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1992,2050);
    retire_early: get_retire_early(1992,2050);
    reliability_decay: 10;
    cost_factor: 26;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 820 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(206.0, 126);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tem18_sprites;
    purchase: tem18_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    speed: tem18_speed;
    purchase_speed: tem18_purchase_speed;
    running_cost_factor: tem18_running_cost_factor;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROM, 1992), 
                                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK));
    bitmask_vehicle_info: tem18_bitmask_vehicle_info;
    cargo_subtype_text: tem18_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: tem18_create_effect;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tem18) {
    property { name: string(STR_TEM18_NAME_LONG); }
  }
}
                	