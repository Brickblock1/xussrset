 // tem2

#define IMAGEFILE  "src/diesel/tem2.png"
purchase_sprites(tem2, 0, 0)
diesel1r_sprites(10, tem2_v1,  32,   40)
diesel1r_sprites(10, tem2_v2,  32,  241)
diesel1r_sprites(10, tem2_v3,  32,  442)
diesel1r_sprites(10, tem2_v4,  32,  643)
diesel1r_sprites(10, tem2_v5,  32,  844)
diesel1r_sprites(10, tem2_v6,  32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem2_sprites_green_left,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..2: tem2_v1_sprites_left;
  3..4: tem2_v2_sprites_left;
        tem2_v3_sprites_left;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_green_right,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..2: tem2_v1_sprites_right;
  3..4: tem2_v2_sprites_right;
        tem2_v3_sprites_right;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_blue_left,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: tem2_v4_sprites_left;
     tem2_v5_sprites_left;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_blue_right,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: tem2_v4_sprites_right;
     tem2_v5_sprites_right;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_left,
  cargo_subtype)
{
  0: tem2_sprites_green_left;
  1: tem2_sprites_blue_left;
  2: tem2_v6_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_right,
  cargo_subtype)
{
  0: tem2_sprites_green_right;
  1: tem2_sprites_blue_right;
  2: tem2_v6_sprites_right;
     align_10_sprites;
}

engine_direction_template(tem2, )
engine_RND_direction_template(tem2, 1, 1)
long_vehicle_sp(tem2)

engine_speed(tem2, 100)
engine_penalise_speed(tem2, 2)

switch (FEAT_TRAINS, PARENT, tem2_bitmask_vehicle_info_advanced2,
  ((vehicle_type_id == tem2) || (vehicle_type_id == tem2_all)) && (build_year < 1971))
{
  1: return bitmask(FLAG_PENALISE_SPEED);
     tem2_bitmask_vehicle_info;
}

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_advanced1,
  build_year < 1971)
{
  1: return bitmask(FLAG_PENALISE_SPEED);
     tem2_bitmask_vehicle_info_advanced2;
}

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_advanced,
  (position_in_consist == 0) || (vehicle_is_not_powered) ||
   (position_in_articulated_veh > 0))
{
  1: return 0;
     tem2_bitmask_vehicle_info_advanced1;
}

livery_template1(tem2_alt, TTD_53)
livery_template_alt2(tem2, TTD_96, TTD_B3)
switch (FEAT_TRAINS, SELF, tem2_running_cost_factor,
[  STORE_TEMP((199 + 4 * (build_year >= 1967) - 3 * (build_year >= 1974))*57/100, 0),    // Моторы
   STORE_TEMP((199 + 4 * (build_year >= 1967) - 3 * (build_year >= 1974))*8/100, 1),     // Бригада
   STORE_TEMP((199 + 4 * (build_year >= 1967) - 3 * (build_year >= 1974))*20/100, 2),    // Износ
   STORE_TEMP(0, 3),             // Сопровождение
   STORE_TEMP((199 + 4 * (build_year >= 1967) - 3 * (build_year >= 1974))*14/100, 4),    // ТО
   STORE_TEMP((199 + 4 * (build_year >= 1967) - 3 * (build_year >= 1974))/100, 5),       // Сертификация

   STORE_TEMP(100, 6),       // Скорость
   STORE_TEMP(120, 7),       // Тара
   STORE_TEMP(120, 8)])      // Максимальная масса
{ all_running_cost_factor; } // 199-203-200
engine_TE_change_build_after2(tem2, get_tec_int(347.1, 120),
                                    get_tec_int(347.1, 120), 1967,
                                    get_tec_int(347.1, 120), 1974)
engine_cost_change_build_after2(tem2, 26,
                                      27, 1967,
                                      26, 1974)
engine_power_change_build_after(tem2, 840,
                                      890, 1967)
engine_diesel_1r_effect(tem2, 0, 2, 11)
engine_direction_template(tem2, _create_effect)
engine_RND_effect_direction_template(tem2, 1, 1)

hint_engine(tem2_b1971,
     STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL,
     STR_PURCHASE_HINT_SECTIONS_1,
     1960, 1984,
     STR_PURCHASE_HINT_FACTORY_BRYANSK)
hint_engine(tem2_a1971,
     STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1960, 1984,
     STR_PURCHASE_HINT_FACTORY_BRYANSK)
hint_change_after(tem2, 1971)

item (FEAT_TRAINS, tem2, 341) {
  property {
    name: string(STR_NAME_TEM2);
    vehicle_dates(1960, 1984, 25, 10, 8, 26)
    vehicle_cargo()
    vehicle_diesel(840, 120, 347.1)
  }
  graphics {
    default: tem2_sprites;
    purchase: tem2_purchase_sprites;
    cost_factor: tem2_cost_factor;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    speed: tem2_speed;
    power: tem2_power;
    running_cost_factor: tem2_running_cost_factor;
    additional_text: tem2_additional_text;
    bitmask_vehicle_info: tem2_bitmask_vehicle_info_advanced;
    tractive_effort_coefficient: tem2_tractive_effort_coefficient;
    cargo_subtype_text: tem2_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: tem2_create_effect;
    colour_mapping: any_cc_colour;
    purchase_speed: return 100;
  }
}

long_name_template(tem2, STR_LONGNAME_TEM2)
models_default_cargo_template(tem2)
allow_diesel(tem2)
