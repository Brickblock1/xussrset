// tem2

#define IMAGEFILE  "src/diesel/tem2.png"
purchase_sprites(tem2, 0, 0)
diesel1r_sprites(10, tem2_v1,  32,   40)
diesel1r_sprites(10, tem2_v2,  32,  241)
diesel1r_sprites(10, tem2_v3,  32,  442)
diesel1r_sprites(10, tem2_v4,  32,  643)
diesel1r_sprites(10, tem2_v5,  32,  844)
diesel1r_sprites(10, tem2_v6,  32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem2_sprites_green_left,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..2: tem2_v1_sprites_left;
  3..4: tem2_v2_sprites_left;
        tem2_v3_sprites_left;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_green_right,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..2: tem2_v1_sprites_right;
  3..4: tem2_v2_sprites_right;
        tem2_v3_sprites_right;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_blue_left,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: tem2_v4_sprites_left;
     tem2_v5_sprites_left;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_blue_right,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: tem2_v4_sprites_right;
     tem2_v5_sprites_right;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_left,
  cargo_subtype)
{
  0: tem2_sprites_green_left;
  1: tem2_sprites_blue_left;
  2: tem2_v6_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_right,
  cargo_subtype)
{
  0: tem2_sprites_green_right;
  1: tem2_sprites_blue_right;
  2: tem2_v6_sprites_right;
     align_10_sprites;
}

engine_direction_template(tem2, )
engine_RND_direction_template(tem2, 1, 1)
long_vehicle_sp(tem2)

engine_speed(tem2, 100)
engine_penalise_speed(tem2, 2)

switch (FEAT_TRAINS, PARENT, tem2_bitmask_vehicle_info_advanced2,
  (vehicle_type_id == tem2) && (build_year < 1971))
{
  1: return bitmask(FLAG_PENALISE_SPEED);
     tem2_bitmask_vehicle_info;
}

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_advanced1,
  build_year < 1971)
{
  1: return bitmask(FLAG_PENALISE_SPEED);
     tem2_bitmask_vehicle_info_advanced2;
}

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_advanced,
  (position_in_consist == 0) || (vehicle_is_not_powered))
{
  1: return 0;
     tem2_bitmask_vehicle_info_advanced1;
}

livery_template_alt20(tem2, STR_REFIT_TTD_53, STR_REFIT_TTD_96, STR_REFIT_TTD_B3)
switch (FEAT_TRAINS, SELF, tem2_running_cost_factor,
[  STORE_TEMP(113 + 2 * (build_year >= 1967) - 2 * (build_year >= 1974), 0),  // Моторы
   STORE_TEMP(16, 1),                                                         // Бригада
   STORE_TEMP(40 + (build_year >= 1967) - (build_year >= 1974), 2),           // Износ
   STORE_TEMP(0, 3),                                                          // Сопровождение
   STORE_TEMP(28 + (build_year >= 1967), 4),                                  // ТО
   STORE_TEMP(2, 5),                                                          // Сертификация

   STORE_TEMP(100, 6),            // Скорость
   STORE_TEMP(120, 7),            // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])  // Максимальная масса
{ all_running_cost_factor; }      // 199-203-200
engine_TE_change_build_after2(tem2, get_tec_int(347.1, 120),
                                    get_tec_int(347.1, 120), 1967,
                                    get_tec_int(347.1, 120), 1974)
engine_cost_change_build_after2(tem2, 26,
                                      27, 1967,
                                      26, 1974)
engine_power_change_build_after(tem2, 840,
                                      890, 1967)
engine_diesel_1r_effect(tem2_create_effect_sprites, 0, 2, 11)
engine_direction_template(tem2, _create_effect)
engine_RND_effect_direction_template(tem2, 1, 1)

hint_engine(tem2_b1971,
     STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL,
     STR_PURCHASE_HINT_SECTIONS_1,
     1960, 1984,
     string(STR_PURCHASE_HINT_FACTORY_BRYANSK))
hint_engine(tem2_a1971,
     STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1960, 1984,
     string(STR_PURCHASE_HINT_FACTORY_BRYANSK))
hint_change_after(tem2, 1971)

item (FEAT_TRAINS, tem2, 341) {
  property {
    name: string(STR_NAME_TEM2);
    vehicle_dates(1960, 1984, 25, 10, 8, 26)
    vehicle_cargo()
    vehicle_diesel(840, 120, 347.1)
  }
  graphics {
                additional_text: tem2_additional_text;
               articulated_part: articulated_part_dummy2;
           bitmask_vehicle_info: tem2_bitmask_vehicle_info_advanced;
               can_attach_wagon: any_can_attach_wagon;
                 cargo_capacity: return 0;
             cargo_subtype_text: tem2_cargo_subtype_text;
                 colour_mapping: any_cc_colour;
                    cost_factor: tem2_cost_factor;
                  create_effect: tem2_create_effect;
                        default: tem2_sprites;
                          power: tem2_power;
                       purchase: tem2_purchase_sprites;
                 purchase_speed: return 100;
            running_cost_factor: tem2_running_cost_factor;
                          speed: tem2_speed;
                     start_stop: any_start_stop;
    tractive_effort_coefficient: tem2_tractive_effort_coefficient;
  }
}

long_name_template(tem2, STR_LONGNAME_TEM2)
models_default_cargo_template(tem2)
allow_diesel(tem2)
