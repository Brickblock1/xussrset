 // tem2

// Спрайты

// 2 окраса в зависимости от года выпуска

#define IMAGEFILE  "src/diesel/tem2.png"

// покупка
spriteset (tem2_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(0, 29, -4)
}

// обычный: погрузка/перемещение
spriteset (tem2_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 29, 0)
} 

spriteset (tem2_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 96, 0)
} 

spritegroup tem2_spritegroup
{
  loading: tem2_loading_spriteset;
  loaded: tem2_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem2_r_loading_spriteset, IMAGEFILE)
{
  s10_template(435, 29, 0)
} 

spriteset (tem2_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(435, 96, 0)
} 

spritegroup tem2_r_spritegroup
{
  loading: tem2_r_loading_spriteset;
  loaded: tem2_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem2_get_spriteset2,
  ((last_computed_result - count_veh_id(tem2)) % 2)
)
{
  0: tem2_spritegroup;
  tem2_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, tem2_get_spriteset1,
  count_veh_id(tem2))
{
  tem2_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tem2_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tem2_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tem2_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tem2_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// меняем tec выпущенным после 1965 года из-за смены массы
switch (FEAT_TRAINS, SELF, tem2_tractive_effort_coefficient,
  build_year >= 1974)
{
  1: return 43; //43.53741
  return 45;
}

item (FEAT_TRAINS, tem2)
{
  property
  {
    name: string(STR_TEM2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1968, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 840 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 120 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.17517;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tem2_get_spriteset;
    purchase: tem2_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: tem2_can_attach_wagon;
    start_stop: tem2_start_stop;
    additional_text: return string(STR_TEM2_PURCHASE_HINT);
    tractive_effort_coefficient: tem2_tractive_effort_coefficient;
  }
}
