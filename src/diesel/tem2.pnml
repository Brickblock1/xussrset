 // tem2

#define IMAGEFILE  "src/diesel/tem2.png"
purchase_sprites(tem2, 0, 0)

diesel1r_sprites(10, tem2_v1,  32,   40)
diesel1r_sprites(10, tem2_v2,  32,  241)
diesel1r_sprites(10, tem2_v3,  32,  442)
diesel1r_sprites(10, tem2_v4,  32,  643)
diesel1r_sprites(10, tem2_v5,  32,  844)
diesel1r_sprites(10, tem2_v6,  32, 1045)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, PARENT, tem2_sprites_green_left, 0 )
{
  1: tem2_v1_sprites_left;
  1: tem2_v2_sprites_left;
  1: tem2_v3_sprites_left;
}

random_switch (FEAT_TRAINS, PARENT, tem2_sprites_green_right, 0 )
{
  dependent: tem2_sprites_green_left;
  1: tem2_v1_sprites_right;
  1: tem2_v2_sprites_right;
  1: tem2_v3_sprites_right;
}

random_switch (FEAT_TRAINS, PARENT, tem2_sprites_blue_left, 0 )
{
  dependent: tem2_sprites_green_left;
  1: tem2_v4_sprites_left;
  1: tem2_v5_sprites_left;
}

random_switch (FEAT_TRAINS, PARENT, tem2_sprites_blue_right, 0 )
{
  dependent: tem2_sprites_green_left;
  1: tem2_v4_sprites_right;
  1: tem2_v5_sprites_right;
}

switch (FEAT_TRAINS, SELF, tem2_sprites_left, 
  cargo_subtype)
{
  0: tem2_sprites_green_left;
  1: tem2_sprites_blue_left;
  2: tem2_v6_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, tem2_sprites_right, 
  cargo_subtype)
{
  0: tem2_sprites_green_right;
  1: tem2_sprites_blue_right;
  2: tem2_v6_sprites_right;
     align_10_sprites; 
}

engine_direction_template(tem2, ) 

engine_RND_direction_template(tem2, 1, 1) 

long_vehicle_sp(tem2)

engine_diesel_1_effect(tem2, 0, 2, 11)

engine_direction_template(tem2, _create_effect) 

engine_RND_effect_direction_template(tem2, 1, 1) 

switch (FEAT_TRAINS, SELF, tem2_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_96);
  2: return string(STR_REFIT_TTD_B3);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tem2_tractive_effort_coefficient,
  (build_year >= 1967) +
  (build_year >= 1974))
{
  0: return get_tec_int(211.0, 120);
  1: return get_tec_int(206.0, 120);
     return get_tec_int(200.0, 120);
}

switch (FEAT_TRAINS, SELF, tem2_power,
  build_year >= 1967)
{
  0: return get_power_in_cb(840);
     return get_power_in_cb(890);
}

switch (FEAT_TRAINS, SELF, tem2_cost_factor,
  (build_year >= 1967) +
  (build_year >= 1974))
{
  0: return 26;
  1: return 27;
     return 26;
}

engine_speed(tem2, 100)

engine_penalise_speed(tem2, 2)

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_pre1971,  
  (position_in_consist == 0) || vehicle_is_not_powered)
{ 
  1: return 0;                                         
     return FLAG_PENALISE_SPEED; 
}                     

switch (FEAT_TRAINS, SELF, tem2_bitmask_vehicle_info_year,
  build_year >= 1971)
{
  0: tem2_bitmask_vehicle_info_pre1971;
     tem2_bitmask_vehicle_info;
}

general_running_cost(tem2_v1, 199)
general_running_cost(tem2_v2, 203)
general_running_cost(tem2_v3, 200)

switch (FEAT_TRAINS, SELF, tem2_running_cost_factor,
  (build_year >= 1967) +
  (build_year >= 1974))
{
  0: tem2_v1_running_cost_factor;
  1: tem2_v2_running_cost_factor;
     tem2_v3_running_cost_factor;
}

item (FEAT_TRAINS, tem2) {
  property {
    name: string(STR_TEM2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1960), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1960,1984);
    retire_early: get_retire_early(1960,1984);
    reliability_decay: 10;
    cost_factor: 26;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 840 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 120 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(206.0, 120);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tem2_sprites;
    purchase: tem2_purchase_sprites;
    cost_factor: tem2_cost_factor;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    speed: tem2_speed;
    purchase_speed: tem2_purchase_speed;
    power: tem2_power;
    running_cost_factor: tem2_running_cost_factor;
    additional_text: return string(STR_TEM2_PURCHASE_HINT);
    tractive_effort_coefficient: tem2_tractive_effort_coefficient;
    bitmask_vehicle_info: tem2_bitmask_vehicle_info_year;
    cargo_subtype_text: tem2_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: tem2_create_effect;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tem2) {
    property { name: string(STR_TEM2_NAME_LONG); }
  }
}

