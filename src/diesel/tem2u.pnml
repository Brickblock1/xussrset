 // tem2u

// Спрайты

// 2 окраса в зависимости от года выпуска

#define IMAGEFILE  "src/diesel/tem2u.png"

// покупка
spriteset (tem2u_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// Окрас 1

// обычный: погрузка/перемещение
spriteset (tem2u_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 29, 0)
} 

spriteset (tem2u_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 96, 0)
} 

spritegroup tem2u_spritegroup
{
  loading: tem2u_loading_spriteset;
  loaded: tem2u_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem2u_r_loading_spriteset, IMAGEFILE)
{
  s10_template(435, 29, 0)
} 

spriteset (tem2u_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(435, 96, 0)
} 

spritegroup tem2u_r_spritegroup
{
  loading: tem2u_r_loading_spriteset;
  loaded: tem2u_r_traveling_spriteset;
}

// Окрас 2

// обычный: погрузка/перемещение
spriteset (tem2u_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 191, 0)
} 

spriteset (tem2u_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 258, 0)
} 

spritegroup tem2u_v2_spritegroup
{
  loading: tem2u_v2_loading_spriteset;
  loaded: tem2u_v2_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem2u_v2_r_loading_spriteset, IMAGEFILE)
{
  s10_template(435, 191, 0)
} 

spriteset (tem2u_v2_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(435, 258, 0)
} 

spritegroup tem2u_v2_r_spritegroup
{
  loading: tem2u_v2_r_loading_spriteset;
  loaded: tem2u_v2_r_traveling_spriteset;
}


#undef IMAGEFILE

// Окрас 1

switch (FEAT_TRAINS, SELF, tem2u_get_spriteset2,
  ((last_computed_result - count_veh_id(tem2u)) % 2)
)
{
  0: tem2u_spritegroup;
  tem2u_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, tem2u_get_spriteset1,
  count_veh_id(tem2u))
{
  tem2u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tem2u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tem2u_get_spriteset1;
}

// Окрас 2

switch (FEAT_TRAINS, SELF, tem2u_v2_get_spriteset2,
  ((last_computed_result - count_veh_id(tem2u)) % 2)
)
{
  0: tem2u_v2_spritegroup;
  tem2u_v2_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, tem2u_v2_get_spriteset1,
  count_veh_id(tem2u))
{
  tem2u_v2_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tem2u_v2_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tem2u_v2_get_spriteset1;
}

// Выбор окраса

switch (FEAT_TRAINS, SELF, tem2u_get_spritegroup, cargo_subtype)
{
  0: tem2u_get_spriteset;
  1: tem2u_v2_get_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, SELF, tem2u_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_V1);
  1: string(STR_REFIT_FACTORY_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, PARENT, tem2u_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Маневровый всегда штрафует других по скорости
switch (FEAT_TRAINS, SELF, tem2u_bitmask_vehicle_info,
  0)
{
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, tem2u)
{
  property
  {
    name: string(STR_TEM2U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1984), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1984,1989);
    retire_early: get_retire_early(1984,1989); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 38;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 840 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 124 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(210.0, 124);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: tem2u_get_spritegroup;
    purchase: tem2u_purchase_spriteset;
    purchase_speed: return 100;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: tem2u_start_stop;
    additional_text: return string(STR_TEM2U_PURCHASE_HINT);
    bitmask_vehicle_info: tem2u_bitmask_vehicle_info;
    cargo_subtype_text: tem2u_cargo_subtype_text;
    cargo_capacity: return 0;
  }
}
