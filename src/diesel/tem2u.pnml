 // tem2u

// Спрайты

// 2 окраса в зависимости от года выпуска

#define IMAGEFILE  "src/diesel/tem2u.png"

// покупка
spriteset (tem2u_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(0, 29, -4)
}

// обычный: погрузка/перемещение
spriteset (tem2u_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 29, 0)
} 

spriteset (tem2u_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 96, 0)
} 

spritegroup tem2u_spritegroup
{
  loading: tem2u_loading_spriteset;
  loaded: tem2u_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tem2u_r_loading_spriteset, IMAGEFILE)
{
  s10_template(435, 29, 0)
} 

spriteset (tem2u_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(435, 96, 0)
} 

spritegroup tem2u_r_spritegroup
{
  loading: tem2u_r_loading_spriteset;
  loaded: tem2u_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem2u_get_spriteset2,
  ((last_computed_result - count_veh_id(tem2u)) % 2)
)
{
  0: tem2u_spritegroup;
  tem2u_r_spritegroup;
}

switch (FEAT_TRAINS, PARENT, tem2u_get_spriteset1,
  count_veh_id(tem2u))
{
  tem2u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tem2u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tem2u_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tem2u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tem2u_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, tem2u)
{
  property
  {
    name: string(STR_TEM2U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1984, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
//    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 840 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 124 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.172811;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
  }
  graphics {
    default: tem2u_get_spriteset;
    purchase: tem2u_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: tem2u_can_attach_wagon;
    start_stop: tem2u_start_stop;
    additional_text: return string(STR_TEM2U_PURCHASE_HINT);
  }
}
