 // tem7
// 404 (194h) 405 (195h)

#define IMAGEFILE  "src/diesel/tem7.png"
purchase_sprites(tem7, 0, 0)

diesel1r_sprites(12, tem7_v1, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tem7a.png"
purchase_sprites(tem7a_v1,   0, 0)
purchase_sprites(tem7a_v2, 100, 0)
purchase_sprites(tem7a_v3, 200, 0)

diesel1r_sprites(12, tem7a_v1, 32,  40)
diesel1r_sprites(12, tem7a_v2, 32, 241)
diesel1r_sprites(12, tem7a_v3, 32, 442)
diesel1r_sprites(12, tem7a_v4, 32, 643)
diesel1r_sprites(12, tem7a_v5, 32, 844)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tem7a_purchase_sprites,
  (current_year >= 2008) +  
  (current_year >= 2012))
{
  0: tem7a_v1_purchase_sprites;
  1: tem7a_v2_purchase_sprites;
     tem7a_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tem7a_sprites_left,
  ((build_year >= 2008) +
  (build_year >= 2012)) * 10 +
  cargo_subtype)
{
   0: tem7a_v1_sprites_left;
   1: tem7a_v2_sprites_left;
  10: tem7a_v3_sprites_left;
  11: tem7a_v4_sprites_left;
  12: tem7a_v5_sprites_left;
  20: tem7a_v5_sprites_left;
  21: tem7a_v3_sprites_left;
      align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, tem7a_sprites_right, 
  ((build_year >= 2008) +
  (build_year >= 2012)) * 10 +
  cargo_subtype)
{
   0: tem7a_v1_sprites_right;
   1: tem7a_v2_sprites_right;
  10: tem7a_v3_sprites_right;
  11: tem7a_v4_sprites_right;
  12: tem7a_v5_sprites_right;
  20: tem7a_v5_sprites_right;
  21: tem7a_v3_sprites_right;
      align_10_sprites; 
}

switch (FEAT_TRAINS, PARENT, tem7_sprites_left, 
  (models_details_level == 0) && (build_year >= 1988))        
{
  1: tem7a_sprites_left;
     tem7_v1_sprites_left;
}

switch (FEAT_TRAINS, PARENT, tem7_sprites_right, 
  (models_details_level == 0) && (build_year >= 1988))        
{
  1: tem7a_sprites_right;
     tem7_v1_sprites_right;
}

engine_direction_template(tem7, ) 
engine_RND_direction_template(tem7, 1, 3) 
long_vehicle_sp(tem7)

engine_diesel_1r_effect(tem7, -1, 5, 12) 
engine_direction_template(tem7, _create_effect) 
engine_RND_effect_direction_template(tem7, 1, 3) 
engine_speed(tem7, 100)
engine_penalise_speed(tem7, 2)
general_running_cost(tem7, 411)

item (FEAT_TRAINS, tem7, 404) {
  property {
    name: string(STR_NAME_TEM7);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1975), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1975,1989);
    retire_early: get_retire_early(1975,1989);
    reliability_decay: 10;
    cost_factor: 54;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1330 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 180 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(580.0, 180);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tem7_sprites;
    purchase: tem7_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1975, 1989), 
                                   string(STR_PURCHASE_HINT_FACTORY_LYUDINOVSK));
    speed: tem7_speed;
    purchase_speed: tem7_purchase_speed;
    running_cost_factor: tem7_running_cost_factor;
    bitmask_vehicle_info: tem7_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: tem7_create_effect;
  }
}

long_name_template(tem7, STR_LONGNAME_TEM7)
models_default_cargo_template(tem7)
allow_diesel(tem7)
// tem7a

engine_direction_template(tem7a, ) 
engine_RND_direction_template(tem7a, 1, 3) 
long_vehicle_sp(tem7a)

livery_template(tem7a_b2008, FACTORY, LV_RZD_PID, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)

livery_template(tem7a_a2008, FACTORY, LV_RZD_PID, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(tem7a_a2008, RZD_TCHE_23_BEKASOVO_SORTIROVOCHNOE)

livery_template(tem7a_a2012, RZD_PID, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(tem7a_a2012, TTD_93)

switch (FEAT_TRAINS, SELF, tem7a_cargo_subtype_text,
  (build_year >= 2008) +
  (build_year >= 2012))
{
  0: tem7a_b2008_cargo_subtype_text;
  1: tem7a_a2008_cargo_subtype_text_pre;
     tem7a_a2012_cargo_subtype_text_pre;
}

engine_diesel_1r_effect(tem7a, -1, 5, 12) 
engine_direction_template(tem7a, _create_effect) 
engine_RND_effect_direction_template(tem7a, 1, 3) 
engine_speed(tem7a, 100)
engine_penalise_speed(tem7a, 2)
general_running_cost(tem7a, 422)

item (FEAT_TRAINS, tem7a, 405) {
  property {
    name: string(STR_NAME_TEM7A);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1988), 1, 1);
    vehicle_life: 25;
    model_life: get_model_life(1988,2050);
    retire_early: get_retire_early(1988,2050);
    reliability_decay: 10;
    cost_factor: 56;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_CARGO;
    power: 1360 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 180 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(583.0, 180);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tem7a_sprites;
    purchase: tem7a_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROM, 1988), 
                                   string(STR_PURCHASE_HINT_FACTORY_LYUDINOVSK));
    speed: tem7a_speed;
    purchase_speed: tem7a_purchase_speed;
    running_cost_factor: tem7a_running_cost_factor;
    bitmask_vehicle_info: tem7a_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: tem7a_create_effect;
    cargo_subtype_text: tem7a_cargo_subtype_text;
  }
}

long_name_template(tem7a, STR_LONGNAME_TEM7A)
models_default_cargo_template(tem7a)
allow_diesel(tem7a)
// TEM7 ALL

switch (FEAT_TRAINS, SELF, tem7_all_purchase_sprites,
  current_year >= 1988)
{
  0: tem7_purchase_sprites;
     tem7a_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tem7_all_additional_text,
  build_year >= 1988)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1975, 1989), 
                   string(STR_PURCHASE_HINT_FACTORY_LYUDINOVSK));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL_WITH_SUBTYPE, string(STR_NAME_TEM7A)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROM, 1988), 
                   string(STR_PURCHASE_HINT_FACTORY_LYUDINOVSK));
}

switch (FEAT_TRAINS, SELF, tem7_all_running_cost_factor,
  build_year >= 1988)
{
  0: tem7_running_cost_factor;
     tem7a_running_cost_factor;
}

engine_power_change_build_after(tem7_all, get_power_in_cb(1330), 
                                          get_power_in_cb(1360), 1988)       
engine_TE_change_build_after(tem7_all, get_tec_int(290.0, 180), 
                                       get_tec_int(300.0, 180), 1988)         
engine_cost_change_build_after(tem7_all, 54, 
                                         56, 1988)       
engine_penalise_speed_build_after(tem7, 1988)

if (models_details_level == 0) {
  item (FEAT_TRAINS, tem7a) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, tem7) {
    property {
      introduction_date: date(get_loc_year(1975), 1, 1);
      model_life: get_model_life(1975,2050);
      retire_early: get_retire_early(1975,2050);
    }
    graphics {
      default: tem7_sprites;
      purchase: tem7_all_purchase_sprites;
      articulated_part: articulated_part_dummy4;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: any_start_stop;
      additional_text: tem7_all_additional_text;
      cargo_subtype_text: tem7a_cargo_subtype_text;
      cargo_capacity: return 0;
      speed: tem7_speed;
      power: tem7_all_power;
      tractive_effort_coefficient: tem7_all_tractive_effort_coefficient;
      purchase_speed: tem7_purchase_speed;
      cost_factor: tem7_all_cost_factor;
      running_cost_factor: tem7_all_running_cost_factor;
      bitmask_vehicle_info: tem7_bitmask_vehicle_info_change;
      create_effect: tem7_create_effect;
    }
  }
}
allow_diesel(tem7)
