 // tep10

// Спрайты

#define IMAGEFILE  "src/diesel/tep10.png"

// покупка
purchase_sprites(tep10_v1, 485, 90)

// обычный: погрузка/перемещение
spriteset (tep10_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (tep10_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup tep10_spritegroup
{
  loading: tep10_loading_spriteset;
  loaded: tep10_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tep10-s1972.png"

// покупка
purchase_sprites(tep10_v2, 485, 90)

// обычный: погрузка/перемещение
spriteset (tep10_v2_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (tep10_v2_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup tep10_v2_spritegroup
{
  loading: tep10_v2_loading_spriteset;
  loaded: tep10_v2_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v2_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10_v3_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 151, 0)
} 

spriteset (tep10_v3_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 217, 0)
} 

spritegroup tep10_v3_spritegroup
{
  loading: tep10_v3_loading_spriteset;
  loaded: tep10_v3_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v3_spriteset, IMAGEFILE)
{
  s11_template(423, 151, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10_v4_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 288, 0)
} 

spriteset (tep10_v4_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 354, 0)
} 

spritegroup tep10_v4_spritegroup
{
  loading: tep10_v4_loading_spriteset;
  loaded: tep10_v4_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v4_spriteset, IMAGEFILE)
{
  s11_template(423, 288, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep10_sprites_start,
  (position_in_consist > 0) +
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 2) +
  cargo_subtype * 2)
{
  0: tep10_spritegroup;
  1: tep10_r_spriteset;
  2: tep10_v2_spritegroup;
  3: tep10_r_v2_spriteset;
  4: tep10_v3_spritegroup;
  5: tep10_r_v3_spriteset;
  6: tep10_v4_spritegroup;
     tep10_r_v4_spriteset;
}

long_vehicle(tep10)

// покупка
switch (FEAT_TRAINS, SELF, tep10_purchase_sprites,
  current_year >= 1972)
{
  0: tep10_v1_purchase_sprites;
     tep10_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tep10_cargo_subtype_text2,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_93);
  1: string(STR_REFIT_TTD_54);
  2: string(STR_REFIT_TTD_B5);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10_cargo_subtype_text,
  build_year >= 1972)
{
  1: tep10_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(tep10, 140, 120, 20)

engine_penalise_speed(tep10, 2)

item (FEAT_TRAINS, tep10) 
{
  property
  {
    name: string(STR_TEP10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1960), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1960,1968);
    retire_early: get_retire_early(1960,1968);
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 51;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2290 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(169.0, 129);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tep10_sprites;
    purchase: tep10_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TEP10_PURCHASE_HINT);
    cargo_subtype_text: tep10_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep10_speed;
    purchase_speed: tep10_purchase_speed;
    bitmask_vehicle_info: tep10_bitmask_vehicle_info;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, tep10)
  {
    property
    { name: string(STR_TEP10_NAME_LONG); }
  }
}

