 // tep10

// Спрайты

#define IMAGEFILE  "src/diesel/tep10-1960.png"

// покупка
spriteset (tep10_purchase_spriteset, IMAGEFILE)
{
  s11_purchase_template(4, 14, -6)
}

// обычный: погрузка/перемещение
spriteset (tep10_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (tep10_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup tep10_spritegroup
{
  loading: tep10_loading_spriteset;
  loaded: tep10_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tep10-1972.png"

// покупка
spriteset (tep10_v2_purchase_spriteset, IMAGEFILE)
{
  s11_purchase_template(4, 14, -6)
}

// обычный: погрузка/перемещение
spriteset (tep10_v2_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 14, 0)
} 

spriteset (tep10_v2_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 80, 0)
} 

spritegroup tep10_v2_spritegroup
{
  loading: tep10_v2_loading_spriteset;
  loaded: tep10_v2_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v2_spriteset, IMAGEFILE)
{
  s11_template(423, 14, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10_v3_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 151, 0)
} 

spriteset (tep10_v3_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 217, 0)
} 

spritegroup tep10_v3_spritegroup
{
  loading: tep10_v3_loading_spriteset;
  loaded: tep10_v3_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v3_spriteset, IMAGEFILE)
{
  s11_template(423, 151, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10_v4_loading_spriteset, IMAGEFILE)
{
  s11_template(4, 288, 0)
} 

spriteset (tep10_v4_traveling_spriteset, IMAGEFILE)
{
  s11_template(4, 354, 0)
} 

spritegroup tep10_v4_spritegroup
{
  loading: tep10_v4_loading_spriteset;
  loaded: tep10_v4_traveling_spriteset;
}

// развёрнутый
spriteset (tep10_r_v4_spriteset, IMAGEFILE)
{
  s11_template(423, 288, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep10_get_spriteset2,
  ((last_computed_result - count_veh_id(tep10)) % 2) +
  ((current_year >= 1972) * 2) +
  cargo_subtype * 2
)
{
  0: tep10_spritegroup;
  1: tep10_r_spriteset;
  2: tep10_v2_spritegroup;
  3: tep10_r_v2_spriteset;
  4: tep10_v3_spritegroup;
  5: tep10_r_v3_spriteset;
  6: tep10_v4_spritegroup;
  tep10_r_v4_spriteset;
}

switch (FEAT_TRAINS, PARENT, tep10_get_spriteset1,
  count_veh_id(tep10))
{
  tep10_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep10_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep10_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, tep10_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: tep10_purchase_spriteset;
  tep10_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tep10_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tep10_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10_cargo_subtype_text2,
  current_year >= 1972)
{
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: tep10_cargo_subtype_text2;
  2: string(STR_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 140 км/ч, после 20 лет - 120 км/ч
switch (FEAT_TRAINS, SELF, tep10_purchase_speed,
  140)
{
  return;
}

switch (FEAT_TRAINS, SELF, tep10_speed,
[
  STORE_TEMP((age_in_days >= (20*365+5)) ? 120 : 140, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

switch (FEAT_TRAINS, SELF, tep10_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tep10), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tep10_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tep10), 0),
  vehicle_type_id == tep10
])
{
  tep10_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tep10_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tep10_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tep10) 
{
  property
  {
    name: string(STR_TEP10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1960, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2290 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.1336813795;
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tep10_get_spriteset;
    purchase: tep10_get_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: tep10_can_attach_wagon;
    start_stop: tep10_start_stop;
    additional_text: return string(STR_TEP10_PURCHASE_HINT);
    cargo_subtype_text: tep10_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep10_speed;
    purchase_speed: tep10_purchase_speed;
    bitmask_vehicle_info: tep10_bitmask_vehicle_info;
  }
}
