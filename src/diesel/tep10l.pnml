 // tep10l

// Спрайты

#define IMAGEFILE  "src/diesel/tep10l-1964.png"

// покупка
purchase_spriteset(tep10l, 485, 90)

// обычный: погрузка/перемещение
spriteset (tep10l_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (tep10l_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup tep10l_spritegroup
{
  loading: tep10l_loading_spriteset;
  loaded: tep10l_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10l_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (tep10l_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup tep10l_v2_spritegroup
{
  loading: tep10l_v2_loading_spriteset;
  loaded: tep10l_v2_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tep10l-1972.png"

// 4: покупка
purchase_spriteset(tep10l_v3, 485, 90)

// обычный: погрузка/перемещение
spriteset (tep10l_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (tep10l_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup tep10l_v3_spritegroup
{
  loading: tep10l_v3_loading_spriteset;
  loaded: tep10l_v3_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep10l_get_spriteset_l,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 2) +
  cargo_subtype * (LOAD_TEMP(2) < date(1972,1,1))
)
{
  0: tep10l_spritegroup;
  1: tep10l_v2_spritegroup;
     tep10l_v3_spritegroup;
}

switch (FEAT_TRAINS, SELF, tep10l_get_spriteset_r,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 2) +
  cargo_subtype * (LOAD_TEMP(2) < date(1972,1,1))
)
{
  0: tep10l_r_spriteset;
  1: tep10l_r_v2_spriteset;
     tep10l_r_v3_spriteset;
}

engine_direction_template(tep10l) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep10l_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep10l_get_spriteset_t;
}

// покупка
switch (FEAT_TRAINS, SELF, tep10l_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: tep10l_purchase_spriteset;
  tep10l_v3_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text2,
  current_year >= 1972)
{
  0: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: tep10l_cargo_subtype_text2;
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 140 км/ч, после 20 лет - 120 км/ч
#define MAX_SPEED 140

switch (FEAT_TRAINS, SELF, tep10l_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, tep10l_speed,
[
  STORE_TEMP(get_max_speed(20, 120, MAX_SPEED), 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}
#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, tep10l_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tep10l), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tep10l_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tep10l), 0),
  vehicle_type_id == tep10l
])
{
  tep10l_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tep10l_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tep10l_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tep10l) 
{
  property
  {
    name: string(STR_TEP10L_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    vehicle_life: 20; // срок службы
    model_life: get_model_life(1964,1967);
    retire_early: get_retire_early(1964,1967); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2290 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 128 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(169.0, 128);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tep10l_get_spriteset;
    purchase: tep10l_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TEP10L_PURCHASE_HINT);
    cargo_subtype_text: tep10l_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep10l_speed;
    purchase_speed: tep10l_purchase_speed;
    bitmask_vehicle_info: tep10l_bitmask_vehicle_info;
    cost_factor: return 91;
  }
}
