 // tep10l

#define IMAGEFILE  "src/diesel/tep10l.png"
purchase_sprites(tep10l_v1,   0, 0)
purchase_sprites(tep10l_v2, 100, 0)

diesel2_sprites(10, tep10l_v1, 32,  40)
diesel2_sprites(10, tep10l_v2, 32, 241)
diesel2_sprites(10, tep10l_v3, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep10l_purchase_sprites,
  (current_year >= 1972) )
{
  0: tep10l_v1_purchase_sprites;
     tep10l_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tep10l_sprites_left,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 2) +
  cargo_subtype * (LOAD_TEMP(2) < date(1972,1,1)))
{
  0: tep10l_v1_sprites_left;
  1: tep10l_v2_sprites_left;
  2: tep10l_v3_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, tep10l_sprites_right,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 2) +
  cargo_subtype * (LOAD_TEMP(2) < date(1972,1,1)) )
{
  0: tep10l_v1_sprites_right;
  1: tep10l_v2_sprites_right;
  2: tep10l_v3_sprites_right;
     align_10_sprites;
}

engine_direction_template(tep10l, ) 

long_vehicle(tep10l)

engine_diesel_1_effect(tep10l, 2, 0, 11)

engine_direction_template(tep10l, _create_effect) 

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_9D);
  1: return string(STR_REFIT_TTD_54);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text,
  build_year >= 1972)
{
  0: tep10l_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(tep10l, 140, 120, 20)

engine_penalise_speed(tep10l, 2)

general_running_cost(tep10l, 459)

item (FEAT_TRAINS, tep10l) {
  property {
    name: string(STR_TEP10L_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    vehicle_life: 20;
    model_life: get_model_life(1964,1967);
    retire_early: get_retire_early(1964,1967);
    reliability_decay: 1;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 47;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2290 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 128 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(169.0, 128);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tep10l_sprites;
    purchase: tep10l_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TEP10L_PURCHASE_HINT);
    cargo_subtype_text: tep10l_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep10l_speed;
    purchase_speed: tep10l_purchase_speed;
    running_cost_factor: tep10l_running_cost_factor;
    bitmask_vehicle_info: tep10l_bitmask_vehicle_info;
    create_effect: tep10l_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tep10l) {
    property { name: string(STR_TEP10L_NAME_LONG); }
  }
}

