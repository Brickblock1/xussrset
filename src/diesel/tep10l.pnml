 // tep10l

// Спрайты

#define IMAGEFILE  "src/diesel/tep10l-1964.png"

// покупка
spriteset (tep10l_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(4, 16, 2)
}

// обычный: погрузка/перемещение
spriteset (tep10l_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (tep10l_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup tep10l_spritegroup
{
  loading: tep10l_loading_spriteset;
  loaded: tep10l_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

// обычный: погрузка/перемещение
spriteset (tep10l_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 153, 0)
} 

spriteset (tep10l_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 219, 0)
} 

spritegroup tep10l_v2_spritegroup
{
  loading: tep10l_v2_loading_spriteset;
  loaded: tep10l_v2_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_v2_spriteset, IMAGEFILE)
{
  s10_template(423, 153, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tep10l-1972.png"

// 4: покупка
spriteset (tep10l_v3_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(4, 16, 2)
}

// обычный: погрузка/перемещение
spriteset (tep10l_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(4, 16, 0)
} 

spriteset (tep10l_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(4, 82, 0)
} 

spritegroup tep10l_v3_spritegroup
{
  loading: tep10l_v3_loading_spriteset;
  loaded: tep10l_v3_traveling_spriteset;
}

// развёрнутый
spriteset (tep10l_r_v3_spriteset, IMAGEFILE)
{
  s10_template(423, 16, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep10l_get_spriteset2,
  ((last_computed_result - count_veh_id(tep10l)) % 2) +
  ((current_year >= 1972) * 4) +
  cargo_subtype * 2 * (current_year < 1972)
)
{
  0: tep10l_spritegroup;
  1: tep10l_r_spriteset;
  2: tep10l_v2_spritegroup;
  3: tep10l_r_v2_spriteset;
  4: tep10l_v3_spritegroup;
  tep10l_r_v3_spriteset;
}

switch (FEAT_TRAINS, PARENT, tep10l_get_spriteset1,
  count_veh_id(tep10l))
{
  tep10l_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep10l_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep10l_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, tep10l_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: tep10l_purchase_spriteset;
  tep10l_v3_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tep10l_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tep10l_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text2,
  cargo_subtype)
{
  0: string(STR_TEP10L_REFIT_V1);
  1: string(STR_TEP10L_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep10l_cargo_subtype_text,
  current_year >= 1972)
{
  0: tep10l_cargo_subtype_text2;
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, tep10l) 
{
  property
  {
    name: string(STR_TEP10L_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1964, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
//    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2290 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 1;
    weight: 128 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.10283801;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
  }
  graphics {
    default: tep10l_get_spriteset;
    purchase: tep10l_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: tep10l_can_attach_wagon;
    start_stop: tep10l_start_stop;
    additional_text: return string(STR_TEP10L_PURCHASE_HINT);
    speed: speed_after20_140to120;
    cargo_subtype_text: tep10l_cargo_subtype_text;
    cargo_capacity: return 0;
  }
}
