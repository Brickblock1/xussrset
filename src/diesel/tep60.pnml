 // tep60

// 1961-64
// 1964-65, 3 варианта
// 1965-69, 2 варианта
// 1970, 2 варианта
#define IMAGEFILE  "src/diesel/tep60.png"
purchase_sprites(tep60_v1,   0, 0)
purchase_sprites(tep60_v2, 100, 0)
purchase_sprites(tep60_v3, 200, 0)
purchase_sprites(tep60_v4, 300, 0)

diesel1r_sprites(11, tep60_v1, 32,   40)
diesel1r_sprites(11, tep60_v2, 32,  241)
diesel1r_sprites(11, tep60_v3, 32,  442)
diesel1r_sprites(11, tep60_v4, 32,  643)
diesel1r_sprites(11, tep60_v5, 32,  844)
diesel1r_sprites(11, tep60_v6, 32, 1045)
diesel1r_sprites(11, tep60_v7, 32, 1246)
diesel1r_sprites(11, tep60_v8, 32, 1447)
diesel1r_sprites(11, tep60_v9, 32, 1648)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep60_purchase_sprites,
  (current_year >= 1964) +
  (current_year >= 1966) +
  (current_year >= 1970) )
{
  0: tep60_v1_purchase_sprites;
  1: tep60_v2_purchase_sprites;
  2: tep60_v3_purchase_sprites;
     tep60_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tep60_sprites_left,
  ((build_year >= 1964)) +
  ((build_year >= 1966) * 3) +
  ((build_year >= 1970) * 2) +
  cargo_subtype)
{
  0: tep60_v1_sprites_left;
  1: tep60_v2_sprites_left;  
  2: tep60_v3_sprites_left;
  3: tep60_v4_sprites_left;
  4: tep60_v5_sprites_left;  
  5: tep60_v6_sprites_left;
  6: tep60_v7_sprites_left;  
  7: tep60_v8_sprites_left;
  8: tep60_v9_sprites_left;
     align_11_sprites; 
}

switch (FEAT_TRAINS, SELF, tep60_sprites_right,
  ((build_year >= 1964)) +
  ((build_year >= 1966) * 3) +
  ((build_year >= 1970) * 2) +
  cargo_subtype)
{
  0: tep60_v1_sprites_right;     
  1: tep60_v2_sprites_right;
  2: tep60_v3_sprites_right;
  3: tep60_v4_sprites_right;
  4: tep60_v5_sprites_right;
  5: tep60_v6_sprites_right;
  6: tep60_v7_sprites_right;
  7: tep60_v8_sprites_right;
  8: tep60_v9_sprites_right;
     align_11_sprites; 
}

engine_direction_template(tep60, ) 
engine_RND_direction_template(tep60, 5, 3) 
long_vehicle_sp(tep60)

switch (FEAT_TRAINS, SELF, tep60_cargo_subtype_text_a1991,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_B5);
  1: return string(STR_REFIT_TTD_7C);
  2: return string(STR_REFIT_BELARUSSIAN_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep60_cargo_subtype_text_a1965,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_B5);
  1: return string(STR_REFIT_TTD_7C);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep60_cargo_subtype_text_a1964,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_9C);
  1: return string(STR_REFIT_TTD_B6);
  2: return string(STR_REFIT_TTD_B5);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep60_cargo_subtype_text,
  (build_year >= 1964) +
  (build_year >= 1965) +
  (build_year >= 1991) )
{
  0: return CB_RESULT_NO_TEXT;
  1: tep60_cargo_subtype_text_a1964;
  2: tep60_cargo_subtype_text_a1965;
     tep60_cargo_subtype_text_a1991;
}

switch (FEAT_TRAINS, SELF, tep60_additional_text,
  (current_year >= 1964) +
  (current_year >= 1965) )
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1987), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   1);
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1987), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   3);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1987), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   2);
}

engine_diesel_1r_effect(tep60, 3, 0, 12)
engine_direction_template(tep60, _create_effect) 
engine_RND_effect_direction_template(tep60, 5, 3) 
engine_speed_with_reduce(tep60, 160, 130, 20)
engine_penalise_speed(tep60, 2)
general_running_cost(tep60, 419)

item (FEAT_TRAINS, tep60) {
  property {
    name: string(STR_TEP60_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1960), 1, 1);
    vehicle_life: 20;
    model_life: get_model_life(1960,1987);
    retire_early: get_retire_early(1960,1987);
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 43;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2310 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 127 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(123.0, 127);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tep60_sprites;
    purchase: tep60_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: tep60_additional_text;
    cargo_subtype_text: tep60_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep60_speed;
    purchase_speed: tep60_purchase_speed;
    running_cost_factor: tep60_running_cost_factor;
    bitmask_vehicle_info: tep60_bitmask_vehicle_info;
    create_effect: tep60_create_effect;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tep60) {
    property { name: string(STR_TEP60_NAME_LONG); }
  }
}

