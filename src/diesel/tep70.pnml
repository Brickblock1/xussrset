 // tep70

#define IMAGEFILE  "src/diesel/tep70.png"
purchase_sprites(tep70_v1,   0, 0)
purchase_sprites(tep70_v2, 100, 0)

diesel1r_sprites(13, tep70_v1,  32,   40)
diesel1r_sprites(13, tep70_v2,  32,  241)
diesel1r_sprites(13, tep70_v3,  32,  442)
diesel1r_sprites(13, tep70_v4,  32,  643)
diesel1r_sprites(13, tep70_v5,  32,  844)
diesel1r_sprites(13, tep70_v6,  32, 1045)
diesel1r_sprites(13, tep70_v7,  32, 1246)
diesel1r_sprites(13, tep70_v8,  32, 1447)
diesel1r_sprites(13, tep70_v9,  32, 1648)
diesel1r_sprites(13, tep70_v10, 32, 1849)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70_purchase_sprites,
  current_year >= 1978 )
{
  0: tep70_v1_purchase_sprites;
     tep70_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tep70_sprites_left,
  ((build_year >= 1978)) +
  ((build_year >= 1990) * 4) +
  cargo_subtype)
{
  0: tep70_v1_sprites_left;
  1: tep70_v2_sprites_left;
  2: tep70_v3_sprites_left;
  3: tep70_v4_sprites_left;
  4: tep70_v5_sprites_left;
  5: tep70_v6_sprites_left;
  6: tep70_v7_sprites_left;
  7: tep70_v8_sprites_left;
  8: tep70_v9_sprites_left;
  9: tep70_v10_sprites_left;
     align_13_sprites;
}

switch (FEAT_TRAINS, SELF, tep70_sprites_right,
  ((build_year >= 1978)) +
  ((build_year >= 1990) * 4) +
  cargo_subtype)
{
  0: tep70_v1_sprites_right;
  1: tep70_v2_sprites_right;
  2: tep70_v3_sprites_right;
  3: tep70_v4_sprites_right;
  4: tep70_v5_sprites_right;
  5: tep70_v6_sprites_right;
  6: tep70_v7_sprites_right;
  7: tep70_v8_sprites_right;
  8: tep70_v9_sprites_right;
  9: tep70_v10_sprites_right;
     align_13_sprites;
}

engine_direction_template(tep70, ) 

engine_RND_direction_template(tep70, 5, 3) 

long_vehicle_sp(tep70)

engine_diesel_1_effect(tep70, 2, 3, 13)

engine_direction_template(tep70, _create_effect) 

engine_RND_effect_direction_template(tep70, 5, 3) 

switch (FEAT_TRAINS, SELF, tep70_additional_text,
  current_year >= 1978 )
{
  1: return string(STR_TEP70_PURCHASE_HINT);
     return string(STR_TEP70_PURCHASE_HINT_PRE);
}

switch (FEAT_TRAINS, SELF, tep70_cargo_subtype_text2,
  cargo_subtype ) 
{
  0: return string(STR_REFIT_OCTOBER_RAILWAY);
  1: return string(STR_REFIT_MOSCOW_RAILWAY, "");
  2: return string(STR_REFIT_BELARUSSIAN_RAILWAY);
  3: return string(STR_REFIT_NORTHERN_RAILWAY);
  4: return string(STR_REFIT_RZD_PID);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70_cargo_subtype_text1,
  cargo_subtype ) 
{
  0: return string(STR_REFIT_OCTOBER_RAILWAY);
  1: return string(STR_REFIT_MOSCOW_RAILWAY, "");
  2: return string(STR_REFIT_BELARUSSIAN_RAILWAY);
  3: return string(STR_REFIT_NORTHERN_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70_cargo_subtype_text,
  (build_year >= 1978) + (build_year >= 1990))
{
  0: return string(STR_REFIT_PREPRODUCTION);
  1: tep70_cargo_subtype_text1;
     tep70_cargo_subtype_text2;
}

switch (FEAT_TRAINS, SELF, tep70_power,
  build_year >= 1978)
{
  1: return get_power_in_cb(3145);
     return get_power_in_cb(3020);
}

switch (FEAT_TRAINS, SELF, tep70_weight,
  build_year >= 1978)
{
  1: return 135;
     return 129;
}

switch (FEAT_TRAINS, SELF, tep70_tractive_effort_coefficient,
  build_year >= 1978)
{
  1: return get_tec_int(167.0, 135);
     return get_tec_int(167.0, 129);
}

switch (FEAT_TRAINS, SELF, tep70_articulated_part,
  build_year >= 1978)
{
  1: articulated_part_dummy5;
     articulated_part_dummy4;
}

engine_speed_with_reduce_and_pre_model(tep70, 160, 140, 25, 7, 1978)

engine_penalise_speed(tep70, 2)

general_running_cost(tep70, 630)

item (FEAT_TRAINS, tep70) {
  property {
    name: string(STR_TEP70_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1973), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1973,2006);
    retire_early: get_retire_early(1973,2006);
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 64;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3145 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(167.0, 129);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tep70_sprites;
    purchase: tep70_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: tep70_additional_text;
    cargo_subtype_text: tep70_cargo_subtype_text;
    cargo_capacity: return 0;
    power: tep70_power;
    weight: tep70_weight;
    tractive_effort_coefficient: tep70_tractive_effort_coefficient;
    articulated_part: tep70_articulated_part;
    speed: tep70_speed;
    purchase_speed: tep70_purchase_speed;
    running_cost_factor: tep70_running_cost_factor;
    bitmask_vehicle_info: tep70_bitmask_vehicle_info;
    create_effect: tep70_create_effect;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tep70) {
    property { name: string(STR_TEP70_NAME_LONG); }
  }
}

