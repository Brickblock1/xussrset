 // tep70

// Спрайты
// До 1978 года один набор, с 1978 года два набора

#define IMAGEFILE  "src/diesel/tep70-1973-1977.png"

// покупка
spriteset (tep70_purchase_spriteset, IMAGEFILE)
{
  s12_purchase_template(0, 27, -8)
}

// обычный: погрузка/перемещение
spriteset (tep70_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 27, 0)
} 

spriteset (tep70_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 94, 0)
} 

spritegroup tep70_spritegroup
{
  loading: tep70_loading_spriteset;
  loaded: tep70_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70_r_loading_spriteset, IMAGEFILE)
{
  s12_template(449, 27, 0)
} 

spriteset (tep70_r_traveling_spriteset, IMAGEFILE)
{
  s12_template(449, 94, 0)
} 

spritegroup tep70_r_spritegroup
{
  loading: tep70_r_loading_spriteset;
  loaded: tep70_r_traveling_spriteset;
}

#undef IMAGEFILE
#define IMAGEFILE  "src/diesel/tep70-1978-2006.png"

// покупка
spriteset (tep70_v2_purchase_spriteset, IMAGEFILE)
{
  s13_purchase_template(120, 32, -10)
}

// обычный: погрузка/перемещение
spriteset (tep70_v2_loading_spriteset, IMAGEFILE)
{
  s13_template(120, 32, 0)
} 

spriteset (tep70_v2_traveling_spriteset, IMAGEFILE)
{
  s13_template(120, 99, 0)
} 

spritegroup tep70_v2_spritegroup
{
  loading: tep70_v2_loading_spriteset;
  loaded: tep70_v2_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70_r_v2_loading_spriteset, IMAGEFILE)
{
  s13_template(575, 32, 0)
} 

spriteset (tep70_r_v2_traveling_spriteset, IMAGEFILE)
{
  s13_template(575, 99, 0)
} 

spritegroup tep70_r_v2_spritegroup
{
  loading: tep70_r_v2_loading_spriteset;
  loaded: tep70_r_v2_traveling_spriteset;
}

// обычный: погрузка/перемещение
spriteset (tep70_v3_loading_spriteset, IMAGEFILE)
{
  s13_template(120, 166, 0)
} 

spriteset (tep70_v3_traveling_spriteset, IMAGEFILE)
{
  s13_template(120, 233, 0)
} 

spritegroup tep70_v3_spritegroup
{
  loading: tep70_v3_loading_spriteset;
  loaded: tep70_v3_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70_r_v3_loading_spriteset, IMAGEFILE)
{
  s13_template(575, 166, 0)
} 

spriteset (tep70_r_v3_traveling_spriteset, IMAGEFILE)
{
  s13_template(575, 233, 0)
} 

spritegroup tep70_r_v3_spritegroup
{
  loading: tep70_r_v3_loading_spriteset;
  loaded: tep70_r_v3_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70_get_spriteset2,
  ((last_computed_result - count_veh_id(tep70)) % 2) +
  ((build_year >= 1978) * 2) +
  cargo_subtype * 2
)
{
  0: tep70_spritegroup;
  1: tep70_r_spritegroup;
  2: tep70_v2_spritegroup;
  3: tep70_r_v2_spritegroup;
  4: tep70_v3_spritegroup;
  5: tep70_r_v3_spritegroup;
  align_12_spriteset;
}

switch (FEAT_TRAINS, PARENT, tep70_get_spriteset1,
  count_veh_id(tep70))
{
  tep70_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep70_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep70_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, tep70_get_purchase_spriteset,
  (current_year >= 1978)
)
{
  0: tep70_purchase_spriteset;
  tep70_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tep70_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tep70_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70_cargo_subtype_text2,
  build_year >= 1978)
{
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: tep70_cargo_subtype_text2;
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70_power,
  build_year >= 1978)
{
  1: return 3145;
  return 3020;
}

switch (FEAT_TRAINS, SELF, tep70_weight,
  build_year >= 1978)
{
  1: return 135;
  return 129;
}

switch (FEAT_TRAINS, SELF, tep70_tractive_effort_coefficient,
  build_year >= 1978)
{
  1: return 32; // 32.314436885
  return 34; // 33.81743395
}

switch (FEAT_TRAINS, SELF, tep70_articulated_part,
  build_year >= 1978)
{
  1: articulated_part_dummy5;
  articulated_part_dummy4;
}

// Макс. скорость: ограничиваем скорость через 7 лет работы 160 --> 140
// Для выпущенных с 1978 через 25 160 --> 140
#define MAX_SPEED ((age_in_days >= (7*365+5)) && (build_year < 1978)) || \
  ((age_in_days >= (25*365+5)) && (build_year >= 1978)) ? 140 : 160

switch (FEAT_TRAINS, SELF, tep70_purchase_speed,
  160)
{
  return;
}

switch (FEAT_TRAINS, SELF, tep70_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, tep70_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tep70), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tep70_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tep70), 0),
  vehicle_type_id == tep70
])
{
  tep70_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tep70_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tep70_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tep70)
{
  property
  {
    name: string(STR_TEP70_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1973, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3145 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.132099351;
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tep70_get_spriteset;
    purchase: tep70_get_purchase_spriteset;
    can_attach_wagon: tep70_can_attach_wagon;
    start_stop: tep70_start_stop;
    additional_text: return string(STR_TEP70_PURCHASE_HINT);
    cargo_subtype_text: tep70_cargo_subtype_text;
    cargo_capacity: return 0;
    power: tep70_power;
    weight: tep70_weight;
    tractive_effort_coefficient: tep70_tractive_effort_coefficient;
    articulated_part: tep70_articulated_part;
    speed: tep70_speed;
    purchase_speed: tep70_purchase_speed;
    bitmask_vehicle_info: tep70_bitmask_vehicle_info;
  }
}
