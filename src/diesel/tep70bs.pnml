 // tep70bs

#define IMAGEFILE  "src/diesel/tep70bs.png"
purchase_sprites(tep70bs, 0, 0)

diesel1r_sprites(13, tep70bs_v1,  32,   40)
diesel1r_sprites(13, tep70bs_v2,  32,  241)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70bs_sprites_left,
  cargo_subtype)
{
  0: tep70bs_v2_sprites_left;
  1: tep70bs_v1_sprites_left;
     align_13_sprites;
}

switch (FEAT_TRAINS, SELF, tep70bs_sprites_right,
  cargo_subtype)
{
  0: tep70bs_v2_sprites_right;
  1: tep70bs_v1_sprites_right;
     align_13_sprites;
}

engine_direction_template(tep70bs, ) 
engine_RND_direction_template(tep70bs, 5, 3) 
long_vehicle_sp(tep70bs)

switch (FEAT_TRAINS, SELF, tep70bs_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_BELARUSSIAN_RAILWAY);
  1: return string(STR_REFIT_RZD_PID);
     return CB_RESULT_NO_TEXT;
}

engine_diesel_1r_effect(tep70bs, 3, 2, 13)
engine_direction_template(tep70bs, _create_effect) 
engine_RND_effect_direction_template(tep70bs, 5, 3) 
// engine_cost_change_build_after(tep70bs, 92, 86, 2008)

switch (FEAT_TRAINS, SELF, tep70bs_refit_cost,
  ((extra_callback_info1 & 0xFF00) == 0x100) +
  ((cargo_subtype == 1) * 2))
{
  1: return -5;
  2: return 9;
     return 0; 
}            

engine_speed_with_reduce(tep70bs, ((cargo_subtype >= 1) ? 120 : 160), ((cargo_subtype >= 1) ? 120 : 140), 30)
engine_penalise_speed(tep70bs, 2)
// engine_RC_change_build_after(tep70bs, 902, 840, 2008)
general_running_cost(tep70bs_160, 902)
general_running_cost(tep70bs_120, 840)

switch (FEAT_TRAINS, SELF, tep70bs_running_cost_factor,
  cargo_subtype >= 1)
{
  0: tep70bs_160_running_cost_factor;
     tep70bs_120_running_cost_factor;
}

item (FEAT_TRAINS, tep70bs) {
  property {
    name: string(STR_NAME_TEP70BS);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(2006,2050);
    retire_early: get_retire_early(2006,2050);
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 92;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3020 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 135 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(167.0, 135);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tep70bs_sprites;
    purchase: tep70bs_purchase_sprites;
//    cost_factor: tep70bs_cost_factor;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROM, 2006), 
                                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA),
                                   string(STR_PURCHASE_HINT_EDITION_NEW_SPEED, 120));
    cargo_subtype_text: tep70bs_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep70bs_speed;
    purchase_speed: tep70bs_purchase_speed;
    running_cost_factor: tep70bs_running_cost_factor;
    bitmask_vehicle_info: tep70bs_bitmask_vehicle_info;
    create_effect: tep70bs_create_effect;
    refit_cost: tep70bs_refit_cost;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tep70bs) {
    property { name: string(STR_LONGNAME_TEP70BS); }
  }
}

