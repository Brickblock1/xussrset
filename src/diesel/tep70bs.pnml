 // tep70bs

// Спрайты

#define IMAGEFILE  "src/diesel/tep70bs.png"

// покупка
spriteset (tep70bs_purchase_spriteset, IMAGEFILE)
{
  s13_purchase_template(57, 32, -10)
}

// обычный: погрузка/перемещение
spriteset (tep70bs_loading_spriteset, IMAGEFILE)
{
  s13_template(57, 32, 0)
} 

spriteset (tep70bs_traveling_spriteset, IMAGEFILE)
{
  s13_template(57, 99, 0)
} 

spritegroup tep70bs_spritegroup
{
  loading: tep70bs_loading_spriteset;
  loaded: tep70bs_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70bs_r_loading_spriteset, IMAGEFILE)
{
  s13_template(512, 32, 0)
} 

spriteset (tep70bs_r_traveling_spriteset, IMAGEFILE)
{
  s13_template(512, 99, 0)
} 

spritegroup tep70bs_r_spritegroup
{
  loading: tep70bs_r_loading_spriteset;
  loaded: tep70bs_r_traveling_spriteset;
}

// обычный: погрузка/перемещение
spriteset (tep70bs_v2_loading_spriteset, IMAGEFILE)
{
  s13_template(57, 166, 0)
} 

spriteset (tep70bs_v2_traveling_spriteset, IMAGEFILE)
{
  s13_template(57, 233, 0)
} 

spritegroup tep70bs_v2_spritegroup
{
  loading: tep70bs_v2_loading_spriteset;
  loaded: tep70bs_v2_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70bs_r_v2_loading_spriteset, IMAGEFILE)
{
  s13_template(512, 166, 0)
} 

spriteset (tep70bs_r_v2_traveling_spriteset, IMAGEFILE)
{
  s13_template(512, 233, 0)
} 

spritegroup tep70bs_r_v2_spritegroup
{
  loading: tep70bs_r_v2_loading_spriteset;
  loaded: tep70bs_r_v2_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70bs_get_spriteset2,
  ((last_computed_result - count_veh_id(tep70bs)) % 2) +
  cargo_subtype * 2
)
{
  0: tep70bs_spritegroup;
  1: tep70bs_r_spritegroup;
  2: tep70bs_v2_spritegroup;
  3: tep70bs_r_v2_spritegroup;
  align_13_spriteset;
}

switch (FEAT_TRAINS, PARENT, tep70bs_get_spriteset1,
  count_veh_id(tep70bs))
{
  tep70bs_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep70bs_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep70bs_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tep70bs_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tep70bs_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tep70bs_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: ограничиваем скорость через 25 лет работы 160 --> 140
// Выпущенным с 2008 года - 120 км/ч
#define MAX_SPEED build_year >= 2008 ? 120 : \
  (age_in_days >= (25*365+5)) ? 140 : 160

switch (FEAT_TRAINS, SELF, tep70bs_purchase_speed,
  current_year >= 2008 ? 120 : 160)
{
  return;
}

switch (FEAT_TRAINS, SELF, tep70bs_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, tep70bs_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tep70bs), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tep70bs_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tep70bs), 0),
  vehicle_type_id == tep70bs
])
{
  tep70bs_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tep70bs_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tep70bs_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tep70bs)
{
  property
  {
    name: string(STR_TEP70BS_NAME);
    climates_available: get_climates_available();
    introduction_date: date(2006, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3020 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: 135 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: 0.1262282;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tep70bs_get_spriteset;
    purchase: tep70bs_purchase_spriteset;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: tep70bs_can_attach_wagon;
    start_stop: tep70bs_start_stop;
    additional_text: return string(STR_TEP70BS_PURCHASE_HINT);
    cargo_subtype_text: tep70bs_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tep70bs_speed;
    purchase_speed: tep70bs_purchase_speed;
    bitmask_vehicle_info: tep70bs_bitmask_vehicle_info;
  }
}
