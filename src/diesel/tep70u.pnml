 // tep70u

// Спрайты

#define IMAGEFILE  "src/diesel/tep70u.png"

// покупка
spriteset (tep70u_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// обычный: погрузка/перемещение
spriteset (tep70u_loading_spriteset, IMAGEFILE)
{
  s13_template(0, 31, 0)
} 

spriteset (tep70u_traveling_spriteset, IMAGEFILE)
{
  s13_template(0, 98, 0)
} 

spritegroup tep70u_spritegroup
{
  loading: tep70u_loading_spriteset;
  loaded: tep70u_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70u_r_loading_spriteset, IMAGEFILE)
{
  s13_template(455, 31, 0)
} 

spriteset (tep70u_r_traveling_spriteset, IMAGEFILE)
{
  s13_template(455, 98, 0)
} 

spritegroup tep70u_r_spritegroup
{
  loading: tep70u_r_loading_spriteset;
  loaded: tep70u_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70u_get_spriteset2,
  ((last_computed_result - count_veh_id(tep70u)) % 2)
)
{
  0: tep70u_spritegroup;
  1: tep70u_r_spritegroup;
  align_13_spriteset;
}

switch (FEAT_TRAINS, PARENT, tep70u_get_spriteset1,
  count_veh_id(tep70u))
{
  tep70u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tep70u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tep70u_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, tep70u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, tep70u_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: ограничиваем скорость через 25 лет работы 160 --> 140
#define MAX_SPEED (age_in_days >= (25*365+5)) ? 140 : 160

switch (FEAT_TRAINS, SELF, tep70u_purchase_speed,
  160)
{
  return;
}

switch (FEAT_TRAINS, SELF, tep70u_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, tep70u_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tep70u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tep70u_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tep70u), 0),
  vehicle_type_id == tep70u
])
{
  tep70u_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tep70u_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tep70u_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tep70u)
{
  property
  {
    name: string(STR_TEP70U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2006,2007);
    retire_early: get_retire_early(2006,2007); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 224;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3020 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 135 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(167.0, 135);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }
  graphics {
    default: tep70u_get_spriteset;
    purchase: tep70u_purchase_spriteset;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: tep70u_can_attach_wagon;
    start_stop: tep70u_start_stop;
    additional_text: return string(STR_TEP70U_PURCHASE_HINT);
    speed: tep70u_speed;
    purchase_speed: tep70u_purchase_speed;
    bitmask_vehicle_info: tep70u_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
