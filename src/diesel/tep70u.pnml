 // tep70u

// Спрайты

#define IMAGEFILE  "src/diesel/tep70u.png"

// покупка
purchase_spriteset(tep70u, 0, 0)

// обычный: погрузка/перемещение
spriteset (tep70u_loading_spriteset, IMAGEFILE)
{
  s13_template(0, 31, 0)
} 

spriteset (tep70u_traveling_spriteset, IMAGEFILE)
{
  s13_template(0, 98, 0)
} 

spritegroup tep70u_spritegroup
{
  loading: tep70u_loading_spriteset;
  loaded: tep70u_traveling_spriteset;
}

// развёрнутый: погрузка/перемещение
spriteset (tep70u_r_loading_spriteset, IMAGEFILE)
{
  s13_template(455, 31, 0)
} 

spriteset (tep70u_r_traveling_spriteset, IMAGEFILE)
{
  s13_template(455, 98, 0)
} 

spritegroup tep70u_r_spritegroup
{
  loading: tep70u_r_loading_spriteset;
  loaded: tep70u_r_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tep70u_sprites_2,
  ((LOAD_TEMP(0) - count_veh_id(tep70u)) % 2))
{
  0: tep70u_spritegroup;
  1: tep70u_r_spritegroup;
     align_13_spriteset;
}

long_vehicle(tep70u, )

engine_speed_with_reduce(tep70u, 160, 140, 25)

engine_penalise_speed(tep70u, 2)

item (FEAT_TRAINS, tep70u)
{
  property
  {
    name: string(STR_TEP70U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 25; // срок службы
    model_life: get_model_life(2006,2007);
    retire_early: get_retire_early(2006,2007); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 224;
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3020 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 135 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(167.0, 135);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }
  graphics {
    default: tep70u_sprites;
    purchase: tep70u_purchase_spriteset;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TEP70U_PURCHASE_HINT);
    speed: tep70u_speed;
    purchase_speed: tep70u_purchase_speed;
    bitmask_vehicle_info: tep70u_bitmask_vehicle_info;
    cargo_capacity: return 0;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, tep70u)
  {
    property
    { name: string(STR_TEP70U_NAME_LONG); }
  }
}

