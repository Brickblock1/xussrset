 // tg102

#define IMAGEFILE  "src/diesel/tg102.png"
purchase_sprites(tg102_v1, 0, 0)
purchase_sprites(tg102_v2, 102, 0)
purchase_sprites(tg102_v3, 204, 0)

diesel2_sprites(9, tg102_v1, 32,   40)
diesel2_sprites(9, tg102_v2, 32,  241)
diesel2_sprites(9, tg102_v3, 32,  442)
diesel2_sprites(9, tg102_v4, 32,  643)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tg102_purchase_sprites,
  (current_year >= 1960) +
  (current_year >= 1963))
{
  0: tg102_v1_purchase_sprites;
  1: tg102_v2_purchase_sprites;
     tg102_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tg102_sprites_left,
  (build_year >= 1960) +
  (build_year >= 1963) +
  (cargo_subtype * 3))
{
  0: tg102_v1_sprites_left;
  1: tg102_v2_sprites_left;
  2: tg102_v4_sprites_left;
  3: tg102_v1_sprites_left;
  4: tg102_v3_sprites_left;
  5: tg102_v4_sprites_left;
     align_9_sprites; 
}
switch (FEAT_TRAINS, SELF, tg102_sprites_right,
  (build_year >= 1960) +
  (build_year >= 1963) +
  (cargo_subtype * 3))
{
  0: tg102_v1_sprites_right;
  1: tg102_v2_sprites_right;
  2: tg102_v4_sprites_right;
  3: tg102_v1_sprites_right;
  4: tg102_v3_sprites_right;
  5: tg102_v4_sprites_right;
     align_9_sprites; 
}

engine_direction_template(tg102, ) 
long_vehicle(tg102)


switch (FEAT_TRAINS, SELF, tg102_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_96);
  1: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tg102_cargo_subtype_text,
  (build_year >= 1960) +
  (build_year >= 1963))
{
  0: return string(STR_REFIT_PREPRODUCTION);
  1: tg102_cargo_subtype_text2;
  2: return string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tg102_additional_text,
  (current_year >= 1960) +
  (current_year >= 1963))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_IN, 1959), 
                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1966), 
                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_DIESEL), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1966), 
                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD));
}

engine_diesel_2_effect(tg102, 0, 1, 1, 2, 11)
engine_direction_template(tg102, _create_effect) 
engine_speed(tg102, 120)
engine_penalise_speed(tg102, 2)
general_running_cost(tg102, 348)

item (FEAT_TRAINS, tg102) {
  property {
    name: string(STR_TG102_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1959), 1, 1);
    vehicle_life: 15;
    model_life: get_model_life(1959,1966);
    retire_early: get_retire_early(1959,1966);
    reliability_decay: 40;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 28;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1505 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: (168/2) ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(159.5, 168/2);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tg102_sprites;
    purchase: tg102_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: tg102_additional_text;
    cargo_subtype_text: tg102_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tg102_speed;
    purchase_speed: tg102_purchase_speed;
    running_cost_factor: tg102_running_cost_factor;
    bitmask_vehicle_info: tg102_bitmask_vehicle_info;
    create_effect: tg102_create_effect_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tg102) {
    property { name: string(STR_TG102_NAME_LONG); }
  }
}

