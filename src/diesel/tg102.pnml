 // tg102

// Спрайты

#define IMAGEFILE  "src/diesel/tg102.png"

// покупка
spriteset (tg102_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// обычный: погрузка/перемещение
spriteset (tg102_loading_spriteset, IMAGEFILE)
{
  s9_template(2, 48, 0)
} 

spriteset (tg102_traveling_spriteset, IMAGEFILE)
{
  s9_template(2, 115, 0)
} 

spritegroup tg102_spritegroup
{
  loading: tg102_loading_spriteset;
  loaded: tg102_traveling_spriteset;
}

// развёрнутый
spriteset (tg102_r_spriteset, IMAGEFILE)
{
  s9_template(433, 48, 0)
} 

// покупка
spriteset (tg102_v2_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(100, 0)
}

// обычный: погрузка/перемещение
spriteset (tg102_v2_loading_spriteset, IMAGEFILE)
{
  s9_template(2, 210, 0)
} 

spriteset (tg102_v2_traveling_spriteset, IMAGEFILE)
{
  s9_template(2, 277, 0)
} 

spritegroup tg102_v2_spritegroup
{
  loading: tg102_v2_loading_spriteset;
  loaded: tg102_v2_traveling_spriteset;
}

// развёрнутый
spriteset (tg102_r_v2_spriteset, IMAGEFILE)
{
  s9_template(433, 210, 0)
} 

// обычный: погрузка/перемещение
spriteset (tg102_v3_loading_spriteset, IMAGEFILE)
{
  s9_template(2, 366, 0)
} 

spriteset (tg102_v3_traveling_spriteset, IMAGEFILE)
{
  s9_template(2, 433, 0)
} 

spritegroup tg102_v3_spritegroup
{
  loading: tg102_v3_loading_spriteset;
  loaded: tg102_v3_traveling_spriteset;
}

// развёрнутый
spriteset (tg102_r_v3_spriteset, IMAGEFILE)
{
  s9_template(433, 366, 0)
} 

// покупка
spriteset (tg102_v4_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(200, 0)
}

// обычный: погрузка/перемещение
spriteset (tg102_v4_loading_spriteset, IMAGEFILE)
{
  s9_template(2, 523, 0)
} 

spriteset (tg102_v4_traveling_spriteset, IMAGEFILE)
{
  s9_template(2, 590, 0)
} 

spritegroup tg102_v4_spritegroup
{
  loading: tg102_v4_loading_spriteset;
  loaded: tg102_v4_traveling_spriteset;
}

// развёрнутый
spriteset (tg102_r_v4_spriteset, IMAGEFILE)
{
  s9_template(433, 523, 0)
} 

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, tg102_get_spriteset_l,
  (build_year >= 1960) +
  (build_year >= 1963) +
  (cargo_subtype * 3))
{
  0: tg102_spritegroup;
  1: tg102_v2_spritegroup;
  2: tg102_v4_spritegroup;
  3: tg102_spritegroup;
  4: tg102_v3_spritegroup;
  5: tg102_v4_spritegroup;

  align_9_spriteset; // параллелепипед
}
switch (FEAT_TRAINS, SELF, tg102_get_spriteset_r,
  (build_year >= 1960) +
  (build_year >= 1963) +
  (cargo_subtype * 3))
{
  0: tg102_r_spriteset;
  1: tg102_r_v2_spriteset;
  2: tg102_r_v4_spriteset;
  3: tg102_r_spriteset;
  4: tg102_r_v3_spriteset;
  5: tg102_r_v4_spriteset;
  align_9_spriteset; // параллелепипед
}

engine_direction_template(tg102) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tg102_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tg102_get_spriteset_t;
}

switch (FEAT_TRAINS, SELF, tg102_get_purchase_spriteset,
  (current_year >= 1960) +
  (current_year >= 1963))
{
  0: tg102_purchase_spriteset;
  1: tg102_v2_purchase_spriteset;
  tg102_v4_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, tg102_cargo_subtype_text,
  (build_year >= 1960) +
  (build_year >= 1963) +
  (cargo_subtype * 3))
{
  0: string(STR_REFIT_PREPRODUCTION);
  1: string(STR_REFIT_LIGHT_BLUE_MALE);
  2: string(STR_REFIT_GREEN_GRASS);
  3: string(STR_REFIT_PREPRODUCTION);
  4: string(STR_REFIT_GREEN_GRASS);
  5: string(STR_REFIT_GREEN_GRASS);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 120 км/ч
#define MAX_SPEED 120

switch (FEAT_TRAINS, SELF, tg102_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, tg102_speed,
  is_penalise_speed() ? get_penalised_speed(MAX_SPEED) : MAX_SPEED)
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, tg102_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(tg102), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, tg102_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(tg102), 0),
  vehicle_type_id == tg102
])
{
  tg102_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, tg102_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  tg102_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, tg102) 
{
  property
  {
    name: string(STR_TG102_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1959), 1, 1);
    vehicle_life: 15; // срок службы
    model_life: get_model_life(1959,1966);
    retire_early: get_retire_early(1959,1966); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: get_diesel_cost_factor();
    running_cost_factor: get_diesel_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1505 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 1;
    weight: (168/2) ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(159.5, 168/2);
    length: 8;
    visual_effect_and_powered: diesel_visual_effect_and_powered(0);
  }
  graphics {
    default: tg102_get_spriteset;
    purchase: tg102_get_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_TG102_PURCHASE_HINT);
    cargo_subtype_text: tg102_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: tg102_speed;
    purchase_speed: tg102_purchase_speed;
    bitmask_vehicle_info: tg102_bitmask_vehicle_info;
    cost_factor: return 95;
  }
}
