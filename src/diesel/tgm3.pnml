 // tgm3

#define IMAGEFILE  "src/diesel/tgm3.png"
purchase_sprites(tgm3_v1,   0, 0)
purchase_sprites(tgm3_v2, 102, 0)

diesel1r_sprites(7, tgm3_v1, 32,  40)
diesel1r_sprites(7, tgm3_v2, 32, 241)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tgm3_purchase_sprites,
  current_year >= 1967)
{
  0: tgm3_v1_purchase_sprites;
     tgm3_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, tgm3_sprites_left,
  build_year >= 1967)
{
   0: tgm3_v1_sprites_left;
   1: tgm3_v2_sprites_left;
      align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, tgm3_sprites_right, 
  build_year >= 1967)
{
   0: tgm3_v1_sprites_right;
   1: tgm3_v2_sprites_right;
      align_10_sprites; 
}

engine_direction_template(tgm3, ) 
engine_RND_direction_template(tgm3, 1, 3) 
long_vehicle_sp(tgm3)

switch (FEAT_TRAINS, SELF, tgm3_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_B6);
  1: return string(STR_REFIT_TTD_B4);
  2: return string(STR_REFIT_TTD_63);
  3: return string(STR_REFIT_TTD_53);
  4: return string(STR_REFIT_TTD_31);
  5: return string(STR_REFIT_TTD_93);
  6: return string(STR_REFIT_TTD_96);
  7: return string(STR_REFIT_TTD_42);
     return CB_RESULT_NO_TEXT;
}


switch (FEAT_TRAINS, SELF, tgm3_colour_mapping,
  cargo_subtype)
{                          
  0: return tgm23_cc + 0;
  1: return tgm23_cc + 1;
  2: return tgm23_cc + 2;
  3: return tgm23_cc + 3;
  4: return tgm23_cc + 4;
  5: return tgm23_cc + 5;
  6: return tgm23_cc + 6;
  7: return tgm23_cc + 7;
     return CB_FAILED;
}

engine_diesel_1r_effect(tgm3, -1, 0, 11) 
engine_direction_template(tgm3, _create_effect) 
engine_RND_effect_direction_template(tgm3, 1, 3) 
engine_penalise_speed(tgm3, 2)
engine_RC_change_build_after(tgm3, 64, 
                                   65, 1967)
engine_speed_change_build_after(tgm3, 60, 
                                   62, 1967)

item (FEAT_TRAINS, tgm3) {
  property {
    name: string(STR_NAME_TGM3);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1959), 1, 1);
    vehicle_life: 20;
    model_life: get_model_life(1959,1967);
    retire_early: get_retire_early(1959,1967);
    reliability_decay: 10;
    cost_factor: 8;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type_diesel();
    ai_special_flag: AI_FLAG_CARGO;
    power: 500 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    weight: 68 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(201.0, 68);
    length: 6;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: tgm3_sprites;
    purchase: tgm3_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_SHUNTING_DIESEL), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1959, 1977), 
                                   string(STR_PURCHASE_HINT_FACTORY_LYUDINOVSK));
    speed: tgm3_speed;
    purchase_speed: tgm3_purchase_speed;
    running_cost_factor: tgm3_running_cost_factor;
    bitmask_vehicle_info: tgm3_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: tgm3_create_effect;
    cargo_subtype_text: tgm3_cargo_subtype_text;
    colour_mapping: tgm3_colour_mapping;
  }
}

long_name_template(tgm3, STR_LONGNAME_TGM3)
models_default_cargo_template(tgm3)
