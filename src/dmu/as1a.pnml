 // as1a

#define IMAGEFILE  "src/dmu/as1a.png"
purchase_sprites(as1a, 0, 0)

MU_3r_0_0_sprites(5, as1a_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/as1a-bl.png"
MU_3r_0_0_sprites(5, as1a_v2, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, as1a_sprites_start,
  ((LOAD_TEMP(0) - count_veh_id(as1a)) % 2) +
  (cargo_subtype * 2))
{
  0: as1a_v1_h_sprites_left;
  1: as1a_v1_h_sprites_right;
  2: as1a_v2_h_sprites_left;
  3: as1a_v2_h_sprites_right;
     align_5_sprites;
}

short_vehicle(as1a)

// Присоединяем не более одной
switch (FEAT_TRAINS, SELF, as1a_can_attach_wagon1,
  (vehicle_type_id == as1a) && (LOAD_TEMP(0) <= 1))
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, as1a_can_attach_wagon,
  STORE_TEMP(num_vehs_in_consist, 0))
{
  as1a_can_attach_wagon1;
}

engine_diesel_1r_effect(as1a, -3, 0, 10)  

switch (FEAT_TRAINS, SELF, as1a_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(as1a)) % 2)
{
  0: as1a_create_effect_sprites_left;
     as1a_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, as1a_create_effect0,
  STORE_TEMP(count_veh_id(as1a), 0))
{
  as1a_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - as1a 
switch (FEAT_TRAINS, PARENT, as1a_create_effect,
  vehicle_type_id == as1a)
{
  1: as1a_create_effect0; 
     return disable_create_effect();
} 

// Даём тягу, только если локомотив - as1a 
switch (FEAT_TRAINS, PARENT, as1a_power,
  vehicle_type_id == as1a)
{
  1: return get_power_in_cb(70); 
     return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - as1a 
switch (FEAT_TRAINS, PARENT, as1a_cargo_capacity,
  vehicle_type_id == as1a)
{
  1: return 24;
     return 0;
}

switch (FEAT_TRAINS, SELF, as1a_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_B7);
  1: return string(STR_REFIT_TTD_93);
     return CB_RESULT_NO_TEXT;
}

general_running_cost(as1a_v1, 13)

switch (FEAT_TRAINS, PARENT, as1a_running_cost_factor,
  vehicle_type_id == as1a)
{
  1: as1a_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

item (FEAT_TRAINS, as1a) {
  property {
    name: string(STR_NAME_AS1A);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    model_life: get_model_life(1964,1980);
    retire_early: get_retire_early(1964,1980);
    vehicle_life: 18;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 15; 
    cost_factor: 2;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    speed: 80 km/h;
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 70 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 24;
    weight: 9 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(10.0, 9);
    length: 5;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: as1a_sprites;
    purchase: as1a_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: as1a_running_cost_factor;
    can_attach_wagon: as1a_can_attach_wagon;
    cargo_subtype_text: as1a_cargo_subtype_text;
    additional_text: return string(STR_AS1A_PURCHASE_HINT);
    power: as1a_power;
    create_effect: as1a_create_effect;
    cargo_capacity: as1a_cargo_capacity;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, as1a) {
    property { name: string(STR_LONGNAME_AS1A); }
  }
}


