 // as1a
// 207 (CFh)

#define IMAGEFILE  "src/dmu/as1a.png"
purchase_sprites(as1a, 0, 0)

MU_3r_0_0_sprites(5, as1a_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/as1a-bl.png"
MU_3r_0_0_sprites(5, as1a_v2, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, as1a_sprites_start,
  ((LOAD_TEMP(0) - count_veh_id(as1a)) % 2) +
  (cargo_subtype * 2))
{
  0: as1a_v1_h_sprites_left;
  1: as1a_v1_h_sprites_right;
  2: as1a_v2_h_sprites_left;
  3: as1a_v2_h_sprites_right;
     align_5_sprites;
}

short_vehicle(as1a)

// Присоединяем не более одной
switch (FEAT_TRAINS, SELF, as1a_can_attach_wagon1,
  (vehicle_type_id == as1a) && (LOAD_TEMP(0) <= 1))
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, as1a_can_attach_wagon,
  STORE_TEMP(num_vehs_in_consist, 0))
{
  as1a_can_attach_wagon1;
}

engine_diesel_1r_effect(as1a, -3, 0, 10)

switch (FEAT_TRAINS, SELF, as1a_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(as1a)) % 2)
{
  0: as1a_create_effect_sprites_left;
     as1a_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, as1a_create_effect0,
  STORE_TEMP(count_veh_id(as1a), 0))
{
  as1a_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - as1a
switch (FEAT_TRAINS, PARENT, as1a_create_effect,
  vehicle_type_id == as1a)
{
  1: as1a_create_effect0;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - as1a
switch (FEAT_TRAINS, PARENT, as1a_power,
  vehicle_type_id == as1a)
{
  1: return get_power_in_cb(70);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - as1a
switch (FEAT_TRAINS, PARENT, as1a_cargo_capacity,
  vehicle_type_id == as1a)
{
  1: return 24;
     return 0;
}

general_running_cost(as1a_v1, 13)

switch (FEAT_TRAINS, PARENT, as1a_running_cost_factor,
  vehicle_type_id == as1a)
{
  1: as1a_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

livery_template1(as1a_alt, TTD_B7)
livery_template_alt(as1a, TTD_93)
engine_speed(as1a, 80)

item (FEAT_TRAINS, as1a, 207) {
  property {
    name: string(STR_NAME_AS1A);
    vehicle_dates(1964, 1980, 18, 10, 5, 2)
    vehicle_dmu(70, 9, 10.0, 24, 15)
  }
  graphics {
    default: as1a_sprites;
    purchase: as1a_purchase_sprites;
    speed: as1a_speed;
    cargo_age_period: return 60;
    running_cost_factor: as1a_running_cost_factor;
    can_attach_wagon: as1a_can_attach_wagon;
    cargo_subtype_text: as1a_cargo_subtype_text;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_M),
                                   string(STR_SECTIONS_DMU_1_2),
                                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1980),
                                   string(STR_PURCHASE_HINT_FACTORY_VELIKOLUKSKY));

    power: as1a_power;
    create_effect: as1a_create_effect;
    cargo_capacity: as1a_cargo_capacity;
    colour_mapping: any_cc_colour;
    purchase_speed: return 80;
  }
}

long_name_template(as1a, STR_LONGNAME_AS1A)
allow_dmu(as1a)
