 // as1a

// Спрайты

#define IMAGEFILE  "src/dmu/as1a.png"

// покупка
purchase_sprites(as1a, 0, 0)

// погрузка/перемещение
spriteset (as1a_loading_spriteset, IMAGEFILE)
{
  s5_template(0, 21, 0)
} 

spriteset (as1a_traveling_spriteset, IMAGEFILE)
{
  s5_template(0, 60, 0)
} 

spritegroup as1a_spritegroup
{
  loading: as1a_loading_spriteset;
  loaded: as1a_traveling_spriteset;
}


// погрузка/перемещение
spriteset (as1a_2_loading_spriteset, IMAGEFILE)
{
  s5_template(0, 99, 0)
} 

spriteset (as1a_2_traveling_spriteset, IMAGEFILE)
{
  s5_template(0, 138, 0)
} 

spritegroup as1a_2_spritegroup
{
  loading: as1a_2_loading_spriteset;
  loaded: as1a_2_traveling_spriteset;
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, as1a_sprites_2,
  cargo_subtype)
{
  0: as1a_spritegroup;
     as1a_2_spritegroup;
}

short_vehicle(as1a)

// Присоединяем не более одной
switch (FEAT_TRAINS, SELF, as1a_can_attach_wagon1,
  (vehicle_type_id == as1a) && (LOAD_TEMP(0) <= 1))
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, as1a_can_attach_wagon,
  STORE_TEMP(num_vehs_in_consist, 0))
{
  as1a_can_attach_wagon1;
}

// Визуализируем эффекты тяги только если локомотив - as1a
switch (FEAT_TRAINS, PARENT, as1a_visual_effect_and_powered,
  vehicle_type_id == as1a)
{
  1: return diesel_visual_effect_and_powered(0);
     return disable_visual_effect_and_powered();
}

// Даём тягу, только если локомотив - as1a 
switch (FEAT_TRAINS, PARENT, as1a_power,
  vehicle_type_id == as1a)
{
  1: return get_power_in_cb(70); 
     return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - as1a 
switch (FEAT_TRAINS, PARENT, as1a_cargo_capacity,
  vehicle_type_id == as1a)
{
  1: return 24;
     return 0;
}

switch (FEAT_TRAINS, SELF, as1a_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_B7);
  1: string(STR_REFIT_TTD_93);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, as1a) 
{
  property
  {
    name: string(STR_AS1A_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    model_life: get_model_life(1964,1980);
    retire_early: get_retire_early(1964,1980);
    vehicle_life: 18;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    speed: 80 km/h;
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 70 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 24;
    weight: 9 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(10.0, 9);
    length: 5;
  }
  graphics {
    default: as1a_sprites;
    purchase: as1a_purchase_sprites;
    can_attach_wagon: as1a_can_attach_wagon;
    cargo_subtype_text: as1a_cargo_subtype_text;
    additional_text: return string(STR_AS1A_PURCHASE_HINT);
    power: as1a_power;
    visual_effect_and_powered: as1a_visual_effect_and_powered;
    cargo_capacity: as1a_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, as1a)
  {
    property
    { name: string(STR_AS1A_NAME_LONG); }
  }
}


