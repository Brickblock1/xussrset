 // d1

// Спрайты

#define IMAGEFILE  "src/dmu/d1.png"
purchase_sprites(d1_v1_h, 0, 0)
purchase_sprites(d1_v1_c, 120, 0)

MU_3r_0_0_sprites(15, d1_v1, 32, 40)
MU_0_0_2_sprites(15,  d1_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1967.png"
purchase_sprites(d1_v1u_h, 0, 0)
purchase_sprites(d1_v1u_c, 120, 0)

MU_3r_0_0_sprites(15, d1_v1u, 32, 40)
MU_0_0_2_sprites(15,  d1_v1u, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1967-szhd.png"
MU_3r_0_0_sprites(15, d1_v2u, 32, 40)
MU_0_0_2_sprites(15,  d1_v2u, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1967-gzhd.png"
MU_3r_0_0_sprites(15, d1_v3u, 32, 40)
MU_0_0_2_sprites(15,  d1_v3u, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1967-mzhd.png"
MU_3r_0_0_sprites(15, d1_v4u, 32, 40)
MU_0_0_2_sprites(15,  d1_v4u, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969.png"
purchase_sprites(d1_v1w_h, 0, 0)
purchase_sprites(d1_v1w_c, 120, 0)

MU_3r_0_0_sprites(15, d1_v1w, 32, 40)
MU_0_0_2_sprites(15,  d1_v1w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-szhd.png"
MU_3r_0_0_sprites(15, d1_v2w, 32, 40)
MU_0_0_2_sprites(15,  d1_v2w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-gzhd.png"
MU_3r_0_0_sprites(15, d1_v3w, 32, 40)
MU_0_0_2_sprites(15,  d1_v3w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-mzhd.png"
MU_3r_0_0_sprites(15, d1_v4w, 32, 40)
MU_0_0_2_sprites(15,  d1_v4w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-tch22nm.png"
MU_3r_0_0_sprites(15, d1_v5w, 32, 40)
MU_0_0_2_sprites(15,  d1_v5w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-tch35u.png"
MU_3r_0_0_sprites(15, d1_v6w, 32, 40)
MU_0_0_2_sprites(15,  d1_v6w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-tch43s.png"
MU_3r_0_0_sprites(15, d1_v7w, 32, 40)
MU_0_0_2_sprites(15,  d1_v7w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-uz.png"
MU_3r_0_0_sprites(15, d1_v8w, 32, 40)
MU_0_0_2_sprites(15,  d1_v8w, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/d1-s1969-lg.png"
MU_3r_0_0_sprites(15, d1_v9w, 32, 40)
MU_0_0_2_sprites(15,  d1_v9w, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, d1_h_purchase_sprites,
  (build_year >= 1967) +
  (build_year >= 1969))
{
  1: d1_v1u_h_purchase_sprites;
  2: d1_v1w_h_purchase_sprites;
     d1_v1_h_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, d1_c_purchase_sprites,
  (build_year >= 1967) +
  (build_year >= 1969))
{
  1: d1_v1u_c_purchase_sprites;
  2: d1_v1w_c_purchase_sprites;
     d1_v1_c_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, d1_h_sprites_l,
  (((build_year >= 1967) +
  (build_year >= 1969)) * 100) +
  cargo_subtype)
{
    0: d1_v1_h_sprites_left;
  100: d1_v1u_h_sprites_left;
  101: d1_v2u_h_sprites_left;
  102: d1_v3u_h_sprites_left;
  103: d1_v4u_h_sprites_left;
  200: d1_v1w_h_sprites_left;
  201: d1_v2w_h_sprites_left;
  202: d1_v3w_h_sprites_left;
  203: d1_v4w_h_sprites_left;
  204: d1_v5w_h_sprites_left;
  205: d1_v6w_h_sprites_left;
  206: d1_v7w_h_sprites_left;
  207: d1_v8w_h_sprites_left;
  208: d1_v9w_h_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, d1_h_sprites_r,
  (((build_year >= 1967) +
  (build_year >= 1969)) * 100) +
  cargo_subtype)
{
    0: d1_v1_h_sprites_right;
  100: d1_v1u_h_sprites_right;
  101: d1_v2u_h_sprites_right;
  102: d1_v3u_h_sprites_right;
  103: d1_v4u_h_sprites_right;
  200: d1_v1w_h_sprites_right;
  201: d1_v2w_h_sprites_right;
  202: d1_v3w_h_sprites_right;
  203: d1_v4w_h_sprites_right;
  204: d1_v5w_h_sprites_right;
  205: d1_v6w_h_sprites_right;
  206: d1_v7w_h_sprites_right;
  207: d1_v8w_h_sprites_right;
  208: d1_v9w_h_sprites_right;
       align_15_sprites;
}

engine_direction_template2m(d1_h, d1_c)

long_vehicle(d1_h)

switch (FEAT_TRAINS, SELF, d1_c_sprites_left,
  (((build_year >= 1967) +
  (build_year >= 1969)) * 100) +
  cargo_subtype)
{
    0: d1_v1_c_sprites_left;
  100: d1_v1u_c_sprites_left;
  101: d1_v2u_c_sprites_left;
  102: d1_v3u_c_sprites_left;
  103: d1_v4u_c_sprites_left;
  200: d1_v1w_c_sprites_left;
  201: d1_v2w_c_sprites_left;
  202: d1_v3w_c_sprites_left;
  203: d1_v4w_c_sprites_left;
  204: d1_v5w_c_sprites_left;
  205: d1_v6w_c_sprites_left;
  206: d1_v7w_c_sprites_left;
  207: d1_v8w_c_sprites_left;
  208: d1_v9w_c_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, d1_c_sprites_right,
  (((build_year >= 1967) +
  (build_year >= 1969)) * 100) +
  cargo_subtype)
{
    0: d1_v1_c_sprites_right;
  100: d1_v1u_c_sprites_right;
  101: d1_v2u_c_sprites_right;
  102: d1_v3u_c_sprites_right;
  103: d1_v4u_c_sprites_right;
  200: d1_v1w_c_sprites_right;
  201: d1_v2w_c_sprites_right;
  202: d1_v3w_c_sprites_right;
  203: d1_v4w_c_sprites_right;
  204: d1_v5w_c_sprites_right;
  205: d1_v6w_c_sprites_right;
  206: d1_v7w_c_sprites_right;
  207: d1_v8w_c_sprites_right;
  208: d1_v9w_c_sprites_right;
       align_15_sprites;
}

random_switch (FEAT_TRAINS, SELF, d1_c_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: d1_c_sprites_left;
  1: d1_c_sprites_right;
}

long_vehicle(d1_c)

switch (FEAT_TRAINS, PARENT, d1_can_attach_wagon_length,
  count_veh_id(d1_h) > 3)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, d1_c_can_attach_wagon1,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  d1_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_4D);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, d1_c_can_attach_wagon_h,
  count_veh_id(d1_h) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     d1_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, d1_c_can_attach_wagon,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  d1_c: d1_c_can_attach_wagon1;
  d1_h: d1_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, d1_can_attach_wagon,
  vehicle_type_id)
{
  d1_h: d1_can_attach_wagon_length;
  d1_c: d1_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, d1_h_start_stop,
  count_veh_id(d1_h) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

switch (FEAT_TRAINS, SELF, d1_h_visual_effect_and_powered1,
  (LOAD_TEMP(0) - count_veh_id(d1_h)) % 2)
{
  0: return diesel_visual_effect_and_powered(-2);
     return diesel_visual_effect_and_powered(7);
}

switch (FEAT_TRAINS, PARENT, d1_h_visual_effect_and_powered0,
  STORE_TEMP(count_veh_id(d1_h), 0))
{
  d1_h_visual_effect_and_powered1;
}

// Визуализируем эффекты тяги только если локомотив - d1_h 
switch (FEAT_TRAINS, PARENT, d1_h_visual_effect_and_powered,
  vehicle_type_id == d1_h)
{
  1: d1_h_visual_effect_and_powered0; 
     return disable_visual_effect_and_powered();
} 

// Даём тягу, только если локомотив - d1_h 
switch (FEAT_TRAINS, PARENT, d1_h_power,
  vehicle_type_id == d1_h)
{
  1: return get_power_in_cb(615); 
     return 0;
} 

switch (FEAT_TRAINS, SELF, d1_cargo_capacity,
  build_year >= 1967)
{
  1: return 72;
     return 77;
}

// Допускается эксплуатация с пассажирами только если голова - d1_h 
switch (FEAT_TRAINS, PARENT, d1_h_cargo_capacity,
  vehicle_type_id == d1_h)
{
  1: d1_cargo_capacity;
     return 0;
}

switch (FEAT_TRAINS, PARENT, d1_c_cargo_capacity,
  vehicle_type_id == d1_h)
{
  1: return 128;
     return 0;
}

// 1963-66 - 1 
// 1967-68 - 1, с 1980 - +3
// 1969-88 - 1, с 1980 - +3, с 1990 - + 5

switch (FEAT_TRAINS, SELF, d1_cargo_subtype_text4,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, d1_cargo_subtype_text3,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_SZD);
  2: string(STR_REFIT_GORKOV_RAILWAY);
  3: string(STR_REFIT_MOSCOW_RAILWAY, "");
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, d1_cargo_subtype_text2,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_SZD);
  2: string(STR_REFIT_GORKOV_RAILWAY);
  3: string(STR_REFIT_MOSCOW_RAILWAY, "");
  4: string(STR_REFIT_RZD_TCH22_NOVOMOSKOVSK);
  5: string(STR_REFIT_RZD_TCH35_UZLOVZYA);
  6: string(STR_REFIT_RZD_TCH43_SMOLENSK);
  7: string(STR_REFIT_UKRAINIAN_RAILWAY);
  8: string(STR_REFIT_LG);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, d1_cargo_subtype_text,
  (build_year >= 1967)+
  (build_year >= 1969) )
{
  0: d1_cargo_subtype_text4;
  1: d1_cargo_subtype_text3;
  2: d1_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, d1_h) 
{
  property
  {
    name: string(STR_D1_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1963), 1, 1);
    model_life: get_model_life(1963,1968);
    retire_early: get_retire_early(1963,1968);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 615 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 77;
    weight: 65 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(95.0, 65);
    length: 8;
  }
  graphics {
    default: d1_h_sprites;
    purchase: d1_h_purchase_sprites;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: d1_can_attach_wagon;
    start_stop: d1_h_start_stop;
    visual_effect_and_powered: d1_h_visual_effect_and_powered;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    additional_text: return string(STR_D1_H_PURCHASE_HINT, string(STR_SECTIONS_DMU_2_4));
    cargo_subtype_text: d1_cargo_subtype_text;
    power: d1_h_power;
    cargo_capacity: d1_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, d1_c) 
{
  property
  {
    name: string(STR_D1_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: get_model_life(1963,1968);
    retire_early: get_retire_early(1963,1968);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 128;
    weight: 37 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: d1_c_sprites;
    purchase: d1_c_purchase_sprites;
    articulated_part: articulated_part_dummy7;
    cargo_subtype_text: d1_cargo_subtype_text;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    can_attach_wagon: d1_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: return string(STR_D1_H_PURCHASE_HINT, string(STR_SECTIONS_DMU_2_4));
    cargo_capacity: d1_c_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, d1_h)
  {
    property
    { name: string(STR_D1_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, d1_c)
  {
    property
    { name: string(STR_D1_C_NAME_LONG); }
  }
}

