 // dr1

// Спрайты

#define IMAGEFILE  "src/dr1/dr1-1963-1969.png"

// Вариант 0 (1963-66, Балтика)
// покупка
spriteset (dr1_m_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(38, 35, -14)
}

spriteset (dr1_c_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(38, 173, -14)
}

// головной: погрузка/перемещение
spriteset (dr1_m_loading_spriteset, IMAGEFILE)
{
  s15_template(38, 35, 0)
} 

spriteset (dr1_m_traveling_spriteset, IMAGEFILE)
{
  s15_template(497, 35, 0)
} 

spritegroup dr1_m_spritegroup
{
  loading: dr1_m_loading_spriteset;
  loaded: dr1_m_traveling_spriteset;
}

// развёрнутая голова
spriteset (dr1_rm_spriteset, IMAGEFILE)
{
  s15_template(38, 104, 0)
} 

// прицепной
spriteset (dr1_c_spriteset, IMAGEFILE)
{
  s15_template(38, 173, 0)
} 

// Вариант 1 (1966-69), Дырка
// покупка
spriteset (dr1_m_2_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(38, 262, -14)
}

spriteset (dr1_c_2_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(38, 400, -14)
}

// головной: погрузка/перемещение
spriteset (dr1_m_2_loading_spriteset, IMAGEFILE)
{
  s15_template(38, 262, 0)
} 

spriteset (dr1_m_2_traveling_spriteset, IMAGEFILE)
{
  s15_template(497, 262, 0)
} 

spritegroup dr1_m_2_spritegroup
{
  loading: dr1_m_2_loading_spriteset;
  loaded: dr1_m_2_traveling_spriteset;
}

// развёрнутая голова
spriteset (dr1_rm_2_spriteset, IMAGEFILE)
{
  s15_template(38, 331, 0)
} 

// прицепной
spriteset (dr1_c_2_spriteset, IMAGEFILE)
{
  s15_template(38, 400, 0)
} 

#undef IMAGEFILE

// Если год постройки >= 1966, используем "дырку"
switch (FEAT_TRAINS, SELF, dr1_c_get_spriteset1,
  build_year >= 1966)
{
  1: dr1_c_2_spriteset;
  dr1_c_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, dr1_c_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  dr1_c_get_spriteset1;
}

// Определяем необходимость разворота моторного вагона по чётности его позиции
// от начала состава
// Если год постройки >= 1966, используем "дырку"
switch (FEAT_TRAINS, SELF, dr1_m_get_spriteset2,
  ((last_computed_result - count_veh_id(dr1_m)) % 2) +
  ((build_year >= 1966) * 2)
)
{
  0: dr1_m_spritegroup;
  1: dr1_rm_spriteset;

  2: dr1_m_2_spritegroup;
  dr1_rm_2_spriteset;
}

switch (FEAT_TRAINS, PARENT, dr1_m_get_spriteset1,
  count_veh_id(dr1_m))
{
  dr1_m_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, dr1_m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  dr1_m_get_spriteset1;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon11,
[
  STORE_TEMP(num_vehs_in_consist-8, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1_m: return CB_RESULT_ATTACH_ALLOW;
  dr1_c: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon10,
  num_vehs_in_consist)
{
  8: return CB_RESULT_ATTACH_ALLOW;
  dr1_m_can_attach_wagon11;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon9,
[
  STORE_TEMP(num_vehs_in_consist-6, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1_m: return CB_RESULT_ATTACH_ALLOW;
  dr1_c: dr1_m_can_attach_wagon10;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon8,
  num_vehs_in_consist)
{
  6: return CB_RESULT_ATTACH_ALLOW;
  dr1_m_can_attach_wagon9;
}

switch (FEAT_TRAINS, SELF, dr1_m_can_attach_wagon7,
  current_year >= 1968)
{
  1: dr1_m_can_attach_wagon8;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon6,
[
  STORE_TEMP(num_vehs_in_consist-4, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1_m: return CB_RESULT_ATTACH_ALLOW;
  dr1_c: dr1_m_can_attach_wagon7;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon5,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1_m_can_attach_wagon6;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon4,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1_m: return CB_RESULT_ATTACH_ALLOW;
  dr1_c: dr1_m_can_attach_wagon5;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  dr1_m_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, dr1_m_can_attach_wagon1,
  count_veh_id(dr1_m) % 2)
{
  1: dr1_m_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, SELF, dr1_m_can_attach_wagon,
  vehicle_type_id)
{
  dr1_m: return CB_RESULT_ATTACH_ALLOW;
  dr1_c: dr1_m_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_m_start_stop,
  count_veh_id(dr1_m) % 2)
{
  0: return CB_RESULT_NO_TEXT;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, dr1_m_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(dr1_m)) % 2)
{
  0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -2,
    DISABLE_WAGON_POWER);
  return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 7,
    DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, PARENT, dr1_m_visual_effect_and_powered,
  count_veh_id(dr1_m))
{
  dr1_m_visual_effect_and_powered1;
}

switch (FEAT_TRAINS, SELF, dr1_m_additional_text,
  (current_year >= 1966) +
  (current_year >= 1968)
)
{
  1: return string(STR_DR1_M_1966_PURCHASE_HINT);
  2: return string(STR_DR1_M_1968_PURCHASE_HINT);
  return string(STR_DR1_M_PURCHASE_HINT);
}

switch (FEAT_TRAINS, SELF, dr1_m_purchase,
  build_year >= 1966)
{
  1: dr1_m_2_purchase_spriteset;
  dr1_m_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, dr1_c_purchase,
  build_year >= 1966)
{
  1: dr1_c_2_purchase_spriteset;
  dr1_c_purchase_spriteset;
}

item (FEAT_TRAINS, dr1_m) 
{
  property
  {
    name: string(STR_DR1_M_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1963, 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 750 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 80;
    weight: 53.5 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.224;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1_m_get_spriteset;
    purchase: dr1_m_purchase;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1_m_can_attach_wagon;
    start_stop: dr1_m_start_stop;
    visual_effect_and_powered: dr1_m_visual_effect_and_powered;
    speed: speed_after20_120to110;
    purchase_speed: purchase_speed_120;
    additional_text: dr1_m_additional_text;
  }
}

item (FEAT_TRAINS, dr1_c) 
{
  property
  {
    name: string(STR_DR1_C_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 128;
    weight: 33.7 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1_c_get_spriteset;
    purchase: dr1_c_purchase;
    articulated_part: articulated_part_dummy7;
  }
}
