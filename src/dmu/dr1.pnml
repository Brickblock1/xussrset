 // dr1
// 208 (D0h) 209 (D1h)

#define IMAGEFILE  "src/dmu/dr1_h.png"
purchase_sprites(dr1_h_v1, 0, 0)
MU_head_middle_sprites(15, dr1_h_b1966_mu, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/dmu/dr1_c.png"
purchase_sprites(dr1_c_v1, 0, 0)
MU_car_sprites(15, dr1_c_b1966_mu, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/dmu/dr1_h-a1966.png"
purchase_sprites(dr1_h_v2, 0, 0)
MU_head_middle_sprites(15, dr1_h_a1966_mu, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/dmu/dr1_c-a1966.png"
purchase_sprites(dr1_c_v2, 0, 0)
MU_car_sprites(15, dr1_c_a1966_mu, 32, 40)
#undef IMAGEFILE
purchase_sprites_select(dr1_h, 1966)
purchase_sprites_select(dr1_c, 1966)

MU_head_direction_template(dr1_h_b1966, dr1_h)
MU_head_direction_template(dr1_h_a1966, dr1_h)
engine_build_year_template(dr1_h, 1966, left, )
engine_build_year_template(dr1_h, 1966, right, )
engine_direction_template2m(dr1_h, dr1_c, )
long_vehicle(dr1_h)

MU_car_direction_template(dr1_c_a1966)
MU_car_direction_template(dr1_c_b1966)
engine_build_year_template(dr1_c, 1966, start, )
long_vehicle_RND(dr1_c, dr1_c_a1966_sprites_start)

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon6,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6D);
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon5,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon6;
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon4,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon5;
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_4D);
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon1,
  LOAD_TEMP(1) == 22)
{
  1: dr1_c_can_attach_wagon4;
     dr1_c_can_attach_wagon2;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon_h,
  count_veh_id(dr1_h) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon1;
  dr1_h: dr1_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1_can_attach_wagon_length3,
  count_veh_id(dr1_h) > 1)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon3,
  vehicle_type_id)
{
  dr1_h: dr1_can_attach_wagon_length3;
  dr1_c: dr1_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon2,
[ STORE_TEMP(
             (LOAD_TEMP(0)>=1966) +
             (LOAD_TEMP(0)>=1968) +
             ((build_year>=1966) *10) +
             ((build_year>=1968) *10), 1) ])
{
   0: dr1_can_attach_wagon3;
   1: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-1965","1966-1967");
   2: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-1965","1968-...");
  10: return string(STR_CAN_ATTACH_WRONG_YEAR,"1966-1967","...-1965");
  11: dr1_can_attach_wagon3;
  12: return string(STR_CAN_ATTACH_WRONG_YEAR,"1966-1967","1968-...");
  20: return string(STR_CAN_ATTACH_WRONG_YEAR,"1968-...","...-1965");
  21: return string(STR_CAN_ATTACH_WRONG_YEAR,"1968-...","1966-1967");
  22: dr1_can_attach_wagon3;
      return string(STR_CAN_ATTACH_WRONG_YEAR,"2000","2000");
}

switch (FEAT_TRAINS, PARENT, dr1_can_attach_wagon1,
[  STORE_TEMP(build_year, 0) ])
{
  dr1_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon,
  vehicle_type_id)
{
  dr1_h: dr1_can_attach_wagon1;
  dr1_c: dr1_can_attach_wagon1;
         return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1_h_start_stop,
  count_veh_id(dr1_h) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

engine_diesel_1r_effect(dr1_h, -2, 7, 12)

switch (FEAT_TRAINS, SELF, dr1_h_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(dr1_h)) % 2)
{
  0: dr1_h_create_effect_sprites_left;
     dr1_h_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, dr1_h_create_effect0,
  STORE_TEMP(count_veh_id(dr1_h), 0))
{
  dr1_h_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - dr1_h
switch (FEAT_TRAINS, PARENT, dr1_h_create_effect,
  vehicle_type_id == dr1_h)
{
  1: dr1_h_create_effect0;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - dr1_h
switch (FEAT_TRAINS, PARENT, dr1_h_power,
  vehicle_type_id == dr1_h )
{
  1: return get_power_in_cb(750);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - dr1_h
switch (FEAT_TRAINS, PARENT, dr1_h_cargo_capacity,
  vehicle_type_id == dr1_h )
{
  1: return 80;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1_c_cargo_capacity,
  vehicle_type_id == dr1_h )
{
  1: return 128;
     return 0;
}

switch (FEAT_TRAINS, SELF, dr1_long_cargo_subtype_text,
  build_year >= 1966)
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_BALTICA), "");
     return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_DYRKA), "");
}

livery_template_longnames_only(dr1)

general_running_cost(dr1_h_v1, 168)

switch (FEAT_TRAINS, PARENT, dr1_h_running_cost_factor,
  vehicle_type_id == dr1_h)
{
  1: dr1_h_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1_c_v1, 45)

switch (FEAT_TRAINS, PARENT, dr1_c_running_cost_factor,
  vehicle_type_id == dr1_h)
{
  1: dr1_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

switch (FEAT_TRAINS, SELF, dr1_h_additional_text,
  (current_year >= 1966) +
  (current_year >= 1968))
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H),
                   string(STR_SECTIONS_DMU_2_4),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  1: return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H),
                   string(STR_SECTIONS_DMU_2_4),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW),
                   string(STR_PURCHASE_HINT_ATTACH_YEAR,"...-1965","1966-..."));
     return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H),
                   string(STR_SECTIONS_DMU_2_6),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW),
                   string(STR_PURCHASE_HINT_ATTACH_YEAR,"...-1967","1968-..."));
}

switch (FEAT_TRAINS, SELF, dr1_c_additional_text,
  (current_year >= 1966) +
  (current_year >= 1968))
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C),
                   string(STR_SECTIONS_DMU_2_4),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  1: return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C),
                   string(STR_SECTIONS_DMU_2_4),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW),
                   string(STR_PURCHASE_HINT_ATTACH_YEAR,"...-1965","1966-..."));
     return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C),
                   string(STR_SECTIONS_DMU_2_6),
                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1969),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW),
                   string(STR_PURCHASE_HINT_ATTACH_YEAR,"...-1967","1968-..."));
}

item (FEAT_TRAINS, dr1_h, 208) {
  property {
    name: string(STR_NAME_DR1);
    vehicle_dates(1963, 1969, 25, 10, 8, 21)
    vehicle_dmu(750, 53.5, 82.8, 80, 48)
  }
  graphics {
    default: dr1_h_sprites;
    purchase: dr1_h_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1_can_attach_wagon;
    start_stop: dr1_h_start_stop;
    create_effect: dr1_h_create_effect;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    cargo_subtype_text: dr1_cargo_subtype_text;
    additional_text: dr1_h_additional_text;
    power: dr1_h_power;
    cargo_capacity: dr1_h_cargo_capacity;
    colour_mapping: any_cc_colour;
  }
}

item (FEAT_TRAINS, dr1_c, 209) {
  property {
    name: string(STR_NAME_DR1);
    vehicle_dates(1963, 1969, 25, 10, 8, 6)
    vehicle_dmu_c(33.7, 128, 48)
  }
  graphics {
    default: dr1_c_sprites;
    purchase: dr1_c_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    can_attach_wagon: dr1_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_subtype_text: dr1_cargo_subtype_text;
    additional_text: dr1_c_additional_text;
    cargo_capacity: dr1_c_cargo_capacity;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(dr1_h, STR_LONGNAME_DR1_H)
long_name_template(dr1_c, STR_LONGNAME_DR1_C)
allow_dmu(dr1_h)
allow_dmu(dr1_c)
