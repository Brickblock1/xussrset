 // dr1

#define IMAGEFILE  "src/dmu/dr1.png"
purchase_sprites(dr1_v1_h, 0, 0)
purchase_sprites(dr1_v1_c, 120, 0)

MU_4r_0_0_sprites(15, dr1_v1, 32, 40)
MU_0_0_2_sprites(15,  dr1_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1-s1966.png"
purchase_sprites(dr1_v1u_h, 0, 0)
purchase_sprites(dr1_v1u_c, 120, 0)

MU_4r_0_0_sprites(15, dr1_v1u, 32, 40)
MU_0_0_2_sprites(15,  dr1_v1u, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dr1_h_purchase_sprites,
  build_year >= 1966)
{
  1: dr1_v1u_h_purchase_sprites;
     dr1_v1_h_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, dr1_c_purchase_sprites,
  build_year >= 1966)
{
  1: dr1_v1u_c_purchase_sprites;
     dr1_v1_c_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, dr1_h_sprites_left,
  ((LOAD_TEMP(4) == dr1_h) *
  (1 + (position_in_consist == 0)) * 100) +
  (build_year >= 1966)) 
{
    0: dr1_v1_h_notpowered_sprites_left;
    1: dr1_v1u_h_notpowered_sprites_left;
  100: dr1_v1_h_notpowered_normal_sprites_left;
  101: dr1_v1u_h_notpowered_normal_sprites_left;
  200: dr1_v1_h_sprites_left;
  201: dr1_v1u_h_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1_h_sprites_right,
  ((LOAD_TEMP(4) == dr1_h) *
  (1 + (position_in_consist_from_end == 1)) * 100) +
  (build_year >= 1966))
{
    0: dr1_v1_h_notpowered_sprites_right;
    1: dr1_v1u_h_notpowered_sprites_right;
  100: dr1_v1_h_notpowered_normal_sprites_right;
  101: dr1_v1u_h_notpowered_normal_sprites_right;
  200: dr1_v1_h_sprites_right;
  201: dr1_v1u_h_sprites_right;
       align_15_sprites;
}

engine_direction_template2m(dr1_h, dr1_c, )

long_vehicle(dr1_h)

switch (FEAT_TRAINS, SELF, dr1_c_sprites_left,
  build_year >= 1966)
{
  1: dr1_v1u_c_sprites_left;
     dr1_v1_c_sprites_left;
}

switch (FEAT_TRAINS, SELF, dr1_c_sprites_right,
  build_year >= 1966)
{
  1: dr1_v1u_c_sprites_right;
     dr1_v1_c_sprites_right;
}

random_switch (FEAT_TRAINS, SELF, dr1_c_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: dr1_c_sprites_left;
  1: dr1_c_sprites_right;
}

long_vehicle_RND(dr1_c, dr1_c_sprites_start)

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon6,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6D);
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon5,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon6;
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon4,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon5;
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_4D);
         return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon1,
  LOAD_TEMP(1) == 22)
{
  1: dr1_c_can_attach_wagon4;
     dr1_c_can_attach_wagon2;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon_h,
  count_veh_id(dr1_h) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1_c_can_attach_wagon,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1_c: dr1_c_can_attach_wagon1;
  dr1_h: dr1_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1_can_attach_wagon_length3,
  count_veh_id(dr1_h) > 1)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon3,
  vehicle_type_id)
{
  dr1_h: dr1_can_attach_wagon_length3;
  dr1_c: dr1_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon2,
[ STORE_TEMP(
             (LOAD_TEMP(0)>=1966) +
             (LOAD_TEMP(0)>=1968) +
             ((build_year>=1966) *10) +
             ((build_year>=1968) *10), 1) ])
{
   0: dr1_can_attach_wagon3;
   1: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-1965","1966-1967");
   2: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-1965","1968-...");
  10: return string(STR_CAN_ATTACH_WRONG_YEAR,"1966-1967","...-1965");
  11: dr1_can_attach_wagon3;
  12: return string(STR_CAN_ATTACH_WRONG_YEAR,"1966-1967","1968-...");
  20: return string(STR_CAN_ATTACH_WRONG_YEAR,"1968-...","...-1965");
  21: return string(STR_CAN_ATTACH_WRONG_YEAR,"1968-...","1966-1967");
  22: dr1_can_attach_wagon3;
      return string(STR_CAN_ATTACH_WRONG_YEAR,"2000","2000");
}

switch (FEAT_TRAINS, PARENT, dr1_can_attach_wagon1,
[  STORE_TEMP(build_year, 0) ])
{
  dr1_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, dr1_can_attach_wagon,
  vehicle_type_id)
{
  dr1_h: dr1_can_attach_wagon1;
  dr1_c: dr1_can_attach_wagon1;
         return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1_h_start_stop,
  count_veh_id(dr1_h) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

engine_diesel_1r_effect(dr1_h, -2, 7, 12)  

switch (FEAT_TRAINS, SELF, dr1_h_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(dr1_h)) % 2)
{
  0: dr1_h_create_effect_sprites_left;
     dr1_h_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, dr1_h_create_effect0,
  STORE_TEMP(count_veh_id(dr1_h), 0))
{
  dr1_h_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - dr1_h 
switch (FEAT_TRAINS, PARENT, dr1_h_create_effect,
  vehicle_type_id == dr1_h)
{
  1: dr1_h_create_effect0; 
     return disable_create_effect();
} 

// Даём тягу, только если локомотив - dr1_h 
switch (FEAT_TRAINS, PARENT, dr1_h_power,
  vehicle_type_id == dr1_h )
{
  1: return get_power_in_cb(750); 
     return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - dr1_h 
switch (FEAT_TRAINS, PARENT, dr1_h_cargo_capacity,
  vehicle_type_id == dr1_h )
{
  1: return 80;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1_c_cargo_capacity,
  vehicle_type_id == dr1_h )
{
  1: return 128;
     return 0;
}

switch (FEAT_TRAINS, SELF, dr1_h_additional_text,
  (current_year >= 1966) +
  (current_year >= 1968) )
{
  0: return string(STR_DR1_H_PURCHASE_HINT, string(STR_SECTIONS_DMU_2_4));
  1: return string(STR_DR1_H_1966_PURCHASE_HINT, string(STR_SECTIONS_DMU_2_4));
     return string(STR_DR1_H_1968_PURCHASE_HINT, string(STR_SECTIONS_DMU_2_6));
}

switch (FEAT_TRAINS, SELF, dr1_cargo_subtype_text,
  (build_year >= 1966) )
{
  0: return string(STR_REFIT_FACTORY_BALTICA, "");
     string(STR_REFIT_FACTORY_DYRKA, "");
}

general_running_cost(dr1_h_v1, 203)

switch (FEAT_TRAINS, PARENT, dr1_h_running_cost_factor,
  vehicle_type_id == dr1_h)
{
  1: dr1_h_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1_c_v1, 55)

switch (FEAT_TRAINS, PARENT, dr1_c_running_cost_factor,
  vehicle_type_id == dr1_h)
{
  1: dr1_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

item (FEAT_TRAINS, dr1_h) {
  property {
    name: string(STR_DR1_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1963), 1, 1);
    model_life: get_model_life(1963,1969);
    retire_early: get_retire_early(1963,1969);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 48; 
    cost_factor: 25;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 750 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 80;
    weight: 53.5 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(120.0, 53.5);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: dr1_h_sprites;
    purchase: dr1_h_purchase_sprites;
    cargo_age_period: return 50;
    running_cost_factor: dr1_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1_can_attach_wagon;
    start_stop: dr1_h_start_stop;
    create_effect: dr1_h_create_effect;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    cargo_subtype_text: dr1_cargo_subtype_text;
    additional_text: dr1_h_additional_text;
    power: dr1_h_power;
    cargo_capacity: dr1_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, dr1_c) {
  property {
    name: string(STR_DR1_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: get_model_life(1963,1969);
    retire_early: get_retire_early(1963,1969);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 48;
    cost_factor: 7;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 128;
    weight: 33.7 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_NONE;
  }
  graphics {
    default: dr1_c_sprites;
    purchase: dr1_c_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    can_attach_wagon: dr1_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_subtype_text: dr1_cargo_subtype_text;
    additional_text: dr1_h_additional_text;
    cargo_capacity: dr1_c_cargo_capacity;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, dr1_h) {
    property { name: string(STR_DR1_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, dr1_c) {
    property { name: string(STR_DR1_C_NAME_LONG); }
  }
}

