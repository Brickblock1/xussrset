 // dr1a                     
// 212 (D4h) 213 (D5h) 214 (D6h)

#define IMAGEFILE  "src/dmu/dr1a.png"
purchase_sprites(dr1a_h, 0, 0)
purchase_sprites(dr1a_c, 120, 0)

MU_4r_0_0_sprites(15, dr1a_v1, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-ldz.png"
purchase_sprites(dr1a_n, 240, 0)

MU_4r_0_0_sprites(15, dr1a_v2, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v2, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v2_n, 32, 576)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-ldzs.png"
MU_4r_0_0_sprites(15, dr1a_v3, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v3, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v3_n, 32, 576)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-lg.png"
MU_4r_0_0_sprites(15, dr1a_v4, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v4, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v4u, 32, 576)
MU_4r_0_0_sprites(15, dr1a_v4_n, 32, 1112)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-bch.png"
MU_4r_0_0_sprites(15, dr1a_v5, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v5, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v5_n, 32, 576)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dr1a_h_sprites_left,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
    (1 + (position_in_consist == 0)) * 100) +
  ((LOAD_TEMP(2) >= date(1994,1,1)) * 50) +
  cargo_subtype)
{
    0: dr1a_v1_h_notpowered_sprites_left;
    1: dr1a_v2_h_notpowered_sprites_left;
    2: dr1a_v3_h_notpowered_sprites_left;
    3: dr1a_v4_h_notpowered_sprites_left;
    4: dr1a_v5_h_notpowered_sprites_left;
   50: dr1a_v1_h_notpowered_sprites_left;
   51: dr1a_v2_h_notpowered_sprites_left;
   52: dr1a_v3_h_notpowered_sprites_left;
   53: dr1a_v4u_h_notpowered_sprites_left;
   54: dr1a_v5_h_notpowered_sprites_left;

  100: dr1a_v1_h_notpowered_normal_sprites_left;
  101: dr1a_v2_h_notpowered_normal_sprites_left;
  102: dr1a_v3_h_notpowered_normal_sprites_left;
  103: dr1a_v4_h_notpowered_normal_sprites_left;
  104: dr1a_v5_h_notpowered_normal_sprites_left;
  150: dr1a_v1_h_notpowered_normal_sprites_left;
  151: dr1a_v2_h_notpowered_normal_sprites_left;
  152: dr1a_v3_h_notpowered_normal_sprites_left;
  153: dr1a_v4u_h_notpowered_normal_sprites_left;
  154: dr1a_v5_h_notpowered_normal_sprites_left;
  
  200: dr1a_v1_h_sprites_left;
  201: dr1a_v2_h_sprites_left;
  202: dr1a_v3_h_sprites_left;
  203: dr1a_v4_h_sprites_left;
  204: dr1a_v5_h_sprites_left;
  250: dr1a_v1_h_sprites_left;
  251: dr1a_v2_h_sprites_left;
  252: dr1a_v3_h_sprites_left;
  253: dr1a_v4u_h_sprites_left;
  254: dr1a_v5_h_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_h_sprites_right,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
    (1 + (position_in_consist_from_end == 1)) * 100) +
  ((LOAD_TEMP(2) >= date(1994,1,1)) * 50) +
  cargo_subtype)
{  
    0: dr1a_v1_h_notpowered_sprites_right;
    1: dr1a_v2_h_notpowered_sprites_right;
    2: dr1a_v3_h_notpowered_sprites_right;
    3: dr1a_v4_h_notpowered_sprites_right;
    4: dr1a_v5_h_notpowered_sprites_right;
   50: dr1a_v1_h_notpowered_sprites_right;
   51: dr1a_v2_h_notpowered_sprites_right;
   52: dr1a_v3_h_notpowered_sprites_right;
   53: dr1a_v4u_h_notpowered_sprites_right;
   54: dr1a_v5_h_notpowered_sprites_right;
     
  100: dr1a_v1_h_notpowered_normal_sprites_right;
  101: dr1a_v2_h_notpowered_normal_sprites_right;
  102: dr1a_v3_h_notpowered_normal_sprites_right;
  103: dr1a_v4_h_notpowered_normal_sprites_right;
  104: dr1a_v5_h_notpowered_normal_sprites_right;
  150: dr1a_v1_h_notpowered_normal_sprites_right;
  151: dr1a_v2_h_notpowered_normal_sprites_right;
  152: dr1a_v3_h_notpowered_normal_sprites_right;
  153: dr1a_v4u_h_notpowered_normal_sprites_right;
  154: dr1a_v5_h_notpowered_normal_sprites_right;
  
  200: dr1a_v1_h_sprites_right;
  201: dr1a_v2_h_sprites_right;
  202: dr1a_v3_h_sprites_right;
  203: dr1a_v4_h_sprites_right;
  204: dr1a_v5_h_sprites_right;
  250: dr1a_v1_h_sprites_right;
  251: dr1a_v2_h_sprites_right;
  252: dr1a_v3_h_sprites_right;
  253: dr1a_v4u_h_sprites_right;
  254: dr1a_v5_h_sprites_right;
       align_15_sprites;
}

engine_direction_template3m(dr1a_h, dr1a_n, dr1a_c, )

long_vehicle(dr1a_h)

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_left,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
  (1 + (position_in_consist == 0)) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_notpowered_sprites_left;
    1: dr1a_v3_n_h_notpowered_sprites_left;
    2: dr1a_v4_n_h_notpowered_sprites_left;
    3: dr1a_v5_n_h_notpowered_sprites_left;
    4: dr1a_v5_n_h_notpowered_sprites_left; // ДРБ1

  100: dr1a_v2_n_h_notpowered_normal_sprites_left;
  101: dr1a_v3_n_h_notpowered_normal_sprites_left;
  102: dr1a_v4_n_h_notpowered_normal_sprites_left;
  103: dr1a_v5_n_h_notpowered_normal_sprites_left;
  104: dr1a_v5_n_h_notpowered_normal_sprites_left; // ДРБ1

  200: dr1a_v2_n_h_sprites_left;
  201: dr1a_v3_n_h_sprites_left;
  202: dr1a_v4_n_h_sprites_left;
  203: dr1a_v5_n_h_sprites_left;
  204: dr1a_v5_n_h_sprites_left; // ДРБ1
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_right,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
  (1 + (position_in_consist_from_end == 1)) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_notpowered_sprites_right;
    1: dr1a_v3_n_h_notpowered_sprites_right;
    2: dr1a_v4_n_h_notpowered_sprites_right;
    3: dr1a_v5_n_h_notpowered_sprites_right;
    4: dr1a_v5_n_h_notpowered_sprites_right; // ДРБ1

  100: dr1a_v2_n_h_notpowered_normal_sprites_right;
  101: dr1a_v3_n_h_notpowered_normal_sprites_right;
  102: dr1a_v4_n_h_notpowered_normal_sprites_right;
  103: dr1a_v5_n_h_notpowered_normal_sprites_right;
  104: dr1a_v5_n_h_notpowered_normal_sprites_right; // ДРБ1

  200: dr1a_v2_n_h_sprites_right;
  201: dr1a_v3_n_h_sprites_right;
  202: dr1a_v4_n_h_sprites_right;
  203: dr1a_v5_n_h_sprites_right;
  204: dr1a_v5_n_h_sprites_right; // ДРБ1
       align_15_sprites;
}

engine_direction_template3m(dr1a_n, dr1a_h, dr1a_c, )

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_DRB1,
  ((position_in_consist != 0) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_sprites_left;
    1: dr1a_v3_n_h_sprites_left;
    2: dr1a_v4_n_h_sprites_left;
    3: dr1a_v5_n_h_sprites_left;
    4: dr1a_v5_n_h_sprites_left; // ДРБ1

  100: dr1a_v2_n_h_sprites_right;
  101: dr1a_v3_n_h_sprites_right;
  102: dr1a_v4_n_h_sprites_right;
  103: dr1a_v5_n_h_sprites_right;
  104: dr1a_v5_n_h_sprites_right; // ДРБ1
       align_15_sprites;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_sprites_start_sp,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(_3m62u) * 2) == 0) && 
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1) )
{
  1: dr1a_n_sprites_DRB1;
     dr1a_n_sprites_start;
}

long_vehicle_sp(dr1a_n)

switch (FEAT_TRAINS, SELF, dr1a_c_sprites_left,
  cargo_subtype)
{
  0: dr1a_v1_c_sprites_left;
  1: dr1a_v2_c_sprites_left;
  2: dr1a_v3_c_sprites_left;
  3: dr1a_v4_c_sprites_left;
  4: dr1a_v5_c_sprites_left;
  5: dr1a_v5_c_sprites_left; // ДРБ1
     align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_c_sprites_right,
  cargo_subtype)
{
  0: dr1a_v1_c_sprites_right;
  1: dr1a_v2_c_sprites_right;
  2: dr1a_v3_c_sprites_right;
  3: dr1a_v4_c_sprites_right;
  4: dr1a_v5_c_sprites_right;
  5: dr1a_v5_c_sprites_right; // ДРБ1
     align_15_sprites;
}

random_switch (FEAT_TRAINS, SELF, dr1a_c_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: dr1a_c_sprites_left;
  1: dr1a_c_sprites_right;
}

long_vehicle_RND(dr1a_c, dr1a_c_sprites_start)

switch (FEAT_TRAINS, PARENT, dr1a_can_attach_wagon_length,
  (count_veh_id(dr1a_h) + count_veh_id(dr1a_n)) > 3)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon3,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1a_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6DN);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon3;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon1,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon2;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon_h,
  (count_veh_id(dr1a_h) + count_veh_id(_2m62u) + count_veh_id(_3m62u) + count_veh_id(dr1a_n)) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1a_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon,
[ set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon1;
  dr1a_n: dr1a_c_can_attach_wagon_h;
  dr1a_h: dr1a_c_can_attach_wagon_h;
  _2m62u: dr1a_c_can_attach_wagon_h;
  _3m62u: dr1a_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon_count2,
  count_veh_id(dr1a_h) >= count_veh_id(dr1a_n))
{
  1: dr1a_can_attach_wagon_length;
     return string(STR_CAN_ATTACH_MHEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon_count1,
[ set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: return CB_RESULT_ATTACH_ALLOW;
          dr1a_n_can_attach_wagon_count2;
}                                        

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon5,
[ set_offset_to(num_vehs_in_consist-10),
  prev_vehicle_type_id() ])
{
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon4,
[ set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon5;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon3,
[ set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon4;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon2,
[ set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon3;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon1,
[ set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon2;
  dr1a_n: dr1a_n_can_attach_wagon_count1;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, drb1_can_attach_wagon1,
  count_veh_id(dr1a_n) + count_veh_id(dr1a_h))
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon,
  count_veh_id(_2m62u) + count_veh_id(_3m62u))
{
  0: dr1a_n_can_attach_wagon1;
  1: drb1_can_attach_wagon1; 
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon,
  count_veh_id(_2m62u) + count_veh_id(_3m62u))
{
  0: dr1a_can_attach_wagon_length;
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, drb1_can_attach_wagon,
  (count_veh_id(dr1a_h) * 2) +
  count_veh_id(dr1a_n) + 
  count_veh_id(_2m62u) + 
  count_veh_id(_3m62u))
{
  0..1: return CB_RESULT_ATTACH_ALLOW;
        return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, SELF, dr1a_can_attach_wagon,
  vehicle_type_id)
{
  dr1a_h: dr1a_h_can_attach_wagon;
  dr1a_n: dr1a_n_can_attach_wagon;
  dr1a_c: dr1a_c_can_attach_wagon;
  _2m62u: drb1_can_attach_wagon;
  _3m62u: drb1_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_start_stop,
  (count_veh_id(dr1a_h) + count_veh_id(dr1a_n) + count_veh_id(_2m62u) + count_veh_id(_3m62u)) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

engine_diesel_1r_effect(dr1a_h, -2, 7, 12)  

switch (FEAT_TRAINS, SELF, dr1a_h_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(dr1a_h) - count_veh_id(dr1a_n)) % 2)
{
  0: dr1a_h_create_effect_sprites_left;
     dr1a_h_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_create_effect0,
  STORE_TEMP(count_veh_id(dr1a_h) + count_veh_id(dr1a_n), 0))
{
  dr1a_h_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - dr1a_h или dr1a_n
switch (FEAT_TRAINS, PARENT, dr1a_h_create_effect,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: dr1a_h_create_effect0; 
     return disable_create_effect();
} 

// Даём тягу, только если локомотив - dr1a_h или dr1a_n
switch (FEAT_TRAINS, PARENT, dr1a_h_power,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: return get_power_in_cb(800); 
     return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - dr1a_h или dr1a_n или 2[3]m62u
switch (FEAT_TRAINS, PARENT, dr1a_h_cargo_capacity,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: return 68;
     return 0;
}

switch (FEAT_TRAINS, SELF, dr1a_n_cargo_capacity_drb1,
  cargo_subtype == 4)
{
  1: return 96;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_cargo_capacity,
  (((((cargo_subtype == LV_BELARUSSIAN_RAILWAY_DRB) && ((vehicle_type_id == _2m62u) || (vehicle_type_id == _3m62u))) ||
    ((vehicle_type_id == dr1a_n) && (cargo_subtype == 4))) && 
    is_drb1sh() && 
    ((count_veh_id(_2m62u) + count_veh_id(_3m62u) + count_veh_id(dr1a_n)) == 2) && 
    (count_veh_id(dr1a_h) == 0) && ((count_veh_id(dr1a_c) + (count_veh_id(dr1a_n))) <= 10)) * 2) +                        // ДРБ1
  (((count_veh_id(_2m62u) + count_veh_id(_3m62u)) == 0) && ((vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n)))) // Не ДРБ1
{
  0: return 0;
  1: return 96;
     dr1a_n_cargo_capacity_drb1;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_capacity_drb1,
  cargo_subtype == 5)
{
  1: return 124;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_cargo_capacity,
  (((((cargo_subtype == LV_BELARUSSIAN_RAILWAY_DRB) && ((vehicle_type_id == _2m62u) || (vehicle_type_id == _3m62u))) ||
    ((vehicle_type_id == dr1a_n) && (cargo_subtype == 4))) && 
    is_drb1sh() && 
    ((count_veh_id(_2m62u) + count_veh_id(_3m62u) + count_veh_id(dr1a_n)) == 2) && 
    (count_veh_id(dr1a_h) == 0) && ((count_veh_id(dr1a_c) + (count_veh_id(dr1a_n))) <= 10)) * 2) +                        // ДРБ1
  (((count_veh_id(_2m62u) + count_veh_id(_3m62u)) == 0) && ((vehicle_type_id == dr1a_h) ||(vehicle_type_id == dr1a_n) ||  // Не ДРБ1
   (vehicle_type_id == dr1a_c))))                                                                                         // одинокий вагон
{
  0: return 0;
  1: return 124;
     dr1a_c_cargo_capacity_drb1;
}

switch (FEAT_TRAINS, SELF, dr1a_n_bitmask_vehicle_info_drb1,
  (position_in_consist_from_end == 1) && (cargo_subtype == 4))
{
  1: return FLAG_DRB1_SH;
     return 0;
}

switch (FEAT_TRAINS, SELF, dr1a_n_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ));
  1: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ_STAR));
  2: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LG));
  3: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY));
  4: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY_DDB1_DRB1));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text2,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
     string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text1_1,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text1_2,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text1_3,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text1,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: dr1a_h_cargo_subtype_text1_1;
  1: dr1a_h_cargo_subtype_text1_2;
     dr1a_h_cargo_subtype_text1_3;
}

switch (FEAT_TRAINS, SELF, dr1a_h_cargo_subtype_text,
  ((current_year >= 1990) || (build_year >=1990)) )
{
  1: dr1a_h_cargo_subtype_text1;
     dr1a_h_cargo_subtype_text2;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text2,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
     string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text1_1,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_BELARUSSIAN_RAILWAY));
  5: return string(STR_REFIT_FACTORY_63323, string(STR_REFIT_BELARUSSIAN_RAILWAY_DDB1_DRB1));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text1_2,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_BELARUSSIAN_RAILWAY));
  5: return string(STR_REFIT_FACTORY_63341, string(STR_REFIT_BELARUSSIAN_RAILWAY_DDB1_DRB1));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text1_3,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
  1: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ));
  2: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ_STAR));
  3: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LG));
  4: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY));
  5: return string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY_DDB1_DRB1));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text1,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: dr1a_c_cargo_subtype_text1_1;
  1: dr1a_c_cargo_subtype_text1_2;
     dr1a_c_cargo_subtype_text1_3;
}

switch (FEAT_TRAINS, SELF, dr1a_c_cargo_subtype_text,
  ((current_year >= 1990) || (build_year >=1990)) )
{
  1: dr1a_c_cargo_subtype_text1;
     dr1a_c_cargo_subtype_text2;
}

general_running_cost(dr1a_h_v1, 179)
general_running_cost(dr1a_h_v2, 177)

switch (FEAT_TRAINS, PARENT, dr1a_h_running_cost_factor,
  ((vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n)) * (1 + (build_year >= 1985)))
{
  1: dr1a_h_v1_running_cost_factor;
  2: dr1a_h_v2_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1a_c_v1, 48)

switch (FEAT_TRAINS, PARENT, dr1a_c_running_cost_factor,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n))
{
  1: dr1a_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1a_n_v1, 113)

switch (FEAT_TRAINS, PARENT, dr1a_n_running_cost_factor,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n))
{
  1: dr1a_n_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

switch (FEAT_TRAINS, SELF, dr1a_h_additional_text,
  (current_year >= 1978) +
  (current_year >= 1985) +
  (current_year >= USSREND) +
  (current_year >= 1994) )
{ 
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_324)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1976,1977),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  1: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_642)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1978,1984),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  2: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_350)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1978,1998),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  3: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_350)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1978,1998),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
     return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_350)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1978,1998),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
}

switch (FEAT_TRAINS, SELF, dr1a_c_additional_text,
  (current_year >= 1978) +
  (current_year >= 1985) +
  (current_year >= USSREND) +
  (current_year >= 1994) )
{ 
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_325)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1976,1977),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  1: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_643)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1978,1984),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  2: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_351)),
                   string(STR_SECTIONS_DMU_2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1985,2008),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  3: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_351)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1985,2008),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
     return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_351)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1985,2008),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR),
                   string(STR_PURCHASE_HINT_DRB1));
}

switch (FEAT_TRAINS, SELF, dr1a_n_additional_text,
  current_year >= 1994)
{ 
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_N_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_352)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1991, 2008),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
     return string(STR_PURCHASE_HINT_ENGINE_MU_WITH_COMMENT,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_N_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_352)),
                   string(STR_SECTIONS_DMU_N2_6_M2),
                   string(STR_PURCHASE_HINT_FROMTO, 1991, 2008),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR),
                   string(STR_PURCHASE_HINT_DRB1));
}

engine_weight_change_build_after(dr1a_h, 60, 
                                         58, 1985)       
engine_weight_change_build_after(dr1a_c, 38, 
                                         37, 1977)       

item (FEAT_TRAINS, dr1a_h, 212) {
  property {
    name: string(STR_NAME_DR1A);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1976), 1, 1);
    model_life: get_model_life(1976,1998);
    retire_early: get_retire_early(1976,1998);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 48; 
    cost_factor: 22;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_normal_track_type_dmu();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 800 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 68;
    weight: 60 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(85.0, 60);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_DIESEL;
  }
  graphics {
    default: dr1a_h_sprites;
    purchase: dr1a_h_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1a_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1a_can_attach_wagon;
    start_stop: dr1a_h_start_stop;
    create_effect: dr1a_h_create_effect;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    additional_text: dr1a_h_additional_text;
    cargo_subtype_text: dr1a_h_cargo_subtype_text;
    power: dr1a_h_power;
    cargo_capacity: dr1a_h_cargo_capacity;
    weight: dr1a_h_weight;
  }
}

item (FEAT_TRAINS, dr1a_n, 213) {
  property {
    name: string(STR_NAME_DR1A);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1991), 1, 1);
    model_life: get_model_life(1991,2008);
    retire_early: get_retire_early(1991,2008);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 48;
    cost_factor: 14;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_normal_track_type_dmu();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 96;
    weight: 40 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_NONE;
  }
  graphics {
    default: dr1a_n_sprites;
    purchase: dr1a_n_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1a_n_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1a_can_attach_wagon;
    start_stop: dr1a_h_start_stop;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    purchase_cargo_capacity: return 96;
    additional_text: dr1a_n_additional_text;
    cargo_subtype_text: dr1a_n_cargo_subtype_text;
    cargo_capacity: dr1a_n_cargo_capacity;
    bitmask_vehicle_info: dr1a_n_bitmask_vehicle_info_drb1;
  }
}

item (FEAT_TRAINS, dr1a_c, 214) {
  property {
    name: string(STR_NAME_DR1A);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1976), 1, 1);
    model_life: get_model_life(1976,2008);
    retire_early: get_retire_early(1976,2008);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 48;
    cost_factor: 7;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_normal_track_type_dmu();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 124;
    weight: 38 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_NONE;
  }
  graphics {
    default: dr1a_c_sprites;
    purchase: dr1a_c_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1a_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    cargo_subtype_text: dr1a_c_cargo_subtype_text;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    purchase_cargo_capacity: return 124;
    power: return 0;
    can_attach_wagon: dr1a_can_attach_wagon; 
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: dr1a_c_additional_text;
    cargo_capacity: dr1a_c_cargo_capacity;
    weight: dr1a_c_weight;
  }
}

long_name_template(dr1a_h, STR_LONGNAME_DR1A_H)
long_name_template(dr1a_n, STR_LONGNAME_DR1A_N)
long_name_template(dr1a_c, STR_LONGNAME_DR1A_C)
allow_dmu(dr1a_h)
allow_dmu(dr1a_n)
allow_dmu(dr1a_c)
