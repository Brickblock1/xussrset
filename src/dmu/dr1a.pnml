 // dr1a                     

// Спрайты

#define IMAGEFILE  "src/dmu/dr1a.png"
purchase_sprites(dr1a_h, 0, 0)
purchase_sprites(dr1a_c, 120, 0)

MU_4r_0_0_sprites(15, dr1a_v1, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-ldz.png"
purchase_sprites(dr1a_n, 240, 0)

MU_4r_0_0_sprites(15, dr1a_v2, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v2, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v2_n, 32, 576)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-ldzs.png"
MU_4r_0_0_sprites(15, dr1a_v3, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v3, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v3_n, 32, 576)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-lg.png"
MU_4r_0_0_sprites(15, dr1a_v4, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v4, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v4u, 32, 576)
MU_4r_0_0_sprites(15, dr1a_v4_n, 32, 1112)
#undef IMAGEFILE

#define IMAGEFILE  "src/dmu/dr1a-bch.png"
MU_4r_0_0_sprites(15, dr1a_v5, 32, 40)
MU_0_0_2_sprites(15,  dr1a_v5, 32, 40)
MU_4r_0_0_sprites(15, dr1a_v5_n, 32, 576)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dr1a_h_sprites_l,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
    (1 + (position_in_consist == 0)) * 100) +
  ((LOAD_TEMP(2) >= date(1994,1,1)) * 50) +
  cargo_subtype)
{
    0: dr1a_v1_h_notpowered_sprites_left;
    1: dr1a_v2_h_notpowered_sprites_left;
    2: dr1a_v3_h_notpowered_sprites_left;
    3: dr1a_v4_h_notpowered_sprites_left;
    4: dr1a_v5_h_notpowered_sprites_left;
   50: dr1a_v1_h_notpowered_sprites_left;
   51: dr1a_v2_h_notpowered_sprites_left;
   52: dr1a_v3_h_notpowered_sprites_left;
   53: dr1a_v4u_h_notpowered_sprites_left;
   54: dr1a_v5_h_notpowered_sprites_left;

  100: dr1a_v1_h_notpowered_normal_sprites_left;
  101: dr1a_v2_h_notpowered_normal_sprites_left;
  102: dr1a_v3_h_notpowered_normal_sprites_left;
  103: dr1a_v4_h_notpowered_normal_sprites_left;
  104: dr1a_v5_h_notpowered_normal_sprites_left;
  150: dr1a_v1_h_notpowered_normal_sprites_left;
  151: dr1a_v2_h_notpowered_normal_sprites_left;
  152: dr1a_v3_h_notpowered_normal_sprites_left;
  153: dr1a_v4u_h_notpowered_normal_sprites_left;
  154: dr1a_v5_h_notpowered_normal_sprites_left;
  
  200: dr1a_v1_h_sprites_left;
  201: dr1a_v2_h_sprites_left;
  202: dr1a_v3_h_sprites_left;
  203: dr1a_v4_h_sprites_left;
  204: dr1a_v5_h_sprites_left;
  250: dr1a_v1_h_sprites_left;
  251: dr1a_v2_h_sprites_left;
  252: dr1a_v3_h_sprites_left;
  253: dr1a_v4u_h_sprites_left;
  254: dr1a_v5_h_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_h_sprites_r,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
    (1 + (position_in_consist_from_end == 1)) * 100) +
  ((LOAD_TEMP(2) >= date(1994,1,1)) * 50) +
  cargo_subtype)
{  
    0: dr1a_v1_h_notpowered_sprites_right;
    1: dr1a_v2_h_notpowered_sprites_right;
    2: dr1a_v3_h_notpowered_sprites_right;
    3: dr1a_v4_h_notpowered_sprites_right;
    4: dr1a_v5_h_notpowered_sprites_right;
   50: dr1a_v1_h_notpowered_sprites_right;
   51: dr1a_v2_h_notpowered_sprites_right;
   52: dr1a_v3_h_notpowered_sprites_right;
   53: dr1a_v4u_h_notpowered_sprites_right;
   54: dr1a_v5_h_notpowered_sprites_right;
     
  100: dr1a_v1_h_notpowered_normal_sprites_right;
  101: dr1a_v2_h_notpowered_normal_sprites_right;
  102: dr1a_v3_h_notpowered_normal_sprites_right;
  103: dr1a_v4_h_notpowered_normal_sprites_right;
  104: dr1a_v5_h_notpowered_normal_sprites_right;
  150: dr1a_v1_h_notpowered_normal_sprites_right;
  151: dr1a_v2_h_notpowered_normal_sprites_right;
  152: dr1a_v3_h_notpowered_normal_sprites_right;
  153: dr1a_v4u_h_notpowered_normal_sprites_right;
  154: dr1a_v5_h_notpowered_normal_sprites_right;
  
  200: dr1a_v1_h_sprites_right;
  201: dr1a_v2_h_sprites_right;
  202: dr1a_v3_h_sprites_right;
  203: dr1a_v4_h_sprites_right;
  204: dr1a_v5_h_sprites_right;
  250: dr1a_v1_h_sprites_right;
  251: dr1a_v2_h_sprites_right;
  252: dr1a_v3_h_sprites_right;
  253: dr1a_v4u_h_sprites_right;
  254: dr1a_v5_h_sprites_right;
       align_15_sprites;
}

engine_direction_template3m(dr1a_h, dr1a_n, dr1a_c)

long_vehicle(dr1a_h)

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_l,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
  (1 + (position_in_consist == 0)) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_notpowered_sprites_left;
    1: dr1a_v3_n_h_notpowered_sprites_left;
    2: dr1a_v4_n_h_notpowered_sprites_left;
    3: dr1a_v5_n_h_notpowered_sprites_left;

  100: dr1a_v2_n_h_notpowered_normal_sprites_left;
  101: dr1a_v3_n_h_notpowered_normal_sprites_left;
  102: dr1a_v4_n_h_notpowered_normal_sprites_left;
  103: dr1a_v5_n_h_notpowered_normal_sprites_left;

  200: dr1a_v2_n_h_sprites_left;
  201: dr1a_v3_n_h_sprites_left;
  202: dr1a_v4_n_h_sprites_left;
  203: dr1a_v5_n_h_sprites_left;
       align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_r,
  (((LOAD_TEMP(4) == dr1a_h) || (LOAD_TEMP(4) == dr1a_n)) *
  (1 + (position_in_consist_from_end == 1)) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_notpowered_sprites_right;
    1: dr1a_v3_n_h_notpowered_sprites_right;
    2: dr1a_v4_n_h_notpowered_sprites_right;
    3: dr1a_v5_n_h_notpowered_sprites_right;

  100: dr1a_v2_n_h_notpowered_normal_sprites_right;
  101: dr1a_v3_n_h_notpowered_normal_sprites_right;
  102: dr1a_v4_n_h_notpowered_normal_sprites_right;
  103: dr1a_v5_n_h_notpowered_normal_sprites_right;

  200: dr1a_v2_n_h_sprites_right;
  201: dr1a_v3_n_h_sprites_right;
  202: dr1a_v4_n_h_sprites_right;
  203: dr1a_v5_n_h_sprites_right;
       align_15_sprites;
}

engine_direction_template3m(dr1a_n, dr1a_h, dr1a_c)

switch (FEAT_TRAINS, SELF, dr1a_n_sprites_DRB1,
  ((position_in_consist != 0) * 100) +
  cargo_subtype)
{
    0: dr1a_v2_n_h_sprites_left;
    1: dr1a_v3_n_h_sprites_left;
    2: dr1a_v4_n_h_sprites_left;
    3: dr1a_v5_n_h_sprites_left;
  100: dr1a_v2_n_h_sprites_right;
  101: dr1a_v3_n_h_sprites_right;
  102: dr1a_v4_n_h_sprites_right;
  103: dr1a_v5_n_h_sprites_right;
       align_15_sprites;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_sprites_start_sp,
  ((num_vehs_in_consist - count_veh_id(dr1a_c) * 2 - count_veh_id(dr1a_n) * 2 - count_veh_id(_2m62u) * 2 - count_veh_id(dm62) * 2) == 0) && 
  ((count_veh_id(dr1a_n) + count_veh_id(dr1a_c)) >= 1) )
{
  1: dr1a_n_sprites_DRB1;
     dr1a_n_sprites_start;
}

long_vehicle_sp(dr1a_n)

switch (FEAT_TRAINS, SELF, dr1a_c_sprites_left,
  cargo_subtype)
{
  0: dr1a_v1_c_sprites_left;
  1: dr1a_v2_c_sprites_left;
  2: dr1a_v3_c_sprites_left;
  3: dr1a_v4_c_sprites_left;
  4: dr1a_v5_c_sprites_left;
     align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1a_c_sprites_right,
  cargo_subtype)
{
  0: dr1a_v1_c_sprites_right;
  1: dr1a_v2_c_sprites_right;
  2: dr1a_v3_c_sprites_right;
  3: dr1a_v4_c_sprites_right;
  4: dr1a_v5_c_sprites_right;
     align_15_sprites;
}

random_switch (FEAT_TRAINS, SELF, dr1a_c_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: dr1a_c_sprites_left;
  1: dr1a_c_sprites_right;
}

long_vehicle(dr1a_c)

switch (FEAT_TRAINS, PARENT, dr1a_can_attach_wagon_length,
  (count_veh_id(dr1a_h) + count_veh_id(dr1a_n)) > 3)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon3,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1a_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6DN);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon3;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon1,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon2;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon_h,
  (count_veh_id(dr1a_h) + count_veh_id(_2m62u) + count_veh_id(dm62) + count_veh_id(dr1a_n)) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1a_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_can_attach_wagon,
[ set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_c_can_attach_wagon1;
  dr1a_n: dr1a_c_can_attach_wagon_h;
  dr1a_h: dr1a_c_can_attach_wagon_h;
  _2m62u: dr1a_c_can_attach_wagon_h;
  dm62:   dr1a_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon_count2,
  count_veh_id(dr1a_h) >= count_veh_id(dr1a_n))
{
  1: dr1a_can_attach_wagon_length;
     return string(STR_CAN_ATTACH_MHEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon_count1,
[ set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: return CB_RESULT_ATTACH_ALLOW;
          dr1a_n_can_attach_wagon_count2;
}                                        

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon5,
[ set_offset_to(num_vehs_in_consist-10),
  prev_vehicle_type_id() ])
{
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon4,
[ set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon5;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon3,
[ set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon4;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon2,
[ set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon3;
  dr1a_n: return string(STR_CAN_ATTACH_MHEAD_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon1,
[ set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1a_c: dr1a_n_can_attach_wagon2;
  dr1a_n: dr1a_n_can_attach_wagon_count1;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, drb1_can_attach_wagon1,
  count_veh_id(dr1a_n) + count_veh_id(dr1a_h))
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, dr1a_n_can_attach_wagon,
  count_veh_id(_2m62u) + count_veh_id(dm62))
{
  0: dr1a_n_can_attach_wagon1;
  1: drb1_can_attach_wagon1; 
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon,
  count_veh_id(_2m62u) + count_veh_id(dm62))
{
  0: dr1a_can_attach_wagon_length;
     return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, PARENT, drb1_can_attach_wagon,
  (count_veh_id(dr1a_h) * 2) +
  count_veh_id(dr1a_n) + 
  count_veh_id(_2m62u) + 
  count_veh_id(dm62))
{
  0..1: return CB_RESULT_ATTACH_ALLOW;
        return string(STR_CAN_ATTACH_WRONG_DRB1);
}

switch (FEAT_TRAINS, SELF, dr1a_can_attach_wagon,
  vehicle_type_id)
{
  dr1a_h: dr1a_h_can_attach_wagon;
  dr1a_n: dr1a_n_can_attach_wagon;
  dr1a_c: dr1a_c_can_attach_wagon;
  _2m62u: drb1_can_attach_wagon;
  dm62:   drb1_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_start_stop,
  (count_veh_id(dr1a_h) + count_veh_id(dr1a_n) + count_veh_id(_2m62u) + count_veh_id(dm62)) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

switch (FEAT_TRAINS, SELF, dr1a_h_visual_effect_and_powered1,
  (LOAD_TEMP(0) - count_veh_id(dr1a_h) - count_veh_id(dr1a_n)) % 2)
{
  0: return diesel_visual_effect_and_powered(-2);
     return diesel_visual_effect_and_powered(7);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_visual_effect_and_powered0,
  STORE_TEMP(count_veh_id(dr1a_h) + count_veh_id(dr1a_n), 0))
{
  dr1a_h_visual_effect_and_powered1;
}

// Визуализируем эффекты тяги только если локомотив - dr1a_h или dr1a_n
switch (FEAT_TRAINS, PARENT, dr1a_h_visual_effect_and_powered,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: dr1a_h_visual_effect_and_powered0; 
     return disable_visual_effect_and_powered();
} 

// Даём тягу, только если локомотив - dr1a_h или dr1a_n
switch (FEAT_TRAINS, PARENT, dr1a_h_power,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: return get_power_in_cb(800); 
     return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - dr1a_h или dr1a_n или 2m62u или dm62
switch (FEAT_TRAINS, PARENT, dr1a_h_cargo_capacity,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) )
{
  1: return 68;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1a_n_cargo_capacity,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) || (vehicle_type_id == _2m62u) || (vehicle_type_id == dm62) )
{
  1: return 96;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1a_c_cargo_capacity,
  (vehicle_type_id == dr1a_h) || (vehicle_type_id == dr1a_n) || (vehicle_type_id == _2m62u) || (vehicle_type_id == dm62) )
{
  1: return 124;
     return 0;
}

switch (FEAT_TRAINS, SELF, dr1a_n_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ,""));
  1: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ_STAR));
  2: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LG));
  3: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text2,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
     string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text1_1,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_SZD));
  1: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ,""));
  2: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LDZ_STAR));
  3: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_LG));
  4: string(STR_REFIT_FACTORY_63323, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text1_2,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_SZD));
  1: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ,""));
  2: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LDZ_STAR));
  3: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_LG));
  4: string(STR_REFIT_FACTORY_63341, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text1_3,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_SZD));
  1: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ,""));
  2: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LDZ_STAR));
  3: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_LG));
  4: string(STR_REFIT_FACTORY_63349, string(STR_REFIT_BELARUSSIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text1,
  (build_year >= 1978) +
  (build_year >= 1985) )
{
  0: dr1a_cargo_subtype_text1_1;
  1: dr1a_cargo_subtype_text1_2;
     dr1a_cargo_subtype_text1_3;
}

switch (FEAT_TRAINS, SELF, dr1a_cargo_subtype_text,
  ((current_year >= 1990) || (build_year >=1990)) )
{
  1: dr1a_cargo_subtype_text1;
     dr1a_cargo_subtype_text2;
}

switch (FEAT_TRAINS, SELF, dr1a_h_additional_text,
  (current_year >= 1978) +
  (current_year >= 1985) +
  (current_year >= 1990) +
  (current_year >= 1994) )
{ 
  0: return string(STR_DR1A_H_PURCHASE_HINT_63324, string(STR_SECTIONS_DMU_2_6));
  1: return string(STR_DR1A_H_PURCHASE_HINT_63642, string(STR_SECTIONS_DMU_2_6));
  2: return string(STR_DR1A_H_PURCHASE_HINT_63350_B90, string(STR_SECTIONS_DMU_2_6));
  3: return string(STR_DR1A_H_PURCHASE_HINT_63350_S90, string(STR_SECTIONS_DMU_N2_6));
     return string(STR_DR1A_H_PURCHASE_HINT_63350_S94, string(STR_SECTIONS_DMU_N2_6));
}

switch (FEAT_TRAINS, SELF, dr1a_c_additional_text,
  (current_year >= 1978) +
  (current_year >= 1985) +
  (current_year >= 1990) +
  (current_year >= 1994) )
{ 
  0: return string(STR_DR1A_C_PURCHASE_HINT_63325, string(STR_SECTIONS_DMU_2_6));
  1: return string(STR_DR1A_C_PURCHASE_HINT_63643, string(STR_SECTIONS_DMU_2_6));
  2: return string(STR_DR1A_C_PURCHASE_HINT_63351_B90, string(STR_SECTIONS_DMU_2_6));
  3: return string(STR_DR1A_C_PURCHASE_HINT_63351_S90, string(STR_SECTIONS_DMU_N2_6));
     return string(STR_DR1A_C_PURCHASE_HINT_63351_S94, string(STR_SECTIONS_DMU_N2_6));
}

switch (FEAT_TRAINS, SELF, dr1a_n_additional_text,
  current_year >= 1994 )
{ 
  0: return string(STR_DR1A_N_PURCHASE_HINT_63352, string(STR_SECTIONS_DMU_N2_6));
     return string(STR_DR1A_N_PURCHASE_HINT_63352_S94, string(STR_SECTIONS_DMU_N2_6));
}

item (FEAT_TRAINS, dr1a_h) 
{
  property
  {
    name: string(STR_DR1A_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1976), 1, 1);
    model_life: get_model_life(1976,1998);
    retire_early: get_retire_early(1976,1998);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 800 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 68;
    weight: 60 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(128.0, 60);
    length: 8;
  }
  graphics {
    default: dr1a_h_sprites;
    purchase: dr1a_h_purchase_sprites;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1a_can_attach_wagon;
    start_stop: dr1a_h_start_stop;
    visual_effect_and_powered: dr1a_h_visual_effect_and_powered;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    additional_text: dr1a_h_additional_text;
    cargo_subtype_text: dr1a_cargo_subtype_text;
    power: dr1a_h_power;
    cargo_capacity: dr1a_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, dr1a_n) 
{
  property
  {
    name: string(STR_DR1A_N_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1990), 1, 1);
    model_life: get_model_life(1990,2008);
    retire_early: get_retire_early(1990,2008);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 96;
    weight: 38 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics
  {
    default: dr1a_n_sprites;
    purchase: dr1a_n_purchase_sprites;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1a_can_attach_wagon;
    start_stop: dr1a_h_start_stop;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    additional_text: dr1a_n_additional_text;
    cargo_subtype_text: dr1a_n_cargo_subtype_text;
    cargo_capacity: dr1a_n_cargo_capacity;
  }
}

item (FEAT_TRAINS, dr1a_c) 
{
  property
  {
    name: string(STR_DR1A_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1976), 1, 1);
    model_life: get_model_life(1976,2008);
    retire_early: get_retire_early(1976,2008);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 124;
    weight: 38 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: dr1a_c_sprites;
    purchase: dr1a_c_purchase_sprites;
    articulated_part: articulated_part_dummy7;
    cargo_subtype_text: dr1a_cargo_subtype_text;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    can_attach_wagon: dr1a_can_attach_wagon; 
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: dr1a_c_additional_text;
    cargo_capacity: dr1a_c_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, dr1a_h)
  {
    property
    { name: string(STR_DR1A_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, dr1a_n)
  {
    property
    { name: string(STR_DR1A_N_NAME_LONG); }
  }
  item (FEAT_TRAINS, dr1a_c)
  {
    property
    { name: string(STR_DR1A_C_NAME_LONG); }
  }
}

