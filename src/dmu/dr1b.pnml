 // dr1b
// 215 (D7h) 216 (D8h) 217 (D9h)

#define IMAGEFILE  "src/dmu/dr1b_h.png"
purchase_sprites(dr1b_h, 0, 0)
MU_head_middle_sprites(15, dr1b_h_mu, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/dmu/dr1b_n.png"
purchase_sprites(dr1b_n, 0, 0)
MU_head_middle_sprites(15, dr1b_n_mu, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/dmu/dr1b_c.png"
purchase_sprites(dr1b_c, 0, 0)
MU_car_sprites(15, dr1b_c_mu, 32, 40)
#undef IMAGEFILE

MU_head_middle_direction_template2(dr1b_h, dr1b_h, dr1b_n)
engine_direction_template3m(dr1b_h, dr1b_n, dr1b_c, )
long_vehicle(dr1b_h)

MU_head_middle_direction_template2(dr1b_n, dr1b_h, dr1b_n)
engine_direction_template3m(dr1b_n, dr1b_h, dr1b_c, )
long_vehicle(dr1b_n)

MU_car_direction_template(dr1b_c)
long_vehicle_RND(dr1b_c, dr1b_c_sprites_start)

switch (FEAT_TRAINS, PARENT, dr1b_can_attach_wagon_length,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_n)) > 3)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, dr1b_c_can_attach_wagon3,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1b_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6DN);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1b_c: dr1b_c_can_attach_wagon3;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_c_can_attach_wagon1,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1b_c: dr1b_c_can_attach_wagon2;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_c_can_attach_wagon_h,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_n)) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1b_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1b_c_can_attach_wagon,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1b_c: dr1b_c_can_attach_wagon1;
  dr1b_n: dr1b_c_can_attach_wagon_h;
  dr1b_h: dr1b_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1b_n_can_attach_wagon,
  count_veh_id(dr1b_h) >= count_veh_id(dr1b_n))
{
  1: dr1b_can_attach_wagon_length;
     return string(STR_CAN_ATTACH_MHEAD_ONLY);
}

switch (FEAT_TRAINS, SELF, dr1b_can_attach_wagon,
  vehicle_type_id)
{
  dr1b_h: dr1b_can_attach_wagon_length;
  dr1b_n: dr1b_n_can_attach_wagon;
  dr1b_c: dr1b_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1b_h_start_stop,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_n)) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

engine_diesel_1r_effect(dr1b_h, -2, 7, 12)

switch (FEAT_TRAINS, SELF, dr1b_h_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(dr1b_h) - count_veh_id(dr1b_n)) % 2)
{
  0: dr1b_h_create_effect_sprites_left;
     dr1b_h_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_create_effect0,
  STORE_TEMP(count_veh_id(dr1b_h) + count_veh_id(dr1b_n), 0))
{
  dr1b_h_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - dr1b_h или dr1b_n
switch (FEAT_TRAINS, PARENT, dr1b_h_create_effect,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: dr1b_h_create_effect0;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - dr1b_h или dr1b_n
switch (FEAT_TRAINS, PARENT, dr1b_h_power,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: return get_power_in_cb(810);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - dr1b_h или dr1b_n
switch (FEAT_TRAINS, PARENT, dr1b_h_cargo_capacity,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: return 68;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1b_n_cargo_capacity,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: return 96;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1b_c_cargo_capacity,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: return 124;
     return 0;
}

general_running_cost(dr1b_h_v1, 186)

switch (FEAT_TRAINS, PARENT, dr1b_h_running_cost_factor,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: dr1b_h_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1b_c_v1, 50)

switch (FEAT_TRAINS, PARENT, dr1b_c_running_cost_factor,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: dr1b_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1b_n_v1, 119)

switch (FEAT_TRAINS, PARENT, dr1b_n_running_cost_factor,
  (vehicle_type_id == dr1b_h) || (vehicle_type_id == dr1b_n))
{
  1: dr1b_n_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

switch (FEAT_TRAINS, SELF, dr1b_cargo_subtype_text,
  enable_long_names)
{
  1: return string(STR_REFIT_FACTORY_63555, string(STR_REFIT_BELARUSSIAN_RAILWAY_LONG));
     return string(STR_REFIT_FACTORY);
}

item (FEAT_TRAINS, dr1b_h, 215) {
  property {
    name: string(STR_NAME_DR1B);
    vehicle_dates(2005, 2009, 28, 10, 8, 23)
    vehicle_dmu(810, 58, 90.0, 68, 48)
  }
  graphics {
    default: dr1b_h_sprites;
    purchase: dr1b_h_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1b_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1b_can_attach_wagon;
    start_stop: dr1b_h_start_stop;
    create_effect: dr1b_h_create_effect;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: dr1b_h_power;
    cargo_capacity: dr1b_h_cargo_capacity;
    cargo_subtype_text: dr1b_cargo_subtype_text;
    colour_mapping: any_cc_colour;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_556)),
                                   string(STR_SECTIONS_DMU_N2_6_M2),
                                   string(STR_PURCHASE_HINT_FROMTO, 2005,2009),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
  }
}

item (FEAT_TRAINS, dr1b_n, 216) {
  property {
    name: string(STR_NAME_DR1B);
    vehicle_dates(2005, 2009, 28, 10, 8, 15)
    vehicle_dmu_c(40, 96, 48)
  }
  graphics {
    default: dr1b_n_sprites;
    purchase: dr1b_n_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1b_n_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1b_can_attach_wagon;
    start_stop: dr1b_h_start_stop;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    cargo_capacity: dr1b_n_cargo_capacity;
    cargo_subtype_text: dr1b_cargo_subtype_text;
    colour_mapping: any_cc_colour;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_N_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_558)),
                                   string(STR_SECTIONS_DMU_N2_6_M2),
                                   string(STR_PURCHASE_HINT_FROMTO, 2005,2009),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
  }
}

item (FEAT_TRAINS, dr1b_c, 217) {
  property {
    name: string(STR_NAME_DR1B);
    vehicle_dates(2005, 2009, 28, 10, 8, 6)
    vehicle_dmu_c(37.5, 124, 48)
  }
  graphics {
    default: dr1b_c_sprites;
    purchase: dr1b_c_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1b_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    can_attach_wagon: dr1b_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_capacity: dr1b_c_cargo_capacity;
    cargo_subtype_text: dr1b_cargo_subtype_text;
    colour_mapping: any_cc_colour;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_557)),
                                   string(STR_SECTIONS_DMU_N2_6_M2),
                                   string(STR_PURCHASE_HINT_FROMTO, 2005,2009),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
  }
}

long_name_template(dr1b_h, STR_LONGNAME_DR1B_H)
long_name_template(dr1b_n, STR_LONGNAME_DR1B_N)
long_name_template(dr1b_c, STR_LONGNAME_DR1B_C)
allow_dmu(dr1b_h)
allow_dmu(dr1b_n)
allow_dmu(dr1b_c)
