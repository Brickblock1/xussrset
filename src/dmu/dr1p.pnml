 // dr1p

// Спрайты

#define IMAGEFILE  "src/dmu/dr1p-1970-1976.png"

// покупка
purchase_spriteset(dr1p_m, 0, 0)

purchase_spriteset(dr1p_c, 100, 0)

// головной: погрузка/перемещение
spriteset (dr1p_m_loading_spriteset, IMAGEFILE)
{
  s15_template(38, 27, 0)
} 

spriteset (dr1p_m_traveling_spriteset, IMAGEFILE)
{
  s15_template(497, 27, 0)
} 

spritegroup dr1p_m_spritegroup
{
  loading: dr1p_m_loading_spriteset;
  loaded: dr1p_m_traveling_spriteset;
}

// развёрнутая голова
spriteset (dr1p_rm_spriteset, IMAGEFILE)
{
  s15_template(38, 96, 0)
} 

// прицепной
spriteset (dr1p_c_spriteset, IMAGEFILE)
{
  s15_template(38, 165, 0)
} 

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, dr1p_c_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  dr1p_c_spriteset;
}

// Определяем необходимость разворота моторного вагона по чётности его позиции
// от начала состава
switch (FEAT_TRAINS, SELF, dr1p_m_get_spriteset2,
  (last_computed_result - count_veh_id(dr1p_m)) % 2  
)
{
  0: dr1p_m_spritegroup;
  dr1p_rm_spriteset;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_get_spriteset1,
  count_veh_id(dr1p_m))
{
  dr1p_m_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, dr1p_m_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  dr1p_m_get_spriteset1;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon10,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id()
])
{
  dr1p_m: return CB_RESULT_ATTACH_ALLOW;
  dr1p_c: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon9,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1p_m_can_attach_wagon10;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon8,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id()
])
{
  dr1p_m: return CB_RESULT_ATTACH_ALLOW;
  dr1p_c: dr1p_m_can_attach_wagon9;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon7,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1p_m_can_attach_wagon8;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon6,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id()
])
{
  dr1p_m: return CB_RESULT_ATTACH_ALLOW;
  dr1p_c: dr1p_m_can_attach_wagon7;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon5,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1p_m_can_attach_wagon6;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon4,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id()
])
{
  dr1p_m: return CB_RESULT_ATTACH_ALLOW;
  dr1p_c: dr1p_m_can_attach_wagon5;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  dr1p_m_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_can_attach_wagon1,
  count_veh_id(dr1p_m) % 2)
{
  1: dr1p_m_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, SELF, dr1p_m_can_attach_wagon,
  vehicle_type_id)
{
  dr1p_m: return CB_RESULT_ATTACH_ALLOW;
  dr1p_c: dr1p_m_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_m_start_stop,
  count_veh_id(dr1p_m) % 2)
{
  0: return CB_RESULT_NO_TEXT;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, dr1p_m_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(dr1p_m)) % 2)
{
  0: return diesel_visual_effect_and_powered(-2);
  return diesel_visual_effect_and_powered(7);
}

switch (FEAT_TRAINS, PARENT, dr1p_m_visual_effect_and_powered0,
  count_veh_id(dr1p_m))
{
  dr1p_m_visual_effect_and_powered1;
}

// Визуализируем эффекты тяги только если локомотив - dr1p_m
switch (FEAT_TRAINS, PARENT, dr1p_m_visual_effect_and_powered,
  (vehicle_type_id == dr1p_m) 
  )
{
  1: dr1p_m_visual_effect_and_powered0; 
  return disable_visual_effect_and_powered();
} 

// Даём тягу, только если локомотив - dr1p_m
switch (FEAT_TRAINS, PARENT, dr1p_m_power,
  (vehicle_type_id == dr1p_m)  
  )
{
  1: return get_power_in_cb(800); 
  return 0;
} 

// Допускается эксплуатация с пассажирами только если голова - dr1p_m
switch (FEAT_TRAINS, PARENT, dr1p_m_cargo_capacity,
  (vehicle_type_id == dr1p_m)  
)
{
  1: return 68;
  return 0;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_cargo_capacity,
  (vehicle_type_id == dr1p_m)  
)
{
  1: return 124;
  return 0;
}


item (FEAT_TRAINS, dr1p_m) 
{
  property
  {
    name: string(STR_DR1P_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1970), 1, 1);
    model_life: get_model_life(1970,1976);
    retire_early: get_retire_early(1970,1976); // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 800 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 68;
    weight: 53.5 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    tractive_effort_coefficient: get_tec(128.0, 53.5);
    length: 8;
  }
  graphics {
    default: dr1p_m_get_spriteset;
    purchase: dr1p_m_purchase_spriteset;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1p_m_can_attach_wagon;
    start_stop: dr1p_m_start_stop;
    visual_effect_and_powered: dr1p_m_visual_effect_and_powered;
    speed: speed_after20_120to110;
    purchase_speed: return 120;
    additional_text: return string(STR_DR1P_M_PURCHASE_HINT);
    power: dr1p_m_power;
    cargo_capacity: dr1p_m_cargo_capacity;
  }
}

item (FEAT_TRAINS, dr1p_c) 
{
  property
  {
    name: string(STR_DR1P_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1970), 1, 1);
    model_life: get_model_life(1970,1976);
    retire_early: get_retire_early(1970,1976); // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_dmu_cost_factor();
    running_cost_factor: get_dmu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 124;
    weight: 34.5 ton;
    engine_class: ENGINE_CLASS_DIESEL;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: dr1p_c_get_spriteset;
    purchase: dr1p_c_purchase_spriteset;
    articulated_part: articulated_part_dummy7;
    speed: speed_after20_120to110;
    purchase_speed: return 120;
    power: return 0;
    cargo_capacity: dr1p_c_cargo_capacity;
    can_attach_wagon: dr1p_m_can_attach_wagon;
    start_stop: return string(STR_CANNOT_START_HEAD);
    additional_text: return string(STR_DR1P_C_PURCHASE_HINT);
  }
}
