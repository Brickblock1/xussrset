 // dr1p
// 210 (D2h) 211 (D3h)

#define IMAGEFILE  "src/dmu/dr1p.png"
purchase_sprites(dr1p_h, 0, 0)
purchase_sprites(dr1p_c, 120, 0)

MU_4r_0_0_sprites(15, dr1p_v1, 32, 40)
MU_0_0_2_sprites(15,  dr1p_v1, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dr1p_h_sprites_left,
  (LOAD_TEMP(4) == dr1p_h) *
  (1 + (position_in_consist == 0)))
{
  0: dr1p_v1_h_notpowered_sprites_left;
  1: dr1p_v1_h_notpowered_normal_sprites_left;
  2: dr1p_v1_h_sprites_left;
     align_15_sprites;
}

switch (FEAT_TRAINS, SELF, dr1p_h_sprites_right,
  (LOAD_TEMP(4) == dr1p_h) *
  (1 + (position_in_consist_from_end == 1)))
{
  0: dr1p_v1_h_notpowered_sprites_right;
  1: dr1p_v1_h_notpowered_normal_sprites_right;
  2: dr1p_v1_h_sprites_right;
     align_15_sprites;
}

engine_direction_template2m(dr1p_h, dr1p_c, )

long_vehicle(dr1p_h)

random_switch (FEAT_TRAINS, SELF, dr1p_c_sprites_start,
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: dr1p_v1_c_sprites_left;
  1: dr1p_v1_c_sprites_right;
}

long_vehicle_RND(dr1p_c, dr1p_c_sprites_start)

switch (FEAT_TRAINS, PARENT, dr1p_can_attach_wagon_length,
  count_veh_id(dr1p_h) > 3)
{
  0: CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, dr1p_c_can_attach_wagon3,
[
  set_offset_to(num_vehs_in_consist-8),
  prev_vehicle_type_id() ])
{
  dr1p_c: return string(STR_WRONG_STRUCTURE_OF_TRAIN_6D);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_can_attach_wagon2,
[
  set_offset_to(num_vehs_in_consist-6),
  prev_vehicle_type_id() ])
{
  dr1p_c: dr1p_c_can_attach_wagon3;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_can_attach_wagon1,
[
  set_offset_to(num_vehs_in_consist-4),
  prev_vehicle_type_id() ])
{
  dr1p_c: dr1p_c_can_attach_wagon2;
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_can_attach_wagon_h,
  count_veh_id(dr1p_h) % 2)
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
     dr1p_c_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_can_attach_wagon,
[
  set_offset_to(num_vehs_in_consist-2),
  prev_vehicle_type_id() ])
{
  dr1p_c: dr1p_c_can_attach_wagon1;
  dr1p_h: dr1p_c_can_attach_wagon_h;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, dr1p_can_attach_wagon,
  vehicle_type_id)
{
  dr1p_h: dr1p_can_attach_wagon_length;
  dr1p_c: dr1p_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, dr1p_h_start_stop,
  count_veh_id(dr1p_h) % 2)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

engine_diesel_1r_effect(dr1p_h, -2, 7, 12)

switch (FEAT_TRAINS, SELF, dr1p_h_create_effect1,
  (LOAD_TEMP(0) - count_veh_id(dr1p_h)) % 2)
{
  0: dr1p_h_create_effect_sprites_left;
     dr1p_h_create_effect_sprites_right;
}

switch (FEAT_TRAINS, PARENT, dr1p_h_create_effect0,
  STORE_TEMP(count_veh_id(dr1p_h), 0))
{
  dr1p_h_create_effect1;
}

// Визуализируем эффекты тяги только если локомотив - dr1p_h
switch (FEAT_TRAINS, PARENT, dr1p_h_create_effect,
  (vehicle_type_id == dr1p_h))
{
  1: dr1p_h_create_effect0;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - dr1p_h
switch (FEAT_TRAINS, PARENT, dr1p_h_power,
  (vehicle_type_id == dr1p_h))
{
  1: return get_power_in_cb(800);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - dr1p_h
switch (FEAT_TRAINS, PARENT, dr1p_h_cargo_capacity,
  vehicle_type_id == dr1p_h)
{
  1: return 68;
     return 0;
}

switch (FEAT_TRAINS, PARENT, dr1p_c_cargo_capacity,
  vehicle_type_id == dr1p_h)
{
  1: return 124;
     return 0;
}

general_running_cost(dr1p_h_v1, 176)

switch (FEAT_TRAINS, PARENT, dr1p_h_running_cost_factor,
  vehicle_type_id == dr1p_h)
{
  1: dr1p_h_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

wagon_running_cost(dr1p_c_v1, 48)

switch (FEAT_TRAINS, PARENT, dr1p_c_running_cost_factor,
  vehicle_type_id == dr1p_h)
{
  1: dr1p_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

switch (FEAT_TRAINS, SELF, dr1p_cargo_subtype_text,
  enable_long_names)
{
  1: return string(STR_REFIT_FACTORY_63319);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, dr1p_h, 210) {
  property {
    name: string(STR_NAME_DR1P);
    vehicle_dates(1970, 1976, 25, 10, 8, 22)
    vehicle_dmu(800, 53.5, 85.0, 68, 48)
  }
  graphics {
    default: dr1p_h_sprites;
    purchase: dr1p_h_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1p_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: dr1p_can_attach_wagon;
    start_stop: dr1p_h_start_stop;
    create_effect: dr1p_h_create_effect;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_H_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_320)),
                                   string(STR_SECTIONS_DMU_2_6_M2),
                                   string(STR_PURCHASE_HINT_FROMTO, 1970, 1976),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
    power: dr1p_h_power;
    cargo_capacity: dr1p_h_cargo_capacity;
    cargo_subtype_text: dr1p_cargo_subtype_text;
    colour_mapping: any_cc_colour;
  }
}

item (FEAT_TRAINS, dr1p_c, 211) {
  property {
    name: string(STR_NAME_DR1P);
    vehicle_dates(1970, 1976, 25, 10, 8, 6)
    vehicle_dmu_c(34.5, 124, 48)
  }
  graphics {
    default: dr1p_c_sprites;
    purchase: dr1p_c_purchase_sprites;
    cargo_age_period: return 60;
    running_cost_factor: dr1p_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    speed: speed_after25_120to110;
    purchase_speed: return 120;
    power: return 0;
    cargo_capacity: dr1p_c_cargo_capacity;
    can_attach_wagon: dr1p_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_DMU_C_WITH_SUBTYPE, string(STR_PURCHASE_HINT_63_321)),
                                   string(STR_SECTIONS_DMU_2_6_M2),
                                   string(STR_PURCHASE_HINT_FROMTO, 1970, 1976),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
    cargo_subtype_text: dr1p_cargo_subtype_text;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(dr1p_h, STR_LONGNAME_DR1P_H)
long_name_template(dr1p_c, STR_LONGNAME_DR1P_C)
allow_dmu(dr1p_h)
allow_dmu(dr1p_c)
