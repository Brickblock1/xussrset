 // dr1a

// Спрайты

#define IMAGEFILE  "src/dr1a/dr1a-1976-2009.png"

// head-trailer-reverse_head
// для сокрытия с карты, в меню видны
spriteset (dr1a_h_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(38, 34, 0)
} 

spriteset (dr1a_rh_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(38, 448, 1)
} 

spriteset (dr1a_c_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(38, 1414, 0)
} 

// покупка
spriteset (dr1a_h_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(326, 34, 4)
}

spriteset (dr1a_c_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(326, 1414, 4)
}

// головной
spriteset (dr1a_h_spriteset, IMAGEFILE)
{
  s15_template(38, 34, 0)
} 

// развёрнутая голова
spriteset (dr1a_rh_spriteset, IMAGEFILE)
{
  s15r_template(38, 448, 0)
} 

// прицепной
spriteset (dr1a_c_spriteset, IMAGEFILE)
{
  s15_template(38, 1414, 0)
} 

// головной: погрузка/перемещение
spriteset (dr1a_h_2_loading_spriteset, IMAGEFILE)
{
  s15_template(38, 34, 0)
} 

spriteset (dr1a_h_2_traveling_spriteset, IMAGEFILE)
{
  s15_template(497, 34, 0)
} 

spritegroup dr1a_h_2_spritegroup
{
  loading: dr1a_h_2_loading_spriteset;
  loaded: dr1a_h_2_traveling_spriteset;
}

#undef IMAGEFILE

//
switch (FEAT_TRAINS, SELF, dr1a_c_get_spriteset,
  var[0x62, 7, 1, 1])
{
  0: dr1a_c_spriteset;
  dr1a_c_hidden_spriteset;
}

switch (FEAT_TRAINS, SELF, dr1a_h_get_spriteset1,
  ((last_computed_result - count_veh_id(dr1a_h)) % 2) +
  ((var[0x62, 7, 1, 1] != 0) ? 2 : 0)
)
{
  0: dr1a_h_2_spritegroup;
  1: dr1a_rh_spriteset;
  2: dr1a_h_hidden_spriteset;
  dr1a_rh_hidden_spriteset;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_get_spriteset,
  count_veh_id(dr1a_h))
{
  dr1a_h_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, dr1a_h_articulated_part,
  extra_callback_info1)
{
  1: return dummy;
  return 0xFF;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon10,
[
  STORE_TEMP(num_vehs_in_consist-8, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1a_h: return CB_RESULT_ATTACH_ALLOW;
  dr1a_c: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon9,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1a_h_can_attach_wagon10;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon8,
[
  STORE_TEMP(num_vehs_in_consist-6, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1a_h: return CB_RESULT_ATTACH_ALLOW;
  dr1a_c: dr1a_h_can_attach_wagon9;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon7,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1a_h_can_attach_wagon8;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon6,
[
  STORE_TEMP(num_vehs_in_consist-4, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1a_h: return CB_RESULT_ATTACH_ALLOW;
  dr1a_c: dr1a_h_can_attach_wagon7;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon5,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1a_h_can_attach_wagon6;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon4,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1a_h: return CB_RESULT_ATTACH_ALLOW;
  dr1a_c: dr1a_h_can_attach_wagon5;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  dr1a_h_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_can_attach_wagon1,
  count_veh_id(dr1a_h) % 2)
{
  1: dr1a_h_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, SELF, dr1a_h_can_attach_wagon,
  vehicle_type_id)
{
  dr1a_h: return CB_RESULT_ATTACH_ALLOW;
  dr1a_c: dr1a_h_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1a_h_start_stop,
  count_veh_id(dr1a_h) % 2)
{
  0: return 0xFF;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, dr1a_h_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(dr1a_h)) % 2)
{
  0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -2,
    DISABLE_WAGON_POWER);
  return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 7,
    DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, PARENT, dr1a_h_visual_effect_and_powered,
  count_veh_id(dr1a_h))
{
  dr1a_h_visual_effect_and_powered1;
}

// ограничиваем скорость через 20 лет работы
switch (FEAT_TRAINS, SELF, dr1a_speed,
  age_in_days >= (20*365+5))
{
  1: return 110;
  return 120;
}

switch (FEAT_TRAINS, SELF, dr1a_purchase_speed,
  0)
{
  return 120;
}

item (FEAT_TRAINS, dr1a_h) 
{
  property
  {
    name: string(STR_DR1A_H_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1976, 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 800 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 68;
    weight: 60 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.22;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1a_h_get_spriteset;
    purchase: dr1a_h_purchase_spriteset;
    articulated_part: dr1a_h_articulated_part;
    can_attach_wagon: dr1a_h_can_attach_wagon;
    start_stop: dr1a_h_start_stop;
    visual_effect_and_powered: dr1a_h_visual_effect_and_powered;
    speed: dr1a_speed;
    purchase_speed: dr1a_purchase_speed;
  }
}

item (FEAT_TRAINS, dr1a_c) 
{
  property
  {
    name: string(STR_DR1A_C_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(get_car_year(1976), 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 124;
    weight: 38 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1a_c_get_spriteset;
    purchase: dr1a_c_purchase_spriteset;
    articulated_part: dr1a_h_articulated_part;
  }
}
