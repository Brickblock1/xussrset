 // dr1b

// Спрайты

#define IMAGEFILE  "src/dr1b/dr1b.png"

// head-trailer-reverse_head
// для сокрытия с карты, в меню видны
spriteset (dr1b_h_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(82, 35, 0)
} 

spriteset (dr1b_rh_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(82, 104, 0)
} 

spriteset (dr1b_c_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(82, 311, 0)
} 

spriteset (dr1b_hn_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(82, 173, 0)
} 

spriteset (dr1b_rhn_hidden_spriteset, IMAGEFILE)
{
  s15_hidden_template(82, 242, 0)
} 

// покупка
spriteset (dr1b_h_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(370, 35, 4)
}

spriteset (dr1b_c_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(370, 311, 4)
}

spriteset (dr1b_hn_purchase_spriteset, IMAGEFILE)
{
  s15_purchase_template(370, 173, 4)
}

// головной: погрузка/перемещение
spriteset (dr1b_h_loading_spriteset, IMAGEFILE)
{
  s15_template(82, 35, 0)
} 

spriteset (dr1b_h_traveling_spriteset, IMAGEFILE)
{
  s15_template(541, 35, 0)
} 

spritegroup dr1b_h_spritegroup
{
  loading: dr1b_h_loading_spriteset;
  loaded: dr1b_h_traveling_spriteset;
}

// головной развёрнутый
spriteset (dr1b_rh_spriteset, IMAGEFILE)
{
  s15_template(82, 104, 0)
} 

// прицепной
spriteset (dr1b_c_spriteset, IMAGEFILE)
{
  s15_template(82, 311, 0)
} 

// головной немоторный: погрузка/перемещение
spriteset (dr1b_hn_loading_spriteset, IMAGEFILE)
{
  s15_template(82, 173, 0)
} 

spriteset (dr1b_hn_traveling_spriteset, IMAGEFILE)
{
  s15_template(541, 173, 0)
} 

spritegroup dr1b_hn_spritegroup
{
  loading: dr1b_hn_loading_spriteset;
  loaded: dr1b_hn_traveling_spriteset;
}

// головной немоторный развёрнутый
spriteset (dr1b_rhn_spriteset, IMAGEFILE)
{
  s15_template(82, 242, 0)
} 

#undef IMAGEFILE

//
switch (FEAT_TRAINS, SELF, dr1b_c_get_spriteset,
  var[0x62, 7, 1, 1])
{
  0: dr1b_c_spriteset;
  dr1b_c_hidden_spriteset;
}

switch (FEAT_TRAINS, SELF, dr1b_h_get_spriteset1,
  ((last_computed_result - count_veh_id(dr1b_h) - count_veh_id(dr1b_hn)) % 2) +
  ((var[0x62, 7, 1, 1] != 0) ? 2 : 0)
)
{
  0: dr1b_h_spritegroup;
  1: dr1b_rh_spriteset;
  2: dr1b_h_hidden_spriteset;
  dr1b_rh_hidden_spriteset;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_get_spriteset,
  count_veh_id(dr1b_h) + count_veh_id(dr1b_hn))
{
  dr1b_h_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, dr1b_hn_get_spriteset1,
  ((last_computed_result - count_veh_id(dr1b_h) - count_veh_id(dr1b_hn)) % 2) +
  ((var[0x62, 7, 1, 1] != 0) ? 2 : 0)
)
{
  0: dr1b_hn_spritegroup;
  1: dr1b_rhn_spriteset;
  2: dr1b_hn_hidden_spriteset;
  dr1b_rhn_hidden_spriteset;
}

switch (FEAT_TRAINS, PARENT, dr1b_hn_get_spriteset,
  count_veh_id(dr1b_h) + count_veh_id(dr1b_hn))
{
  dr1b_hn_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, dr1b_h_articulated_part,
  extra_callback_info1)
{
  1: return dummy;
  return 0xFF;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon10,
[
  STORE_TEMP(num_vehs_in_consist-8, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_ALLOW;
  dr1b_c: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon9,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1b_h_can_attach_wagon10;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon8,
[
  STORE_TEMP(num_vehs_in_consist-6, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_ALLOW;
  dr1b_c: dr1b_h_can_attach_wagon9;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon7,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1b_h_can_attach_wagon8;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon6,
[
  STORE_TEMP(num_vehs_in_consist-4, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_ALLOW;
  dr1b_c: dr1b_h_can_attach_wagon7;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon5,
  num_vehs_in_consist)
{
  4: return CB_RESULT_ATTACH_ALLOW;
  dr1b_h_can_attach_wagon6;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon4,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_ALLOW;
  dr1b_c: dr1b_h_can_attach_wagon5;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon3,
  num_vehs_in_consist)
{
  2: return CB_RESULT_ATTACH_ALLOW;
  dr1b_h_can_attach_wagon4;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon1,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_hn)) % 2)
{
  1: dr1b_h_can_attach_wagon3;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon16,
[
  STORE_TEMP(num_vehs_in_consist-10, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_DISALLOW;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon15,
[
  STORE_TEMP(num_vehs_in_consist-8, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_DISALLOW;
  dr1b_c: dr1b_h_can_attach_wagon16;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon14,
[
  STORE_TEMP(num_vehs_in_consist-6, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_DISALLOW;
  dr1b_c: dr1b_h_can_attach_wagon15;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon13,
[
  STORE_TEMP(num_vehs_in_consist-4, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_DISALLOW;
  dr1b_c: dr1b_h_can_attach_wagon14;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon12,
[
  STORE_TEMP(num_vehs_in_consist-2, 0x10F),
  var[0x61, 0, 0xFFFF, 0xC6]
])
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: return CB_RESULT_ATTACH_DISALLOW;
  dr1b_c: dr1b_h_can_attach_wagon13;
  return CB_RESULT_ATTACH_DISALLOW;
}

// количество голов чётное?
switch (FEAT_TRAINS, PARENT, dr1b_h_can_attach_wagon11,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_hn)) % 2)
{
  0: return CB_RESULT_ATTACH_ALLOW; // да
  dr1b_h_can_attach_wagon12; // нет, проверяем последнюю на соответствие dr1b_h
}

switch (FEAT_TRAINS, SELF, dr1b_h_can_attach_wagon,
  vehicle_type_id)
{
  dr1b_h: return CB_RESULT_ATTACH_ALLOW;
  dr1b_hn: dr1b_h_can_attach_wagon11;
  dr1b_c: dr1b_h_can_attach_wagon1;
  return CB_RESULT_ATTACH_DISALLOW;
}

switch (FEAT_TRAINS, PARENT, dr1b_h_start_stop,
  (count_veh_id(dr1b_h) + count_veh_id(dr1b_hn)) % 2)
{
  0: return 0xFF;
  return string(STR_CANNOT_START);
}

switch (FEAT_TRAINS, SELF, dr1b_h_visual_effect_and_powered1,
  (last_computed_result - count_veh_id(dr1b_h) - count_veh_id(dr1b_hn)) % 2)
{
  0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -2,
    DISABLE_WAGON_POWER);
  return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 7,
    DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, PARENT, dr1b_h_visual_effect_and_powered,
  count_veh_id(dr1b_h) + count_veh_id(dr1b_hn))
{
  dr1b_h_visual_effect_and_powered1;
}

// ограничиваем скорость через 20 лет работы
switch (FEAT_TRAINS, SELF, dr1b_speed,
  age_in_days >= (20*365+5))
{
  1: return 110;
  return 120;
}

switch (FEAT_TRAINS, SELF, dr1b_purchase_speed,
  0)
{
  return 120;
}

item (FEAT_TRAINS, dr1b_h) 
{
  property
  {
    name: string(STR_DR1B_H_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(2005, 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 121 km/h;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 810 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    dual_headed: 0;
    cargo_capacity: 68;
    weight: 58 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.224;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1b_h_get_spriteset;
    purchase: dr1b_h_purchase_spriteset;
    articulated_part: dr1b_h_articulated_part;
    can_attach_wagon: dr1b_h_can_attach_wagon;
    start_stop: dr1b_h_start_stop;
    visual_effect_and_powered: dr1b_h_visual_effect_and_powered;
    speed: dr1b_speed;
    purchase_speed: dr1b_purchase_speed;
  }
}

item (FEAT_TRAINS, dr1b_hn) 
{
  property
  {
    name: string(STR_DR1B_HN_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(get_car_year(2005), 1, 1);
    model_life: 25;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 96;
    weight: 40 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1b_hn_get_spriteset;
    purchase: dr1b_hn_purchase_spriteset;
    articulated_part: dr1b_h_articulated_part;
    can_attach_wagon: dr1b_h_can_attach_wagon;
    start_stop: dr1b_h_start_stop;
    power: return 0; // сам тянуть составы не может
  }
}

item (FEAT_TRAINS, dr1b_c) 
{
  property
  {
    name: string(STR_DR1B_C_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(get_car_year(2005), 1, 1);
    model_life: 25; // VEHICLE_NEVER_EXPIRES;
    vehicle_life: 25;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
    loading_speed: 12;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    cargo_capacity: 124;
    weight: 37.5 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_DIESEL;
    extra_power_per_wagon: 0 kW;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dr1b_c_get_spriteset;
    purchase: dr1b_c_purchase_spriteset;
    articulated_part: dr1b_h_articulated_part;
  }
}
