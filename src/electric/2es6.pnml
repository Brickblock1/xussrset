 // 2es6

#define IMAGEFILE  "src/electric/2es6.png"
purchase_sprites(_2es6_v1,   0, 0)
purchase_sprites(_2es6_v2, 110, 0)
purchase_sprites(_2es6_v3, 220, 0)
purchase_sprites(_2es6_v4, 330, 0)

electric2_1_sprites(10, _2es6_v1, 32,  40)
electric2_1_sprites(10, _2es6_v2, 32, 241)
electric2_1_sprites(10, _2es6_v3, 32, 442)
electric2_1_sprites(10, _2es6_v4, 32, 643)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2es6_purchase_sprites,
  ((vehicle_is_testing == 1) * 10) + 
  (current_year >= 2009) +
  (current_year >= 2012))
{
  0: _2es6_v2_purchase_sprites;
  1: _2es6_v3_purchase_sprites;
  2: _2es6_v4_purchase_sprites;
     _2es6_v1_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, _2es6_sprites_left,
  ((built_during_preview == 1) *10) + 
  (build_year >= 2009) +
  (build_year >= 2012))
{      
       0: _2es6_v2_sprites_left;
       1: _2es6_v3_sprites_left;
       2: _2es6_v4_sprites_left;
  10..12: _2es6_v1_sprites_left;
          align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, _2es6_sprites_right,
  ((built_during_preview == 1) *10) + 
  (build_year >= 2009) +
  (build_year >= 2012))
{ 
       0: _2es6_v2_sprites_right;
       1: _2es6_v3_sprites_right;
       2: _2es6_v4_sprites_right;
  10..12: _2es6_v1_sprites_right;
          align_10_sprites; 
}

engine_direction_template(_2es6, ) 

long_vehicle(_2es6)

engine_electric2_1_effect(_2es6, -1, 4)

engine_direction_template(_2es6, _create_effect) 

engine_speed(_2es6, 120)

engine_penalise_speed(_2es6, 4)

general_running_cost(_2es6, 654)

item (FEAT_TRAINS, _2es6) {
  property {
    name: string(STR_2ES6_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2008), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(2008,2050);
    retire_early: get_retire_early(2008,2050);
    reliability_decay: 1;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 84;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 4000 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 100 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(209.0, 100);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: _2es6_sprites;
    purchase: _2es6_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2ES6_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: _2es6_speed;
    purchase_speed: _2es6_purchase_speed;
    bitmask_vehicle_info: _2es6_bitmask_vehicle_info;
    create_effect: _2es6_create_effect_sprites_start;
    running_cost_factor: _2es6_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _2es6) {
    property { name: string(STR_2ES6_NAME_LONG); }
  }
}

