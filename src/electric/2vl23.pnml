 // 2vl23

// Спрайты

#define IMAGEFILE  "src/electric/2vl23.png"

// покупка
spriteset (_2vl23_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// нет тока
spriteset (_2vl23_notpowered_spriteset, IMAGEFILE)
{
  s10_template(0, 24, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2vl23_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 90, 0)
} 

spriteset (_2vl23_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 156, 0)
} 

spritegroup _2vl23_spritegroup
{
  loading: _2vl23_loading_spriteset;
  loaded: _2vl23_traveling_spriteset;
}

spriteset (_2vl23_speedover50_spriteset, IMAGEFILE)
{
  s10_template(0, 222, 0)
} 

// нет тока
spriteset (_2vl23_r_notpowered_spriteset, IMAGEFILE)
{
  s10_template(419, 24, 0)
} 

// развёрнутый: погрузка/перемещение
spriteset (_2vl23_r_loading_spriteset, IMAGEFILE)
{
  s10_template(419, 90, 0)
} 

spriteset (_2vl23_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(419, 156, 0)
} 

spritegroup _2vl23_r_spritegroup
{
  loading: _2vl23_r_loading_spriteset;
  loaded: _2vl23_r_traveling_spriteset;
}

spriteset (_2vl23_r_speedover50_spriteset, IMAGEFILE)
{
  s10_template(419, 222, 0)
} 

// нет тока
spriteset (_2vl23_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(0, 293, 0)
} 

// обычный: погрузка/перемещение
spriteset (_2vl23_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 359, 0)
} 

spriteset (_2vl23_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 425, 0)
} 

spritegroup _2vl23_v2_spritegroup
{
  loading: _2vl23_v2_loading_spriteset;
  loaded: _2vl23_v2_traveling_spriteset;
}

spriteset (_2vl23_v2_speedover50_spriteset, IMAGEFILE)
{
  s10_template(0, 491, 0)
} 

// нет тока
spriteset (_2vl23_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(419, 293, 0)
} 

// развёрнутый: погрузка/перемещение
spriteset (_2vl23_r_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(419, 359, 0)
} 

spriteset (_2vl23_r_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(419, 425, 0)
} 

spritegroup _2vl23_r_v2_spritegroup
{
  loading: _2vl23_r_v2_loading_spriteset;
  loaded: _2vl23_r_v2_traveling_spriteset;
}

spriteset (_2vl23_r_v2_speedover50_spriteset, IMAGEFILE)
{
  s10_template(419, 491, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _2vl23_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(_2vl23)) % 2) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  (cargo_subtype * 6)
)
{
  0: _2vl23_notpowered_spriteset;
  1: _2vl23_spritegroup;
  2: _2vl23_speedover50_spriteset;
  3: _2vl23_r_notpowered_spriteset;
  4: _2vl23_r_spritegroup;
  5: _2vl23_r_speedover50_spriteset;

  6: _2vl23_v2_notpowered_spriteset;
  7: _2vl23_v2_spritegroup;
  8: _2vl23_v2_speedover50_spriteset;
  9: _2vl23_r_v2_notpowered_spriteset;
  10: _2vl23_r_v2_spritegroup;
  11: _2vl23_r_v2_speedover50_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, _2vl23_get_spriteset1,
[
  STORE_TEMP(count_veh_id(_2vl23), 0),
  STORE_TEMP(current_speed, 1)
])
{
  _2vl23_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _2vl23_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _2vl23_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _2vl23_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, _2vl23_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _2vl23_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _2vl23_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _2vl23_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _2vl23_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_2vl23), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _2vl23_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_2vl23), 0),
  vehicle_type_id == _2vl23
])
{
  _2vl23_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _2vl23_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _2vl23_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, _2vl23_cost_factor,
  (current_year >= 1990)
)
{
  0: return 131;
  return 137;
}

item (FEAT_TRAINS, _2vl23)
{
  property
  {
    name: string(STR_2VL23_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1987, 1, 1);
    vehicle_life: 8; // срок службы
    model_life: get_model_life(1987,1990); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 131;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (274/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: (222.0/274/2/9.8);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: _2vl23_get_spriteset;
    purchase: _2vl23_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: _2vl23_can_attach_wagon;
    start_stop: _2vl23_start_stop;
    additional_text: return string(STR_2VL23_PURCHASE_HINT);
    cargo_subtype_text: _2vl23_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2vl23_speed;
    purchase_speed: _2vl23_purchase_speed;
    bitmask_vehicle_info: _2vl23_bitmask_vehicle_info;
    cost_factor: _2vl23_cost_factor;
  }
}
