 // 3es5k

// Спрайты
// 2ЭС5К и 3ЭС5К "Ермак" - новочеркасские электровозы переменного тока.
// Частично используются из 2es5k.pnml, второй вариант

#define IMAGEFILE  "src/electric/2es5k-3es5k.png"

// покупка
spriteset (_3es5k_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 40)
}

#undef IMAGEFILE

// расчёт ориентации секций локомотива. Первые 4 ориентируются парами, далее по номеру в составе. 
switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset2l2,
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) )
{
  0: _2es5k_v2_notpowered_spriteset;
  1: _2es5k_v2_spritegroup;
     _2es5k_v2_speedoverTH_spriteset;
}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset2r,
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) )
{ 0: _2es5k_b_v2_notpowered_spriteset;
     _2es5k_b_v2_spriteset;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset2l,
[ set_offset_to(2),
  (prev_vehicle_type_id() == _3es5k) || (prev_vehicle_type_id() == _2es5k) || 
  (position_in_consist == 0) ])
{ 1: _3es5k_get_spriteset2l2;
     _3es5k_get_spriteset2r;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_old,
  ((LOAD_TEMP(0) - count_veh_id(_3es5k) + 1) % 2) )
{ 1: _3es5k_get_spriteset2l;
     _3es5k_get_spriteset2r;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_10,
[  set_offset_to(-10),
  ((prev_vehicle_type_id() == _2es5k) ||
   (prev_vehicle_type_id() == _3es5k)) +
  ((prev_vehicle_type_id() == _3es5k_m) * 2)])
{ 1: _3es5k_get_spriteset_old;
  2: _3es5k_get_spriteset2r;
     _3es5k_get_spriteset2l;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_8,
[  set_offset_to(-8),
  ((prev_vehicle_type_id() == _2es5k) ||
   (prev_vehicle_type_id() == _3es5k)) +
  ((prev_vehicle_type_id() == _3es5k_m) * 2)])
{ 1: _3es5k_get_spriteset_10;
  2: _3es5k_get_spriteset2l;
     _3es5k_get_spriteset2r;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_6,
[  set_offset_to(-6),
  ((prev_vehicle_type_id() == _2es5k) ||
   (prev_vehicle_type_id() == _3es5k)) +
  ((prev_vehicle_type_id() == _3es5k_m) * 2)])
{ 1: _3es5k_get_spriteset_8;
  2: _3es5k_get_spriteset2r;
     _3es5k_get_spriteset2l;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_4,
[  set_offset_to(-4),
  ((prev_vehicle_type_id() == _2es5k) ||
   (prev_vehicle_type_id() == _3es5k)) +
  ((prev_vehicle_type_id() == _3es5k_m) * 2)])
{ 1: _3es5k_get_spriteset_6;
  2: _3es5k_get_spriteset2l;
     _3es5k_get_spriteset2r;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset_2,
[  set_offset_to(-2),
  (prev_vehicle_type_id() == _2es5k) ||
  (prev_vehicle_type_id() == _3es5k)])
{ 1: _3es5k_get_spriteset_4;
     _3es5k_get_spriteset2l;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset4,
[  set_offset_to(2),
  prev_vehicle_type_id() == _3es5k_m    
])
{ 1: _3es5k_get_spriteset2l2;
     _3es5k_get_spriteset_2;}

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset3,
[  set_offset_to(-2),
  prev_vehicle_type_id() == _3es5k_m  
])
{ 1: _3es5k_get_spriteset2r;
     _3es5k_get_spriteset4;}

switch (FEAT_TRAINS, PARENT, _3es5k_get_spriteset1,
[
  STORE_TEMP(count_veh_id(_3es5k), 0),
  STORE_TEMP(current_speed, 1) ])
{
     _3es5k_get_spriteset3;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3es5k_get_spriteset1;
}

// Если хотя бы 3 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _3es5k_start_stop2,
[
  set_offset_to(2),
  STORE_TEMP((prev_vehicle_type_id() == _3es5k) || ((prev_vehicle_type_id() == _3es5k_m)), 0),
  set_offset_to(4),
  ((prev_vehicle_type_id() == _3es5k) || (prev_vehicle_type_id() == _3es5k_m)) && LOAD_TEMP(0)
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_3_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 3, то не выпускать
switch (FEAT_TRAINS, PARENT, _3es5k_start_stop,
  (count_veh_id(_3es5k) + count_veh_id(_3es5k_m)) >= 3)
{
  1: _3es5k_start_stop2;
  return string(STR_START_STOP_NEED_3_OR_MORE_ENGINE);
}

// Макс. скорость: 110 км/ч
#define MAX_SPEED 110

switch (FEAT_TRAINS, SELF, _3es5k_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _3es5k_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _3es5k_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_3es5k), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 6)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _3es5k_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_3es5k), 0),
  vehicle_type_id == _3es5k
])
{
  _3es5k_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _3es5k_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _3es5k_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, _3es5k_cost_factor,
  current_year >= 2009)
{
  1: return 360;
  return 356;
}

item (FEAT_TRAINS, _3es5k)
{
  property
  {
    name: string(STR_3ES5K_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2006,2025);
    retire_early: get_retire_early(2006,2025); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 4065 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 96 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(211.0, 96);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _3es5k_get_spriteset;
    purchase: _3es5k_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: _3es5k_start_stop;
    additional_text: return string(STR_3ES5K_PURCHASE_HINT);
    speed: _3es5k_speed;
    purchase_speed: _3es5k_purchase_speed;
    bitmask_vehicle_info: _3es5k_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    cost_factor: _3es5k_cost_factor;
  }
}
