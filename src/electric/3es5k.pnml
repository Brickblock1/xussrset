 // 3es5k

// Спрайты
// 2ЭС5К и 3ЭС5К "Ермак" - новочеркасские электровозы переменного тока.
// Частично используются из 2es5k.pnml, второй вариант

#define IMAGEFILE  "src/electric/2es5k-3es5k.png"

// покупка
spriteset (_3es5k_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(152, 294, -4)
}

// бустерная секция (средняя)
spriteset (_3es5k_c_spriteset, IMAGEFILE)
{
  s10_template(152, 562, 0)
} 

spriteset (_3es5k_cr_spriteset, IMAGEFILE)
{
  s10_template(610, 562, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(_3es5k)) % 3) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot)))
)
{
  0: _2es5k_v2_notpowered_spriteset;
  1: _2es5k_v2_spritegroup;
  2: _2es5k_v2_speedover50_spriteset;
  3..5: _3es5k_cr_spriteset;
  6: _2es5k_b_v2_notpowered_spriteset;
  7..8: _2es5k_b_v2_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, _3es5k_get_spriteset1,
[
  STORE_TEMP(count_veh_id(_3es5k), 0),
  STORE_TEMP(current_speed, 1)
])
{
  _3es5k_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3es5k_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3es5k_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, _3es5k_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Если хотя бы 3 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _3es5k_start_stop2,
[
  set_offset_to(2),
  STORE_TEMP(prev_vehicle_type_id() == _3es5k, 0),
  set_offset_to(4),
  (prev_vehicle_type_id() == _3es5k) && LOAD_TEMP(0)
])
{
  1: return CB_RESULT_NO_TEXT;
  return string(STR_START_STOP_NEED_AT_LEAST_3_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 3, то не выпускать
switch (FEAT_TRAINS, PARENT, _3es5k_start_stop,
  count_veh_id(_3es5k) >= 3)
{
  1: _3es5k_start_stop2;
  return string(STR_START_STOP_NEED_3_OR_MORE_ENGINE);
}

// Макс. скорость: 110 км/ч
#define MAX_SPEED 110

switch (FEAT_TRAINS, SELF, _3es5k_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _3es5k_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, _3es5k_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(_3es5k), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 6)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, _3es5k_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(_3es5k), 0),
  vehicle_type_id == _3es5k
])
{
  _3es5k_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, _3es5k_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  _3es5k_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, _3es5k)
{
  property
  {
    name: string(STR_3ES5K_NAME);
    climates_available: get_climates_available();
    introduction_date: date(2006, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 4066 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 96 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: 0.22463151;
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: _3es5k_get_spriteset;
    purchase: _3es5k_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: _3es5k_can_attach_wagon;
    start_stop: _3es5k_start_stop;
    additional_text: return string(STR_3ES5K_PURCHASE_HINT);
    speed: _3es5k_speed;
    purchase_speed: _3es5k_purchase_speed;
    bitmask_vehicle_info: _3es5k_bitmask_vehicle_info;
  }
}
