 // 3vl23

// Спрайты

#define IMAGEFILE  "src/electric/3vl23.png"

// покупка
purchase_spriteset(_3vl23, 0, 0)

// нет тока
spriteset (_3vl23_notpowered_spriteset, IMAGEFILE)
{
  s10_template(0, 23, 0)
} 

// обычный: погрузка/перемещение
spriteset (_3vl23_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 89, 0)
} 

spriteset (_3vl23_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 155, 0)
} 

spritegroup _3vl23_spritegroup
{
  loading: _3vl23_loading_spriteset;
  loaded: _3vl23_traveling_spriteset;
}

spriteset (_3vl23_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(0, 221, 0)
} 

// нет тока
spriteset (_3vl23_bc_notpowered_spriteset, IMAGEFILE)
{
  s10_template(419, 23, 0)
} 

// секции Б, В: погрузка/перемещение
spriteset (_3vl23_bc_loading_spriteset, IMAGEFILE)
{
  s10_template(419, 89, 0)
} 

spriteset (_3vl23_bc_traveling_spriteset, IMAGEFILE)
{
  s10_template(419, 155, 0)
} 

spritegroup _3vl23_bc_spritegroup
{
  loading: _3vl23_bc_loading_spriteset;
  loaded: _3vl23_bc_traveling_spriteset;
}

spriteset (_3vl23_bc_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(419, 221, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _3vl23_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(_3vl23)) % 3) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot)))
)
{
  0: _3vl23_notpowered_spriteset;
  1: _3vl23_spritegroup;
  2: _3vl23_speedoverTH_spriteset;
  3: _3vl23_bc_notpowered_spriteset;
  4: _3vl23_bc_spritegroup;
  5: _3vl23_bc_speedoverTH_spriteset;
  6: _3vl23_bc_notpowered_spriteset;
  7: _3vl23_bc_spritegroup;
  8: _3vl23_bc_speedoverTH_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, _3vl23_get_spriteset1,
[
  STORE_TEMP(count_veh_id(_3vl23), 0),
  STORE_TEMP(current_speed, 1)
])
{
  _3vl23_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _3vl23_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _3vl23_get_spriteset1;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, _3vl23_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, _3vl23_speed,
[
  STORE_TEMP(get_max_speed(8, 90, MAX_SPEED), 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

engine_penalise_speed(_3vl23, 3)

item (FEAT_TRAINS, _3vl23)
{
  property
  {
    name: string(STR_3VL23_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1987), 1, 1);
    vehicle_life: 8; // срок службы
    model_life: get_model_life(1987,1987);
    retire_early: get_retire_early(1987,1987); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 131;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: (411/3) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(222.0, 411/3);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: _3vl23_get_spriteset;
    purchase: _3vl23_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_3VL23_PURCHASE_HINT);
    speed: _3vl23_speed;
    purchase_speed: _3vl23_purchase_speed;
    bitmask_vehicle_info: _3vl23_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
