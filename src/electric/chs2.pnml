// chs2

#define IMAGEFILE  "src/electric/chs2-25e0.png"
purchase_sprites(chs2_v1, 0, 0)
electric1r_2_sprites(11, chs2_v1a, 32, 40)
electric1r_2_sprites(11, chs2_v1b, 32, 375)
#undef IMAGEFILE
#define IMAGEFILE  "src/electric/chs2-34e0.png"
purchase_sprites(chs2_v2, 0, 0)
electric1_2_sprites(11, chs2_v2, 32,  40)
electric1_2_sprites(11, chs2_v3, 32, 241)
#undef IMAGEFILE
#define IMAGEFILE  "src/electric/chs2-34e2.png"
purchase_sprites(chs2_v3, 0, 0)
electric1_2_sprites(11, chs2_v4, 32,  40)
electric1_2_sprites(11, chs2_v5, 32, 241)
#undef IMAGEFILE
#define IMAGEFILE  "src/electric/chs2-53e.png"
purchase_sprites(chs2_v4,   0, 0)
purchase_sprites(chs2_v5, 100, 0)
electric1r_2_sprites(11, chs2_v6, 32,  40)
electric1r_2_sprites(11, chs2_v7, 32, 375)
#undef IMAGEFILE
purchase_sprites_select4(chs2, 1962, 1963, 1964, 1972)

switch (FEAT_TRAINS, SELF, chs2_sprites_left_25e0,
  cargo_subtype)
{
           0: chs2_v1a_sprites_left;
  LV_RAL6002: chs2_v1b_sprites_left;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_right_25e0,
  cargo_subtype)
{
           0: chs2_v1a_sprites_right;
  LV_RAL6002: chs2_v1b_sprites_right;
     align_10_sprites;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_left_34e0,
  check_year(GREAT_CHANGE_YEAR))
{
  0: chs2_v2_sprites_start;
     chs2_v3_sprites_start;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_left_34e2,
  check_year(GREAT_CHANGE_YEAR))
{
  0: chs2_v4_sprites_start;
     chs2_v5_sprites_start;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_left_53e,
  check_year(GREAT_CHANGE_YEAR))
{
  0: chs2_v6_sprites_left;
     chs2_v7_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_right_53e,
  check_year(GREAT_CHANGE_YEAR))
{
  0: chs2_v6_sprites_right;
     chs2_v7_sprites_right;
}

switch (FEAT_TRAINS, SELF, chs2_aGAMEEND_sprites_left,
  cargo_subtype)
{
  0: chs2_v1a_sprites_left;
  1: chs2_v1b_sprites_left;
  2: chs2_v2_sprites_start;
  3: chs2_v3_sprites_start;
  4: chs2_v4_sprites_start;
  5: chs2_v5_sprites_start;
  6: chs2_v6_sprites_left;
     chs2_v7_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs2_aGAMEEND_sprites_right,
  cargo_subtype)
{
  0: chs2_v1a_sprites_right;
  1: chs2_v1b_sprites_right;
  2: chs2_v2_sprites_start;
  3: chs2_v3_sprites_start;
  4: chs2_v4_sprites_start;
  5: chs2_v5_sprites_start;
  6: chs2_v6_sprites_right;
     chs2_v7_sprites_right;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_left,
  (build_year >= 1962) +
  (build_year >= 1963) +
  (build_year >= 1964) +
  (build_year >= 1972) +
  (build_year >= GAMEEND))
{
  0: chs2_sprites_left_25e0;
  1: chs2_sprites_left_34e0;
  2: chs2_sprites_left_34e2;
  3: chs2_sprites_left_53e;
  4: chs2_v7_sprites_left;
     chs2_aGAMEEND_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_right,
  (build_year >= 1962) +
  (build_year >= 1963) +
  (build_year >= 1964) +
  (build_year >= 1972) +
  (build_year >= GAMEEND))
{
  0: chs2_sprites_right_25e0;
  1: chs2_sprites_left_34e0;
  2: chs2_sprites_left_34e2;
  3: chs2_sprites_right_53e;
  4: chs2_v7_sprites_right;
     chs2_aGAMEEND_sprites_right;
}

engine_RNDany_direction_template(chs2, 5, 3)
long_vehicle_sp(chs2)

switch (FEAT_TRAINS, SELF, chs2_aGAMEEND_cargo_subtype_text, cargo_subtype)
{
  0: return string(STR_REFIT_GAMEEND, 1958);
  1: return string(STR_REFIT_GAMEEND_SP, 1958, string(STR_REFIT_TTD_53));
  2: return string(STR_REFIT_GAMEEND, 1962);
  3: return string(STR_REFIT_GAMEEND_SERVICE, 1962, 1972);
  4: return string(STR_REFIT_GAMEEND, 1963);
  5: return string(STR_REFIT_GAMEEND_SERVICE, 1963, 1972);
  6: return string(STR_REFIT_GAMEEND, 1964);
  7: return string(STR_REFIT_GAMEEND_SERVICE, 1964, 1972);
     return CB_FAILED;
}

  livery_template_none(chs2_b1960)

  livery_template_base_list(chs2_a1960, LV_RAL6002)

  livery_template_none(chs2_a1962)
livery_template_build_year3(chs2, 1960, 1962, GAMEEND)
engine_electric1_2_effect(chs2_create_effect, -2, 5)
engine_weight_change_build_after2(chs2, 120,
                                        125, 1962,
                                        126, 1964)
engine_TE_change_build_after2(chs2, get_tec_int(277.7, 120),
                                    get_tec_int(264.7, 125), 1962,
                                    get_tec_int(264.7, 126), 1964)
engine_power_change_build_after(chs2, 4030,
                                      4917, 1962)
engine_cost_change_build_after2(chs2, 61,
                                      71, 1962,
                                      72, 1964)
engine_speed_with_reduce_change_build_after(chs2, 140,
                                                  130, 30,
                                                  160, 1962)
engine_penalise_speed(chs2, 2)

switch (FEAT_TRAINS, SELF, chs2_running_cost_factor,
[  STORE_TEMP(251 + 43 * (build_year >= 1962) + (build_year >= 1964), 0),  // Моторы
   STORE_TEMP(35 + 6 * (build_year >= 1962), 1),                           // Бригада
   STORE_TEMP(88 + 15 * (build_year >= 1962), 2),                          // Износ
   STORE_TEMP(0, 3),                                                       // Сопровождение
   STORE_TEMP(62 + 11 * (build_year >= 1962), 4),                          // ТО
   STORE_TEMP(4 + (build_year >= 1962), 5),                                // Сертификация

   STORE_TEMP(140 + 20 * (build_year >= 1962), 6),                        // Скорость
   STORE_TEMP(120 + 5 * (build_year >= 1962) + (build_year >= 1964), 7),  // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])                                          // Максимальная масса
{ all_running_cost_factor; }                                              // 440 - 516 - 517

hint_engine_with_subtype(chs2_b1962,
     STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_SKODA_25E0,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1958, 1961,
     fact_skoda())
hint_engine_with_subtype_in(chs2_a1962,
     STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_SKODA_34E0,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1962,
     fact_skoda())
hint_engine_with_subtype_in(chs2_a1963,
     STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_SKODA_34E2,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1963,
     fact_skoda())
hint_engine_with_subtype(chs2_a1964,
     STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_SKODA_53E,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1964, 1982,
     fact_skoda())
hint_change_after3(chs2, 1962, 1963, 1964)

item (FEAT_TRAINS, chs2, 304) {
  property {
    name: string(STR_NAME_CHS2);
    vehicle_dates(1958, 1982, 30, 10, 8, 60)
    vehicle_pass()
    vehicle_dcelectric(4917, 120, 264.7)
  }
  graphics {
                additional_text: chs2_additional_text;
               articulated_part: articulated_part_dummy3;
           bitmask_vehicle_info: chs2_bitmask_vehicle_info;
               can_attach_wagon: any_can_attach_wagon;
                 cargo_capacity: return 0;
             cargo_subtype_text: chs2_cargo_subtype_text;
                 colour_mapping: any_cc_colour;
                    cost_factor: chs2_cost_factor;
                  create_effect: chs2_create_effect;
                        default: chs2_sprites;
                          power: chs2_power;
                       purchase: chs2_purchase_sprites;
                 purchase_speed: chs2_purchase_speed;
            running_cost_factor: chs2_running_cost_factor;
                          speed: chs2_speed;
                     start_stop: any_start_stop;
    tractive_effort_coefficient: chs2_tractive_effort_coefficient;
                         weight: chs2_weight;
  }
}

long_name_template(chs2, STR_LONGNAME_CHS2)
models_default_cargo_template(chs2)
allow_dcelectric(chs2)
