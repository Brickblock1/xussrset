 // chs2
// 304 (130h)

#define IMAGEFILE  "src/electric/chs2.png"
purchase_sprites(chs2_v1,   0, 0)
purchase_sprites(chs2_v2, 100, 0)
purchase_sprites(chs2_v3, 200, 0)
purchase_sprites(chs2_v4, 300, 0)

electric1_2_sprites(11, chs2_v1,  32,   40)
electric1_2_sprites(11, chs2_v2,  32,  241)
electric1_2_sprites(11, chs2_v3,  32,  442)
electric1_2_sprites(11, chs2_v4,  32,  643)
electric1r_2_sprites(11, chs2_v5, 32,  844)
electric1r_2_sprites(11, chs2_v6, 32, 1179)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, chs2_purchase_sprites,
  (current_year >= 1963) +
  (current_year >= 1964) +
  (current_year >= 1972))
{
  0: chs2_v1_purchase_sprites;
  1: chs2_v2_purchase_sprites;
  2: chs2_v3_purchase_sprites;
     chs2_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, chs2_sprites_left,
  ((build_year >= 1963) * 2) +
  ((build_year >= 1964) * 2) +
  ((build_year >= 1972) * 2) +
  (LOAD_TEMP(2) >= date(1972, 1, 1)))
{
     0: chs2_v1_sprites_start;
     1: chs2_v2_sprites_start;
     2: chs2_v3_sprites_start;
     3: chs2_v4_sprites_start;
     4: chs2_v5_sprites_left;
  5..7: chs2_v6_sprites_left;
        align_11_sprites; 
}

switch (FEAT_TRAINS, SELF, chs2_sprites_right,
  ((build_year >= 1963) * 2) +
  ((build_year >= 1964) * 2) +
  ((build_year >= 1972) * 2) +
  (LOAD_TEMP(2) >= date(1972, 1, 1)))
{
     0: chs2_v1_sprites_start;
     1: chs2_v2_sprites_start;
     2: chs2_v3_sprites_start;
     3: chs2_v4_sprites_start;
     4: chs2_v5_sprites_right;
  5..7: chs2_v6_sprites_right;
        align_11_sprites; 
}

engine_RNDany_direction_template(chs2, 5, 3) 
long_vehicle_sp(chs2)

switch (FEAT_TRAINS, SELF, chs2_cargo_subtype_text2,
  (build_year >= 1963) +
  (build_year >= 1964))
{
  0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_34E0), "");
  1: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_34E2), "");
     return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_53E), "");
}

switch (FEAT_TRAINS, SELF, chs2_cargo_subtype_text,
  cargo_subtype)
{
  0: chs2_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs2_additional_text,
  (current_year >= 1963) +
  (current_year >= 1964))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_34E0)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_IN, 1962), 
                   string(STR_PURCHASE_HINT_FACTORY_SKODA));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_34E2)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_IN, 1963), 
                   string(STR_PURCHASE_HINT_FACTORY_SKODA));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_53E)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1982), 
                   string(STR_PURCHASE_HINT_FACTORY_SKODA));
}

engine_electric1_2_effect(chs2, -2, 5)
engine_RC_change_build_after(chs2, 430, 
                                   431, 1964)
engine_weight_change_build_after(chs2, 125, 
                                       126, 1964)
engine_cost_change_build_after(chs2, 60, 
                                     61, 1964)
engine_speed_with_reduce(chs2, 160, 
                               130, 30)
engine_penalise_speed(chs2, 2)

item (FEAT_TRAINS, chs2, 304) {
  property {
    name: string(STR_NAME_CHS2);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1962), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1962,1982);
    retire_early: get_retire_early(1962,1982);
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 60;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4915 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 125 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(264.7, 125);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: chs2_sprites;
    purchase: chs2_purchase_sprites;
    cost_factor: chs2_cost_factor;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: chs2_additional_text;
    cargo_subtype_text: chs2_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs2_speed;
    purchase_speed: chs2_purchase_speed;
    bitmask_vehicle_info: chs2_bitmask_vehicle_info;
    weight: chs2_weight;
    create_effect: chs2_create_effect;
    running_cost_factor: chs2_running_cost_factor;
  }
}

long_name_template(chs2, STR_LONGNAME_CHS2)
models_default_cargo_template(chs2)
allow_dcelectric(chs2)
