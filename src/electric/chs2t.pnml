 // chs2t

// 1972-1973 - 1
// 1974-1975 - 2, с 1977 + 5
// 1976      - 2, с 1977 + 4

#define IMAGEFILE  "src/electric/chs2t.png"
purchase_sprites(chs2t_v1,   0, 0)
purchase_sprites(chs2t_v2, 100, 0)
purchase_sprites(chs2t_v3, 200, 0)

electric1_2_sprites(11chs2t, chs2t_v1,  32,   40)
electric1_2_sprites(11chs2t, chs2t_v2,  32,  241)
electric1_2_sprites(11chs2t, chs2t_v3,  32,  442)
electric1_2_sprites(11chs2t, chs2t_v4,  32,  643)
electric1_2_sprites(11chs2t, chs2t_v5,  32,  844)
electric1_2_sprites(11chs2t, chs2t_v6,  32, 1045)
electric1_2_sprites(11chs2t, chs2t_v7,  32, 1246)
electric1_2_sprites(11chs2t, chs2t_v8,  32, 1447)
electric1_2_sprites(11chs2t, chs2t_v9,  32, 1648)
electric1_2_sprites(11chs2t, chs2t_v10, 32, 1849)
electric1_2_sprites(11chs2t, chs2t_v11, 32, 2050)
electric1_2_sprites(11chs2t, chs2t_v12, 32, 2251)
electric1_2_sprites(11chs2t, chs2t_v13, 32, 2452)
electric1_2_sprites(11chs2t, chs2t_v14, 32, 2653)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, PARENT, chs2t_sprites_63E1_RND, 0)
{
   1: chs2t_v3_sprites_left;
   1: chs2t_v2_sprites_left;
}

random_switch (FEAT_TRAINS, PARENT, chs2t_sprites_63E1_RND2, 0)
{
   1: chs2t_v8_sprites_left;
   1: chs2t_v7_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs2t_sprites_63E1, cargo_subtype)
{
  0: chs2t_sprites_63E1_RND;
  1: chs2t_v4_sprites_left;
  2: chs2t_v5_sprites_left;
  3: chs2t_v6_sprites_left;
  4: chs2t_sprites_63E1_RND2;
     align_11_sprites;  
}

random_switch (FEAT_TRAINS, PARENT, chs2t_sprites_63E2_RND, 0)
{
   1: chs2t_v10_sprites_left;
   1: chs2t_v9_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs2t_sprites_63E2, cargo_subtype)
{
  0: chs2t_sprites_63E2_RND;
  1: chs2t_v11_sprites_left;
  2: chs2t_v12_sprites_left;
  3: chs2t_v13_sprites_left;
  4: chs2t_v14_sprites_left;
     align_11_sprites;  
}

switch (FEAT_TRAINS, SELF, chs2t_sprites_start,
  (build_year >= 1974) + 
  (build_year >= 1976) )
{
  0: chs2t_v1_sprites_left;
  1: chs2t_sprites_63E1;
  2: chs2t_sprites_63E2;
     align_11_sprites; 
}

long_vehicle(chs2t)

engine_electric1_2_effect(chs2t, -2, 5)

switch (FEAT_TRAINS, SELF, chs2t_purchase_sprites,
  (current_year >= 1974) + 
  (current_year >= 1976) )
{
  0: chs2t_v1_purchase_sprites;
  1: chs2t_v2_purchase_sprites;
     chs2t_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, chs2t_cargo_subtype_text_63E1, cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63E1, string(STR_REFIT_FACTORY_SKODA));
  1: return string(STR_REFIT_FACTORY_63E1, string(STR_REFIT_TTD_7C));
  2: return string(STR_REFIT_FACTORY_63E1, string(STR_REFIT_TTD_A5));
  3: return string(STR_REFIT_FACTORY_63E1, string(STR_REFIT_TTD_B4));
  4: return string(STR_REFIT_FACTORY_63E1, string(STR_REFIT_RED_ARROW));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs2t_cargo_subtype_text_63E2, cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_63E2, string(STR_REFIT_FACTORY_SKODA));
  1: return string(STR_REFIT_FACTORY_63E2, string(STR_REFIT_TTD_7C));
  2: return string(STR_REFIT_FACTORY_63E2, string(STR_REFIT_TTD_A5));
  3: return string(STR_REFIT_FACTORY_63E2, string(STR_REFIT_TTD_B4));
  4: return string(STR_REFIT_FACTORY_63E2, string(STR_REFIT_RED_ARROW));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs2t_cargo_subtype_text0,
  (build_year >= 1974) + (build_year >= 1976))
{
  0: return string(STR_REFIT_FACTORY_63E0);
  1: chs2t_cargo_subtype_text_63E1;
  2: chs2t_cargo_subtype_text_63E2;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs2t_cargo_subtype_text01,
  (build_year >= 1974) + (build_year >= 1976))
{
  0: return string(STR_REFIT_FACTORY_63E0);
  1: return string(STR_REFIT_FACTORY_63E1,"");
  2: return string(STR_REFIT_FACTORY_63E2,"");
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs2t_cargo_subtype_text,
  (current_year >= 1977))
{
  0: chs2t_cargo_subtype_text01;
     chs2t_cargo_subtype_text0;
}

switch (FEAT_TRAINS, SELF, chs2t_additional_text,
  (build_year >= 1974) + (build_year >= 1976))
{
  0: return string(STR_CHS2T_PURCHASE_HINT_S63E0);
  1: return string(STR_CHS2T_PURCHASE_HINT_S63E1);
     return string(STR_CHS2T_PURCHASE_HINT_S63E2);
}

engine_speed_with_reduce_and_pre_model(chs2t, 160, 130, 30, 10, 1974)

engine_penalise_speed0(chs2t)

general_running_cost(chs2t, 819)

item (FEAT_TRAINS, chs2t) {
  property {
    name: string(STR_CHS2T_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1972), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1972,1982);
    retire_early: get_retire_early(1972,1982);
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 70;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5490 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 126 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(159.0, 126);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: chs2t_sprites;
    purchase: chs2t_purchase_sprites;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: chs2t_additional_text;
    cargo_subtype_text: chs2t_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs2t_speed;
    purchase_speed: chs2t_purchase_speed;
    bitmask_vehicle_info: chs2t_bitmask_vehicle_info;
    create_effect: chs2t_create_effect_sprites_start;
    running_cost_factor: chs2t_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, chs2t) {
    property { name: string(STR_CHS2T_NAME_LONG); }
  }
}

