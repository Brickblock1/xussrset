 // chs4
// 193 (C1h) 

#define IMAGEFILE  "src/electric/chs4.png"
purchase_sprites(chs4_v1,   0, 0)
purchase_sprites(chs4_v2, 100, 0)

electric1r_2_sprites(12, chs4_v1, 32,   40)
electric1r_2_sprites(12, chs4_v2, 32,  375)
electric1r_2_sprites(12, chs4_v3, 32,  710)
electric1r_2_sprites(12, chs4_v4, 32, 1045)
electric1r_2_sprites(12, chs4_v5, 32, 1380)
electric1r_2_sprites(12, chs4_v6, 32, 1715)
#undef IMAGEFILE

#define IMAGEFILE  "src/electric/chs4z.png"
electric1r_2_sprites(12, chs4z_v1, 32,  40)
electric1r_2_sprites(12, chs4z_v2, 32, 375)
electric1r_2_sprites(12, chs4z_v3, 32, 710)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, chs4_purchase_sprites,
  current_year >= 1967)
{
  0: chs4_v1_purchase_sprites;
     chs4_v2_purchase_sprites;
}

random_switch (FEAT_TRAINS, PARENT, chs4_sprites_left_rnd, 0)
{
   1: chs4_v2_sprites_left;
   1: chs4_v3_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs4_sprites_left_factory,
  build_year >= 1967)
{
  0: chs4_v1_sprites_left;
     chs4_sprites_left_rnd;
}

switch (FEAT_TRAINS, SELF, chs4_sprites_left, 
  cargo_subtype)
{
  0: chs4_sprites_left_factory;
  1: chs4_v4_sprites_left;      
  2: chs4_v5_sprites_left;    
  3: chs4_v6_sprites_left;    
  4: chs4z_v1_sprites_left;       
  5: chs4z_v3_sprites_left;   
  6: chs4z_v2_sprites_left;   
  0xE0: chs4z_v1_sprites_left;       
  0xE1: chs4z_v3_sprites_left;   
  0xE2: chs4z_v2_sprites_left;   
        align_12_sprites; 
}

random_switch (FEAT_TRAINS, PARENT, chs4_sprites_right_rnd, 0)
{
   1: chs4_v2_sprites_right;
   1: chs4_v3_sprites_right;
}

switch (FEAT_TRAINS, SELF, chs4_sprites_right_factory,
  build_year >= 1967)
{
  0: chs4_v1_sprites_right;
     chs4_sprites_right_rnd;
}

switch (FEAT_TRAINS, SELF, chs4_sprites_right, 
  cargo_subtype)
{
  0: chs4_sprites_right_factory;
  1: chs4_v4_sprites_right;      
  2: chs4_v5_sprites_right;    
  3: chs4_v6_sprites_right;    
  4: chs4z_v1_sprites_right;       
  5: chs4z_v3_sprites_right;   
  6: chs4z_v2_sprites_right;   
  0xE0: chs4z_v1_sprites_right;       
  0xE1: chs4z_v3_sprites_right;   
  0xE2: chs4z_v2_sprites_right;   
        align_12_sprites; 
}

engine_RNDany_direction_template(chs4, 5, 3) 

long_vehicle_sp(chs4)

switch (FEAT_TRAINS, SELF, chs4_refit_cost,
  ((extra_callback_info1 & 0xFF00) >= 0x0400)+
  ((cargo_subtype >= 0x04) * 2))
{
  1: return 1;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, chs4_cargo_subtype_text_aUSSR,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), "");
  1: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_RZD_TCH15_BALASHOV));
  2: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_RZD_TCH8_KIROV));
  3: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_UZ_STANDARD_TCH1_KIEV));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs4_cargo_subtype_text_a2002,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), "");
  1: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_RZD_TCH15_BALASHOV));
  2: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_RZD_TCH8_KIROV));
  3: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), string(STR_REFIT_UZ_STANDARD_TCH1_KIEV));

  4: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), "");
  5: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), string(STR_REFIT_UZ_STANDARD_TCH1_KIEV));
  6: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), string(STR_REFIT_UZ_ARENA_2000));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs4_cargo_subtype_text_long,
  ((current_year >= USSREND) || (build_year >= USSREND)) +
  ((current_year >= 2002) || (build_year >= 2002)))
{
  0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_52E), "");
  1: chs4_cargo_subtype_text_aUSSR;
     chs4_cargo_subtype_text_a2002;
}

livery_template(chs4_b2002, FACTORY, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt3(chs4_b2002, RZD_TCH15_BALASHOV, RZD_TCH8_KIROV, UZ_STANDARD_TCH1_KIEV)

switch (FEAT_TRAINS, SELF, chs4_a2002_cargo_subtype_text_pre,
  cargo_subtype)
{
  0xE0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), "");                                                        
  0xE1: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), string(STR_REFIT_UZ_STANDARD_TCH1_KIEV));    
  0xE2: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_NAME_CHS4Z), string(STR_REFIT_UZ_ARENA_2000));            
        chs4_b2002_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, SELF, chs4_cargo_subtype_text_short,
  (build_year >= 2002) || (current_year >= 2002))
{
  0: chs4_b2002_cargo_subtype_text_pre;
     chs4_a2002_cargo_subtype_text_pre;
}    

switch (FEAT_TRAINS, SELF, chs4_cargo_subtype_text,
  enable_long_names)
{
  1: chs4_cargo_subtype_text_long;
     chs4_cargo_subtype_text_short;
}

switch (FEAT_TRAINS, SELF, chs4_additional_text,
  current_year >= 2002)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_52E)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1965, 1972), 
                   string(STR_PURCHASE_HINT_FACTORY_SKODA));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_52E)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1965, 1972), 
                   string(STR_PURCHASE_HINT_FACTORY_SKODA),
                   string(STR_PURCHASE_HINT_MAJOR_OVERHAUL));
}

engine_electric1_2_effect(chs4, -1, 5)
engine_speed_with_reduce(chs4, 160, 140, 40)
engine_penalise_speed0(chs4)
general_running_cost(chs4, 577)

item (FEAT_TRAINS, chs4, 193) {
  property {
    name: string(STR_NAME_CHS4);
    vehicle_dates(1965, 1972, 40, 10, 8, 82)
    vehicle_pass()
    vehicle_acelectric(6720, 123, 264.7)
  }
  graphics {
    default: chs4_sprites;
    purchase: chs4_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: chs4_additional_text;
    cargo_subtype_text: chs4_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs4_speed;
    purchase_speed: chs4_purchase_speed;
    bitmask_vehicle_info: chs4_bitmask_vehicle_info;
    refit_cost: chs4_refit_cost;
    create_effect: chs4_create_effect;
    running_cost_factor: chs4_running_cost_factor;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(chs4, STR_LONGNAME_CHS4)
models_default_cargo_template(chs4)
allow_acelectric(chs4)
