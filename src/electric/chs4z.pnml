 // chs4z

// Спрайты
// с 2002: 3 окраски

#define IMAGEFILE  "src/electric/chs4z.png"

// покупка
spriteset (chs4z_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// нет тока
spriteset (chs4z_notpowered_spriteset, IMAGEFILE)
{
  s12_template(0, 61, 0)
} 

// аверс: погрузка/перемещение
spriteset (chs4z_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 128, 0)
} 

spriteset (chs4z_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 195, 0)
} 

spritegroup chs4z_spritegroup
{
  loading: chs4z_loading_spriteset;
  loaded: chs4z_traveling_spriteset;
}

spriteset (chs4z_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(0, 262, 0)
} 

// 2: нет тока
spriteset (chs4z_v2_notpowered_spriteset, IMAGEFILE)
{
  s12_template(0, 354, 0)
} 

// 2: обычный: погрузка/перемещение
spriteset (chs4z_v2_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 421, 0)
} 

spriteset (chs4z_v2_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 488, 0)
} 

spritegroup chs4z_v2_spritegroup
{
  loading: chs4z_v2_loading_spriteset;
  loaded: chs4z_v2_traveling_spriteset;
}

spriteset (chs4z_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(0, 555, 0)
} 

// 3: нет тока
spriteset (chs4z_v3_notpowered_spriteset, IMAGEFILE)
{
  s12_template(0, 647, 0)
} 

// обычный: погрузка/перемещение
spriteset (chs4z_v3_loading_spriteset, IMAGEFILE)
{
  s12_template(0, 714, 0)
} 

spriteset (chs4z_v3_traveling_spriteset, IMAGEFILE)
{
  s12_template(0, 781, 0)
} 

spritegroup chs4z_v3_spritegroup
{
  loading: chs4z_v3_loading_spriteset;
  loaded: chs4z_v3_traveling_spriteset;
}

spriteset (chs4z_v3_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(0, 848, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, chs4z_get_spriteset2,
  // поправка на нет тока
  ((last_computed_result > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot)) +
  cargo_subtype * 3
)
{
  0: chs4z_notpowered_spriteset;
  1: chs4z_spritegroup;
  2: chs4z_speedoverTH_spriteset;
  3: chs4z_v2_notpowered_spriteset;
  4: chs4z_v2_spritegroup;
  5: chs4z_v2_speedoverTH_spriteset;
  6: chs4z_v3_notpowered_spriteset;
  7: chs4z_v3_spritegroup;
  chs4z_v3_speedoverTH_spriteset;
}

switch (FEAT_TRAINS, PARENT, chs4z_get_spriteset1,
  current_speed)
{
  chs4z_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, chs4z_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  chs4z_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, chs4z_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_CHS4Z_REFIT_V1);
  1: string(STR_CHS4Z_REFIT_V2);
  2: string(STR_CHS4Z_REFIT_V3);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 160 км/ч после 15 лет - 140 км/ч
#define MAX_SPEED get_max_speed(15, 140, 160)

switch (FEAT_TRAINS, SELF, chs4z_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, chs4z_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, chs4z_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, chs4z) 
{
  property
  {
    name: string(STR_CHS4Z_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2002), 1, 1);
    vehicle_life: 15; // срок службы
    model_life: get_model_life(2002,2012);
    retire_early: get_retire_early(2002,2012); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 6710 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 123 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(165.0, 123);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: chs4z_get_spriteset;
    purchase: chs4z_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_CHS4Z_PURCHASE_HINT);
    cargo_subtype_text: chs4z_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs4z_speed;
    purchase_speed: chs4z_purchase_speed;
    bitmask_vehicle_info: chs4z_bitmask_vehicle_info;
    cost_factor: return 452;
  }
}
