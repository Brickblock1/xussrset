 // chs8
// 196 (C4h)

#define IMAGEFILE  "src/electric/chs8.png"
purchase_sprites(chs8, 0, 0)

electric2_1_sprites(9, chs8_v1,  32,   40)  // Заводская 1
electric2_1_sprites(9, chs8_v2,  32,  241)  // Заводская 2
electric2_1_sprites(9, chs8_v3,  32,  442)  // МПС1
electric2_1_sprites(9, chs8_v4,  32,  643)  // РЖД 1
electric2_1_sprites(9, chs8_v5,  32,  844)  // РЖД 2 - СКЖД
electric2_1_sprites(9, chs8_v6,  32, 1045)  // Сочи
electric2_1_sprites(9, chs8_v7,  32, 1246)  // Кубань
electric2_1_sprites(9, chs8_v8,  32, 1447)  // Арена 2000
electric2_1_sprites(9, chs8_v9,  32, 1648)  // УЗ ТЧ 1
electric2_1_sprites(9, chs8_v10, 32, 1849)  // Столичный экспресс
#undef IMAGEFILE

random_switch (FEAT_TRAINS, PARENT, chs8_sprites_l_ran, 0)
{
   1: chs8_v1_sprites_left;
   1: chs8_v2_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs8_sprites_left,
  cargo_subtype)
{
  0: chs8_sprites_l_ran;
  1: chs8_v3_sprites_left;
  2: chs8_v4_sprites_left;
  3: chs8_v9_sprites_left;
  LV_NORTHERN_KAVKAZ_RAILWAY: chs8_v5_sprites_left;
  LV_RITSA_SOCHI:             chs8_v6_sprites_left;
  LV_KUBAN:                   chs8_v7_sprites_left;
  LV_UZ_ARENA_2000:           chs8_v8_sprites_left;
  LV_UZ_CAPITAL_EXPRESS:      chs8_v10_sprites_left;
  align_9_sprites;
}

random_switch (FEAT_TRAINS, PARENT, chs8_sprites_r_ran, 0)
{
   1: chs8_v1_sprites_right;
   1: chs8_v2_sprites_right;
}

switch (FEAT_TRAINS, SELF, chs8_sprites_right,
  cargo_subtype)
{
  0: chs8_sprites_r_ran;
  1: chs8_v3_sprites_right;
  2: chs8_v4_sprites_right;
  3: chs8_v9_sprites_right;
  LV_NORTHERN_KAVKAZ_RAILWAY: chs8_v5_sprites_right;
  LV_RITSA_SOCHI:             chs8_v6_sprites_right;
  LV_KUBAN:                   chs8_v7_sprites_right;
  LV_UZ_ARENA_2000:           chs8_v8_sprites_right;
  LV_UZ_CAPITAL_EXPRESS:      chs8_v10_sprites_right;
  align_9_sprites;
}

engine_direction_template(chs8, )
long_vehicle(chs8)

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text_a1995,
  cargo_subtype)
{
  1:                          return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_TTD_59));
  2:                          return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_RZD_TCH11_TIMASHEVSKAYA));
  3:                          return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_UZ_STANDARD_TCH1_KIEV));
  LV_NORTHERN_KAVKAZ_RAILWAY: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_NORTHERN_KAVKAZ_RAILWAY));
  LV_RITSA_SOCHI:             return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_SOCHI));
  LV_KUBAN:                   return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_KUBAN));
  LV_UZ_ARENA_2000:           return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_UZ_ARENA_2000));
  LV_UZ_CAPITAL_EXPRESS:      return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), string(STR_REFIT_UZ_CAPITAL_EXPRESS));
                              return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), "");
}

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text_long,
  check_year(1995))
{
  0: return string(STR_REFIT_FACTORY_SKODA_TYPE, string(STR_PURCHASE_HINT_SKODA_81E), "");
     chs8_cargo_subtype_text_a1995;
}

livery_template(chs8_short, FACTORY, LV_NORTHERN_KAVKAZ_RAILWAY, LV_RITSA_SOCHI, LV_KUBAN, LV_UZ_ARENA_2000, LV_UZ_CAPITAL_EXPRESS, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt3(chs8_short, TTD_59, RZD_TCH11_TIMASHEVSKAYA, UZ_STANDARD_TCH1_KIEV)

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text_short,
  check_year(USSREND))
{
  0: chs8_short_cargo_subtype_text;
     chs8_short_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text,
  enable_long_names)
{
  1: chs8_cargo_subtype_text_long;
     chs8_cargo_subtype_text_short;
}

switch (FEAT_TRAINS, SELF, chs8_additional_text,
   current_year >= 1993)
{
  0: return string(STR_PURCHASE_HINT_ENGINE,
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_81E)),
                   string(STR_PURCHASE_HINT_SECTIONS_2),
                   string(STR_PURCHASE_HINT_FROMTO, 1983, 2000),
                   string(STR_PURCHASE_HINT_FACTORY_SKODA));
     return string(STR_PURCHASE_HINT_ENGINE,
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_SKODA_81E)),
                   string(STR_PURCHASE_HINT_SECTIONS_2),
                   string(STR_PURCHASE_HINT_FROMTO, 1983, 2000),
                   string(STR_PURCHASE_HINT_FACTORY_SKODA_A1993));
}

engine_electric2_1_effect(chs8, -1, 2)
engine_direction_template(chs8, _create_effect)
engine_speed_with_reduce(chs8, 180, 140, 40)
engine_penalise_speed(chs8, 2)
general_running_cost(chs8, 520)

item (FEAT_TRAINS, chs8, 196) {
  property {
    name: string(STR_NAME_CHS8);
    vehicle_dates(1983, 2000, 40, 10, 8, 78)
    vehicle_pass()
    vehicle_acelectric(5770, 87.5, 235.5)
  }
  graphics {
    default: chs8_sprites;
    purchase: chs8_purchase_sprites;
    purchase_speed: chs8_purchase_speed;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: chs8_additional_text;
    cargo_subtype_text: chs8_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs8_speed;
    bitmask_vehicle_info: chs8_bitmask_vehicle_info;
    create_effect: chs8_create_effect_sprites_start;
    running_cost_factor: chs8_running_cost_factor;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(chs8, STR_LONGNAME_CHS8)
models_default_cargo_template(chs8)
allow_acelectric(chs8)
