 // chs8

// 1983-2000: 2 заводские
// с 1990   : +1 деповская
// с 1995   : +7 деповских

#define IMAGEFILE  "src/electric/chs8.png"
purchase_sprites(chs8, 0, 0)

electric2_1_sprites(9, chs8_v1,  32,   40)
electric2_1_sprites(9, chs8_v2,  32,  241)
electric2_1_sprites(9, chs8_v3,  32,  442)
electric2_1_sprites(9, chs8_v4,  32,  643)
electric2_1_sprites(9, chs8_v5,  32,  844)
electric2_1_sprites(9, chs8_v6,  32, 1045)
electric2_1_sprites(9, chs8_v7,  32, 1246)
electric2_1_sprites(9, chs8_v8,  32, 1447)
electric2_1_sprites(9, chs8_v9,  32, 1648)
electric2_1_sprites(9, chs8_v10, 32, 1849)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, PARENT, chs8_sprites_l_ran, 0)
{
   1: chs8_v1_sprites_left;
   1: chs8_v2_sprites_left;
}

switch (FEAT_TRAINS, SELF, chs8_sprites_l,
  cargo_subtype)
{
  0: chs8_sprites_l_ran;
  1: chs8_v3_sprites_left;
  2: chs8_v4_sprites_left;
  3: chs8_v5_sprites_left;
  4: chs8_v9_sprites_left;
  5: chs8_v6_sprites_left;
  6: chs8_v7_sprites_left;
  7: chs8_v8_sprites_left;
  8: chs8_v10_sprites_left;
     align_9_sprites; 
}

random_switch (FEAT_TRAINS, PARENT, chs8_sprites_r_ran, 0)
{
   1: chs8_v1_sprites_right;
   1: chs8_v2_sprites_right;
}

switch (FEAT_TRAINS, SELF, chs8_sprites_r,
  cargo_subtype)
{
  0: chs8_sprites_r_ran;
  1: chs8_v3_sprites_right;
  2: chs8_v4_sprites_right;
  3: chs8_v5_sprites_right;
  4: chs8_v9_sprites_right;
  5: chs8_v6_sprites_right;
  6: chs8_v7_sprites_right;
  7: chs8_v8_sprites_right;
  8: chs8_v10_sprites_right;
     align_9_sprites; 
}

engine_direction_template(chs8, )

long_vehicle(chs8)

engine_electric2_1_effect(chs8, -1, 2)

engine_direction_template(chs8, _create_effect) 

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_81E, "");
  1: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_NORTHERN_KAVKAZ_RAILWAY));
}

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text1,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_81E, "");
  1: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_NORTHERN_KAVKAZ_RAILWAY));
  2: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_RZD_TCH11_TIMASHEVSKAYA));
  3: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_TTD_59));
  4: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_UKRAINIAN_RAILWAY_STANDARD_TCH1_KIEV));
  5: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_SOCHI));
  6: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_KUBAN));
  7: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_UKRAINIAN_RAILWAY_ARENA_2000));
  8: return string(STR_REFIT_FACTORY_81E, string(STR_REFIT_UKRAINIAN_RAILWAY_CAPITAL_EXPRESS));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, chs8_cargo_subtype_text,
  ((current_year >= 1990) || (build_year >= 1990)) +
  ((current_year >= 1995) || (build_year >= 1995)) )
{
  0: return string(STR_REFIT_FACTORY_81E, "");
  1: chs8_cargo_subtype_text2;
     chs8_cargo_subtype_text1;
}

engine_speed_with_reduce(chs8, 160, 140, 40)

engine_penalise_speed(chs8, 2)

item (FEAT_TRAINS, chs8)
{
  property
  {
    name: string(STR_CHS8_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1983), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1983,2000);
    retire_early: get_retire_early(1983,2000);
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: 172;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5765 hpM;
    speed: 160 km/h;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 87.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(144.0, 87.5);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: chs8_sprites;
    purchase: chs8_purchase_sprites;
    purchase_speed: chs8_purchase_speed;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_CHS8_PURCHASE_HINT);
    cargo_subtype_text: chs8_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: chs8_speed;
    bitmask_vehicle_info: chs8_bitmask_vehicle_info;
    create_effect: chs8_create_effect_sprites_start;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, chs8)
  {
    property
    { name: string(STR_CHS8_NAME_LONG); }
  }
}

