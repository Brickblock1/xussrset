 // ds3

// Спрайты

#define IMAGEFILE  "src/electric/ds3.png"

// покупка
spriteset (ds3_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// обычный: погрузка/перемещение
spriteset (ds3_loading_spriteset, IMAGEFILE)
{
  s10_template(0, 19, 0)
} 

spriteset (ds3_traveling_spriteset, IMAGEFILE)
{
  s10_template(0, 86, 0)
} 

spritegroup ds3_spritegroup
{
  loading: ds3_loading_spriteset;
  loaded: ds3_traveling_spriteset;
}

spriteset (ds3_speedover50_spriteset, IMAGEFILE)
{
  s10_template(0, 153, 0)
} 

// развёрнутый: погрузка/перемещение
spriteset (ds3_r_loading_spriteset, IMAGEFILE)
{
  s10_template(443, 19, 0)
} 

spriteset (ds3_r_traveling_spriteset, IMAGEFILE)
{
  s10_template(443, 86, 0)
} 

spritegroup ds3_r_spritegroup
{
  loading: ds3_r_loading_spriteset;
  loaded: ds3_r_traveling_spriteset;
}

spriteset (ds3_r_speedover50_spriteset, IMAGEFILE)
{
  s10_template(443, 153, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ds3_get_spriteset2,
  ((LOAD_TEMP(0) - count_veh_id(ds3)) % 2) +
  ((LOAD_TEMP(1) > threshold_of_speed()) * 2)
)
{
  0: ds3_spritegroup;
  1: ds3_r_spritegroup;
  2: ds3_speedover50_spriteset;
  ds3_r_speedover50_spriteset;
}

switch (FEAT_TRAINS, PARENT, ds3_get_spriteset1,
[
  STORE_TEMP(count_veh_id(ds3), 0),
  STORE_TEMP(current_speed, 1)
])
{
  ds3_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, ds3_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  ds3_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, ds3_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, ds3_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 120 км/ч
#define MAX_SPEED 120

switch (FEAT_TRAINS, SELF, ds3_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, ds3_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, ds3_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(ds3), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, ds3_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(ds3), 0),
  vehicle_type_id == ds3
])
{
  ds3_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, ds3_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  ds3_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, ds3)
{
  property
  {
    name: string(STR_DS3_NAME);
    climates_available: get_climates_available();
    introduction_date: date(2002, 1, 1);
    vehicle_life: 20; // срок службы
    model_life: 20; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5800 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 90 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: 0.15873;
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: ds3_get_spriteset;
    purchase: ds3_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: ds3_can_attach_wagon;
    start_stop: ds3_start_stop;
    additional_text: return string(STR_DS3_PURCHASE_HINT);
    speed: ds3_speed;
    purchase_speed: ds3_purchase_speed;
    bitmask_vehicle_info: ds3_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    cost_factor: return 754;
  }
}
