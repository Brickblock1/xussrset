 // e5k

// Спрайты
// Э5К "Ермак" - односекционный грузопассажирский электровоз переменного тока.

#define IMAGEFILE  "src/electric/e5k.png"

// покупка
spriteset (e5k_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(0, 0) }
spriteset (e5k_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(0, 0) }
switch (FEAT_TRAINS, SELF, e5k_purchase_spriteset, 
  enable_icons)
{  1: e5k_purchase_spriteset_ico;
      e5k_purchase_spriteset_noico;}

// нет тока
spriteset (e5k_notpowered_spriteset, IMAGEFILE)
{
  s11_template(0, 42, 0)
} 

// обычный: погрузка/перемещение
spriteset (e5k_loading_spriteset, IMAGEFILE)
{
  s11_template(0, 109, 0)
} 

spriteset (e5k_traveling_spriteset, IMAGEFILE)
{
  s11_template(0, 176, 0)
} 

spritegroup e5k_spritegroup
{
  loading: e5k_loading_spriteset;
  loaded: e5k_traveling_spriteset;
}

spriteset (e5k_speedoverTH_spriteset, IMAGEFILE)
{
  s11_template(0, 243, 0)
} 

// развёрнутый: погрузка/перемещение
spriteset (e5k_r_loading_spriteset, IMAGEFILE)
{
  s11_template(440, 109, 0)
} 

spriteset (e5k_r_traveling_spriteset, IMAGEFILE)
{
  s11_template(440, 176, 0)
} 

/*
// временно не используется
spritegroup e5k_r_spritegroup
{
  loading: e5k_r_loading_spriteset;
  loaded: e5k_r_traveling_spriteset;
}
*/

spriteset (e5k_r_speedoverTH_spriteset, IMAGEFILE)
{
  s11_template(440, 243, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, e5k_get_spriteset2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot)))
)
{
  0: e5k_notpowered_spriteset;
  1: e5k_spritegroup;
  2: e5k_speedoverTH_spriteset;
  align_11_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, e5k_get_spriteset1,
[
  STORE_TEMP(current_speed, 1)
])
{
  e5k_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, e5k_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  e5k_get_spriteset1;
}

// Макс. скорость: 110 км/ч
#define MAX_SPEED 110

switch (FEAT_TRAINS, SELF, e5k_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, e5k_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, e5k_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, e5k)
{
  property
  {
    name: string(STR_E5K_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2007), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2007,2020);
    retire_early: get_retire_early(2007,2020); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 4065 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 100 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(212.0, 100);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: e5k_get_spriteset;
    purchase: e5k_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_E5K_PURCHASE_HINT);
    speed: e5k_speed;
    purchase_speed: e5k_purchase_speed;
    bitmask_vehicle_info: e5k_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    cost_factor: return 370;
  }
}
