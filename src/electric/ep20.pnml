 // ep20
// 421 (1A5h)

#define IMAGEFILE  "src/electric/ep20.png"
purchase_sprites(ep20, 0, 0)

electric1r_2_sprites(13, ep20_v1, 32,   40)
electric1r_2_sprites(13, ep20_v2, 32,  375)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ep20_sprites_left,
  current_railtype)
{
  ERA1: ep20_v1_sprites_left;
  ERA2: ep20_v1_sprites_left;
  ERA3: ep20_v1_sprites_left;
  ERA4: ep20_v1_sprites_left;
  ER2S: ep20_v1_sprites_left;
        ep20_v2_sprites_left;
}

switch (FEAT_TRAINS, SELF, ep20_sprites_right,
  current_railtype)
{
  ERA1: ep20_v1_sprites_right;
  ERA2: ep20_v1_sprites_right;
  ERA3: ep20_v1_sprites_right;
  ERA4: ep20_v1_sprites_right;
  ER2S: ep20_v1_sprites_right;
        ep20_v2_sprites_right;
}

engine_RNDany_direction_template(ep20, 5, 3) 

long_vehicle_sp(ep20)

engine_electric1_2_effect(ep20_v1, -2, 7)
engine_electric1_2_effect(ep20_v2, -1, 6)

switch (FEAT_TRAINS, SELF, ep20_create_effect,
  current_railtype)
{
  ERA1: ep20_v1_create_effect;
  ERA2: ep20_v1_create_effect;
  ERA3: ep20_v1_create_effect;
  ERA4: ep20_v1_create_effect;
  ER2S: ep20_v1_create_effect;
        ep20_v2_create_effect;
}

switch (FEAT_TRAINS, SELF, ep20_refit_cost,
  ((extra_callback_info1 & 0xFF00) == (0x100 * LV_OCTOBER_RAILWAY)) +
  ((cargo_subtype == LV_OCTOBER_RAILWAY) * 2))
{
  1: return 189;
  2: return 189;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, ep20_tractive_effort_coefficient,
  cargo_subtype == LV_OCTOBER_RAILWAY)
{
  1: return get_tec_int(350.0, 129);
     return get_tec_int(450.0, 129);
}
livery_template(ep20, RZD_PID, LV_OCTOBER_RAILWAY, LV_RZD_PID, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
general_running_cost(ep20_200, 1619)
general_running_cost(ep20_160, 1527)
switch (FEAT_TRAINS, SELF, ep20_running_cost_factor,
  cargo_subtype == LV_OCTOBER_RAILWAY)
{
  1: ep20_200_running_cost_factor;
     ep20_160_running_cost_factor;
}
engine_speed(ep20_200, 200)
engine_speed(ep20_160, 160)
switch (FEAT_TRAINS, SELF, ep20_speed,
  cargo_subtype == LV_OCTOBER_RAILWAY)
{
  1: ep20_200_speed;
     ep20_160_speed;
}
engine_penalise_speed0(ep20)

item (FEAT_TRAINS, ep20, 421) {
  property {
    name: string(STR_NAME_EP20);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2012), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(2012,2050);
    retire_early: get_retire_early(2012,2050);
    reliability_decay: 10;
    cost_factor: 217;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dual_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 8660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 129 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(450.0, 129);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }
  graphics {
    default: ep20_sprites;
    purchase: ep20_purchase_sprites;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_ACDC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                                   string(STR_PURCHASE_HINT_FROM, 2012), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK),
                                   string(STR_PURCHASE_HINT_EDITION_NEW_SPEED, 200));
    tractive_effort_coefficient: ep20_tractive_effort_coefficient;
    speed: ep20_speed;
    purchase_speed: ep20_160_purchase_speed;
    bitmask_vehicle_info: ep20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: ep20_cargo_subtype_text;
    create_effect: ep20_create_effect;
    running_cost_factor: ep20_running_cost_factor;
    refit_cost: ep20_refit_cost;
  }
}

long_name_template(ep20, STR_LONGNAME_EP20)
models_default_cargo_template(ep20)
