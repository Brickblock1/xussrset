 // ep2k

#define IMAGEFILE  "src/electric/ep2k.png"
purchase_sprites(ep2k_v1,   0, 0)
purchase_sprites(ep2k_v2, 100, 0)
purchase_sprites(ep2k_v3, 200, 0)

electric1r_2_sprites(13, ep2k_v1, 32,   40)
electric1r_2_sprites(13, ep2k_v2, 32,  375)
electric1r_2_sprites(13, ep2k_v3, 32,  710)
electric1r_2_sprites(13, ep2k_v4, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ep2k_purchase_sprites,
  (current_year >= 2009) +
  (current_year >= 2012) )
{
  0: ep2k_v1_purchase_sprites;
  1: ep2k_v2_purchase_sprites;
     ep2k_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, ep2k_sprites_l2,
  cargo_subtype)
{
  0: ep2k_v2_sprites_left;
  1: ep2k_v3_sprites_left;
  2: ep2k_v4_sprites_left;
     align_13_sprites; 
}

switch (FEAT_TRAINS, SELF, ep2k_sprites_l,
  (build_year>= 2009) +
  (build_year>= 2012) )
{
  0: ep2k_v1_sprites_left;
  1: ep2k_sprites_l2;
  2: ep2k_v4_sprites_left;
     align_13_sprites; 
}

switch (FEAT_TRAINS, SELF, ep2k_sprites_r2,
  cargo_subtype)
{
  0: ep2k_v2_sprites_right;
  1: ep2k_v3_sprites_right;
  2: ep2k_v4_sprites_right;
     align_13_sprites; 
}

switch (FEAT_TRAINS, SELF, ep2k_sprites_r,
  (build_year>= 2009) +
  (build_year>= 2012) )
{
  0: ep2k_v1_sprites_right;
  1: ep2k_sprites_r2;
  2: ep2k_v4_sprites_right;
     align_13_sprites; 
}

engine_RNDany_direction_template(ep2k, 5, 3) 

long_vehicle_sp(ep2k)

engine_electric1r_2_effect(ep2k, -2, 7, -2, 7)

engine_RNDany_effect_direction_template(ep2k, 5, 3) 

switch (FEAT_TRAINS, SELF, ep2k_cargo_subtype_text4,
  cargo_subtype)
{
  0: return string(STR_REFIT_OCTOBER_RAILWAY);
  1: return string(STR_REFIT_WEST_SIBERIAN_RAILWAY);
  2: return string(STR_REFIT_RZD_PID);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ep2k_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_OCTOBER_RAILWAY);
  1: return string(STR_REFIT_WEST_SIBERIAN_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ep2k_cargo_subtype_text2,
  (current_year>= 2012))
{
  0: ep2k_cargo_subtype_text3;
  1: ep2k_cargo_subtype_text4;
     return CB_RESULT_NO_TEXT;
} 

switch (FEAT_TRAINS, SELF, ep2k_cargo_subtype_text,
  (build_year>= 2009) +
  (build_year>= 2012) )
{
  0: return string(STR_REFIT_OCTOBER_RAILWAY);
  1: ep2k_cargo_subtype_text2;
  2: return string(STR_REFIT_RZD_PID);
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(ep2k, 160, 130, 30)

engine_penalise_speed(ep2k, 2)

item (FEAT_TRAINS, ep2k) 
{
  property
  {
    name: string(STR_EP2K_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2006), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(2006,2050);
    retire_early: get_retire_early(2006,2050);
    reliability_decay: 20;
    cost_factor: 107;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5745 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 135 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(167.0, 135);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }
  graphics {
    default: ep2k_sprites;
    purchase: ep2k_purchase_sprites;
    articulated_part: articulated_part_dummy5;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_EP2K_PURCHASE_HINT);
    speed: ep2k_speed;
    purchase_speed: ep2k_purchase_speed;
    bitmask_vehicle_info: ep2k_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: ep2k_cargo_subtype_text;
    create_effect: ep2k_create_effect;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ep2k)
  {
    property
    { name: string(STR_EP2K_NAME_LONG); }
  }
}

