 // n8

// Спрайты

#define IMAGEFILE  "src/electric/n8-1953.png"

// покупка
spriteset (n8_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(4, 18, 0)
}

// нет тока
spriteset (n8_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 53, 0)
} 

spriteset (n8_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 88, 0)
} 

spritegroup n8_spritegroup
{
  loading: n8_loading_spriteset;
  loaded: n8_traveling_spriteset;
}

spriteset (n8_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 123, 0)
} 

// нет тока
spriteset (n8_r_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 18, 0)
} 

// развёрнутый
spriteset (n8_r_spriteset, IMAGEFILE)
{
  s8_template(291, 53, 0)
} 

// нет тока
spriteset (n8_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 163, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_v2_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 198, 0)
} 

spriteset (n8_v2_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 233, 0)
} 

spritegroup n8_v2_spritegroup
{
  loading: n8_v2_loading_spriteset;
  loaded: n8_v2_traveling_spriteset;
}

spriteset (n8_v2_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 268, 0)
} 

// нет тока
spriteset (n8_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 163, 0)
} 

// развёрнутый
spriteset (n8_r_v2_spriteset, IMAGEFILE)
{
  s8_template(291, 198, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/electric/n8-1956.png"

// покупка
spriteset (n8_v3_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(4, 18, 0)
}

// нет тока
spriteset (n8_v3_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_v3_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 53, 0)
} 

spriteset (n8_v3_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 88, 0)
} 

spritegroup n8_v3_spritegroup
{
  loading: n8_v3_loading_spriteset;
  loaded: n8_v3_traveling_spriteset;
}

spriteset (n8_v3_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 123, 0)
} 

// нет тока
spriteset (n8_r_v3_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 18, 0)
} 

// развёрнутый
spriteset (n8_r_v3_spriteset, IMAGEFILE)
{
  s8_template(291, 53, 0)
} 

// нет тока
spriteset (n8_v4_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 163, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_v4_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 198, 0)
} 

spriteset (n8_v4_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 233, 0)
} 

spritegroup n8_v4_spritegroup
{
  loading: n8_v4_loading_spriteset;
  loaded: n8_v4_traveling_spriteset;
}

spriteset (n8_v4_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 268, 0)
} 

// нет тока
spriteset (n8_r_v4_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 163, 0)
} 

// развёрнутый
spriteset (n8_r_v4_spriteset, IMAGEFILE)
{
  s8_template(291, 198, 0)
} 

#undef IMAGEFILE
#define IMAGEFILE  "src/electric/n8-1957-vl8.png"

// покупка
spriteset (n8_v5_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(4, 18, 0)
}

// нет тока
spriteset (n8_v5_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_v5_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 53, 0)
} 

spriteset (n8_v5_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 88, 0)
} 

spritegroup n8_v5_spritegroup
{
  loading: n8_v5_loading_spriteset;
  loaded: n8_v5_traveling_spriteset;
}

spriteset (n8_v5_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 123, 0)
} 

// нет тока
spriteset (n8_r_v5_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 18, 0)
} 

// развёрнутый
spriteset (n8_r_v5_spriteset, IMAGEFILE)
{
  s8_template(291, 53, 0)
} 

// нет тока
spriteset (n8_v6_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 163, 0)
} 

// обычный: погрузка/перемещение
spriteset (n8_v6_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 198, 0)
} 

spriteset (n8_v6_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 233, 0)
} 

spritegroup n8_v6_spritegroup
{
  loading: n8_v6_loading_spriteset;
  loaded: n8_v6_traveling_spriteset;
}

spriteset (n8_v6_speedover50_spriteset, IMAGEFILE)
{
  s8_template(4, 268, 0)
} 

// нет тока
spriteset (n8_r_v6_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 163, 0)
} 

// развёрнутый
spriteset (n8_r_v6_spriteset, IMAGEFILE)
{
  s8_template(291, 198, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, n8_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(n8)) % 2) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((build_year >= 1956) * 12) +
  ((build_year >= 1957) * 12) +
  (cargo_subtype * 6)
)
{
  0: n8_notpowered_spriteset;
  1: n8_spritegroup;
  2: n8_speedover50_spriteset;
  3: n8_r_notpowered_spriteset;
  4..5: n8_r_spriteset;
  6: n8_v2_notpowered_spriteset;
  7: n8_v2_spritegroup;
  8: n8_v2_speedover50_spriteset;
  9: n8_r_v2_notpowered_spriteset;
  10..11: n8_r_v2_spriteset;

  12: n8_v3_notpowered_spriteset;
  13: n8_v3_spritegroup;
  14: n8_v3_speedover50_spriteset;
  15: n8_r_v3_notpowered_spriteset;
  16..17: n8_r_v3_spriteset;

  18: n8_v4_notpowered_spriteset;
  19: n8_v4_spritegroup;
  20: n8_v4_speedover50_spriteset;
  21: n8_r_v4_notpowered_spriteset;
  22..23: n8_r_v4_spriteset;
  24: n8_v5_notpowered_spriteset;
  25: n8_v5_spritegroup;
  26: n8_v5_speedover50_spriteset;
  27: n8_r_v5_notpowered_spriteset;
  28..29: n8_r_v5_spriteset;
  30: n8_v6_notpowered_spriteset;
  31: n8_v6_spritegroup;
  32: n8_v6_speedover50_spriteset;
  33: n8_r_v6_notpowered_spriteset;
  34..35: n8_r_v6_spriteset;
  align_8_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, n8_get_spriteset1,
[
  STORE_TEMP(count_veh_id(n8), 0),
  STORE_TEMP(current_speed, 1)
])
{
  n8_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, n8_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  n8_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, n8_get_purchase_spriteset,
  (current_year >= 1956) +
  (current_year >= 1957)
)
{
  0: n8_purchase_spriteset;
  1: n8_v3_purchase_spriteset;
  n8_v5_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, n8_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Если хотя бы 2 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, n8_start_stop2,
[
  set_offset_to(1),
  prev_vehicle_type_id() == n8
])
{
  1: return CB_RESULT_NO_TEXT;
  return string(STR_START_STOP_NEED_AT_LEAST_2_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше двух, то не выпускать
switch (FEAT_TRAINS, PARENT, n8_start_stop,
  count_veh_id(n8) >= 2)
{
  1: n8_start_stop2;
  return string(STR_START_STOP_NEED_2_OR_MORE_ENGINE);
}

switch (FEAT_TRAINS, SELF, n8_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

// меняем скорость: 90 для купленных до 1957, 100 - после 1957
// с 1962 - всем 80 км/ч
// Макс. скорость: 90 км/ч до 1962, 80 км/ч после
#define MAX_SPEED current_year >= 1962 ? 80 : build_year >= 1957 ? 100 : 90

switch (FEAT_TRAINS, SELF, n8_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, n8_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, n8_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(n8), 0),
  (last_computed_result && (position_in_consist == LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, n8_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(n8), 0),
  vehicle_type_id == n8
])
{
  n8_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, n8_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  n8_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, n8_cost_factor,
  (current_year >= 1955) +
  (current_year >= 1958) +
  (current_year >= 1962)
)
{
  0: return 41;
  1: return 44;
  2: return 47;
  return 52;
}

item (FEAT_TRAINS, n8) 
{
  property
  {
    name: string(STR_N8_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1953, 1, 1);
    vehicle_life: 10; // срок службы
    model_life: 10; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 41;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2485 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (180/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: (148.5/180/2/9.8);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: n8_get_spriteset;
    purchase: n8_get_purchase_spriteset;
    can_attach_wagon: n8_can_attach_wagon;
    start_stop: n8_start_stop;
    additional_text: return string(STR_N8_PURCHASE_HINT);
    cargo_subtype_text: n8_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: n8_speed;
    purchase_speed: n8_purchase_speed;
    bitmask_vehicle_info: n8_bitmask_vehicle_info;
    cost_factor: n8_cost_factor;
  }
}
