// pe2

#define IMAGEFILE  "src/electric/pe2.png"
purchase_sprites(pe2, 0, 0)
electric1r_1_2b_sprites(11, pe2, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, pe2_sprites_left_check22,
[  set_offset_to(-4),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_normal_sprites_left_notfirst_light;
  pe2_notpowered_sprites_left_d_2_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_left_check2,
[  set_offset_to(-2),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_sprites_left_check22;
  pe2_notpowered_sprites_left_d_2_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_left_check1,
[  set_offset_to(-2),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_normal_sprites_left_notfirst_light;
  pe2_notpowered_sprites_left_d_2_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_left,
  position_in_consist)
{
  0: pe2_sprites_left_first;
  2: pe2_sprites_left_check1;
  4: pe2_sprites_left_check2;
  pe2_notpowered_sprites_left_d_2_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_right_check22,
[  set_offset_to(-4),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_normal_sprites_right_notfirst_light;
  pe2_notpowered_sprites_right_d_1_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_right_check2,
[  set_offset_to(-2),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_sprites_right_check22;
  pe2_notpowered_sprites_right_d_1_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_right_check1,
[  set_offset_to(-2),
   prev_vehicle_type_id()])
{
  pwd_dumpcar: pe2_normal_sprites_right_notfirst_light;
  pe2_notpowered_sprites_right_d_1_up;
}

switch (FEAT_TRAINS, SELF, pe2_sprites_right,
  position_in_consist)
{
  0: pe2_sprites_right_first;
  2: pe2_sprites_right_check1;
  4: pe2_sprites_right_check2;
  pe2_notpowered_sprites_right_d_1_up;
}

engine_RNDany_direction_template(pe2, 7, 1)
long_vehicle_sp(pe2)

engine_electric1r_1_effect(pe2_create_effect_sprites, -2, 4)
engine_RNDany_effect_direction_template(pe2, 7, 1)
engine_speed(pe2, 65)

switch (FEAT_TRAINS, PARENT, pe2_bitmask_vehicle_info2,
   vehicle_type_id == pwd_dumpcar)
{
  1: return 0;
  return bitmask(FLAG_PENALISE_SPEED);
}

switch (FEAT_TRAINS, PARENT, pe2_bitmask_vehicle_info4,
[  set_offset_to(2),
   (prev_vehicle_type_id() != pwd_dumpcar)])
{
  1: return bitmask(FLAG_PENALISE_SPEED);
  pe2_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, PARENT, pe2_bitmask_vehicle_info6,
[  set_offset_to(4),
   (prev_vehicle_type_id() != pwd_dumpcar)])
{
  1: return bitmask(FLAG_PENALISE_SPEED);
  pe2_bitmask_vehicle_info4;
}

switch (FEAT_TRAINS, SELF, pe2_bitmask_vehicle_info1,
  position_in_consist)
{
  0: return 0;
  2: pe2_bitmask_vehicle_info2;
  4: pe2_bitmask_vehicle_info4;
  6: pe2_bitmask_vehicle_info6;
  return bitmask(FLAG_PENALISE_SPEED);
}

switch (FEAT_TRAINS, SELF, pe2_bitmask_vehicle_info,
  vehicle_is_not_powered)
{
  1: return 0;
  pe2_bitmask_vehicle_info1;
}

switch (FEAT_TRAINS, SELF, pe2_running_cost_factor,
[  STORE_TEMP(24 - 5 * (build_year >= 1985), 0),  // Моторы
   STORE_TEMP(3, 1),                              // Бригада
   STORE_TEMP(8 - (build_year >= 1985), 2),       // Износ
   STORE_TEMP(0, 3),                              // Сопровождение
   STORE_TEMP(6 - (build_year >= 1985), 4),       // ТО
   STORE_TEMP(0, 5),                              // Сертификация

   STORE_TEMP(65, 6),             // Скорость
   STORE_TEMP(120, 7),            // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])  // Максимальная масса
{ all_running_cost_factor; }      // 41 - 33

engine_power_change_build_after2(pe2, 860,
                                      930, 1970,
                                      1000, 1985)
engine_cost_change_build_after2(pe2, 13,
                                     13, 1970,
                                     11, 1985)
hint_engine(pe2_b1970,
     STR_PURCHASE_HINT_ENGINE_TYPE_INDUSTRIAL_ELECTRIC_DDC,
     STR_PURCHASE_HINT_SECTIONS_1,
     1967, 1970,
     fact_dnepropetrovsk())
hint_engine_with_subtype(pe2_a1970,
     STR_PURCHASE_HINT_ENGINE_TYPE_INDUSTRIAL_ELECTRIC_DDC_WITH_SUBTYPE, STR_NAME_PE2M,
     STR_PURCHASE_HINT_SECTIONS_1,
     1970, 1985,
     fact_dnepropetrovsk())
hint_engine_with_subtype_noto(pe2_a1985,
     STR_PURCHASE_HINT_ENGINE_TYPE_INDUSTRIAL_ELECTRIC_DDC_WITH_SUBTYPE, STR_NAME_PE2U,
     STR_PURCHASE_HINT_SECTIONS_1,
     1985,
     fact_dnepropetrovsk())
hint_change_pre_after2(pe2, 1970, 1985)

item (FEAT_TRAINS, pe2, 758) {
  property {
    name: string(STR_NAME_PE2);
    vehicle_dates(1967, 2050, 20, 15, 8, 13)
    vehicle_cargo()
    vehicle_ddcelectric(2500, 120, 227.0)
  }
  graphics {
                 default: pe2_sprites;
                purchase: pe2_purchase_sprites;
        articulated_part: articulated_part_dummy3;
             cost_factor: pe2_cost_factor;
        can_attach_wagon: any_can_attach_wagon;
              start_stop: any_start_stop;
                   speed: pe2_speed;
                   power: pe2_power;
    bitmask_vehicle_info: pe2_bitmask_vehicle_info;
          cargo_capacity: return 0;
           create_effect: pe2_create_effect;
     running_cost_factor: pe2_running_cost_factor;
          purchase_speed: return 65;
         additional_text: pe2_additional_text;
      cargo_subtype_text: pe_type_cargo_subtype_text;
          colour_mapping: pe_type_colour_mapping;
  }
}

long_name_template(pe2, STR_LONGNAME_PE2)
models_default_cargo_template(pe2)
allow_dcelectric(pe2)
