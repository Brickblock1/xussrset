 // ss
// 310 (136h) 

#define IMAGEFILE  "src/electric/ss.png"
purchase_sprites(ss_v1, 0, 0)
purchase_sprites(ss_v2, 100, 0)

electric1r_2_sprites(10, ss_v1,  32,   40)
electric1r_2_sprites(10, ss_v2,  32,  375)
electric1r_2_sprites(10, ss_v3,  32,  710)
electric1r_2_sprites(10, ss_v4,  32, 1045)
electric1r_2_sprites(10, ssm_v1, 32, 1380)
electric1r_2_sprites(10, ssm_v2, 32, 1715)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ss_purchase_sprites,
  current_year >= 1933)
{
  0: ss_v1_purchase_sprites;
     ss_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, ss_sprites_left_a1933,
  (build_year >= 1933) +
  cargo_subtype)
{
  0: ss_v2_sprites_left;
  1: ss_v4_sprites_left;
  LV_RAL6002: ss_v2_sprites_left;
  LV_RAL6020: ss_v3_sprites_left;

  0xE0: ssm_v1_sprites_left;
  0xE1: ssm_v2_sprites_left;
        align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, ss_sprites_left,
  build_year >= 1933)
{
  0: ss_v1_sprites_left;
     ss_sprites_left_a1933;
}

switch (FEAT_TRAINS, SELF, ss_sprites_right_a1933,
  cargo_subtype)
{
  0: ss_v2_sprites_right;
  1: ss_v4_sprites_right;
  LV_RAL6002: ss_v2_sprites_right;
  LV_RAL6020: ss_v3_sprites_right;

  0xE0: ssm_v1_sprites_right;
  0xE1: ssm_v2_sprites_right;
        align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, ss_sprites_right,
  build_year >= 1933)
{
  0: ss_v1_sprites_right;
     ss_sprites_right_a1933;
}

engine_RNDany_direction_template(ss, 5, 3) 

long_vehicle_sp(ss)

switch (FEAT_TRAINS, SELF, ss_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0xE000) || ((extra_callback_info1 & 0xFF00) == 0xE100))+
  (((cargo_subtype == 0xE0) || (cargo_subtype == 0xE1)) * 2))
{
  1: return 2;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, ss_power,
  cargo_subtype)
{
  0xE0..0xE1: return get_power_in_cb(2450);
              return get_power_in_cb(2430);
}

livery_template(ss_b1952, TTD_53, LV_RAL6002, LV_RAL6020, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(ss_b1952, TTD_96)

switch (FEAT_TRAINS, SELF, ss_a1952_cargo_subtype_text_pre,
  cargo_subtype)
{
  0xE0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_SSM), string(STR_REFIT_TTD_53));
  0xE1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_SSM), string(STR_REFIT_TTD_96));
        ss_b1952_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, SELF, ss_a1933_cargo_subtype_text_pre,
  (build_year >= 1952) || (current_year >= 1952))
{
  0: ss_b1952_cargo_subtype_text_pre;
     ss_a1952_cargo_subtype_text_pre;
}    

livery_template_prealt(ss, 1933)

switch (FEAT_TRAINS, SELF, ss_additional_text,
  (current_year >= 1933)+
  (current_year >= 1952))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA),
                   string(STR_PURCHASE_HINT_UPGRADE));
}

engine_electric1_2_effect(ss, -2, 4)
engine_speed_change_after2(ss, 65, 
                               80, 1940, 
                               70, 1945)
engine_penalise_speed(ss, 2)
general_running_cost(ss, 241)

item (FEAT_TRAINS, ss, 310) {
  property {
    name: string(STR_NAME_SS);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1932,1934);
    retire_early: get_retire_early(1932,1934);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 31;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2430 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(353.0, 132);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: ss_sprites;
    purchase: ss_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: ss_additional_text;
    cargo_subtype_text: ss_cargo_subtype_text_pre;
    cargo_capacity: return 0;
    speed: ss_speed;
    power: ss_power; 
    purchase_speed: ss_purchase_speed;
    bitmask_vehicle_info: ss_bitmask_vehicle_info;
    refit_cost: ss_refit_cost;
    create_effect: ss_create_effect;
    running_cost_factor: ss_running_cost_factor;
  }
}

long_name_template(ss, STR_LONGNAME_SS)
models_default_cargo_template(ss)
allow_dcelectric(ss)
