 // ss

// "Сурамский советский" (1932-1934) - первый серийный электровоз
// советского производства.

#define IMAGEFILE  "src/electric/ss.png"
purchase_sprites(ssm, 200, 0)
purchase_sprites(ss_v1, 0, 0)
purchase_sprites(ss_v2, 100, 0)

electric1r_2_sprites(10, ss_v1,   32,   40)
electric1r_2_sprites(10, ss_v2,   32,  375)
electric1r_2_sprites(10, ss_v3,   32,  710)
electric1r_2_sprites(10, ss_v4,   32, 1045)
electric1r_2_sprites(10, ssm_v1,  32, 1380)
electric1r_2_sprites(10, ssm_v2,  32, 1715)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ss_purchase_sprites,
  current_year >= 1933 )
{
  0: ss_v1_purchase_sprites;
     ss_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, ss_sprites_left,
  (build_year >= 1933) +
  cargo_subtype)
{
  0: ss_v1_sprites_left;
  1: ss_v2_sprites_left;
  2: ss_v3_sprites_left;
  3: ss_v4_sprites_left;

  4: ssm_v1_sprites_left;
  5: ssm_v2_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, ss_sprites_right,
  (build_year >= 1933) +
  cargo_subtype)
{
  0: ss_v1_sprites_right;
  1: ss_v2_sprites_right;
  2: ss_v3_sprites_right;
  3: ss_v4_sprites_right;

  4: ssm_v1_sprites_right;
  5: ssm_v2_sprites_right;
     align_10_sprites; 
}

engine_RNDany_direction_template(ss, 5, 3) 

long_vehicle_sp(ss)

switch (FEAT_TRAINS, SELF, ss_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0x300) || ((extra_callback_info1 & 0xFF00) == 0x400))+
  (((cargo_subtype == 3) || (cargo_subtype == 4)) * 2))
{
  1: return 2;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, ss_power,
  cargo_subtype)
{
  3..4: return get_power_in_cb(2450);
        return get_power_in_cb(2430);
}

switch (FEAT_TRAINS, SELF, ss_cargo_subtype_text_a1933,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ss_cargo_subtype_text_a1952,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_TTD_96);
  3: return string(STR_REFIT_FACTORY_SSM, string(STR_REFIT_TTD_53));
  4: return string(STR_REFIT_FACTORY_SSM, string(STR_REFIT_TTD_96));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, ss_cargo_subtype_text,
  (build_year >= 1933) +
  ((current_year >= 1952) || (build_year >= 1952)) )
{
  0: return string(STR_REFIT_PREPRODUCTION);
  1: ss_cargo_subtype_text_a1933;
  2: ss_cargo_subtype_text_a1952;
     return CB_RESULT_NO_TEXT;
}  

switch (FEAT_TRAINS, SELF, ss_additional_text,
   (current_year >= 1933) + 
   (current_year >= 1952))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   1);
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   4);
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1934), 
                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                   6);
}

engine_electric1_2_effect(ss, -2, 4)
engine_speed_change_after2(ss, 65, 80, 1940, 70, 1945)
engine_penalise_speed(ss, 2)
general_running_cost(ss, 241)

item (FEAT_TRAINS, ss) {
  property {
    name: string(STR_SS_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1932,1934);
    retire_early: get_retire_early(1932,1934);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 31;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2430 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(201.0, 132);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: ss_sprites;
    purchase: ss_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: ss_additional_text;
    cargo_subtype_text: ss_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: ss_speed;
    power: ss_power; 
    purchase_speed: ss_purchase_speed;
    bitmask_vehicle_info: ss_bitmask_vehicle_info;
    refit_cost: ss_refit_cost;
    create_effect: ss_create_effect;
    running_cost_factor: ss_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, ss) {
    property { name: string(STR_SS_NAME_LONG); }
  }
}

 // ssm

// СсМ (1952-1956) - модернизированная версия грузопассажирского Сс.

switch (FEAT_TRAINS, SELF, ssm_sprites_left, cargo_subtype)
{
  0: ssm_v1_sprites_left;
  1: ssm_v2_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, ssm_sprites_right, cargo_subtype)
{
  0: ssm_v1_sprites_right;
  1: ssm_v2_sprites_right;
     align_10_sprites; 
}

engine_RNDany_direction_template(ssm, 5, 3) 

long_vehicle_sp(ssm)

switch (FEAT_TRAINS, SELF, ssm_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}
   
engine_electric1_2_effect(ssm, -2, 4)
engine_speed(ssm, 70)
engine_penalise_speed(ssm, 2)
general_running_cost(ssm, 242)

item (FEAT_TRAINS, ssm) {
  property {
    name: string(STR_SSM_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1952), 1, 1);
    vehicle_life: 16;
    model_life: get_model_life(1952,1956);
    retire_early: get_retire_early(1952,1956);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 31;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2450 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(201.0, 132);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: ssm_sprites;
    purchase: ssm_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1952, 1956), 
                                   string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                                   2);
    cargo_subtype_text: ssm_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: ssm_speed;
    purchase_speed: ssm_purchase_speed;
    bitmask_vehicle_info: ssm_bitmask_vehicle_info;
    create_effect: ssm_create_effect;
    running_cost_factor: ssm_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, ssm) {
    property { name: string(STR_SSM_NAME_LONG); }
  }
}

