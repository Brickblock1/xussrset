 // ssm

// СсМ (1952-1956) - модернизированная версия грузопассажирского Сс.

#define IMAGEFILE  "src/electric/ssm.png"

// покупка
purchase_spriteset(ssm, 0, 0)

// нет тока
spriteset (ssm_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 21, 0)
} 

// обычный: погрузка/перемещение
spriteset (ssm_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 88, 0)
} 

spriteset (ssm_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 155, 0)
} 

spritegroup ssm_spritegroup
{
  loading: ssm_loading_spriteset;
  loaded: ssm_traveling_spriteset;
}

spriteset (ssm_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 222, 0)
} 

// 2: нет тока
spriteset (ssm_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 290, 0)
} 

// обычный: погрузка/перемещение
spriteset (ssm_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 357, 0)
} 

spriteset (ssm_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 424, 0)
} 

spritegroup ssm_v2_spritegroup
{
  loading: ssm_v2_loading_spriteset;
  loaded: ssm_v2_traveling_spriteset;
}

spriteset (ssm_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 491, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ssm_sprites_2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  (cargo_subtype * 3)
)
{
  0: ssm_notpowered_spriteset;
  1: ssm_spritegroup;
  2: ssm_speedoverTH_spriteset;

  3: ssm_v2_notpowered_spriteset;
  4: ssm_v2_spritegroup;
  5: ssm_v2_speedoverTH_spriteset;
     align_10_spriteset; // параллелепипед
}

long_vehicle(ssm, )

switch (FEAT_TRAINS, SELF, ssm_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_53);
  1: string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}
   
engine_speed(ssm, 70)

engine_penalise_speed(ssm, 2)

item (FEAT_TRAINS, ssm) 
{
  property
  {
    name: string(STR_SSM_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1952), 1, 1);
    vehicle_life: 16; // срок службы
    model_life: get_model_life(1952,1956);
    retire_early: get_retire_early(1952,1956); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 32;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2450 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(201.0, 132);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: ssm_sprites;
    purchase: ssm_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_SSM_PURCHASE_HINT);
    cargo_subtype_text: ssm_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: ssm_speed;
    purchase_speed: ssm_purchase_speed;
    bitmask_vehicle_info: ssm_bitmask_vehicle_info;
    cost_factor: return 32;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ssm)
  {
    property
    { name: string(STR_SSM_NAME_LONG); }
  }
}

