 // t8

// Т8 (1961-1962) - предсерийная версия будушего электровоза ВЛ10.

#define IMAGEFILE  "src/electric/t8-vl10.png"

// покупка
spriteset (t8_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// нет тока
spriteset (t8_notpowered_spriteset, IMAGEFILE)
{
  s8_template(3, 36, 0)
} 

// обычный: погрузка/перемещение
spriteset (t8_loading_spriteset, IMAGEFILE)
{
  s8_template(3, 71, 0)
} 

spriteset (t8_traveling_spriteset, IMAGEFILE)
{
  s8_template(3, 106, 0)
} 

spritegroup t8_spritegroup
{
  loading: t8_loading_spriteset;
  loaded: t8_traveling_spriteset;
}

spriteset (t8_speedover50_spriteset, IMAGEFILE)
{
  s8_template(3, 141, 0)
} 

// нет тока
spriteset (t8_r_notpowered_spriteset, IMAGEFILE)
{
  s8_template(290, 36, 0)
} 

// развёрнутый
spriteset (t8_r_spriteset, IMAGEFILE)
{
  s8_template(290, 71, 0)
} 

// покупка
spriteset (t8_v2_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// нет тока
spriteset (t8_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(3, 201, 0)
} 

// обычный: погрузка/перемещение
spriteset (t8_v2_loading_spriteset, IMAGEFILE)
{
  s8_template(3, 236, 0)
} 

spriteset (t8_v2_traveling_spriteset, IMAGEFILE)
{
  s8_template(3, 271, 0)
} 

spritegroup t8_v2_spritegroup
{
  loading: t8_v2_loading_spriteset;
  loaded: t8_v2_traveling_spriteset;
}

spriteset (t8_v2_speedover50_spriteset, IMAGEFILE)
{
  s8_template(3, 306, 0)
} 

// нет тока
spriteset (t8_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(290, 201, 0)
} 

// развёрнутый
spriteset (t8_r_v2_spriteset, IMAGEFILE)
{
  s8_template(290, 236, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, t8_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(t8)) % 2) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((current_year >= 1972) * 6)
)
{
  0: t8_notpowered_spriteset;
  1: t8_spritegroup;
  2: t8_speedover50_spriteset;
  3: t8_r_notpowered_spriteset;
  4..5: t8_r_spriteset;
  6: t8_v2_notpowered_spriteset;
  7: t8_v2_spritegroup;
  8: t8_v2_speedover50_spriteset;
  9: t8_r_v2_notpowered_spriteset;
  10..11: t8_r_v2_spriteset;
  align_8_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, t8_get_spriteset1,
[
  STORE_TEMP(count_veh_id(t8), 0),
  STORE_TEMP(current_speed, 1)
])
{
  t8_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, t8_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  t8_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, t8_get_purchase_spriteset,
  current_year >= 1972)
{
  0: t8_purchase_spriteset;
  t8_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, t8_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Если хотя бы 2 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, t8_start_stop2,
[
  set_offset_to(1),
  prev_vehicle_type_id() == t8
])
{
  1: return CB_RESULT_NO_TEXT;
  return string(STR_START_STOP_NEED_AT_LEAST_2_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше двух, то не выпускать
switch (FEAT_TRAINS, PARENT, t8_start_stop,
  count_veh_id(t8) >= 2)
{
  1: t8_start_stop2;
  return string(STR_START_STOP_NEED_2_OR_MORE_ENGINE);
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, t8_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, t8_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, t8_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(t8), 0),
  (last_computed_result && (position_in_consist == LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, t8_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(t8), 0),
  vehicle_type_id == t8
])
{
  t8_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, t8_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  t8_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, t8_cost_factor,
  (current_year >= 1962)
)
{
  0: return 67;
  return 68;
}

item (FEAT_TRAINS, t8) 
{
  property
  {
    name: string(STR_T8_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1961, 1, 1);
    vehicle_life: 10; // срок службы
    model_life: get_model_life(1961,1962); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 67;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2960 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (184/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: (155.5/184/2/9.8);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: t8_get_spriteset;
    purchase: t8_get_purchase_spriteset;
    can_attach_wagon: t8_can_attach_wagon;
    start_stop: t8_start_stop;
    additional_text: return string(STR_T8_PURCHASE_HINT);
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    speed: t8_speed;
    purchase_speed: t8_purchase_speed;
    bitmask_vehicle_info: t8_bitmask_vehicle_info;
    cost_factor: t8_cost_factor;
  }
}
