 // t8

// Т8 (1961-1962) - предсерийная версия будушего электровоза ВЛ10.
#define IMAGEFILE  "src/electric/vl10.png"
purchase_sprites(t8, 0, 0)

purchase_sprites(vl10_v1, 200, 0)
purchase_sprites(vl10_v2, 300, 0)
purchase_sprites(vl10_v3, 400, 0)
purchase_sprites(vl10_v4, 500, 0)

electric2_1_sprites(8, t8_v1, 32,  40)
electric2_1_sprites(8, t8_v2, 32, 241)

electric2_1_sprites(9, vl10_v1, 32,  442)
electric2_1_sprites(9, vl10_v2, 32,  643)
electric2_1_sprites(9, vl10_v3, 32,  844)
electric2_1_sprites(9, vl10_v4, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, t8_sprites_left,
  LOAD_TEMP(2) >= date(1972, 1, 1))
{
  0: t8_v1_sprites_left;
  1: t8_v2_sprites_left;
     align_8_sprites; 
}

switch (FEAT_TRAINS, SELF, t8_sprites_right,
  LOAD_TEMP(2) >= date(1972, 1, 1))
{
  0: t8_v1_sprites_right;
  2: t8_v2_sprites_right;
     align_8_sprites; 
}

engine_direction_template(t8, ) 
long_vehicle(t8)

engine_electric2_1_effect(t8, 3, -3)
engine_direction_template(t8, _create_effect) 
engine_speed(t8, 100)
engine_penalise_speed(t8, 2)
engine_RC_change_build_after(vl10, 256, 260, 1964)

item (FEAT_TRAINS, t8) {
  property {
    name: string(STR_T8_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1961), 1, 1);
    vehicle_life: 10;
    model_life: get_model_life(1961,1962);
    retire_early: get_retire_early(1961,1962);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 33;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2960 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (184/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(155.5, 184/2);
    length: 7;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: t8_sprites;
    purchase: t8_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_DC_PROTOTYPE), 
                                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1961, 1962), 
                                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK_ELP), string(STR_PURCHASE_HINT_FACTORY_TBILISI_ELP)));
    cargo_capacity: return 0;
    speed: t8_speed;
    purchase_speed: t8_purchase_speed;
    bitmask_vehicle_info: t8_bitmask_vehicle_info;
    create_effect: t8_create_effect_sprites_start;
    running_cost_factor: vl10_v1_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, t8) {
    property { name: string(STR_T8_NAME_LONG); }
  }
}

 // vl10

// ВЛ10 (1964-1976) - основной серийный электровоз, основатель целого
// семейства моделей с унифицированным кузовом

switch (FEAT_TRAINS, SELF, vl10_purchase_sprites,
  (current_year >= 1964) +
  (current_year >= 1972) +
  (current_year >= 1973) +
  (current_year >= 1974))
{
  0: t8_purchase_sprites;
  1: vl10_v1_purchase_sprites;
  2: vl10_v2_purchase_sprites;
  3: vl10_v3_purchase_sprites;
     vl10_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl10_sprites_left,
  (build_year >= 1964) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && (build_year < 1972) && (build_year >= 1964)) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{   
    0: t8_sprites_left;
    1: vl10_v1_sprites_left;
    2: vl10_v2_sprites_left;
    3: vl10_v3_sprites_left;
    4: vl10_v4_sprites_left;
       align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl10_sprites_right,
  (build_year >= 1964) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && (build_year < 1972) && (build_year >= 1964)) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{ 
    0: t8_sprites_right;
    1: vl10_v1_sprites_right;
    2: vl10_v2_sprites_right;
    3: vl10_v3_sprites_right;
    4: vl10_v4_sprites_right;
       align_9_sprites; 
}

engine_direction_template(vl10, ) 
long_vehicle(vl10)

engine_electric2_1_effect(vl10_v1, 3, -3)
engine_direction_template(vl10, _v1_create_effect) 
engine_electric2_1_effect(vl10_v2, -2, 3)
engine_direction_template(vl10, _v2_create_effect) 

switch (FEAT_TRAINS, SELF, vl10_create_effect_sprites,
  build_year >= 1964)
{
  0: vl10_v1_create_effect_sprites_start;
     vl10_v2_create_effect_sprites_start;
}

switch (FEAT_TRAINS, SELF, vl10_cargo_subtype_text,
  build_year >= 1964)
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_T8_NAME), "");
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl10_additional_text,
   current_year >= 1964)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_DC_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1961, 1962), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK_ELP), string(STR_PURCHASE_HINT_FACTORY_TBILISI_ELP)));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1976), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK_ELP), string(STR_PURCHASE_HINT_FACTORY_TBILISI_ELP)));
}
                        
engine_TE_change_build_after(vl10, get_tec_int(155.5, 92), get_tec_int(157.0, 92), 1964)
engine_power_change_build_after(vl10, 2960, 3030, 1964)
engine_length_change_build_after(vl10, 7, 8, 1964)
engine_speed(vl10, 100)
engine_penalise_speed(vl10, 2)

item (FEAT_TRAINS, vl10) {
  property {
    name: string(STR_VL10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1961), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1961,1976);
    retire_early: get_retire_early(1961,1976);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 33;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 92);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl10_sprites;
    purchase: vl10_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl10_additional_text;
    cargo_capacity: return 0;
    speed: vl10_speed;
    power: vl10_power;
    length: vl10_length;
    tractive_effort_coefficient: vl10_tractive_effort_coefficient;
    purchase_speed: vl10_purchase_speed;
    bitmask_vehicle_info: vl10_bitmask_vehicle_info;
    cargo_subtype_text: vl10_cargo_subtype_text;
    create_effect: vl10_create_effect_sprites;
    running_cost_factor: vl10_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl10) {
    property { name: string(STR_VL10_NAME_LONG); }
  }
}

