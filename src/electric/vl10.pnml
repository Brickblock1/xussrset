 // t8

// Т8 (1961-1962) - предсерийная версия будушего электровоза ВЛ10.

#define IMAGEFILE  "src/electric/t8.png"

// покупка
purchase_sprites(t8_v1, 0, 0)

// нет тока
spriteset (t8_notpowered_spriteset, IMAGEFILE)
{
  s8_template(3, 36, 0)
} 

// обычный: погрузка/перемещение
spriteset (t8_loading_spriteset, IMAGEFILE)
{
  s8_template(3, 71, 0)
} 

spriteset (t8_traveling_spriteset, IMAGEFILE)
{
  s8_template(3, 106, 0)
} 

spritegroup t8_spritegroup
{
  loading: t8_loading_spriteset;
  loaded: t8_traveling_spriteset;
}

spriteset (t8_speedoverTH_spriteset, IMAGEFILE)
{
  s8_template(3, 141, 0)
} 

// нет тока
spriteset (t8_r_notpowered_spriteset, IMAGEFILE)
{
  s8_template(290, 36, 0)
} 

// развёрнутый
spriteset (t8_r_spriteset, IMAGEFILE)
{
  s8_template(290, 71, 0)
} 

// покупка
purchase_sprites(t8_v2, 100, 0)

// нет тока
spriteset (t8_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(3, 201, 0)
} 

// обычный: погрузка/перемещение
spriteset (t8_v2_loading_spriteset, IMAGEFILE)
{
  s8_template(3, 236, 0)
} 

spriteset (t8_v2_traveling_spriteset, IMAGEFILE)
{
  s8_template(3, 271, 0)
} 

spritegroup t8_v2_spritegroup
{
  loading: t8_v2_loading_spriteset;
  loaded: t8_v2_traveling_spriteset;
}

spriteset (t8_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s8_template(3, 306, 0)
} 

// нет тока
spriteset (t8_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(290, 201, 0)
} 

// развёрнутый
spriteset (t8_r_v2_spriteset, IMAGEFILE)
{
  s8_template(290, 236, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, t8_purchase_sprites,
  current_year >= 1972)
{
  0: t8_v1_purchase_sprites;
     t8_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, t8_sprites_l,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 3))
{
  0: t8_notpowered_spriteset;
  1: t8_spritegroup;
  2: t8_speedoverTH_spriteset;
  3: t8_v2_notpowered_spriteset;
  4: t8_v2_spritegroup;
  5: t8_v2_speedoverTH_spriteset;
     align_8_sprites; 
}

switch (FEAT_TRAINS, SELF, t8_sprites_r,
    (!(vehicle_is_not_powered || vehicle_is_in_depot)) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 2))
{
  0: t8_r_notpowered_spriteset;
  1: t8_r_spriteset;
  2: t8_r_v2_notpowered_spriteset;
  3: t8_r_v2_spriteset;
     align_8_sprites; 
}

engine_direction_template(t8) 

long_vehicle(t8)

engine_speed(t8, 100)

engine_penalise_speed(t8, 2)

item (FEAT_TRAINS, t8) 
{
  property
  {
    name: string(STR_T8_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1961), 1, 1);
    vehicle_life: 10;
    model_life: get_model_life(1961,1962);
    retire_early: get_retire_early(1961,1962);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 67;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2960 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (184/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(155.5, 184/2);
    length: 7;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: t8_sprites;
    purchase: t8_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_T8_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: t8_speed;
    purchase_speed: t8_purchase_speed;
    bitmask_vehicle_info: t8_bitmask_vehicle_info;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, t8)
  {
    property
    { name: string(STR_T8_NAME_LONG); }
  }
}

 // vl10

// ВЛ10 (1964-1976) - основной серийный электровоз, основатель целого
// семейства моделей с унифицированным кузовом

#define IMAGEFILE  "src/electric/vl10.png"

purchase_sprites(vl10_v1, 0, 0)
purchase_sprites(vl10_v2, 100, 0)
purchase_sprites(vl10_v3, 200, 0)
purchase_sprites(vl10_v4, 300, 0)

electric2_1_sprites(9, vl10_v1, 3, 53)
electric2_1_sprites(9, vl10_v2, 3, 373)
electric2_1_sprites(9, vl10_v3, 3, 691)
electric2_1_sprites(9, vl10_v4, 3, 1009)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl10_purchase_sprites,
  (current_year >= 1964) +
  (current_year >= 1972) +
  (current_year >= 1973) +
  (current_year >= 1974))
{
  0: t8_v1_purchase_sprites;
  1: vl10_v1_purchase_sprites;
  2: vl10_v2_purchase_sprites;
  3: vl10_v3_purchase_sprites;
     vl10_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl10_sprites_l,
  (build_year >= 1964) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && (build_year < 1972) && (build_year >= 1964)) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{   
    0: t8_sprites_l;
    1: vl10_v1_sprites_left;
    2: vl10_v2_sprites_left;
    3: vl10_v3_sprites_left;
    4: vl10_v4_sprites_left;
       align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl10_sprites_r,
  (build_year >= 1964) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && (build_year < 1972) && (build_year >= 1964)) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{ 
    0: t8_sprites_r;
    1: vl10_v1_sprites_right;
    2: vl10_v2_sprites_right;
    3: vl10_v3_sprites_right;
    4: vl10_v4_sprites_right;
       align_9_sprites; 
}

engine_direction_template(vl10) 

long_vehicle(vl10)

switch (FEAT_TRAINS, SELF, vl10_cargo_subtype_text,
  build_year >= 1964)
{
  0: string(STR_REFIT_FACTORY_T8);
     return CB_RESULT_NO_TEXT;
}
                        
switch (FEAT_TRAINS, SELF, vl10_tractive_effort_coefficient,
  build_year >= 1964)
{
  0: return get_tec_int(155.5, 92);
     return get_tec_int(157.0, 92);
}

switch (FEAT_TRAINS, SELF, vl10_power,
  build_year >= 1964)
{
  0: return get_power_in_cb(2960);
     return get_power_in_cb(3030);
}
  
switch (FEAT_TRAINS, SELF, vl10_length,
  build_year >= 1964)
{
  0: return 7;
     return 8;
}
  
engine_speed(vl10, 100)

engine_penalise_speed(vl10, 2)

item (FEAT_TRAINS, vl10) 
{
  property
  {
    name: string(STR_VL10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1961), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1961,1976);
    retire_early: get_retire_early(1961,1976);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 73;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 92);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl10_sprites;
    purchase: vl10_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL10_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl10_speed;
    power: vl10_power;
    length: vl10_length;
    tractive_effort_coefficient: vl10_tractive_effort_coefficient;
    purchase_speed: vl10_purchase_speed;
    bitmask_vehicle_info: vl10_bitmask_vehicle_info;
    cargo_subtype_text: vl10_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl10)
  {
    property
    { name: string(STR_VL10_NAME_LONG); }
  }
}

