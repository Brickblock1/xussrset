 // vl10

// ВЛ10 (1964-1976) - основной серийный электровоз, основатель целого
// семейства моделей с унифицированным кузовом

#define IMAGEFILE  "src/electric/vl10.png"

purchase_sprites(vl10_v1, 0, 0)
purchase_sprites(vl10_v2, 100, 0)
purchase_sprites(vl10_v3, 200, 0)
purchase_sprites(vl10_v4, 300, 0)

electric2_1_sprites(9, vl10_v1, 3, 53)
electric2_1_sprites(9, vl10_v2, 3, 373)
electric2_1_sprites(9, vl10_v3, 3, 691)
electric2_1_sprites(9, vl10_v4, 3, 1009)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl10_purchase_sprites,
  (current_year >= 1972) +
  (current_year >= 1973) +
  (current_year >= 1974))
{
  0: vl10_v1_purchase_sprites;
  1: vl10_v2_purchase_sprites;
  2: vl10_v3_purchase_sprites;
     vl10_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl10_sprites_l,
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && build_year < 1972) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{   0: vl10_v1_sprites_left;
    1: vl10_v2_sprites_left;
    2: vl10_v3_sprites_left;
    3: vl10_v4_sprites_left;
       align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl10_sprites_r,
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && build_year < 1972) +
  (build_year >= 1972) +
  (build_year >= 1973) +
  (build_year >= 1974) )
{ 
    0: vl10_v1_sprites_right;
    1: vl10_v2_sprites_right;
    2: vl10_v3_sprites_right;
    3: vl10_v4_sprites_right;
       align_9_sprites; 
}

engine_direction_template(vl10) 

long_vehicle(vl10)
  
engine_speed(vl10, 100)

engine_penalise_speed(vl10, 2)

item (FEAT_TRAINS, vl10) 
{
  property
  {
    name: string(STR_VL10_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1964), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1964,1976);
    retire_early: get_retire_early(1964,1976); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 73;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (184/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 184/2);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl10_sprites;
    purchase: vl10_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL10_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl10_speed;
    purchase_speed: vl10_purchase_speed;
    bitmask_vehicle_info: vl10_bitmask_vehicle_info;
    cost_factor: return 73;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl10)
  {
    property
    { name: string(STR_VL10_NAME_LONG); }
  }
}

