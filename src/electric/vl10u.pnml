 // vl10u

// ВЛ10у "Утюг" (1974-1986) - усиленная модификация ВЛ10 с повышенной
// нагрузкой на ось для большего сцепного веса

#define IMAGEFILE  "src/electric/vl10u.png"

// покупка
spriteset (vl10u_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(5, 44, -2)
}

// нет тока
spriteset (vl10u_notpowered_spriteset, IMAGEFILE)
{
  s9_template(5, 44, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl10u_loading_spriteset, IMAGEFILE)
{
  s9_template(5, 111, 0)
} 

spriteset (vl10u_traveling_spriteset, IMAGEFILE)
{
  s9_template(5, 178, 0)
} 

spritegroup vl10u_spritegroup
{
  loading: vl10u_loading_spriteset;
  loaded: vl10u_traveling_spriteset;
}

spriteset (vl10u_speedover50_spriteset, IMAGEFILE)
{
  s9_template(5, 245, 0)
} 

// нет тока
spriteset (vl10u_r_notpowered_spriteset, IMAGEFILE)
{
  s9_template(436, 44, 0)
} 

// развёрнутый
spriteset (vl10u_r_spriteset, IMAGEFILE)
{
  s9_template(436, 111, 0)
} 

// покупка
spriteset (vl10u_v2_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(5, 356, -2)
}

// нет тока
spriteset (vl10u_v2_notpowered_spriteset, IMAGEFILE)
{
  s9_template(5, 356, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl10u_v2_loading_spriteset, IMAGEFILE)
{
  s9_template(5, 423, 0)
} 

spriteset (vl10u_v2_traveling_spriteset, IMAGEFILE)
{
  s9_template(5, 490, 0)
} 

spritegroup vl10u_v2_spritegroup
{
  loading: vl10u_v2_loading_spriteset;
  loaded: vl10u_v2_traveling_spriteset;
}

spriteset (vl10u_v2_speedover50_spriteset, IMAGEFILE)
{
  s9_template(5, 557, 0)
} 

// нет тока
spriteset (vl10u_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s9_template(436, 356, 0)
} 

// развёрнутый
spriteset (vl10u_r_v2_spriteset, IMAGEFILE)
{
  s9_template(436, 423, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl10u_get_spriteset2,
  (((LOAD_TEMP(0) - count_veh_id(vl10u)) % 2) * 3) +
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((build_year >= 1976) * 6)
)
{
  0: vl10u_notpowered_spriteset;
  1: vl10u_spritegroup;
  2: vl10u_speedover50_spriteset;
  3: vl10u_r_notpowered_spriteset;
  4..5: vl10u_r_spriteset;
  6: vl10u_v2_notpowered_spriteset;
  7: vl10u_v2_spritegroup;
  8: vl10u_v2_speedover50_spriteset;
  9: vl10u_r_v2_notpowered_spriteset;
  10..11: vl10u_r_v2_spriteset;
  align_9_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl10u_get_spriteset1,
[
  STORE_TEMP(count_veh_id(vl10u), 0),
  STORE_TEMP(current_speed, 1)
])
{
  vl10u_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl10u_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl10u_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, vl10u_get_purchase_spriteset,
  current_year >= 1976)
{
  0: vl10u_purchase_spriteset;
  vl10u_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, vl10u_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Если хотя бы 2 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, vl10u_start_stop2,
[
  set_offset_to(2),
  prev_vehicle_type_id() == vl10u
])
{
  1: return CB_RESULT_NO_TEXT;
  return string(STR_START_STOP_NEED_AT_LEAST_2_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше двух, то не выпускать
switch (FEAT_TRAINS, PARENT, vl10u_start_stop,
  count_veh_id(vl10u) >= 2)
{
  1: vl10u_start_stop2;
  return string(STR_START_STOP_NEED_2_OR_MORE_ENGINE);
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, vl10u_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, vl10u_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, vl10u_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(vl10u), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, vl10u_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(vl10u), 0),
  vehicle_type_id == vl10u
])
{
  vl10u_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, vl10u_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  vl10u_bitmask_vehicle_info2;
}

switch (FEAT_TRAINS, SELF, vl10u_cost_factor,
  (current_year >= 1976) +
  (current_year >= 1980) +
  (current_year >= 1986))
{
  0: return 88;
  1: return 91;
  2: return 97;
  return 106;
}

item (FEAT_TRAINS, vl10u) 
{
  property
  {
    name: string(STR_VL10U_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1974, 1, 1);
    vehicle_life: 30; // срок службы
    model_life: 30; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 88;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: (6060/2) hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (200/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: (360.0/200/9.8);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl10u_get_spriteset;
    purchase: vl10u_get_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: vl10u_can_attach_wagon;
    start_stop: vl10u_start_stop;
    additional_text: return string(STR_VL10U_PURCHASE_HINT);
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    speed: vl10u_speed;
    purchase_speed: vl10u_purchase_speed;
    bitmask_vehicle_info: vl10u_bitmask_vehicle_info;
    cost_factor: vl10u_cost_factor;
  }
}
