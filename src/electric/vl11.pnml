 // vl11 ВЛ11

#define IMAGEFILE  "src/electric/vl11.png"
purchase_sprites(vl11_v1, 0, 0)
purchase_sprites(vl11_v2, 100, 0)

electric2_1_sprites(9vl11, vl11_v1, 32,   40)     
electric2_1_sprites(9vl11, vl11_v2, 32,  241)     
electric2_1_sprites(9vl11, vl11_v3, 32,  442)     
electric2_1_sprites(9vl11, vl11_v4, 32,  643)     
electric2_1_sprites(9vl11, vl11_v5, 32,  844)     
electric2_1_sprites(9vl11, vl11_v6, 32, 1045)     
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl11_purchase_sprites,
  current_year >= 1978)
{
  0: vl11_v1_purchase_sprites;
     vl11_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl11_sprites_left,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{   0..4: vl11_v1_sprites_left;
    5..6: vl11_v3_sprites_left;
       7: vl11_v5_sprites_left;
  10..14: vl11_v2_sprites_left;
  15..16: vl11_v4_sprites_left;
      17: vl11_v6_sprites_left;
          align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl11_sprites_right,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{ 
    0..4: vl11_v1_sprites_right;
    5..6: vl11_v3_sprites_right;
       7: vl11_v5_sprites_right;
  10..14: vl11_v2_sprites_right;
  15..16: vl11_v4_sprites_right;
      17: vl11_v6_sprites_right;
          align_9_sprites; 
}

engine_direction_template(vl11, ) 

long_vehicle(vl11)

engine_electric2_1_effect(vl11, -2, 3)

engine_direction_template(vl11, _create_effect) 

random_switch (FEAT_TRAINS, PARENT, vl11_colour_mapping_1, 0)
{
  1: return vl11_cc + 3;
  1: return vl11_cc + 4;
}

random_switch (FEAT_TRAINS, PARENT, vl11_colour_mapping_2, 0)
{
  1: return vl11_cc + 5;
  1: return vl11_cc + 6;
}

switch (FEAT_TRAINS, SELF, vl11_colour_mapping,
  cargo_subtype)
{
  0: return vl11_cc + 0;
  1: return vl11_cc + 1;
  2: return vl11_cc + 2;
  3: vl11_colour_mapping_1; 
  4: vl11_colour_mapping_2;
  5: return vl11_cc + 1;
  6: return vl11_cc + 5;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, vl11_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_FACTORY));
  1: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_TTD_53)); 
  2: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_TTD_59));
  3: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_TTD_9D));
  4: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_TTD_62));
  5: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_53)));
  6: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_63)));
  7: return string(STR_REFIT_FACTORY_VL118, string(STR_REFIT_SVERDLOVSK_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl11_cargo_subtype_text1,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_53); 
  2: return string(STR_REFIT_TTD_59);
  3: return string(STR_REFIT_TTD_9D);
  4: return string(STR_REFIT_TTD_62);
  5: return string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_53));
  6: return string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_63));
  7: return string(STR_REFIT_SVERDLOVSK_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl11_cargo_subtype_text,
  build_year >= 1986)
{
  0: vl11_cargo_subtype_text1;
     vl11_cargo_subtype_text2;
}
                              
engine_speed(vl11, 100)

engine_penalise_speed(vl11, 4)

general_running_cost(vl11, 260)

item (FEAT_TRAINS, vl11) {
  property {
    name: string(STR_VL11_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1974), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life (1974,1987);
    retire_early: get_retire_early(1974,1987);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 33;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 92);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl11_sprites;
    purchase: vl11_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL11_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl11_speed;
    purchase_speed: vl11_purchase_speed;
    bitmask_vehicle_info: vl11_bitmask_vehicle_info;
    cargo_subtype_text: vl11_cargo_subtype_text;
    colour_mapping: vl11_colour_mapping;
    create_effect: vl11_create_effect_sprites_start;
    running_cost_factor: vl11_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl11) {
    property { name: string(STR_VL11_NAME_LONG); }
  }
}

