 // vl11 ВЛ11

#define IMAGEFILE  "src/electric/vl11.png"

purchase_spriteset(vl11, 0, 0)
purchase_spriteset(vl11_v2, 0, 20)

electric2_spriteset(9vl11, vl11, 5, 44)
electric2_spriteset(9vl11, vl11_v2, 5, 356)
electric2_spriteset(9vl11, vl11_v3, 5, 676)
electric2_spriteset(9vl11, vl11_v4, 5, 988)
electric2_spriteset(9vl11, vl11_v5, 5, 1301)
electric2_spriteset(9vl11, vl11_v6, 5, 1613)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl11_get_purchase_spriteset,
  current_year >= 1978)
{
  0: vl11_purchase_spriteset;
     vl11_v2_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, vl11_get_spriteset_l,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{   0..4: vl11_get_spriteset_left;
    5..6: vl11_v3_get_spriteset_left;
       7: vl11_v5_get_spriteset_left;
  10..14: vl11_v2_get_spriteset_left;
  15..16: vl11_v4_get_spriteset_left;
      17: vl11_v6_get_spriteset_left;
          align_9_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, SELF, vl11_get_spriteset_r,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{ 
    0..4: vl11_get_spriteset_right;
    5..6: vl11_v3_get_spriteset_right;
       7: vl11_v5_get_spriteset_right;
  10..14: vl11_v2_get_spriteset_right;
  15..16: vl11_v4_get_spriteset_right;
      17: vl11_v6_get_spriteset_right;
          align_9_spriteset; // параллелепипед
}

engine_direction_template(vl11) 

long_vehicle(vl11, )

random_switch (FEAT_TRAINS, PARENT, vl11_colour_mapping_1, 0)
{
  1: return vl11_cc + 3;
  1: return vl11_cc + 4;
}

random_switch (FEAT_TRAINS, PARENT, vl11_colour_mapping_2, 0)
{
  1: return vl11_cc + 5;
  1: return vl11_cc + 6;
}

switch (FEAT_TRAINS, SELF, vl11_colour_mapping,
  cargo_subtype)
{
  0: return vl11_cc + 0;
  1: return vl11_cc + 1;
  2: return vl11_cc + 2;
  3: vl11_colour_mapping_1; 
  4: vl11_colour_mapping_2;
  5: return vl11_cc + 1;
  6: return vl11_cc + 5;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, vl11_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_53); 
  2: string(STR_REFIT_TTD_5A);
  3: string(STR_REFIT_TTD_9D);
  4: string(STR_REFIT_TTD_62);
  5: string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_53));
  6: string(STR_REFIT_MOSCOW_RAILWAY, string(STR_REFIT_TTD_63));
  7: string(STR_REFIT_SVERDLOVSK_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

engine_speed(vl11, 100)

engine_penalise_speed(vl11, 4)

item (FEAT_TRAINS, vl11) 
{
  property
  {
    name: string(STR_VL11_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1975), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life (1975,1986);
    retire_early: get_retire_early(1975,1986);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 89;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 92);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl11_get_spriteset;
    purchase: vl11_get_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL11_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl11_speed;
    purchase_speed: vl11_purchase_speed;
    bitmask_vehicle_info: vl11_bitmask_vehicle_info;
    cost_factor: return 89;
    cargo_subtype_text: vl11_cargo_subtype_text;
    colour_mapping: vl11_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl11)
  {
    property
    { name: string(STR_VL11_NAME_LONG); }
  }
}

