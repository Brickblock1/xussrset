 // vl11m ВЛ11м

switch (FEAT_TRAINS, SELF, vl11m_sprites_l,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{   0..4: vl11_v1_sprites_left;
    5..6: vl11_v3_sprites_left;
       7: vl11_v5_sprites_left;
  10..14: vl11_v2_sprites_left;
  15..16: vl11_v4_sprites_left;
      17: vl11_v6_sprites_left;
          align_9_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, SELF, vl11m_sprites_r,
  cargo_subtype + 
  (build_year >= 1978) * 10)
{ 
    0..4: vl11_v1_sprites_right;
    5..6: vl11_v3_sprites_right;
       7: vl11_v5_sprites_right;
  10..14: vl11_v2_sprites_right;
  15..16: vl11_v4_sprites_right;
      17: vl11_v6_sprites_right;
          align_9_spriteset; // параллелепипед
}

engine_direction_template2(vl11m, vl11u) 

long_vehicle(vl11m, )

engine_speed(vl11m, 100)

engine_penalise_speed2(vl11m, vl11u, 4)

item (FEAT_TRAINS, vl11m) 
{
  property
  {
    name: string(STR_VL11M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1986), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life (1986,1996);
    retire_early: get_retire_early(1986,1996);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 89;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3030 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(157.0, 92);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl11m_sprites;
    purchase: vl11_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL11M_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl11m_speed;
    purchase_speed: vl11m_purchase_speed;
    bitmask_vehicle_info: vl11m_bitmask_vehicle_info;
    cargo_subtype_text: vl11_cargo_subtype_text;
    colour_mapping: vl11_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl11m)
  {
    property
    { name: string(STR_VL11M_NAME_LONG); }
  }
}

