 // vl19
// 312 (138h)

#define IMAGEFILE  "src/electric/vl19.png"
purchase_sprites(vl19_v1,   0, 0)
purchase_sprites(vl19_v2, 100, 0)

electric1r_2_sprites(10, vl19_v1,   32,   40)
electric1r_2_sprites(10, vl19_v2,   32,  375)
electric1r_2_sprites(10, vl19_v3,   32,  710)
electric1r_2_sprites(10, vl19_v4,   32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl19_purchase_sprites,
  current_year >= 1933)
{
  0: vl19_v1_purchase_sprites;
     vl19_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl19_sprites_left_a1933,
  cargo_subtype)
{
  0: vl19_v2_sprites_left;
  1: vl19_v4_sprites_left;
  LV_RAL6002: vl19_v2_sprites_left;
  LV_RAL6020: vl19_v3_sprites_left;

  0xE0: vl19_v2_sprites_left;
  0xE1: vl19_v3_sprites_left;
  0xE2: vl19_v4_sprites_left;
        align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl19_sprites_left,
  build_year >= 1933)
{
  0: vl19_v1_sprites_left;
     vl19_sprites_left_a1933;
}

switch (FEAT_TRAINS, SELF, vl19_sprites_right_a1933,
  cargo_subtype)
{
  0: vl19_v2_sprites_right;
  1: vl19_v4_sprites_right;
  LV_RAL6002: vl19_v2_sprites_right;
  LV_RAL6020: vl19_v3_sprites_right;

  0xE0: vl19_v2_sprites_right;
  0xE1: vl19_v3_sprites_right;
  0xE2: vl19_v4_sprites_right;
        align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl19_sprites_right,
  build_year >= 1933)
{
  0: vl19_v1_sprites_right;
     vl19_sprites_right_a1933;
}

engine_RNDany_direction_template(vl19, 5, 3) 

long_vehicle_sp(vl19)

switch (FEAT_TRAINS, SELF, vl19_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0xE000) || ((extra_callback_info1 & 0xFF00) == 0xE100) || ((extra_callback_info1 & 0xFF00) == 0xE200)) +
  (((cargo_subtype == 0xE0) || (cargo_subtype == 0xE1) || (cargo_subtype == 0xE2)) * 2))
{
  1: return 2;
     return 0; 
}            

livery_template(vl19_b1947, TTD_53, LV_RAL6002, LV_RAL6020, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(vl19_b1947, TTD_96)

switch (FEAT_TRAINS, SELF, vl19_a1947_cargo_subtype_text_pre,
  cargo_subtype)
{
  0xE0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL19M), string(STR_REFIT_TTD_53));
  0xE1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL19M), string(STR_REFIT_TTD_59));
  0xE2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL19M), string(STR_REFIT_TTD_96));
        vl19_b1947_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, SELF, vl19_a1933_cargo_subtype_text_pre,
  (build_year >= 1947) || (current_year >= 1947))
{
  0: vl19_b1947_cargo_subtype_text_pre;
     vl19_a1947_cargo_subtype_text_pre;
}    

livery_template_prealt(vl19, 1933)

switch (FEAT_TRAINS, SELF, vl19_additional_text,
   current_year >= 1933)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_IN, 1932), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_DINAMO)));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1933, 1938), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_DINAMO)));
}

switch (FEAT_TRAINS, SELF, vl19_power,
  cargo_subtype)
{
  0xE0..0xE2: return get_power_in_cb(2661);
              return get_power_in_cb(2447);
}

switch (FEAT_TRAINS, SELF, vl19_weight,
  cargo_subtype)
{
  0xE0..0xE2: return 120;
              return 117;
}

switch (FEAT_TRAINS, SELF, vl19_tractive_effort_coefficient,
  cargo_subtype)
{
  0xE0..0xE2: return get_tec_int(392.0, 120);
              return get_tec_int(335.0, 117);
}

general_running_cost(vl19o, 241)
general_running_cost(vl19m, 242)
switch (FEAT_TRAINS, SELF, vl19_running_cost_factor,
  cargo_subtype)
{
  0xE0..0xE2: vl19m_running_cost_factor;
              vl19o_running_cost_factor;
}
engine_electric1_2_effect(vl19, -2, 4)
engine_speed(vl19o, 90)
engine_speed(vl19m, 85)
switch (FEAT_TRAINS, SELF, vl19_speed,
  cargo_subtype >= 0xE0)
{
  1: vl19m_speed;
     vl19o_speed;
}
engine_penalise_speed(vl19, 2)

item (FEAT_TRAINS, vl19, 312) {
  property {
    name: string(STR_NAME_VL19);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1932,1938);
    retire_early: get_retire_early(1932,1938);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 31;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2447 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 117 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(322.0, 117);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl19_sprites;
    purchase: vl19_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl19_additional_text;
    cargo_subtype_text: vl19_cargo_subtype_text_pre;
    cargo_capacity: return 0;
    speed: vl19_speed;
    purchase_speed: vl19o_purchase_speed;
    bitmask_vehicle_info: vl19_bitmask_vehicle_info;
    create_effect: vl19_create_effect;
    running_cost_factor: vl19_running_cost_factor;
    refit_cost: vl19_refit_cost;
    power: vl19_power;
    weight: vl19_weight;
    tractive_effort_coefficient: vl19_tractive_effort_coefficient;
  }
}

long_name_template(vl19, STR_LONGNAME_VL19)
models_default_cargo_template(vl19)
allow_dcelectric(vl19)
