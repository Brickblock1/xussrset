 // vl19

// ВЛ19 ("Владимир Ленин", 1932-1938) - первый серийный электровоз
// советской разработки.

#define IMAGEFILE  "src/electric/vl19.png"
purchase_sprites(vl19_v1,   0, 0)
purchase_sprites(vl19_v2, 100, 0)

electric1r_2_sprites(10, vl19_v1,   32,   40)
electric1r_2_sprites(10, vl19_v2,   32,  375)
electric1r_2_sprites(10, vl19_v3,   32,  710)
electric1r_2_sprites(10, vl19_v4,   32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl19_purchase_sprites,
  current_year >= 1933)
{
  0: vl19_v1_purchase_sprites;
     vl19_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl19_sprites_left,
  (build_year >= 1933) +
  cargo_subtype)
{
  0: vl19_v1_sprites_left;

  1: vl19_v2_sprites_left;
  2: vl19_v3_sprites_left;
  3: vl19_v4_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl19_sprites_right,
  (build_year >= 1933) +
  cargo_subtype)
{
  0: vl19_v1_sprites_right;

  1: vl19_v2_sprites_right;
  2: vl19_v3_sprites_right;
  3: vl19_v4_sprites_right;
     align_10_sprites; 
}

engine_RNDany_direction_template(vl19, 5, 3) 

long_vehicle_sp(vl19)

switch (FEAT_TRAINS, SELF, vl19_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl19_cargo_subtype_text,
  (build_year >= 1933) )
{
  0: return string(STR_REFIT_PREPRODUCTION);
     vl19_cargo_subtype_text2;
}    

switch (FEAT_TRAINS, SELF, vl19_additional_text,
   current_year >= 1933)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_IN, 1932), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_DINAMO)));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1933, 1938), 
                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_DINAMO)));
}

engine_electric1_2_effect(vl19, -2, 4)
engine_speed(vl19, 85)
engine_penalise_speed(vl19, 2)
general_running_cost(vl19, 241)

item (FEAT_TRAINS, vl19) {
  property {
    name: string(STR_VL19_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    vehicle_life: 38;
    model_life: get_model_life(1932,1938);
    retire_early: get_retire_early(1932,1938);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 31;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2447 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 117 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(167.0, 117);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl19_sprites;
    purchase: vl19_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl19_additional_text;
    cargo_subtype_text: vl19_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl19_speed;
    purchase_speed: vl19_purchase_speed;
    bitmask_vehicle_info: vl19_bitmask_vehicle_info;
    create_effect: vl19_create_effect;
    running_cost_factor: vl19_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl19) {
    property { name: string(STR_VL19_NAME_LONG); }
  }
}

