 // vl19

// ВЛ19 ("Владимир Ленин", 1932-1938) - первый серийный электровоз
// советской разработки.

#define IMAGEFILE  "src/electric/vl19.png"

// покупка
purchase_spriteset(vl19, 500, 0)

// нет тока
spriteset (vl19_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl19_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 85, 0)
} 

spriteset (vl19_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 152, 0)
} 

spritegroup vl19_spritegroup
{
  loading: vl19_loading_spriteset;
  loaded: vl19_traveling_spriteset;
}

spriteset (vl19_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 219, 0)
} 

// 2: покупка
purchase_spriteset(vl19_v2, 500, 20)

// нет тока
spriteset (vl19_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 287, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl19_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 354, 0)
} 

spriteset (vl19_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 421, 0)
} 

spritegroup vl19_v2_spritegroup
{
  loading: vl19_v2_loading_spriteset;
  loaded: vl19_v2_traveling_spriteset;
}

spriteset (vl19_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 488, 0)
} 

// 3: нет тока
spriteset (vl19_v3_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 560, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl19_v3_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 627, 0)
} 

spriteset (vl19_v3_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 694, 0)
} 

spritegroup vl19_v3_spritegroup
{
  loading: vl19_v3_loading_spriteset;
  loaded: vl19_v3_traveling_spriteset;
}

spriteset (vl19_v3_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 761, 0)
} 

// нет тока
spriteset (vl19_v4_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 836, 0)
} 

// 4: обычный: погрузка/перемещение
spriteset (vl19_v4_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 903, 0)
} 

spriteset (vl19_v4_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 970, 0)
} 

spritegroup vl19_v4_spritegroup
{
  loading: vl19_v4_loading_spriteset;
  loaded: vl19_v4_traveling_spriteset;
}

spriteset (vl19_v4_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 1037, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl19_get_spriteset2,
  // поправка на нет тока
  (((last_computed_result > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +

  ((build_year >= 1933) * 3) +
  (cargo_subtype * 3)
)
{
  0: vl19_notpowered_spriteset;
  1: vl19_spritegroup;
  2: vl19_speedoverTH_spriteset;

  3: vl19_v2_notpowered_spriteset;
  4: vl19_v2_spritegroup;
  5: vl19_v2_speedoverTH_spriteset;
  6: vl19_v3_notpowered_spriteset;
  7: vl19_v3_spritegroup;
  8: vl19_v3_speedoverTH_spriteset;
  9: vl19_v4_notpowered_spriteset;
  10: vl19_v4_spritegroup;
  11: vl19_v4_speedoverTH_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl19_get_spriteset1,
  current_speed)
{
  vl19_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl19_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl19_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, vl19_get_purchase_spriteset,
  (current_year >= 1933)
)
{
  0: vl19_purchase_spriteset;
  vl19_v2_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, vl19_cargo_subtype_text,
  (build_year >= 1933) +
  (cargo_subtype * 2))
{
  0: string(STR_REFIT_PREPRODUCTION);
  1: string(STR_REFIT_GREEN_GRASS);
  3: string(STR_REFIT_RAL_6020);
  5: string(STR_REFIT_LIGHT_BLUE_MALE);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 85 км/ч
#define MAX_SPEED 85

switch (FEAT_TRAINS, SELF, vl19_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, vl19_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, vl19_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(vl19), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, vl19_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(vl19), 0),
  vehicle_type_id == vl19
])
{
  vl19_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, vl19_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  vl19_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, vl19) 
{
  property
  {
    name: string(STR_VL19_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    vehicle_life: 38; // срок службы
    model_life: get_model_life(1932,1938);
    retire_early: get_retire_early(1932,1938); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 17;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2447 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 117 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(167.0, 117);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl19_get_spriteset;
    purchase: vl19_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL19_PURCHASE_HINT);
    cargo_subtype_text: vl19_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl19_speed;
    purchase_speed: vl19_purchase_speed;
    bitmask_vehicle_info: vl19_bitmask_vehicle_info;
    cost_factor: return 17;
  }
}
