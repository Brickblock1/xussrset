// vl22

#define IMAGEFILE  "src/electric/vl22.png"
purchase_sprites(vl22, 0, 0)

electric1_2_sprites(12, vl22_v1, 32,   40)
electric1_2_sprites(12, vl22_v2, 32,  241)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl22_sprites_start,
  cargo_subtype)
{
           0: vl22_v1_sprites_start;
           1: vl22_v2_sprites_start;
  LV_RAL6002: vl22_v1_sprites_start;
        0xE0: vl22_v1_sprites_start;
        0xE1: vl22_v2_sprites_start;
              align_10_sprites;
}

long_vehicle(vl22)

livery_template_alt0(vl22_base, STR_REFIT_TTD_53, STR_REFIT_TTD_96)
livery_template_refit2(vl22, STR_REFIT_FACTORY_GENERAL_TYPE_SP, STR_REFIT_PASSENGER_EDITION, STR_REFIT_TTD_53, STR_REFIT_TTD_96, vl22_base)

engine_refit_cost_for_a2_livery_change(vl22, 0xE0, 1, 2)

switch (FEAT_TRAINS, SELF, vl22_tractive_effort_coefficient,
  cargo_subtype >= 0xE0)
{
  1: return get_tec_int(353.0, 132);
     return get_tec_int(322.0, 132);
}

switch (FEAT_TRAINS, SELF, vl22_power,
  (cargo_subtype >= 0xE0))
{
  0: return get_power_in_cb(2430);
     return get_power_in_cb(2447);
}

engine_electric1_2_effect(vl22_create_effect, -2, 4)
engine_speed(vl22cargo, 70)
engine_speed(vl22pass, 85)
switch (FEAT_TRAINS, SELF, vl22_speed,
  cargo_subtype >= 0xE0)
{
  1: vl22pass_speed;
     vl22cargo_speed;
}
engine_penalise_speed(vl22, 2)

switch (FEAT_TRAINS, SELF, vl22pass_running_cost_factor,
[  STORE_TEMP(144, 0),  // Моторы
   STORE_TEMP(20, 1),   // Бригада
   STORE_TEMP(50, 2),   // Износ
   STORE_TEMP(0, 3),    // Сопровождение
   STORE_TEMP(35, 4),   // ТО
   STORE_TEMP(3, 5),    // Сертификация

   STORE_TEMP(85, 6),             // Скорость
   STORE_TEMP(132, 7),            // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])  // Максимальная масса
{ all_running_cost_factor; }      // 252
switch (FEAT_TRAINS, SELF, vl22cargo_running_cost_factor,
[  STORE_TEMP(143, 0),  // Моторы
   STORE_TEMP(20, 1),   // Бригада
   STORE_TEMP(50, 2),   // Износ
   STORE_TEMP(0, 3),    // Сопровождение
   STORE_TEMP(35, 4),   // ТО
   STORE_TEMP(3, 5),    // Сертификация

   STORE_TEMP(70, 6),             // Скорость
   STORE_TEMP(132, 7),            // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])  // Максимальная масса
{ all_running_cost_factor; }      // 250
switch (FEAT_TRAINS, SELF, vl22_running_cost_factor,
  cargo_type_in_veh == PASS)
{ 1: vl22pass_running_cost_factor;
     vl22cargo_running_cost_factor; }

hint_engine_2fact_comment(vl22,
     STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC,
     STR_PURCHASE_HINT_SECTIONS_1_2,
     1938, 1946,
     string(STR_PURCHASE_HINT_FACTORY_KOLOMNA),
     string(STR_PURCHASE_HINT_FACTORY_DINAMO),
     string(STR_PURCHASE_HINT_PASSENGER_EDITION1))

item (FEAT_TRAINS, vl22, 313) {
  property {
    name: string(STR_NAME_VL22);
    vehicle_dates(1938, 1946, 40, 10, 8, 32)
    vehicle_passcargo()
    vehicle_dcelectric(2430, 132, 353.0)
  }
  graphics {
                        default: vl22_sprites;
                       purchase: vl22_purchase_sprites;
               articulated_part: articulated_part_dummy2;
               can_attach_wagon: any_can_attach_wagon;
                     start_stop: any_start_stop;
             cargo_subtype_text: vl22_cargo_subtype_text;
                 cargo_capacity: return 0;
                          speed: vl22_speed;
                          power: vl22_power;
    tractive_effort_coefficient: vl22_tractive_effort_coefficient;
           bitmask_vehicle_info: vl22_bitmask_vehicle_info;
                  create_effect: vl22_create_effect;
            running_cost_factor: vl22_running_cost_factor;
                     refit_cost: vl22_refit_cost;
                 colour_mapping: any_cc_colour;
                 purchase_speed: return 70;
                additional_text: vl22_additional_text;
  }
}

long_name_template(vl22, STR_LONGNAME_VL22)
models_default_cargo_template(vl22)
allow_dcelectric(vl22)
