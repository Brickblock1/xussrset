 // vl22

// ВЛ22 (1938-1946) - массовый грузопассажирский электровоз

#define IMAGEFILE  "src/electric/vl22.png"

// покупка
spriteset (vl22_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(0, 0) }
spriteset (vl22_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(0, 0) }
switch (FEAT_TRAINS, SELF, vl22_purchase_spriteset, 
  enable_icons)
{  1: vl22_purchase_spriteset_ico;
      vl22_purchase_spriteset_noico;}

// нет тока
spriteset (vl22_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 21, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl22_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 88, 0)
} 

spriteset (vl22_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 155, 0)
} 

spritegroup vl22_spritegroup
{
  loading: vl22_loading_spriteset;
  loaded: vl22_traveling_spriteset;
}

spriteset (vl22_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 222, 0)
} 

// 2: нет тока
spriteset (vl22_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(2, 291, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl22_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(2, 358, 0)
} 

spriteset (vl22_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(2, 425, 0)
} 

spritegroup vl22_v2_spritegroup
{
  loading: vl22_v2_loading_spriteset;
  loaded: vl22_v2_traveling_spriteset;
}

spriteset (vl22_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(2, 492, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl22_get_spriteset2,
  // поправка на нет тока
  (((last_computed_result > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  (cargo_subtype * 3)
)
{
  0: vl22_notpowered_spriteset;
  1: vl22_spritegroup;
  2: vl22_speedoverTH_spriteset;

  3: vl22_v2_notpowered_spriteset;
  4: vl22_v2_spritegroup;
  5: vl22_v2_speedoverTH_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl22_get_spriteset1,
  current_speed)
{
  vl22_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl22_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl22_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, vl22_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_REFIT_LIGHT_BLUE_MALE);
  return CB_RESULT_NO_TEXT;
}

// Макс. скорость: 70 км/ч
#define MAX_SPEED 70

switch (FEAT_TRAINS, SELF, vl22_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, vl22_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, vl22_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(vl22), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, vl22_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(vl22), 0),
  vehicle_type_id == vl22
])
{
  vl22_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, vl22_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  vl22_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, vl22) 
{
  property
  {
    name: string(STR_VL22_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1938), 1, 1);
    vehicle_life: 40; // срок службы
    model_life: get_model_life(1938,1946);
    retire_early: get_retire_early(1938,1946); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 19;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2447 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(167.0, 132);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl22_get_spriteset;
    purchase: vl22_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL22_PURCHASE_HINT);
    cargo_subtype_text: vl22_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl22_speed;
    purchase_speed: vl22_purchase_speed;
    bitmask_vehicle_info: vl22_bitmask_vehicle_info;
    cost_factor: return 19;
  }
}
