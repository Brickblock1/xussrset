 // vl22m

// ВЛ22м (1947-1958) - модернизированная версия ВЛ22

#define IMAGEFILE  "src/electric/vl22m.png"
purchase_sprites(vl22m, 0, 0)

electric1_2_sprites(12, vl22m_v1, 32,   40)
electric1_2_sprites(12, vl22m_v2, 32,  241)
electric1_2_sprites(12, vl22m_v3, 32,  442)
electric1_2_sprites(12, vl22m_v4, 32,  643)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl22m_sprites_start,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) + 
  (cargo_subtype * 2))
{
  0: vl22m_v1_sprites_start;
  1: vl22m_v2_sprites_start;
  2: vl22m_v3_sprites_start;
  3: vl22m_v4_sprites_start;
     align_10_sprites; 
}

long_vehicle(vl22m)

switch (FEAT_TRAINS, SELF, vl22m_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}

engine_refit_cost_for_cargo_change(vl22m, 1)
engine_TE_change_build_after2(vl22m, (cargo_type_in_veh == PASS) ? get_tec_int(353.0, 132) : get_tec_int(378.5, 132), 
                                     (cargo_type_in_veh == PASS) ? get_tec_int(355.0, 132) : get_tec_int(369.0, 132), 1956, 
                                     (cargo_type_in_veh == PASS) ? get_tec_int(292.0, 132) : get_tec_int(364.8, 132), 1957)
engine_power_change_build_after2(vl22m, (cargo_type_in_veh == PASS) ? get_power_in_cb(2480) : get_power_in_cb(2470), 
                                        (cargo_type_in_veh == PASS) ? get_power_in_cb(2450) : get_power_in_cb(2460), 1956, 
                                        (cargo_type_in_veh == PASS) ? get_power_in_cb(2460) : get_power_in_cb(2480), 1957)
engine_electric1_2_effect(vl22m, -2, 4)
engine_speed_change_build_after(vl22m, (cargo_type_in_veh == PASS) ? 90 : 75, 
                                       (cargo_type_in_veh == PASS) ? 100 : 80, 1957)
engine_penalise_speed(vl22m, 2)
engine_RC_change_build_after2(vl22m, (cargo_type_in_veh == PASS) ? 267 : 241, 
                                     (cargo_type_in_veh == PASS) ? 265 : 243, 1956, 
                                     (cargo_type_in_veh == PASS) ? 260 : 234, 1957)

item (FEAT_TRAINS, vl22m) {
  property {
    name: string(STR_NAME_VL22M);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1947), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1947,1958);
    retire_early: get_retire_early(1947,1958);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD, PASS];
    default_cargo_type: GOOD;
    cost_factor: 30;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2480 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 132 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(364.8, 132);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl22m_sprites;
    purchase: vl22m_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_DC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1947, 1958), 
                                   string(STR_PURCHASE_HINT_LIST2, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), string(STR_PURCHASE_HINT_FACTORY_DINAMO)), 
                                   string(STR_PURCHASE_HINT_PASSENGER_EDITION1));
    cargo_subtype_text: vl22m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl22m_speed;
    power: vl22m_power;
    tractive_effort_coefficient: vl22m_tractive_effort_coefficient;
    purchase_speed: vl22m_purchase_speed;
    bitmask_vehicle_info: vl22m_bitmask_vehicle_info;
    create_effect: vl22m_create_effect;
    running_cost_factor: vl22m_running_cost_factor;
    refit_cost: vl22m_refit_cost;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl22m) {
    property { name: string(STR_LONGNAME_VL22M); }
  }
}

models_default_cargo_template_pass(vl22m)
