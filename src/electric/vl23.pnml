 // vl23

#define IMAGEFILE  "src/electric/vl23.png"
purchase_sprites(vl23_v1,   0, 0)
purchase_sprites(vl23_v2, 100, 0)
purchase_sprites(vl23_v3, 200, 0)

electric1_2_sprites(10, vl23_v1, 32,   40)
electric1_2_sprites(10, vl23_v2, 32,  241)
electric1_2_sprites(10, vl23_v3, 32,  442)
electric1_2_sprites(10, vl23_v4, 32,  643)
electric1_2_sprites(10, vl23_v5, 32,  844)
electric1_2_sprites(10, vl23_v6, 32, 1045)
electric1_2_sprites(10, vl23_v7, 32, 1246)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl23_purchase_sprites,
  (current_year >= 1958) +
  (current_year >= 1972) )
{
  0: vl23_v1_purchase_sprites;
  1: vl23_v2_purchase_sprites;
     vl23_v3_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl23_sprites3, cargo_subtype)
{
  0: vl23_v1_sprites_left;
  1: vl23_v2_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites4, cargo_subtype)
{
   0: vl23_v3_sprites_left;
   1: vl23_v4_sprites_left;
      align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites5, cargo_subtype)
{
   0: vl23_v5_sprites_left;
   1: vl23_v6_sprites_left;
   2: vl23_v7_sprites_left;
   3: vl23_v6_sprites_left;
   4: vl23_v7_sprites_left;
   5: vl23_v6_sprites_left;
      align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites_start,
  (build_year >= 1958) +
  (((LOAD_TEMP(2) >= date(1972,1,1))) * 2) )
{
  0: vl23_sprites3;
  1: vl23_sprites4;
     vl23_sprites5;
}

long_vehicle(vl23)

engine_electric1_2_effect(vl23, -2, 4)

switch (FEAT_TRAINS, SELF, vl23_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0x300) || ((extra_callback_info1 & 0xFF00) == 0x400) || ((extra_callback_info1 & 0xFF00) == 0x500))+
  (((cargo_subtype == 3) || (cargo_subtype == 4) || (cargo_subtype == 5)) * 2))
{
  1: return 1;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text5,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_09);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text4,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_93);
  1: return string(STR_REFIT_TTD_09);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text3,
  cargo_subtype)
{
  0: return string(STR_REFIT_SOUTHERN_RAILWAY);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_TTD_B5);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text3m,
  cargo_subtype)
{
  0: return string(STR_REFIT_SOUTHERN_RAILWAY);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_TTD_B5);

  3: return string(STR_REFIT_FACTORY_2VL23, string(STR_REFIT_TTD_53));
  4: return string(STR_REFIT_FACTORY_2VL23, string(STR_REFIT_TTD_B5));
  5: return string(STR_REFIT_FACTORY_3VL23, string(STR_REFIT_TTD_53));

     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text2,
  (build_year >= 1958)) 
{
  0: vl23_cargo_subtype_text4;
  1: vl23_cargo_subtype_text5;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, PARENT, vl23_cargo_subtype_text,
  (date_of_last_service >= date(1972,1,1)) *
  (1 + ((current_year >= 1987) || (build_year >= 1987))) ) 
{
  0: vl23_cargo_subtype_text2;
  1: vl23_cargo_subtype_text3;
  2: vl23_cargo_subtype_text3m;
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(vl23, 100, 90, 40)

engine_penalise_speed(vl23, 2)

switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m4,            
  (LOAD_TEMP(0) - count_veh_id(vl23)) == 2) 
{ 
  1: return 0;                                                   
     return FLAG_PENALISE_SPEED; 
}                   

switch (FEAT_TRAINS, PARENT, vl23_bitmask_vehicle_info_m3,            
[ STORE_TEMP(count_veh_id(vl23), 0),                                        
  (vehicle_type_id == vl23) && (cargo_subtype == 5) ])                                      
{ 
  1: vl23_bitmask_vehicle_info_m4;                                                   
     return FLAG_PENALISE_SPEED; 
}                   

switch (FEAT_TRAINS, PARENT, vl23_bitmask_vehicle_info_m2,            
  vehicle_type_id == vl23)                                      
{ 
  1: return 0;                                                   
     return FLAG_PENALISE_SPEED; 
}   
                  
switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m1,            
  position_in_consist * vehicle_is_powered)                                      
{ 
  0: return 0;    
  2: vl23_bitmask_vehicle_info_m2;
  4: vl23_bitmask_vehicle_info_m3;
     return FLAG_PENALISE_SPEED; 
}                               

switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m,
  cargo_subtype == 5)
{
  1: vl23_bitmask_vehicle_info_m1;
     vl23_bitmask_vehicle_info;
}

general_running_cost(vl23, 555)

item (FEAT_TRAINS, vl23) {
  property {
    name: string(STR_VL23_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1956), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1956,1961);
    retire_early: get_retire_early(1956,1961);
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 50;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 137 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(222.0, 137);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl23_sprites;
    purchase: vl23_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL23_PURCHASE_HINT);
    cargo_subtype_text: vl23_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl23_speed;
    purchase_speed: vl23_purchase_speed;
    bitmask_vehicle_info: vl23_bitmask_vehicle_info_m;
    refit_cost: vl23_refit_cost;
    create_effect: vl23_create_effect_sprites_start;
    running_cost_factor: vl23_running_cost_factor;
    purchase_running_cost_factor: vl23_purchase_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl23) {
    property { name: string(STR_VL23_NAME_LONG); }
  }
}

 // 2vl23

switch (FEAT_TRAINS, SELF, _2vl23_sprites_start, cargo_subtype)
{
  0: vl23_v6_sprites_left;
  1: vl23_v7_sprites_left;
      align_10_sprites; 
}

long_vehicle(_2vl23)

engine_speed_with_reduce(_2vl23, 100, 90, 8)

engine_penalise_speed(_2vl23, 2)

switch (FEAT_TRAINS, SELF, _2vl23_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_B5);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, _2vl23) {
  property {
    name: string(STR_2VL23_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1987), 1, 1);
    vehicle_life: 8;
    model_life: get_model_life(1987,1990);
    retire_early: get_retire_early(1987,1990);
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 50;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (274/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(222.0, 274/2);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: _2vl23_sprites;
    purchase: vl23_v2_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_2VL23_PURCHASE_HINT);
    cargo_subtype_text: _2vl23_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: _2vl23_speed;
    purchase_speed: _2vl23_purchase_speed;
    bitmask_vehicle_info: _2vl23_bitmask_vehicle_info;
    create_effect: vl23_create_effect_sprites_start;
    running_cost_factor: vl23_running_cost_factor;
    purchase_running_cost_factor: vl23_purchase_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _2vl23) {
    property { name: string(STR_2VL23_NAME_LONG); }
  }
}

 // 3vl23

switch (FEAT_TRAINS, SELF, _3vl23_sprites_start, cargo_subtype)
{
  0: vl23_v6_sprites_left;
     align_10_sprites; 
}

long_vehicle(_3vl23)

engine_speed_with_reduce(_3vl23, 100, 90, 8)

engine_penalise_speed(_3vl23, 2)

item (FEAT_TRAINS, _3vl23) {
  property {
    name: string(STR_3VL23_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1987), 1, 1);
    vehicle_life: 8;
    model_life: get_model_life(1987,1987);
    retire_early: get_retire_early(1987,1987);
    reliability_decay: 20;
    cost_factor: 50;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: (411/3) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(222.0, 411/3);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: _3vl23_sprites;
    purchase: vl23_v3_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_3VL23_PURCHASE_HINT);
    speed: _3vl23_speed;
    purchase_speed: _3vl23_purchase_speed;
    bitmask_vehicle_info: _3vl23_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: vl23_create_effect_sprites_start;
    running_cost_factor: vl23_running_cost_factor;
    purchase_running_cost_factor: vl23_purchase_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, _3vl23) {
    property { name: string(STR_3VL23_NAME_LONG); }
  }
}

