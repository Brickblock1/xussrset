 // vl23
// 180 (B4h) 

#define IMAGEFILE  "src/electric/vl23.png"
purchase_sprites(vl23_v1,   0, 0)
purchase_sprites(vl23_v2, 100, 0)

electric1_2_sprites(10, vl23_v1, 32,   40)
electric1_2_sprites(10, vl23_v2, 32,  241)
electric1_2_sprites(10, vl23_v3, 32,  442)
electric1_2_sprites(10, vl23_v4, 32,  643)
electric1_2_sprites(10, vl23_v5, 32,  844)
electric1_2_sprites(10, vl23_v6, 32, 1045)
electric1_2_sprites(10, vl23_v7, 32, 1246)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl23_purchase_sprites,
  current_year >= 1958)
{
  0: vl23_v1_purchase_sprites;
     vl23_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl23_sprites_b1958, cargo_subtype)
{
  0: vl23_v1_sprites_start;
  1: vl23_v2_sprites_start;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites_start_green, 
 (LOAD_TEMP(2) >= LV_GREAT_CHANGE_DATE) || (build_year >= LV_GREAT_CHANGE_YEAR))
{
  0: vl23_v3_sprites_start;
  1: vl23_v6_sprites_start;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites_start_grey, 
 (LOAD_TEMP(2) >= LV_GREAT_CHANGE_DATE) || (build_year >= LV_GREAT_CHANGE_YEAR))
{
  0: vl23_v4_sprites_start;
  1: vl23_v7_sprites_start;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites_a1958,
  cargo_subtype)
{
  0: vl23_sprites_start_green;
  1: vl23_sprites_start_grey;
  LV_RAL6002: vl23_sprites_start_green;
  LV_SOUTHERN_RAILWAY: vl23_v5_sprites_start;

  0xE0: vl23_v6_sprites_start;
  0xE1: vl23_v7_sprites_start;                     	
  0xE2: vl23_v6_sprites_start;
        align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl23_sprites_start,
  build_year >= 1958)
{
  0: vl23_sprites_b1958;
     vl23_sprites_a1958;
}

long_vehicle(vl23)

switch (FEAT_TRAINS, SELF, vl23_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0xE000) || ((extra_callback_info1 & 0xFF00) == 0xE100) || ((extra_callback_info1 & 0xFF00) == 0xE200)) +
  (((cargo_subtype == 0xE0) || (cargo_subtype == 0xE1) || (cargo_subtype == 0xE2)) * 2))
{
  1: return 1;
     return 0; 
}            

livery_template(vl23_b1958, TTD_93, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(vl23_b1958, TTD_09)

livery_template(vl23_a1958, TTD_53, LV_RAL6002, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(vl23_a1958, TTD_09)

livery_template(vl23_a1972, TTD_53, LV_RAL6002, LV_SOUTHERN_RAILWAY, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(vl23_a1972, TTD_B5)

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text_a1987,
  cargo_subtype)
{
  0xE0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_2VL23), string(STR_REFIT_TTD_53));
  0xE1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_2VL23), string(STR_REFIT_TTD_B5));
  0xE2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_3VL23), string(STR_REFIT_TTD_53));
        vl23_a1972_cargo_subtype_text_pre;
}

switch (FEAT_TRAINS, PARENT, vl23_cargo_subtype_text_a1958,
  ((date_of_last_service >= LV_GREAT_CHANGE_DATE) || (build_year >= LV_GREAT_CHANGE_YEAR)) *
  (1 + ((current_year >= 1987) || (build_year >= 1987)))) 
{
  0: vl23_a1958_cargo_subtype_text_pre;
  1: vl23_a1972_cargo_subtype_text_pre;
  2: vl23_cargo_subtype_text_a1987;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl23_cargo_subtype_text,
  (build_year >= 1958)) 
{
  0: vl23_b1958_cargo_subtype_text_pre;
  1: vl23_cargo_subtype_text_a1958;
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(vl23, 100, 90, 40)
engine_penalise_speed(vl23, 2)

switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m4,            
  (LOAD_TEMP(0) - count_veh_id(vl23)) == 2) 
{ 
  1: return 0;                                                   
     return FLAG_PENALISE_SPEED; 
}                   

switch (FEAT_TRAINS, PARENT, vl23_bitmask_vehicle_info_m3,            
[ STORE_TEMP(count_veh_id(vl23), 0),                                        
  (vehicle_type_id == vl23) && (cargo_subtype == 0xE2) ])                                      
{ 
  1: vl23_bitmask_vehicle_info_m4;                                                   
     return FLAG_PENALISE_SPEED; 
}                   

switch (FEAT_TRAINS, PARENT, vl23_bitmask_vehicle_info_m2,            
  vehicle_type_id == vl23)                                      
{ 
  1: return 0;                                                   
     return FLAG_PENALISE_SPEED; 
}   
                  
switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m1,            
  position_in_consist * vehicle_is_powered)                                      
{ 
  0: return 0;    
  2: vl23_bitmask_vehicle_info_m2;
  4: vl23_bitmask_vehicle_info_m3;
     return FLAG_PENALISE_SPEED; 
}                               

switch (FEAT_TRAINS, SELF, vl23_bitmask_vehicle_info_m,
  cargo_subtype == 0xE2)
{
  1: vl23_bitmask_vehicle_info_m1;
     vl23_bitmask_vehicle_info;
}

switch (FEAT_TRAINS, SELF, vl23_additional_text,
  current_year >= 1987)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1956, 1961), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_DC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1_2), 
                   string(STR_PURCHASE_HINT_FROMTO, 1956, 1961), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK),
                   string(STR_PURCHASE_HINT_UPGRADE));
}

engine_electric1_2_effect(vl23, -2, 4)
general_running_cost(vl23, 392)

item (FEAT_TRAINS, vl23, 180) {
  property {
    name: string(STR_NAME_VL23);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1956), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1956,1961);
    retire_early: get_retire_early(1956,1961);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 50;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU); 
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3700 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 137 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(446.2, 137);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl23_sprites;
    purchase: vl23_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl23_additional_text;
    cargo_subtype_text: vl23_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl23_speed;
    purchase_speed: vl23_purchase_speed;
    bitmask_vehicle_info: vl23_bitmask_vehicle_info_m;
    refit_cost: vl23_refit_cost;
    create_effect: vl23_create_effect;
    running_cost_factor: vl23_running_cost_factor;
  }
}

long_name_template(vl23, STR_LONGNAME_VL23)
models_default_cargo_template(vl23)
allow_dcelectric(vl23)
