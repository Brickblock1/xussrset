 // vl60

// ВЛ60 - первый массовый отечественный электровоз переменного тока.
// Н60 - предсерийная версия

#define IMAGEFILE  "src/electric/n60-vl60.png"

// покупка
purchase_spriteset(vl60, 100, 0)

// нет тока
spriteset (vl60_notpowered_spriteset, IMAGEFILE)
{
  s12_template(6, 318, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl60_loading_spriteset, IMAGEFILE)
{
  s12_template(6, 385, 0)
} 

spriteset (vl60_traveling_spriteset, IMAGEFILE)
{
  s12_template(6, 452, 0)
} 

spritegroup vl60_spritegroup
{
  loading: vl60_loading_spriteset;
  loaded: vl60_traveling_spriteset;
}

spriteset (vl60_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(6, 519, 0)
} 

// покупка
purchase_spriteset(vl60_v2, 200, 0)

// нет тока
spriteset (vl60_v2_notpowered_spriteset, IMAGEFILE)
{
  s12_template(6, 606, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl60_v2_loading_spriteset, IMAGEFILE)
{
  s12_template(6, 673, 0)
} 

spriteset (vl60_v2_traveling_spriteset, IMAGEFILE)
{
  s12_template(6, 740, 0)
} 

spritegroup vl60_v2_spritegroup
{
  loading: vl60_v2_loading_spriteset;
  loaded: vl60_v2_traveling_spriteset;
}

spriteset (vl60_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(6, 807, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl60_get_purchase_spriteset,
  (current_year >= 1972))
{
  0: vl60_purchase_spriteset;
     vl60_v2_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, vl60_get_spriteset2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 3)
)
{
  0: vl60_notpowered_spriteset;
  1: vl60_spritegroup;
  2: vl60_speedoverTH_spriteset;
  3: vl60_v2_notpowered_spriteset;
  4: vl60_v2_spritegroup;
  5: vl60_v2_speedoverTH_spriteset;
     align_12_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl60_get_spriteset1,
[ STORE_TEMP(current_speed, 1),
  STORE_TEMP(date_of_last_service, 2) ])
{
  vl60_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl60_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
     vl60_get_spriteset1;
}

engine_speed(vl60, 100)

engine_penalise_speed2(vl60, vl60k, 2)

item (FEAT_TRAINS, vl60) 
{
  property
  {
    name: string(STR_VL60_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1960), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1960,1965);
    retire_early: get_retire_early(1960,1965); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 80;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4380 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(228.0, 138);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: vl60_get_spriteset;
    purchase: vl60_get_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL60_PURCHASE_HINT);
    speed: vl60_speed;
    purchase_speed: vl60_purchase_speed;
    bitmask_vehicle_info: vl60_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
