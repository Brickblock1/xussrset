 // n60
// 319 (13Fh)

#define IMAGEFILE  "src/electric/vl60.png"
purchase_sprites(n60, 0, 0)
purchase_sprites(vl60_v1, 100, 0)

electric1_2_sprites(12, n60,     32,   40)
electric1_2_sprites(12, vl60_v1, 32,  241)
electric1_2_sprites(12, vl60_v2, 32,  442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl60_purchase_sprites,
  current_year >= 1960)
{
  0: n60_purchase_sprites;
     vl60_v1_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl60_sprites_start,
  ((build_year >= 1960) * 2) +
  (LOAD_TEMP(2) >= date(1972, 1, 1)))
{
  0..1: n60_sprites_start;
  2: vl60_v1_sprites_start;
  3: vl60_v2_sprites_start;
     align_12_sprites; 
}

long_vehicle(vl60)

switch (FEAT_TRAINS, SELF, vl60_refit_cost,
  ((extra_callback_info1 & 0xFF00) == 0xE000) +
  ((cargo_subtype == 0xE0) * 2))
{
  1: return 1;
     return 0; 
}            

livery_template(vl60_b1966, FACTORY, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)

switch (FEAT_TRAINS, SELF, vl60_a1966_cargo_subtype_text,
  cargo_subtype)
{
  0: vl60_b1966_cargo_subtype_text;
  0xE0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60K), "");
        return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60_a1960_cargo_subtype_text_pre,
  (build_year >= 1966) || (current_year >= 1966))
{
  0: vl60_b1966_cargo_subtype_text;
     vl60_a1966_cargo_subtype_text;
}     

switch (FEAT_TRAINS, SELF, vl60_a1959_cargo_subtype_text_pre,
  build_year >= 1960)
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_N60), "");
     vl60_a1960_cargo_subtype_text_pre;
}

livery_template_prealt(vl60, 1959)

switch (FEAT_TRAINS, SELF, vl60_additional_text,
  (current_year >= 1959) +
  (current_year >= 1960) +
  (current_year >= 1966))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC_PROTOTYPE), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1957, 1958), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_IN, 1959), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
  2: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1965), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1965), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK),
                   string(STR_PURCHASE_HINT_EDITION_NEW_SECTIONS, string(STR_PURCHASE_HINT_SECTIONS_1_2_WITH_ATTACH, string(STR_PURCHASE_HINT_LIST2, string(STR_NAME_VL60K), string(STR_NAME_VL60P)))));
}

engine_electric1_2_effect(vl60, -2, 6)
engine_speed(vl60, 100)
engine_penalise_speed3(vl60, vl60k, vl60p, 2)
engine_TE_change_build_after2(vl60, get_tec_int(487.0, 141), 
                                    get_tec_int(449.0, 140), 1958,
                                    get_tec_int(487.0, 138), 1959)
engine_power_change_build_after4(vl60, get_power_in_cb(4830), 
                                       get_power_in_cb(4840), 1958,
                                       get_power_in_cb(4470), 1959,
                                       get_power_in_cb(4380), 1960,
                                       get_power_in_cb(5360), 1965)
engine_RC_change_build_after4(vl60, 624, 
                                    558, 1958,
                                    482, 1959,
                                    451, 1960,
                                    531, 1965)
engine_weight_change_build_after2(vl60, 141, 
                                        140, 1958,
                                        138, 1959)
engine_cost_change_build_after4(vl60, 89, 
                                      79, 1958,
                                      69, 1959,
                                      64, 1960,
                                      75, 1965)

switch (FEAT_TRAINS, PARENT, vl60_bitmask_vehicle_info_advanced2,
  (cargo_subtype < 0xE0) && (vehicle_type_id == vl60p))
{
  1: return FLAG_PENALISE_SPEED;
     vl60_bitmask_vehicle_info;
}    
                  
switch (FEAT_TRAINS, SELF, vl60_bitmask_vehicle_info_advanced1,
  cargo_subtype == 0)
{
  1: return FLAG_PENALISE_SPEED;
     vl60_bitmask_vehicle_info_advanced2;
}    

switch (FEAT_TRAINS, SELF, vl60_bitmask_vehicle_info_advanced,
  (position_in_consist == 0) || vehicle_is_not_powered)
{ 
  1: return 0;                                                              
     vl60_bitmask_vehicle_info_advanced1; 
}

item (FEAT_TRAINS, vl60, 319) {
  property {
    name: string(STR_NAME_VL60);
    climates_available: get_climates_available();
    vehicle_dates(1957, 1965, 30, 10)
    vehicle_passcargo()
    vehicle_acelectric(4830)
    length: 8;
  }
  graphics {
    default: vl60_sprites;
    purchase: vl60_purchase_sprites;
    cost_factor: vl60_cost_factor;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl60_additional_text;
    speed: vl60_speed;
    power: vl60_power;                           
    tractive_effort_coefficient: vl60_tractive_effort_coefficient;     
    weight: vl60_weight;                          
    purchase_speed: vl60_purchase_speed;
    bitmask_vehicle_info: vl60_bitmask_vehicle_info_advanced;
    cargo_capacity: return 0;
    cargo_subtype_text: vl60_cargo_subtype_text_pre;
    create_effect: vl60_create_effect;
    running_cost_factor: vl60_running_cost_factor;
    refit_cost: vl60_refit_cost;
  }
}

long_name_template(vl60, STR_LONGNAME_VL60)
models_default_cargo_template(vl60)
allow_acelectric(vl60)
