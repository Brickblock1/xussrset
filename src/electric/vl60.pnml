 // n60

// ВЛ60 - первый массовый отечественный электровоз переменного тока.
// Н60 - предсерийная версия

#define IMAGEFILE  "src/electric/vl60.png"
purchase_sprites(n60, 0, 0)
purchase_sprites(vl60_v1, 100, 0)
purchase_sprites(vl60_v2, 200, 0)

electric1_2_sprites(12, n60_v1, 32,   40)
electric1_2_sprites(12, vl60_v1, 32,  241)
electric1_2_sprites(12, vl60_v2, 32,  442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, n60_sprites_start, 0)
{
  n60_v1_sprites_left;
}

long_vehicle(n60)

engine_electric1_2_effect(n60, -2, 7)

engine_speed(n60, 100)

engine_penalise_speed(n60, 2)

item (FEAT_TRAINS, n60) 
{
  property
  {
    name: string(STR_N60_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1959), 1, 1);
    vehicle_life: 20;
    model_life: get_model_life(1959,1959);
    retire_early: get_retire_early(1959,1959);
    reliability_decay: 20;
    cost_factor: 80;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4470 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 140 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(255.0, 140);
    length: 8;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: n60_sprites;
    purchase: n60_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_N60_PURCHASE_HINT);
    speed: n60_speed;
    purchase_speed: n60_purchase_speed;
    bitmask_vehicle_info: n60_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: n60_create_effect_sprites_start;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, n60)
  {
    property
    { name: string(STR_N60_NAME_LONG); }
  }
}

 // vl60

// ВЛ60 - первый массовый отечественный электровоз переменного тока.
// Н60 - предсерийная версия

switch (FEAT_TRAINS, SELF, vl60_purchase_sprites,
  (current_year >= 1960) +
  (current_year >= 1972))
{
  0: n60_purchase_sprites;
  1: vl60_v1_purchase_sprites;
     vl60_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl60_sprites_start,
  ((build_year >= 1960) * 2) +
  (LOAD_TEMP(2) >= date(1972, 1, 1)))
{
  0..1: n60_v1_sprites_left;
  2: vl60_v1_sprites_left;
  3: vl60_v2_sprites_left;
     align_12_sprites; 
}

long_vehicle(vl60)

engine_electric1_2_effect(vl60, -2, 7)

switch (FEAT_TRAINS, SELF, vl60_tractive_effort_coefficient,
  build_year >= 1960)
{
  0: return get_tec_int(255.0, 140);
     return get_tec_int(228.0, 138);
}

switch (FEAT_TRAINS, SELF, vl60_power,
  build_year >= 1960)
{
  0: return get_power_in_cb(4470);
     return get_power_in_cb(4380);
}
  
switch (FEAT_TRAINS, SELF, vl60_weight,
  build_year >= 1960)
{
  0: return 140;
     return 138;
}

switch (FEAT_TRAINS, SELF, vl60_cargo_subtype_text,
  build_year >= 1960)
{
  0: return string(STR_REFIT_FACTORY_N60);
     return CB_RESULT_NO_TEXT;
}

engine_speed(vl60, 100)

engine_penalise_speed3(vl60, vl60k, vl60pk, 2)

item (FEAT_TRAINS, vl60) 
{
  property
  {
    name: string(STR_VL60_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1957), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1957,1965);
    retire_early: get_retire_early(1957,1965);
    reliability_decay: 20;
    cost_factor: 80;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4380 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(228.0, 138);
    length: 8;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
    engine_class: ENGINE_CLASS_ELECTRIC;
  }
  graphics {
    default: vl60_sprites;
    purchase: vl60_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL60_PURCHASE_HINT);
    speed: vl60_speed;
    power: vl60_power;                           
    tractive_effort_coefficient: vl60_tractive_effort_coefficient;     
    weight: vl60_weight;                          
    purchase_speed: vl60_purchase_speed;
    bitmask_vehicle_info: vl60_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: vl60_cargo_subtype_text;
    create_effect: vl60_create_effect_sprites_start;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl60)
  {
    property
    { name: string(STR_VL60_NAME_LONG); }
  }
}

