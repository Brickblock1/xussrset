 // vl60k

// ВЛ60к - первый массовый отечественный электровоз переменного тока.

#define IMAGEFILE  "src/electric/vl60k.png"

// покупка
spriteset (vl60k_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// нет тока
spriteset (vl60k_notpowered_spriteset, IMAGEFILE)
{
  s12_template(5, 21, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl60k_loading_spriteset, IMAGEFILE)
{
  s12_template(5, 88, 0)
} 

spriteset (vl60k_traveling_spriteset, IMAGEFILE)
{
  s12_template(5, 155, 0)
} 

spritegroup vl60k_spritegroup
{
  loading: vl60k_loading_spriteset;
  loaded: vl60k_traveling_spriteset;
}

spriteset (vl60k_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(5, 222, 0)
} 

// покупка
spriteset (vl60k_v2_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(100, 0)
}

// нет тока
spriteset (vl60k_v2_notpowered_spriteset, IMAGEFILE)
{
  s12_template(5, 309, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl60k_v2_loading_spriteset, IMAGEFILE)
{
  s12_template(5, 376, 0)
} 

spriteset (vl60k_v2_traveling_spriteset, IMAGEFILE)
{
  s12_template(5, 443, 0)
} 

spritegroup vl60k_v2_spritegroup
{
  loading: vl60k_v2_loading_spriteset;
  loaded: vl60k_v2_traveling_spriteset;
}

spriteset (vl60k_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s12_template(5, 510, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl60k_get_spriteset2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 3)
)
{
  0: vl60k_notpowered_spriteset;
  1: vl60k_spritegroup;
  2: vl60k_speedoverTH_spriteset;
  3: vl60k_v2_notpowered_spriteset;
  4: vl60k_v2_spritegroup;
  5: vl60k_v2_speedoverTH_spriteset;
  align_12_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl60k_get_spriteset1,
[ STORE_TEMP(current_speed, 1),
  STORE_TEMP(date_of_last_service, 2)])
{
  vl60k_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl60k_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl60k_get_spriteset1;
}

// Макс. скорость: 100 км/ч
#define MAX_SPEED 100

switch (FEAT_TRAINS, SELF, vl60k_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, vl60k_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, vl60k_bitmask_vehicle_info3,
[
  STORE_TEMP(LOAD_TEMP(0) - count_veh_id(vl60) - count_veh_id(vl60k), 0),
  (last_computed_result && (position_in_consist == 2 * LOAD_TEMP(0)) &&
    LOAD_TEMP(0) < 2)
])
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, PARENT, vl60k_bitmask_vehicle_info2,
[
  STORE_TEMP(count_veh_id(vl60) + count_veh_id(vl60k), 0),
  (vehicle_type_id == vl60) || (vehicle_type_id == vl60k)
])
{
  vl60k_bitmask_vehicle_info3;
}

switch (FEAT_TRAINS, SELF, vl60k_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  vl60k_bitmask_vehicle_info2;
}

item (FEAT_TRAINS, vl60k) 
{
  property
  {
    name: string(STR_VL60K_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1962), 1, 1);
    vehicle_life: 40; // срок службы
    model_life: get_model_life(1962,1967);
    retire_early: get_retire_early(1962,1967); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 80;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5360 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(258.0, 138);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: vl60k_get_spriteset;
    purchase: vl60k_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL60K_PURCHASE_HINT);
    speed: vl60k_speed;
    purchase_speed: vl60k_purchase_speed;
    bitmask_vehicle_info: vl60k_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
