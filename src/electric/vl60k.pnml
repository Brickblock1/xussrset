 // vl60k

// ВЛ60к - первый массовый отечественный электровоз переменного тока.

#define IMAGEFILE  "src/electric/vl60k.png"
purchase_sprites(vl60k, 0, 0)

electric1_2_sprites(12, vl60k_v1, 32,   40)
electric1_2_sprites(12, vl60k_v2, 32,  241)
electric1_2_sprites(12, vl60k_v3, 32,  442)
electric1_2_sprites(12, vl60k_v4, 32,  643)
electric1_2_sprites(12, vl60k_v5, 32,  844)
electric1_2_sprites(12, vl60k_v6, 32, 1045)
#undef IMAGEFILE                  

switch (FEAT_TRAINS, SELF, vl60k_sprites_start,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) + 
  (cargo_subtype * 2))
{
  0: vl60k_v1_sprites_start;
  1: vl60k_v2_sprites_start;
  2: vl60k_v3_sprites_start;
  3: vl60k_v4_sprites_start;
  4: vl60k_v5_sprites_start;
  5: vl60k_v5_sprites_start;
  6: vl60k_v6_sprites_start;
  7: vl60k_v6_sprites_start;
     align_12_sprites; 
}

long_vehicle(vl60k)

switch (FEAT_TRAINS, SELF, vl60k_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60k_cargo_subtype_text1,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_UKRAINIAN_RAILWAY);
  3: return string(STR_REFIT_NORTHERN_RAILWAY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60k_cargo_subtype_text,
  (current_year >= USSREND) || (build_year >= USSREND))
{
  1: vl60k_cargo_subtype_text1;
     vl60k_cargo_subtype_text2;
}

engine_electric1_2_effect(vl60k, -2, 6)
engine_speed(vl60k, 100)
engine_penalise_speed3(vl60k, vl60, vl60pk, 2)
general_running_cost(vl60k, 531)

switch (FEAT_TRAINS, PARENT, vl60k_bitmask_vehicle_info_advanced,
  (vehicle_type_id == vl60) && (build_year < 1960) && vehicle_is_powered)
{
  1: return FLAG_PENALISE_SPEED;
     vl60k_bitmask_vehicle_info;
}    

item (FEAT_TRAINS, vl60k) {
  property {
    name: string(STR_NAME_VL60K);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1962), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1962,1967);
    retire_early: get_retire_early(1962,1967);
    reliability_decay: 10;
    cost_factor: 75;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 5360 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(258.0, 138);
    length: 8;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl60k_sprites;
    purchase: vl60k_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2_WITH_ATTACH, string(STR_PURCHASE_HINT_LIST2, string(STR_NAME_VL60), string(STR_NAME_VL60PK))), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1962, 1967), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
    speed: vl60k_speed;
    purchase_speed: vl60k_purchase_speed;
    bitmask_vehicle_info: vl60k_bitmask_vehicle_info_advanced;
    cargo_subtype_text: vl60k_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: vl60k_create_effect;
    running_cost_factor: vl60k_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl60k) {
    property { name: string(STR_LONGNAME_VL60K); }
  }
}

if (models_default_cargo == 1) {
  item (FEAT_TRAINS, vl60k) {
    property {
      cargo_allow_refit: [GOOD];
      default_cargo_type: GOOD;
    }
  }
}

if (models_default_cargo == 2) {
  item (FEAT_TRAINS, vl60k) {
    property {
      cargo_allow_refit: [PASS];
      default_cargo_type: PASS;
    }
  }
}

if (models_default_cargo == 3) {
  item (FEAT_TRAINS, vl60k) {
    property {
      cargo_allow_refit: [MAIL];
      default_cargo_type: MAIL;
    }
  }
}

