 // vl60p

switch (FEAT_TRAINS, SELF, vl60p_sprites_start,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) + 
  (cargo_subtype * 2))
{
  0: vl60k_v1_sprites_start;
  1: vl60k_v2_sprites_start;
  2: vl60k_v3_sprites_start;
  3: vl60k_v4_sprites_start;
  4: vl60k_v1_sprites_start;
  5: vl60k_v2_sprites_start;
  6: vl60k_v3_sprites_start;
  7: vl60k_v4_sprites_start;

  8: vl60k_v5_sprites_start;
  9: vl60k_v5_sprites_start;
 10: vl60k_v6_sprites_start;
 11: vl60k_v6_sprites_start;
 12: vl60k_v5_sprites_start;
 13: vl60k_v5_sprites_start;
 14: vl60k_v6_sprites_start;
 15: vl60k_v6_sprites_start;
     align_12_sprites; 
}

long_vehicle(vl60p)

switch (FEAT_TRAINS, SELF, vl60p_refit_cost,
  (((extra_callback_info1 & 0xFF00) == 0x200) || ((extra_callback_info1 & 0xFF00) == 0x300) || ((extra_callback_info1 & 0xFF00) == 0x600) || ((extra_callback_info1 & 0xFF00) == 0x700))+
  (((cargo_subtype == 2) || (cargo_subtype == 3) || (cargo_subtype == 6) || (cargo_subtype == 7)) * 2))
{
  1: return 2;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, vl60p_cargo_subtype_text_b1966,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60p_cargo_subtype_text_a1966,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_53));
  3: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_59));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60p_cargo_subtype_text_aussr,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_53));
  3: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_59));
  4: return string(STR_REFIT_UKRAINIAN_RAILWAY);
  5: return string(STR_REFIT_NORTHERN_RAILWAY);
  6: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_UKRAINIAN_RAILWAY));
  7: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_NORTHERN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl60p_cargo_subtype_text,
  ((current_year >= 1966) || (build_year >= 1966)) +
  ((current_year >= USSREND) || (build_year >= USSREND)))
{
  0: vl60p_cargo_subtype_text_b1966;
  1: vl60p_cargo_subtype_text_a1966;
     vl60p_cargo_subtype_text_aussr;
}

engine_electric1_2_effect(vl60p, -2, 6)
engine_speed(vl60p, 110)
engine_penalise_speed3(vl60p, vl60, vl60k, 2)
general_running_cost(vl60p, 400)

switch (FEAT_TRAINS, PARENT, vl60p_bitmask_vehicle_info_advanced2,
  ((cargo_subtype == 0) && (vehicle_type_id == vl60)))
{
  1: return FLAG_PENALISE_SPEED;
     vl60p_bitmask_vehicle_info;
}    
                  
switch (FEAT_TRAINS, SELF, vl60p_bitmask_vehicle_info_advanced1,
  (cargo_subtype == 2) || (cargo_subtype == 3) || (cargo_subtype == 6) || (cargo_subtype == 7))
{
  1: return FLAG_PENALISE_SPEED;
     vl60p_bitmask_vehicle_info_advanced2;
}    

switch (FEAT_TRAINS, SELF, vl60p_bitmask_vehicle_info_advanced,
  (position_in_consist == 0) || vehicle_is_not_powered)
{ 
  1: return 0;                                                              
     vl60p_bitmask_vehicle_info_advanced1; 
}

item (FEAT_TRAINS, vl60p) {
  property {
    name: string(STR_NAME_VL60P);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1962), 1, 1);
    vehicle_life: 40;
    model_life: get_model_life(1962,1965);
    retire_early: get_retire_early(1962,1965);
    reliability_decay: 10;
    cost_factor: 57;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4370 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 138 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(487.0, 138);
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl60p_sprites;
    purchase: vl60k_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1_2_WITH_ATTACH, string(STR_PURCHASE_HINT_LIST2, string(STR_NAME_VL60), string(STR_NAME_VL60K))), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1962, 1965), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
    speed: vl60p_speed;
    purchase_speed: vl60p_purchase_speed;
    bitmask_vehicle_info: vl60p_bitmask_vehicle_info_advanced;
    cargo_subtype_text: vl60p_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: vl60p_create_effect;
    running_cost_factor: vl60p_running_cost_factor;
    refit_cost: vl60p_refit_cost;
  }
}

long_name_template(vl60p, STR_LONGNAME_VL60P)
models_default_cargo_template(vl60p)
