 // vl60p
// 422 (1A6h)

switch (FEAT_TRAINS, SELF, vl60p_sprites_start,
  cargo_subtype)
{
  0: vl60k_sprites_start_gr1;
  LV_RAL6002: vl60k_sprites_start_gr1;
  LV_RAL6020: vl60k_sprites_start_gr2;
  LV_UKRAINIAN_RAILWAY: vl60k_v5_sprites_start;
  LV_NORTHERN_RAILWAY: vl60k_v6_sprites_start;
  align_12_sprites; 
}

long_vehicle(vl60p)

switch (FEAT_TRAINS, SELF, vl60p_refit_cost,
  ((extra_callback_info1 & 0xFF00) >= 0xE000) +
  ((cargo_subtype >= 0xE0) * 2))
{
  1: return 2;
     return 0; 
}            

livery_template(vl60p_b1966, TTD_53, LV_RAL6002, LV_RAL6020, LV_UKRAINIAN_RAILWAY, LV_NORTHERN_RAILWAY, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)

switch (FEAT_TRAINS, SELF, vl60p_a1966_cargo_subtype_text,
  cargo_subtype)
{
  0xE0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_53));
  0xE1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_TTD_59));
  0xE2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_UKRAINIAN_RAILWAY));
  0xE3: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL60PK), string(STR_REFIT_NORTHERN_RAILWAY));
        vl60p_b1966_cargo_subtype_text;
}

switch (FEAT_TRAINS, SELF, vl60p_cargo_subtype_text,
  (current_year >= 1966) || (build_year >= 1966))
{
  0: vl60p_b1966_cargo_subtype_text;
     vl60p_a1966_cargo_subtype_text;
}

switch (FEAT_TRAINS, SELF, vl60p_additional_text,
  current_year >= 1966)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1962, 1965), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1962, 1965), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK),
                   string(STR_PURCHASE_HINT_EDITION_NEW_SECTIONS, string(STR_PURCHASE_HINT_SECTIONS_1_2_WITH_ATTACH, string(STR_PURCHASE_HINT_LIST2, string(STR_NAME_VL60), string(STR_NAME_VL60K)))));
}

engine_electric1_2_effect(vl60p, -2, 6)
engine_speed(vl60p, 110)
engine_penalise_speed3(vl60p, vl60, vl60k, 2)
general_running_cost(vl60p, 400)

switch (FEAT_TRAINS, PARENT, vl60p_bitmask_vehicle_info_advanced2,
  ((cargo_subtype < 0xE0) && (vehicle_type_id == vl60)))
{
  1: return FLAG_PENALISE_SPEED;
     vl60p_bitmask_vehicle_info;
}    
                  
switch (FEAT_TRAINS, SELF, vl60p_bitmask_vehicle_info_advanced1,
  cargo_subtype < 0xE0)
{
  1: return FLAG_PENALISE_SPEED;
     vl60p_bitmask_vehicle_info_advanced2;
}    

switch (FEAT_TRAINS, SELF, vl60p_bitmask_vehicle_info_advanced,
  (position_in_consist == 0) || vehicle_is_not_powered)
{ 
  1: return 0;                                                              
     vl60p_bitmask_vehicle_info_advanced1; 
}

item (FEAT_TRAINS, vl60p, 422) {
  property {
    name: string(STR_NAME_VL60P);
    climates_available: get_climates_available();
    vehicle_dates(1962, 1965, 40, 10)
    vehicle_pass()
    vehicle_acelectric()
    cost_factor: 57;
    power: 4370 hpM;
    weight: 138 ton;
    tractive_effort_coefficient: get_tec(375.1, 138);
    length: 8;
  }
  graphics {
    default: vl60p_sprites;
    purchase: vl60k_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl60p_additional_text;
    speed: vl60p_speed;
    purchase_speed: vl60p_purchase_speed;
    bitmask_vehicle_info: vl60p_bitmask_vehicle_info_advanced;
    cargo_subtype_text: vl60p_cargo_subtype_text;
    cargo_capacity: return 0;
    create_effect: vl60p_create_effect;
    running_cost_factor: vl60p_running_cost_factor;
    refit_cost: vl60p_refit_cost;
  }
}

long_name_template(vl60p, STR_LONGNAME_VL60P)
models_default_cargo_template(vl60p)
allow_acelectric(vl60p)
