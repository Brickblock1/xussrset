 // vl61
// 189 (BDh) 

#define IMAGEFILE  "src/electric/vl61.png"
purchase_sprites(vl61,    0, 0)

electric1r_2_sprites(10, vl61_v1,  32,   40)
electric1r_2_sprites(10, vl61_v2,  32,  375)
electric1r_2_sprites(10, vl61d_v1, 32,  710)
electric1r_2_sprites(10, vl61d_v2, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl61_sprites_left,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) +
  cargo_subtype * 2)
{
  0: vl61_v1_sprites_left;
  1: vl61_v2_sprites_left;
  2: vl61d_v1_sprites_left;
  3: vl61d_v2_sprites_left;
     align_10_sprites; 
}

switch (FEAT_TRAINS, SELF, vl61_sprites_right,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) +
  cargo_subtype * 2)
{
  0: vl61_v1_sprites_right;
  1: vl61_v2_sprites_right;
  2: vl61d_v1_sprites_right;
  3: vl61d_v2_sprites_right;
     align_10_sprites; 
}

engine_RNDany_direction_template(vl61, 5, 3) 

long_vehicle_sp(vl61)

switch (FEAT_TRAINS, SELF, vl61_refit_cost,
  ((extra_callback_info1 & 0xFF00) == 0x100) +
  ((cargo_subtype == 1) * 2))
{
  1: return 14;
  2: return 14;
     return 0; 
}            

switch (FEAT_TRAINS, SELF, vl61_power1,
  cargo_subtype)
{
  1: return get_power_in_cb(2660);
     return get_power_in_cb(0);
}

switch (FEAT_TRAINS, SELF, vl61_power,
  current_railtype)
{
  ERA1: return get_power_in_cb(2660);
  ERA2: return get_power_in_cb(2660);
  ERA3: return get_power_in_cb(2660);
  ERA4: return get_power_in_cb(2660);
  ER2S: return get_power_in_cb(2660);
        vl61_power1;
}

switch (FEAT_TRAINS, SELF, vl61_cargo_subtype_text1,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL61), "");
  1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_NAME_VL61D), "");
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl61_cargo_subtype_text,
  (current_year >= 1963) || (build_year >= 1963))
{
  0: return CB_RESULT_NO_TEXT;
     vl61_cargo_subtype_text1;
}

switch (FEAT_TRAINS, SELF, vl61_additional_text,
  current_year >= 1963)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1954, 1957), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
     return string(STR_PURCHASE_HINT_ENGINE_WITH_COMMENT, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_AC), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1954, 1957), 
                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK),
                   string(STR_PURCHASE_HINT_UPGRADE_AVAILABLE, string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_ACDC)));
}

engine_speed_change_after(vl61, 75, 
                                85, 1960)
engine_penalise_speed0(vl61)

switch (FEAT_TRAINS, PARENT, vl61_bitmask_vehicle_info_advanced2,            
  (vehicle_type_id == vl61) && (cargo_subtype == 1))                                      
{ 
  1: return 0;                                                   
     return FLAG_PENALISE_SPEED; 
}   
                  
switch (FEAT_TRAINS, SELF, vl61_bitmask_vehicle_info_advanced1,            
  position_in_consist * vehicle_is_powered)                                      
{ 
  0: return 0;    
  2: vl61_bitmask_vehicle_info_advanced2;
     return FLAG_PENALISE_SPEED; 
}                               

switch (FEAT_TRAINS, SELF, vl61_bitmask_vehicle_info_advanced,
  cargo_subtype == 1)
{
  1: vl61_bitmask_vehicle_info_advanced1;
     vl61_bitmask_vehicle_info;
}

general_running_cost(vl61_v1, 278)
general_running_cost(vl61d, 350)

switch (FEAT_TRAINS, SELF, vl61_running_cost_factor,
  cargo_subtype == 1)
{
  0: vl61_v1_running_cost_factor;
     vl61d_running_cost_factor;
}

engine_electric1_2_effect(vl61, -2, 4)

item (FEAT_TRAINS, vl61, 189) {
  property {
    name: string(STR_NAME_VL61);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1954), 1, 1);
    vehicle_life: 20;
    model_life: get_model_life(1954,1957);
    retire_early: get_retire_early(1954,1957);
    reliability_decay: 10;
    cost_factor: 39;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 75 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dual_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 131 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(431.0, 131);
    length: 8;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl61_sprites;
    purchase: vl61_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: vl61_additional_text;
    speed: vl61_speed;
    purchase_speed: vl61_purchase_speed;
    bitmask_vehicle_info: vl61_bitmask_vehicle_info_advanced;
    cargo_capacity: return 0;
    cargo_subtype_text: vl61_cargo_subtype_text;
    refit_cost: vl61_refit_cost;
    power: vl61_power;
    create_effect: vl61_create_effect;
    running_cost_factor: vl61_running_cost_factor;
  }
}

long_name_template(vl61, STR_LONGNAME_VL61)
models_default_cargo_template(vl61)
allow_acelectric(vl61)
