 // vl61

// Спрайты

#define IMAGEFILE  "src/electric/vl61-vl61d.png"

// покупка
purchase_spriteset(vl61, 666, 0)

// нет тока
spriteset (vl61_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 85, 0)
} 

spriteset (vl61_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 152, 0)
} 

spritegroup vl61_spritegroup
{
  loading: vl61_loading_spriteset;
  loaded: vl61_traveling_spriteset;
}

spriteset (vl61_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(95, 219, 0)
} 

// 2: покупка
purchase_spriteset(vl61_v2, 666, 20)

// нет тока
spriteset (vl61_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 311, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 378, 0)
} 

spriteset (vl61_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 445, 0)
} 

spritegroup vl61_v2_spritegroup
{
  loading: vl61_v2_loading_spriteset;
  loaded: vl61_v2_traveling_spriteset;
}

spriteset (vl61_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(95, 512, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl61_sprites_2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 3) )
{
  0: vl61_notpowered_spriteset;
  1: vl61_spritegroup;
  2: vl61_speedoverTH_spriteset;
  3: vl61_v2_notpowered_spriteset;
  4: vl61_v2_spritegroup;
  5: vl61_v2_speedoverTH_spriteset;
     align_10_spriteset; // параллелепипед
}

long_vehicle(vl61, )

// покупка
switch (FEAT_TRAINS, SELF, vl61_purchase_sprites,
  (current_year >= 1972) )
{
  0: vl61_purchase_spriteset;
     vl61_v2_purchase_spriteset;
}

engine_speed_change_after(vl61, 75, 85, 1960)

engine_penalise_speed0(vl61)

item (FEAT_TRAINS, vl61) 
{
  property
  {
    name: string(STR_VL61_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1954), 1, 1);
    vehicle_life: 10; // срок службы
    model_life: get_model_life(1954,1957);
    retire_early: get_retire_early(1954,1957); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 60;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 75 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 131 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(163.0, 131);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: vl61_sprites;
    purchase: vl61_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL61_PURCHASE_HINT);
    speed: vl61_speed;
    purchase_speed: vl61_purchase_speed;
    bitmask_vehicle_info: vl61_bitmask_vehicle_info;
    cargo_capacity: return 0;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl61)
  {
    property
    { name: string(STR_VL61_NAME_LONG); }
  }
}

