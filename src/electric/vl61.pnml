 // vl61

// Спрайты

#define IMAGEFILE  "src/electric/vl61-vl61d.png"

// покупка
spriteset (vl61_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(666, 0) }
spriteset (vl61_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(666, 0) }
switch (FEAT_TRAINS, SELF, vl61_purchase_spriteset, 
  enable_icons)
{  1: vl61_purchase_spriteset_ico;
      vl61_purchase_spriteset_noico;}

// нет тока
spriteset (vl61_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 85, 0)
} 

spriteset (vl61_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 152, 0)
} 

spritegroup vl61_spritegroup
{
  loading: vl61_loading_spriteset;
  loaded: vl61_traveling_spriteset;
}

spriteset (vl61_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(95, 219, 0)
} 

// 2: покупка
spriteset (vl61_v2_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(666, 20) }
spriteset (vl61_v2_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(666, 20) }
switch (FEAT_TRAINS, SELF, vl61_v2_purchase_spriteset, 
  enable_icons)
{  1: vl61_v2_purchase_spriteset_ico;
      vl61_v2_purchase_spriteset_noico;}

// нет тока
spriteset (vl61_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 311, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 378, 0)
} 

spriteset (vl61_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 445, 0)
} 

spritegroup vl61_v2_spritegroup
{
  loading: vl61_v2_loading_spriteset;
  loaded: vl61_v2_traveling_spriteset;
}

spriteset (vl61_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s10_template(95, 512, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl61_get_spriteset2,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) * 3)
)
{
  0: vl61_notpowered_spriteset;
  1: vl61_spritegroup;
  2: vl61_speedoverTH_spriteset;
  3: vl61_v2_notpowered_spriteset;
  4: vl61_v2_spritegroup;
  5: vl61_v2_speedoverTH_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl61_get_spriteset1,
[ STORE_TEMP(current_speed, 1),
  STORE_TEMP(date_of_last_service, 2)] )
{
  vl61_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl61_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl61_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, vl61_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: vl61_purchase_spriteset;
  vl61_v2_purchase_spriteset;
}

// Макс. скорость: 75 км/ч до 1960, 85 км/ч после
#define MAX_SPEED current_year >= 1960 ? 85 : 75

switch (FEAT_TRAINS, SELF, vl61_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, vl61_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, vl61_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, vl61) 
{
  property
  {
    name: string(STR_VL61_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1954), 1, 1);
    vehicle_life: 10; // срок службы
    model_life: get_model_life(1954,1957);
    retire_early: get_retire_early(1954,1957); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: 60;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 75 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 131 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(163.0, 131);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cargo_capacity: 1;
  }
  graphics {
    default: vl61_get_spriteset;
    purchase: vl61_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL61_PURCHASE_HINT);
    speed: vl61_speed;
    purchase_speed: vl61_purchase_speed;
    bitmask_vehicle_info: vl61_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }
}
