 // vl61

// Спрайты

#define IMAGEFILE  "src/electric/vl61.png"

// покупка
spriteset (vl61_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(5, 105, -4)
}

// обычный: погрузка/перемещение
spriteset (vl61_loading_spriteset, IMAGEFILE)
{
  s10_template(5, 105, 0)
} 

spriteset (vl61_traveling_spriteset, IMAGEFILE)
{
  s10_template(5, 172, 0)
} 

spritegroup vl61_spritegroup
{
  loading: vl61_loading_spriteset;
  loaded: vl61_traveling_spriteset;
}

spriteset (vl61_speedover50_spriteset, IMAGEFILE)
{
  s10_template(5, 239, 0)
} 

// 2: покупка
spriteset (vl61_v2_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(5, 335, -4)
}

// обычный: погрузка/перемещение
spriteset (vl61_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(5, 335, 0)
} 

spriteset (vl61_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(5, 402, 0)
} 

spritegroup vl61_v2_spritegroup
{
  loading: vl61_v2_loading_spriteset;
  loaded: vl61_v2_traveling_spriteset;
}

spriteset (vl61_v2_speedover50_spriteset, IMAGEFILE)
{
  s10_template(5, 469, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl61_get_spriteset2,
  (last_computed_result > threshold_of_speed()) +
  ((current_year >= 1972) * 2)
)
{
  0: vl61_spritegroup;
  1: vl61_speedover50_spriteset;
  2: vl61_v2_spritegroup;
  vl61_v2_speedover50_spriteset;
}

switch (FEAT_TRAINS, PARENT, vl61_get_spriteset1,
  current_speed)
{
  vl61_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl61_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl61_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, vl61_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: vl61_purchase_spriteset;
  vl61_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, vl61_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, vl61_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// меняем скорость: 75 для купленных до 1960, 85 - после
switch (FEAT_TRAINS, SELF, vl61_speed,
  (build_year >= 1960)
)
{
  0: return 75;
  return 85;
}

item (FEAT_TRAINS, vl61) 
{
  property
  {
    name: string(STR_VL61_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1954, 1, 1);
    vehicle_life: 10; // срок службы
    model_life: 10; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
//    refittable_cargo_classes: bitmask(CC_PASSENGERS);
//    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 75 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: ELRL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    dual_headed: 0;
//    cargo_capacity: 1;
    weight: 131 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_ELECTRIC;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.12696681;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, DISABLE_WAGON_POWER);
  }
  graphics {
    default: vl61_get_spriteset;
    purchase: vl61_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: vl61_can_attach_wagon;
    start_stop: vl61_start_stop;
    additional_text: return string(STR_VL61_PURCHASE_HINT);
//    cargo_capacity: return 0;
    speed: vl61_speed;
  }
}
