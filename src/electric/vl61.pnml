 // vl61

// Спрайты

#define IMAGEFILE  "src/electric/vl61-vl61d.png"

// покупка
spriteset (vl61_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(95, 18, -4)
}

// нет тока
spriteset (vl61_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 85, 0)
} 

spriteset (vl61_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 152, 0)
} 

spritegroup vl61_spritegroup
{
  loading: vl61_loading_spriteset;
  loaded: vl61_traveling_spriteset;
}

spriteset (vl61_speedover50_spriteset, IMAGEFILE)
{
  s10_template(95, 219, 0)
} 

// 2: покупка
spriteset (vl61_v2_purchase_spriteset, IMAGEFILE)
{
  s10_purchase_template(95, 311, -4)
}

// нет тока
spriteset (vl61_v2_notpowered_spriteset, IMAGEFILE)
{
  s10_template(95, 311, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl61_v2_loading_spriteset, IMAGEFILE)
{
  s10_template(95, 378, 0)
} 

spriteset (vl61_v2_traveling_spriteset, IMAGEFILE)
{
  s10_template(95, 445, 0)
} 

spritegroup vl61_v2_spritegroup
{
  loading: vl61_v2_loading_spriteset;
  loaded: vl61_v2_traveling_spriteset;
}

spriteset (vl61_v2_speedover50_spriteset, IMAGEFILE)
{
  s10_template(95, 512, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl61_get_spriteset2,
  // поправка на нет тока
  (((last_computed_result > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  ((current_year >= 1972) * 3)
)
{
  0: vl61_notpowered_spriteset;
  1: vl61_spritegroup;
  2: vl61_speedover50_spriteset;
  3: vl61_v2_notpowered_spriteset;
  4: vl61_v2_spritegroup;
  5: vl61_v2_speedover50_spriteset;
  align_10_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, PARENT, vl61_get_spriteset1,
  current_speed)
{
  vl61_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl61_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl61_get_spriteset1;
}

// покупка
switch (FEAT_TRAINS, SELF, vl61_get_purchase_spriteset,
  (current_year >= 1972)
)
{
  0: vl61_purchase_spriteset;
  vl61_v2_purchase_spriteset;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, vl61_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, vl61_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

// меняем скорость: 75 для купленных до 1960, 85 - после
switch (FEAT_TRAINS, SELF, vl61_speed,
  (build_year >= 1960)
)
{
  0: return 75;
  return 85;
}

item (FEAT_TRAINS, vl61) 
{
  property
  {
    name: string(STR_VL61_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1954, 1, 1);
    vehicle_life: 10; // срок службы
    model_life: 10; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cost_factor: get_electric_cost_factor();
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 75 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    track_type: ELRL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2660 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    weight: 131 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: 0.12696681;
    shorten_vehicle: SHORTEN_TO_8_8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl61_get_spriteset;
    purchase: vl61_get_purchase_spriteset;
    articulated_part: articulated_part_dummy2;
    can_attach_wagon: vl61_can_attach_wagon;
    start_stop: vl61_start_stop;
    additional_text: return string(STR_VL61_PURCHASE_HINT);
    speed: vl61_speed;
  }
}
