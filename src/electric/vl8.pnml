 // vl8

// Спрайты
// определены в n8.pnml

switch (FEAT_TRAINS, SELF, vl8_get_spriteset_l,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  (cargo_subtype * 3))
{
  0: n8_v5_notpowered_spriteset;
  1: n8_v5_spritegroup;
  2: n8_v5_speedoverTH_spriteset;
  3: n8_v6_notpowered_spriteset;
  4: n8_v6_spritegroup;
  5: n8_v6_speedoverTH_spriteset;
  align_8_spriteset; // параллелепипед
}

switch (FEAT_TRAINS, SELF, vl8_get_spriteset_r,
    (!(vehicle_is_not_powered || vehicle_is_in_depot)) +
  (cargo_subtype * 2))
{
  0: n8_r_v5_notpowered_spriteset;
  1: n8_r_v5_spriteset;
  2: n8_r_v6_notpowered_spriteset;
  3: n8_r_v6_spriteset;
  align_8_spriteset; // параллелепипед
}

engine_direction_template(vl8) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl8_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl8_get_spriteset_t;
}

switch (FEAT_TRAINS, SELF, vl8_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

engine_speed(vl8, 80)

engine_penalise_speed(vl8, 2)

item (FEAT_TRAINS, vl8) 
{
  property
  {
    name: string(STR_VL8_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1963), 1, 1);
    vehicle_life: 40; // срок службы
    model_life: get_model_life(1963,1967);
    retire_early: get_retire_early(1963,1967); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 47;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2485 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (180/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(148.5, 180/2);
    length: 7;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl8_get_spriteset;
    purchase: n8_v5_purchase_spriteset;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_VL8_PURCHASE_HINT);
    cargo_subtype_text: vl8_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl8_speed;
    purchase_speed: vl8_purchase_speed;
    bitmask_vehicle_info: vl8_bitmask_vehicle_info;
    cost_factor: return 47;
  }
}
