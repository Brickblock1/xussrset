 // vl8

// Спрайты
// определены в n8.pnml

switch (FEAT_TRAINS, SELF, vl8_get_spriteset2,
  ((last_computed_result - count_veh_id(vl8)) % 2) +
  // x км/ч * 0.2778 == value
  ((current_speed > 13) * 2) +  // 50 км/ч --> 13.89
  cargo_subtype * 4
)
{
  0: n8_v5_spritegroup;
  1: n8_r_v5_spriteset;
  2: n8_v5_speedover50_spriteset;
  3: n8_r_v5_spriteset;
  4: n8_v6_spritegroup;
  5: n8_r_v6_spriteset;
  6: n8_v6_speedover50_spriteset;
  n8_r_v6_spriteset;
}

switch (FEAT_TRAINS, PARENT, vl8_get_spriteset1,
  count_veh_id(vl8))
{
  vl8_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl8_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl8_get_spriteset1;
}

// Что присоединяем?
switch (FEAT_TRAINS, SELF, vl8_can_attach_wagon,
  0)
{
  return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, vl8_start_stop,
  0)
{
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, vl8_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_VL8_REFIT_V1);
  1: string(STR_VL8_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, vl8) 
{
  property
  {
    name: string(STR_VL8_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1963, 1, 1);
    vehicle_life: 40; // срок службы
    model_life: 40; // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    refittable_cargo_classes: bitmask(CC_PASSENGERS);
//    loading_speed: 12; // скорость погрузки/выгрузки, пассажиров
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 80 km/h;
//    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: ELRL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4970 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    dual_headed: 0;
    cargo_capacity: 1;
    weight: 180 ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_ELECTRIC;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.16836734693;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, DISABLE_WAGON_POWER);
  }
  graphics {
    default: vl8_get_spriteset;
    purchase: n8_v5_purchase_spriteset;
//    articulated_part: articulated_part_dummy2;
    can_attach_wagon: vl8_can_attach_wagon;
    start_stop: vl8_start_stop;
    additional_text: return string(STR_VL8_PURCHASE_HINT);
    cargo_subtype_text: vl8_cargo_subtype_text;
    cargo_capacity: return 0;
  }
}
