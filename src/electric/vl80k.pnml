 // vl80k

// ВЛ80К "Выло" (1963-1971)

#define IMAGEFILE  "src/electric/vl80k.png"
purchase_sprites(vl80k_v1,   0, 0)
purchase_sprites(vl80k_v2, 100, 0)
purchase_sprites(vl80k_v3, 200, 0)

electric2_1_sprites(9, vl80k_v1, 32,   40)
electric2_1_sprites(9, vl80k_v2, 32,  241)
electric2_1_sprites(9, vl80k_v3, 32,  442)
electric2_1_sprites(9, vl80k_v4, 32,  643)
electric2_1_sprites(9, vl80k_v5, 32,  844)
electric2_1_sprites(9, vl80k_v6, 32, 1045)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl80k_purchase_sprites,
  (current_year >= 1965) +
  (current_year >= 1969))
{
  0: vl80k_v1_purchase_sprites;
  1: vl80k_v2_purchase_sprites;
     vl80k_v3_purchase_sprites;
}   

switch (FEAT_TRAINS, SELF, vl80k_sprites_left,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) +
  ((build_year >= 1966) * 2) +
  ((build_year >= 1969) * 2))
{
  0: vl80k_v1_sprites_left;
  1: vl80k_v2_sprites_left;
  2: vl80k_v3_sprites_left;
  3: vl80k_v4_sprites_left;
  4: vl80k_v5_sprites_left;
  5: vl80k_v6_sprites_left;
     align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl80k_sprites_right,
  (LOAD_TEMP(2) >= date(1972, 1, 1)) +
  ((build_year >= 1966) * 2) +
  ((build_year >= 1969) * 2))
{
  0: vl80k_v1_sprites_right;
  1: vl80k_v2_sprites_right;
  2: vl80k_v3_sprites_right;
  3: vl80k_v4_sprites_right;
  4: vl80k_v5_sprites_right;
  5: vl80k_v6_sprites_right;
     align_9_sprites; 
}  

engine_direction_template(vl80k, ) 
long_vehicle(vl80k)

engine_electric2_1_effect(vl80k, -2, 4)
engine_direction_template(vl80k, _create_effect) 
engine_speed(vl80k, 100)
engine_penalise_speed(vl80k, 2)
engine_power_change_build_after(vl80k, 3915, 
                                       4055, 1966)
engine_TE_change_build_after(vl80k, get_tec_int(319.0, 92), 
                                    get_tec_int(325.0, 92), 1966)
general_running_cost(vl80k, 390)

item (FEAT_TRAINS, vl80k) {
  property {
    name: string(STR_NAME_VL80K);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1963), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1963,1971);
    retire_early: get_retire_early(1963,1971);
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 53;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 100 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT); 
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: (7830/2) hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (184/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(371.0, 184);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl80k_sprites;
    purchase: vl80k_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_AC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1963, 1971), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
    cargo_capacity: return 0;
    speed: vl80k_speed;
    power: vl80k_power;
    tractive_effort_coefficient: vl80k_tractive_effort_coefficient;
    purchase_speed: vl80k_purchase_speed;
    bitmask_vehicle_info: vl80k_bitmask_vehicle_info;
    create_effect: vl80k_create_effect_sprites_start;
    running_cost_factor: vl80k_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl80k) {
    property { name: string(STR_LONGNAME_VL80K); }
  }
}

if (models_default_cargo == 1) {
  item (FEAT_TRAINS, vl80k) {
    property {
      cargo_allow_refit: [GOOD];
      default_cargo_type: GOOD;
    }
  }
}

if (models_default_cargo == 2) {
  item (FEAT_TRAINS, vl80k) {
    property {
      cargo_allow_refit: [PASS];
      default_cargo_type: PASS;
    }
  }
}

if (models_default_cargo == 3) {
  item (FEAT_TRAINS, vl80k) {
    property {
      cargo_allow_refit: [MAIL];
      default_cargo_type: MAIL;
    }
  }
}

