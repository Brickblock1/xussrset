 // vl80s

// ВЛ80с "Выло" (1979-1995-2003)

#define IMAGEFILE  "src/electric/vl80s.png"

// покупка
purchase_spriteset(vl80s, 0, 0)

// нет тока
spriteset (vl80s_notpowered_spriteset, IMAGEFILE)
{
  s9_template(6, 57, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl80s_loading_spriteset, IMAGEFILE)
{
  s9_template(6, 124, 0)
} 

spriteset (vl80s_traveling_spriteset, IMAGEFILE)
{
  s9_template(6, 191, 0)
} 

spritegroup vl80s_spritegroup
{
  loading: vl80s_loading_spriteset;
  loaded: vl80s_traveling_spriteset;
}

spriteset (vl80s_speedoverTH_spriteset, IMAGEFILE)
{
  s9_template(6, 258, 0)
} 

// нет тока
spriteset (vl80s_r_notpowered_spriteset, IMAGEFILE)
{
  s9_template(437, 57, 0)
} 

// развёрнутый
spriteset (vl80s_r_spriteset, IMAGEFILE)
{
  s9_template(437, 124, 0)
} 

#undef IMAGEFILE

// расчёт ориентации секций локомотива. Первые 6 ориентируются парами, далее по номеру в составе. 
switch (FEAT_TRAINS, SELF, vl80s_get_spriteset_l,
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) )
{ 0: vl80s_notpowered_spriteset;
  1: vl80s_spritegroup;
     vl80s_speedoverTH_spriteset;}

switch (FEAT_TRAINS, SELF, vl80s_get_spriteset_r,
  ((!(vehicle_is_not_powered || vehicle_is_in_depot))) )
{ 0: vl80s_r_notpowered_spriteset;
     vl80s_r_spriteset;}

engine_direction_template(vl80s) 

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, vl80s_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  vl80s_get_spriteset_t;
}


engine_speed_with_reduce(vl80s, 110, 100, 30)

engine_penalise_speed(vl80s, 4)

item (FEAT_TRAINS, vl80s) 
{
  property
  {
    name: string(STR_VL80S_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1979), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(1979,2003);
    retire_early: get_retire_early(1979,2003); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 70;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 110 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 4055 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 92 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(201.0, 92);
    length: 8;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl80s_get_spriteset;
    purchase: vl80s_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_VL80S_PURCHASE_HINT);
    cargo_capacity: return 0;
    speed: vl80s_speed;
    purchase_speed: vl80s_purchase_speed;
    bitmask_vehicle_info: vl80s_bitmask_vehicle_info;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl80s)
  {
    property
    { name: string(STR_VL80S_NAME_LONG); }
  }
}

