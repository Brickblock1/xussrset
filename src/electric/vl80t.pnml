 // vl80t

// ВЛ80Т "Выло" (1969-1980) - усиленная модификация ВЛ10 с повышенной
// нагрузкой на ось для большего сцепного веса

#define IMAGEFILE  "src/electric/vl80t.png"
purchase_sprites(vl80t_v1,   0, 0)
purchase_sprites(vl80t_v2, 100, 0)
purchase_sprites(vl80t_v3, 200, 0)

electric2_1_sprites(9, vl80t_v1, 32,  40)
electric2_1_sprites(9, vl80t_v2, 32, 241)
electric2_1_sprites(9, vl80t_v3, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl80t_purchase_sprites,
  (current_year >= 1972) +
  (current_year >= 1978))
{
  0: vl80t_v1_purchase_sprites;
  1: vl80t_v2_purchase_sprites;
     vl80t_v3_purchase_sprites;
}   

switch (FEAT_TRAINS, SELF, vl80t_sprites_left,
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && build_year < 1978) +
  ((build_year >= 1978) * 2))
{
  0: vl80t_v1_sprites_left;
  1: vl80t_v2_sprites_left;
  2: vl80t_v3_sprites_left;
     align_9_sprites; 
}
switch (FEAT_TRAINS, SELF, vl80t_sprites_right,
  ((LOAD_TEMP(2) >= date(1972, 1, 1)) && build_year < 1978)  +
  ((build_year >= 1978) * 2))
{
  0: vl80t_v1_sprites_right;
  1: vl80t_v2_sprites_right;
  2: vl80t_v3_sprites_right;
     align_9_sprites; 
}
   
engine_direction_template(vl80t, ) 
long_vehicle(vl80t)

engine_electric2_1_effect(vl80t, -2, 4)
engine_direction_template(vl80t, _create_effect) 
engine_speed_with_reduce(vl80t, 110, 
                                100, 30)
engine_penalise_speed(vl80t, 2)
engine_RC_change_build_after(vl80t, 375,
                                    372, 1971)
engine_TE_change_build_after(vl80t, get_tec_int(330.0, 95), 
                                    get_tec_int(324.6, 92), 1971)

engine_weight_change_build_after(vl80t, 94.5,
                                        92, 1971)

item (FEAT_TRAINS, vl80t) {
  property {
    name: string(STR_NAME_VL80T);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1967), 1, 1);
    model_life: get_model_life(1967,1980);
    retire_early: get_retire_early(1967,1980);
    vehicle_life: 30;
    reliability_decay: 10;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cost_factor: 53;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    speed: 110 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT); 
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_CARGO;
    power: 4055 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 94.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(330.0, 94.5);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl80t_sprites;
    purchase: vl80t_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_ELECTRIC_AC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_2), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1967, 1980), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK));
    cargo_capacity: return 0;
    speed: vl80t_speed;
    tractive_effort_coefficient: vl80t_tractive_effort_coefficient;
    weight: vl80t_weight;
    purchase_speed: vl80t_purchase_speed;
    bitmask_vehicle_info: vl80t_bitmask_vehicle_info;
    create_effect: vl80t_create_effect_sprites_start;
    running_cost_factor: vl80t_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl80t) {
    property { name: string(STR_LONGNAME_VL80T); }
  }
}

if (models_default_cargo == 1) {
  item (FEAT_TRAINS, vl80t) {
    property {
      cargo_allow_refit: [GOOD];
      default_cargo_type: GOOD;
    }
  }
}

if (models_default_cargo == 2) {
  item (FEAT_TRAINS, vl80t) {
    property {
      cargo_allow_refit: [PASS];
      default_cargo_type: PASS;
    }
  }
}

if (models_default_cargo == 3) {
  item (FEAT_TRAINS, vl80t) {
    property {
      cargo_allow_refit: [MAIL];
      default_cargo_type: MAIL;
    }
  }
}

