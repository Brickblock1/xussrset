 // vl82m

#define IMAGEFILE  "src/electric/vl82m.png"

purchase_sprites(vl82m_v1, 0, 0)
purchase_sprites(vl82m_v2, 100, 0)

electric2_1_sprites(9, vl82m_v1, 32,  40)
electric2_1_sprites(9, vl82m_v2, 32, 241)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl82m_purchase_sprites,
  (current_year >= 1978))
{
  0: vl82m_v1_purchase_sprites;
     vl82m_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, vl82m_sprites_left,
  build_year >= 1978 )
{
  0: vl82m_v1_sprites_left;
  1: vl82m_v2_sprites_left;
     align_9_sprites; 
}

switch (FEAT_TRAINS, SELF, vl82m_sprites_right,
  build_year >= 1978 )
{
  0: vl82m_v1_sprites_right;
  1: vl82m_v2_sprites_right;
     align_9_sprites; 
}

engine_direction_template(vl82m, ) 
long_vehicle(vl82m)

switch (FEAT_TRAINS, SELF, vl82m_colour_mapping,
  cargo_subtype)
{
  0: return vl11_cc + 0;
  1: return vl11_cc + 5;
  2: return vl11_cc + 3;
  3: return vl11_cc + 7;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, vl82m_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_62);
  2: return string(STR_REFIT_TTD_9D);
  3: return string(STR_REFIT_TTD_94);
     return CB_RESULT_NO_TEXT;
}

engine_electric2_1_effect(vl82m, -2, 3)
engine_direction_template(vl82m, _create_effect) 
engine_speed(vl82m, 110)
engine_penalise_speed(vl82m, 3)
general_running_cost(vl82m, 476)

item (FEAT_TRAINS, vl82m) {
  property {
    name: string(STR_VL82M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1972), 1, 1);
    vehicle_life: 30;
    model_life: get_model_life(1972,1979);
    retire_early: get_retire_early(1972,1979);
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 68;
    sprite_id: SPRITE_ID_NEW_TRAIN; 
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dual_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3820 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: 100 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(196.0, 100.0);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: vl82m_sprites;
    purchase: vl82m_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_FREIGHT_ELECTRIC_ACDC), 
                                   string(STR_PURCHASE_HINT_SECTIONS_2_4), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1972, 1979), 
                                   string(STR_PURCHASE_HINT_FACTORY_NOVOCHERKASSK_ELP));
    cargo_capacity: return 0;
    speed: vl82m_speed;
    purchase_speed: vl82m_purchase_speed;
    bitmask_vehicle_info: vl82m_bitmask_vehicle_info;
    cargo_subtype_text: vl82m_cargo_subtype_text;
    colour_mapping: vl82m_colour_mapping;
    create_effect: vl82m_create_effect_sprites_start;
    running_cost_factor: vl82m_running_cost_factor;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, vl82m) {
    property { name: string(STR_VL82M_NAME_LONG); }
  }
}

