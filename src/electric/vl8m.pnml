 // vl8m

// Спрайты

#define IMAGEFILE  "src/electric/vl8m.png"

// покупка
purchase_sprites(vl8m, 0, 0)

// нет тока
spriteset (vl8m_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 18, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl8m_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 53, 0)
} 

spriteset (vl8m_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 88, 0)
} 

spritegroup vl8m_spritegroup
{
  loading: vl8m_loading_spriteset;
  loaded: vl8m_traveling_spriteset;
}

spriteset (vl8m_speedoverTH_spriteset, IMAGEFILE)
{                  
  s8_template(4, 123, 0)
} 

// нет тока
spriteset (vl8m_r_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 18, 0)
} 

// развёрнутый
spriteset (vl8m_r_spriteset, IMAGEFILE)
{
  s8_template(291, 53, 0)
} 

// нет тока
spriteset (vl8m_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(4, 163, 0)
} 

// обычный: погрузка/перемещение
spriteset (vl8m_v2_loading_spriteset, IMAGEFILE)
{
  s8_template(4, 198, 0)
} 

spriteset (vl8m_v2_traveling_spriteset, IMAGEFILE)
{
  s8_template(4, 233, 0)
} 

spritegroup vl8m_v2_spritegroup
{
  loading: vl8m_v2_loading_spriteset;
  loaded: vl8m_v2_traveling_spriteset;
}

spriteset (vl8m_v2_speedoverTH_spriteset, IMAGEFILE)
{
  s8_template(4, 268, 0)
} 

// нет тока
spriteset (vl8m_r_v2_notpowered_spriteset, IMAGEFILE)
{
  s8_template(291, 163, 0)
} 

// развёрнутый
spriteset (vl8m_r_v2_spriteset, IMAGEFILE)
{
  s8_template(291, 198, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, vl8m_sprites_l,
  // поправка на нет тока
  (((LOAD_TEMP(1) > threshold_of_speed()) + 1) *
    (!(vehicle_is_not_powered || vehicle_is_in_depot))) +
  (cargo_subtype * 3))
{
  0: vl8m_notpowered_spriteset;
  1: vl8m_spritegroup;
  2: vl8m_speedoverTH_spriteset;
  3: vl8m_v2_notpowered_spriteset;
  4: vl8m_v2_spritegroup;
  5: vl8m_v2_speedoverTH_spriteset;
  align_8_sprites; 
}

switch (FEAT_TRAINS, SELF, vl8m_sprites_r,
    (!(vehicle_is_not_powered || vehicle_is_in_depot)) +
  (cargo_subtype * 2))
{
  0: vl8m_r_notpowered_spriteset;
  1: vl8m_r_spriteset;
  2: vl8m_r_v2_notpowered_spriteset;
  3: vl8m_r_v2_spriteset;
  align_8_sprites; 
}

engine_direction_template(vl8m) 

long_vehicle(vl8m)

switch (FEAT_TRAINS, SELF, vl8m_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_SOUTHERN_RAILWAY);
  1: string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}

engine_speed_with_reduce(vl8m, 95, 90, 15)

engine_penalise_speed(vl8m, 2)

item (FEAT_TRAINS, vl8m) 
{
  property
  {
    name: string(STR_VL8M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1976), 1, 1);
    vehicle_life: 15;
    model_life: get_model_life(1976,1985);
    retire_early: get_retire_early(1976,1985); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    cost_factor: 71;
    running_cost_factor: get_electric_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 95 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2485 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 1;
    weight: (180/2) ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(148.5, 180/2);
    length: 7;
    visual_effect_and_powered: electric_visual_effect_and_powered(0);
  }
  graphics {
    default: vl8m_sprites;
    purchase: vl8m_purchase_sprites;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_VL8M_PURCHASE_HINT);
    cargo_subtype_text: vl8m_cargo_subtype_text;
    cargo_capacity: return 0;
    speed: vl8m_speed;
    purchase_speed: vl8m_purchase_speed;
    bitmask_vehicle_info: vl8m_bitmask_vehicle_info;
    cost_factor: return 71;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, vl8m)
  {
    property
    { name: string(STR_VL8M_NAME_LONG); }
  }
}

