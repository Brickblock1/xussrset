 // epr
// 226 (E2h)

#define IMAGEFILE  "src/emu/epr.png"
purchase_sprites(epr, 0, 0)

MU_4r_0_0_sprites(11, epr,  32,  40)
MU_0_4_0_sprites(10, epr_1, 32,  40)
MU_0_4_0_sprites(10, epr_2, 32, 576)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, epr_h_sprites_left_check,
  position_in_consist == 0)
{
  1: epr_h_sprites_left;
     epr_h_notpowered_normal_sprites_left;
}

random_switch (FEAT_TRAINS, SELF, epr_a_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: epr_h_sprites_left_check;
  1: epr_h_sprites_left_check;
}

switch (FEAT_TRAINS, SELF, epr_b_sprites_start,
[  set_offset_to(-2),
   ((LOAD_TEMP(4) != epr) * 2) +
   (prev_vehicle_randombits() % 2)])
{
  0: epr_1_m_notpowered_normal_sprites_left;
  1: epr_2_m_notpowered_normal_sprites_right;
  2: epr_1_m_notpowered_sprites_left;
  3: epr_2_m_notpowered_sprites_right;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epr_c_sprites_start,
[  set_offset_to(-4),
   ((LOAD_TEMP(4) != epr) * 2) +
   (prev_vehicle_randombits() % 2)])
{
  0: epr_2_m_sprites_left;
  1: epr_1_m_sprites_right;
  2: epr_2_m_notpowered_sprites_left;
  3: epr_1_m_notpowered_sprites_right;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epr_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: epr_h_sprites_right;
     epr_h_notpowered_normal_sprites_right;
}

switch (FEAT_TRAINS, SELF, epr_never_train, 0)
{
  1: epr_1_m_sprites_left;
  2: epr_1_m_normal_sprites;
  3: epr_2_m_sprites_right;
  4: epr_2_m_r_normal_sprites;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epr_d_sprites_start,
  LOAD_TEMP(4) == epr)
{
   1: epr_h_sprites_right_check;
   0: epr_h_notpowered_sprites_right;
      epr_never_train;
}

switch (FEAT_TRAINS, SELF, epr_sprites_start, 
  position_in_articulated_veh)    
{ 
  0: epr_a_sprites_start;
  2: epr_b_sprites_start;                                        
  4: epr_c_sprites_start;                                        
  6: epr_d_sprites_start;                                        
     dummy_sprites; 
}                                           

long_vehicle_RND(epr, epr_a_sprites_start)

// Можно ли ещё присоединить (1..3 секции)
switch (FEAT_TRAINS, SELF, epr_can_attach_wagon2,
  LOAD_TEMP(0) < 3)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

// считаем уже имеющиеся вагоны

switch (FEAT_TRAINS, PARENT, epr_can_attach_wagon1,
  STORE_TEMP(count_veh_id(epr) + count_veh_id(epg), 0))
{
  epr_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, epr_can_attach_wagon,
  (vehicle_type_id == epr) || (vehicle_type_id == epg))
{
  1: epr_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, epr_articulated_part,
  extra_callback_info1)
{
  1: return dummy3;
  2: return epr_part2;
  3: return dummy2;
  4: return epr_part2;
  5: return dummy2;
  6: return epr_part2;
  7: return dummy3;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// даём тягу, если локомотив epr
switch (FEAT_TRAINS, PARENT, epr_power,
  (vehicle_type_id == epr) || (vehicle_type_id == epg))
{
  1: return get_power_in_cb(2719);
     return 0;
}

// показываем визуальные эффекты, если локомотив epr
switch (FEAT_TRAINS, SELF, epr_c_create_effect1, 
  electric_create_effect(4)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, PARENT, epr_c_create_effect,
  (vehicle_type_id == epr) || (vehicle_type_id == epg))
{
  1: epr_c_create_effect1;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, epr_create_effect, 
  position_in_articulated_veh)    
{ 
  4: epr_c_create_effect;                                        
     return disable_create_effect();
}                                           

// Допускается эксплуатация с пассажирами только если голова - epr
switch (FEAT_TRAINS, PARENT, epr_cargo_capacity,
  (vehicle_type_id == epr) || (vehicle_type_id == epg))
{
  1: return 217;
     return 0;
}

emu_running_cost(epr_v1, 436 * 2, 186 * 2)

switch (FEAT_TRAINS, PARENT, epr_running_cost_factor,
  (vehicle_type_id == epr) || (vehicle_type_id == epg))
{
  1: epr_v1_running_cost_factor;
     return RC_MU_AS_CAR * 4;
}

engine_speed(epr, 160)

item (FEAT_TRAINS, epr, 226) {
  property {
    name: string(STR_NAME_EPR);
    vehicle_dates(2011, 2050, 30, 10)
    vehicle_acemu(2719, 132, 200.0, 216, 30*6)
    cost_factor: 255;
    length: 8;
  }
  graphics {
    default: epr_sprites;
    purchase: epr_purchase_sprites;
    speed: epr_speed;
    purchase_speed: epr_purchase_speed;
    cargo_age_period: return 90;
    running_cost_factor: epr_running_cost_factor;
    articulated_part: epr_articulated_part;
    can_attach_wagon: epr_can_attach_wagon;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_2_2),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_SR_AC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_FLIRT)),
                                   string(STR_SECTIONS_MU_S1_3of4),
                                   string(STR_PURCHASE_HINT_FROM, 2011),
                                   string(STR_PURCHASE_HINT_FACTORY_BUSSNANG));
    power: epr_power;
    cost_factor: return 276;
    cargo_capacity: epr_cargo_capacity;
    create_effect: epr_create_effect;
  }
}

long_name_template(epr, STR_LONGNAME_EPR)
allow_acemu(epr)

item (FEAT_TRAINS, epr_part2, 456) {
  property {
    vehicle_no_dates(2011, 2050, 30, 10)
    vehicle_acemu(0, 1, 0, 0, 0)
    length: 8;
  }
  graphics {
    default: epr_sprites;
    create_effect: epr_create_effect;
  }
}
