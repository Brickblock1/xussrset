 // er1

// Спрайты
// 2 окраски, только головной с 1972 имеет замену на
// др. 2 окраски

#define IMAGEFILE  "src/emu/er1.png"
purchase_sprites(er1_h, 0, 0)
purchase_sprites(er1_m, 120, 0)
purchase_sprites(er1_c, 240, 0)

emu_3r_0_0_sprites(13, er1_v1, 32, 40)
emu_0_3_0_sprites(13, er1_v1, 32, 40)
emu_0_0_2_sprites(13, er1_v1, 32, 40)
emu_3r_0_0_sprites(13, er1_v1u, 32, 442)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/er1-gr.png"
emu_3r_0_0_sprites(13, er1_v2, 32, 40)
emu_0_3_0_sprites(13, er1_v2, 32, 40)
emu_0_0_2_sprites(13, er1_v2, 32, 40)
emu_3r_0_0_sprites(13, er1_v2u, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, er1_h_sprites_l,
  cargo_subtype +
  ((LOAD_TEMP(3) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_h_sprites_left;
    1: er1_v2_h_sprites_left;
  100: er1_v1_h_notpowered_sprites_left;
  101: er1_v2_h_notpowered_sprites_left;
  200: er1_v1u_h_sprites_left;
  201: er1_v2u_h_sprites_left;
  300: er1_v1u_h_notpowered_sprites_left;
  301: er1_v2u_h_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er1_h_sprites_r,
  cargo_subtype +
  ((LOAD_TEMP(3) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_h_sprites_right;
    1: er1_v2_h_sprites_right;
  100: er1_v1_h_notpowered_sprites_right;
  101: er1_v2_h_notpowered_sprites_right;
  200: er1_v1u_h_sprites_right;
  201: er1_v2u_h_sprites_right;
  300: er1_v1u_h_notpowered_sprites_right;
  301: er1_v2u_h_notpowered_sprites_right;
       align_12_sprites;
}

engine_direction_template3m2(er1_h, er1_m, er1_c)

long_vehicle(er1_h)

switch (FEAT_TRAINS, SELF, er1_m_sprites_start,
  ((count_veh_id(er1_m) * 2) <= LOAD_TEMP(0)) +
  (cargo_subtype * 2) +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_m_sprites_left;
    1: er1_v1_m_sprites_right;
    2: er1_v2_m_sprites_left;
    3: er1_v2_m_sprites_right;
  100: er1_v1_m_notpowered_sprites_left;
  101: er1_v1_m_notpowered_sprites_right;
  102: er1_v2_m_notpowered_sprites_left;
  103: er1_v2_m_notpowered_sprites_right;
       align_12_sprites;
}

long_vehicle(er1_m)

switch (FEAT_TRAINS, SELF, er1_c_sprites_start,
  ((count_veh_id(er1_c) * 2) <= LOAD_TEMP(0)) +
  (cargo_subtype * 2)  +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_c_sprites_left;
    1: er1_v1_c_sprites_right;
    2: er1_v2_c_sprites_left;
    3: er1_v2_c_sprites_right;
  100: er1_v1_c_notpowered_sprites_left;
  101: er1_v1_c_notpowered_sprites_right;
  102: er1_v2_c_notpowered_sprites_left;
  103: er1_v2_c_notpowered_sprites_right;
       align_12_sprites;
}

long_vehicle(er1_c)

switch (FEAT_TRAINS, PARENT, er1_h_can_attach_wagon3, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er1_c: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_CAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_h_can_attach_wagon2, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er1_h: return string(STR_CAN_ATTACH_CAR_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er1_h_can_attach_wagon, 
  (count_veh_id(er1_h) > 1) ? 0 : 
  (((count_veh_id(er1_h) + count_veh_id(er1_m) + count_veh_id(er1_c)) < 3) ||
  ((count_veh_id(er1_h) + count_veh_id(er1_m) + count_veh_id(er1_c)) % 2 == 0 ) ) ? 1 : (2 + count_veh_id(er1_m)) )
{
     0: return string(STR_CAN_ATTACH_NO_MORE_THAN_2_HEAD);
     1: return string(STR_CAN_ATTACH_CAR_ONLY);
     2: er1_h_can_attach_wagon2;
  6..8: er1_h_can_attach_wagon3;
        return CB_RESULT_ATTACH_ALLOW; 
}

switch (FEAT_TRAINS, PARENT, er1_m_can_attach_wagon, 
  ((count_veh_id(er1_m) - count_veh_id(er1_c) - count_veh_id(er1_h)) < 1 ) * (1 + count_veh_id(er1_m)))
{
  0: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
     return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon4, 
[  set_offset_to(num_vehs_in_consist-8),
   prev_vehicle_type_id()])
{
  er1_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon3, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er1_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon2, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er1_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon1, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er1_c: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_HEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon0, 
  ((count_veh_id(er1_m) - count_veh_id(er1_c) - count_veh_id(er1_h)) >= 0 ) * (count_veh_id(er1_m)))
{
  0: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
  1: return CB_RESULT_ATTACH_ALLOW;
  2: er1_c_can_attach_wagon1;
  3: er1_c_can_attach_wagon2;
  4: er1_c_can_attach_wagon3;
  5: er1_c_can_attach_wagon4;
     return string(STR_CAN_ATTACH_HEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_c_can_attach_wagon, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er1_m: er1_c_can_attach_wagon0;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}


switch (FEAT_TRAINS, SELF, er1_can_attach_wagon2,
  vehicle_type_id)
{
  er1_h: er1_h_can_attach_wagon;
  er1_m: er1_m_can_attach_wagon;
  er1_c: er1_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_can_attach_wagon1,
  (count_veh_id(er1_h) > 1) ? 0 : 
  (((count_veh_id(er1_h) + count_veh_id(er1_m) + count_veh_id(er1_c)) >= 9) && (LOAD_TEMP(0) != er1_h)) ? 1 : 2)
{
  0: return string(STR_CAN_ATTACH_NO_AFTER_REAR_HEAD);
  1: return string(STR_CAN_ATTACH_HEAD_ONLY);
     er1_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, er1_can_attach_wagon,
  STORE_TEMP(vehicle_type_id, 0))
{
  er1_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, er1_h_start_stop,
  count_veh_id(er1_h))
{
  2: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

// Моторный самостоятельно выпускать из депо нельзя
switch (FEAT_TRAINS, SELF, er1_m_start_stop,
  0)
{
     return string(STR_START_STOP_MOTORIZED_CAR_CANNOT_LEAVE_DEPOT);
}

// Визуализируем эффекты тяги у er1_m, только если локомотив - er1_h
switch (FEAT_TRAINS, SELF, er1_m_visual_effect_and_powered2,
  (count_veh_id(er1_m) * 2) <= LOAD_TEMP(0) )
{
  0: return electric_visual_effect_and_powered(-3);
     return electric_visual_effect_and_powered(5);
}

switch (FEAT_TRAINS, PARENT, er1_m_visual_effect_and_powered1,
[ STORE_TEMP(count_veh_id(er1_m), 0) ])                                                          
{
  er1_m_visual_effect_and_powered2;
}

switch (FEAT_TRAINS, PARENT, er1_m_visual_effect_and_powered,
  vehicle_type_id == er1_h)
{
  1: er1_m_visual_effect_and_powered1;
     return disable_visual_effect_and_powered();
}

// Даём тягу, только если локомотив - er1_h
switch (FEAT_TRAINS, PARENT, er1_m_power,
  vehicle_type_id == er1_h)
{
  1: return get_power_in_cb(853);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - er1_h
switch (FEAT_TRAINS, PARENT, er1_h_cargo_capacity,
  (vehicle_type_id == er1_h)
)
{
  1: return 88;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er1_m_cargo_capacity,
  (vehicle_type_id == er1_h) 
)
{
  1: return 110;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er1_c_cargo_capacity,
  (vehicle_type_id == er1_h) 
)
{
  1: return 110;
     return 0;
}

switch (FEAT_TRAINS, SELF, er1_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_6211, string(STR_REFIT_TTD_53));
  1: string(STR_REFIT_FACTORY_6211, string(STR_REFIT_TTD_59));
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, er1_h) 
{
  property
  {
    name: string(STR_ER1_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 130 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 88;
    weight: 38.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: er1_h_sprites;
    purchase: er1_h_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: er1_h_start_stop;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    additional_text: return string(STR_ER1_H_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    power: return 0;
    cargo_capacity: er1_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, er1_m) 
{
  property
  {
    name: string(STR_ER1_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 853 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 110;
    weight: 52.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(35.0, 52.5);
    length: 8;
  }
  graphics {
    default: er1_m_sprites;
    purchase: er1_m_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: er1_m_start_stop;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    additional_text: return string(STR_ER1_M_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    visual_effect_and_powered: er1_m_visual_effect_and_powered;
    power: er1_m_power;
    cargo_capacity: er1_m_cargo_capacity;
  }
}

item (FEAT_TRAINS, er1_c) 
{
  property
  {
    name: string(STR_ER1_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 110;
    weight: 36.9 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: er1_c_sprites;
    purchase: er1_c_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_ER1_C_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    power: return 0;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_capacity: er1_c_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, er1_h)
  {
    property
    { name: string(STR_ER1_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, er1_m)
  {
    property
    { name: string(STR_ER1_M_NAME_LONG); }
  }
  item (FEAT_TRAINS, er1_c)
  {
    property
    { name: string(STR_ER1_C_NAME_LONG); }
  }
}

