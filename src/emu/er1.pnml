 // er1

// 2 окраски, только головной с 1972 имеет замену на
// др. 2 окраски

#define IMAGEFILE  "src/emu/er1.png"
purchase_sprites(er1_h, 0, 0)
purchase_sprites(er1_m, 120, 0)
purchase_sprites(er1_c, 240, 0)

MU_3r_0_0_sprites(12, er1_v1, 32, 40)
MU_0_3_0_sprites(12, er1_v1, 32, 40)
MU_0_0_2_sprites(12, er1_v1, 32, 40)
MU_3r_0_0_sprites(12, er1_v1u, 32, 442)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/er1-gr.png"
MU_3r_0_0_sprites(12, er1_v2, 32, 40)
MU_0_3_0_sprites(12, er1_v2, 32, 40)
MU_0_0_2_sprites(12, er1_v2, 32, 40)
MU_3r_0_0_sprites(12, er1_v2u, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, er1_h_sprites_left,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er1_h) * 100) +
  cargo_subtype)
{
    0: er1_v1_h_sprites_left;
    1: er1_v2_h_sprites_left;
  100: er1_v1_h_notpowered_sprites_left;
  101: er1_v2_h_notpowered_sprites_left;
  200: er1_v1u_h_sprites_left;
  201: er1_v2u_h_sprites_left;
  300: er1_v1u_h_notpowered_sprites_left;
  301: er1_v2u_h_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er1_h_sprites_right,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er1_h) * 100) +
  cargo_subtype)
{
    0: er1_v1_h_sprites_right;
    1: er1_v2_h_sprites_right;
  100: er1_v1_h_notpowered_sprites_right;
  101: er1_v2_h_notpowered_sprites_right;
  200: er1_v1u_h_sprites_right;
  201: er1_v2u_h_sprites_right;
  300: er1_v1u_h_notpowered_sprites_right;
  301: er1_v2u_h_notpowered_sprites_right;
       align_12_sprites;
}

engine_direction_template3m2(er1_h, er1_m, er1_c, )

long_vehicle(er1_h)

switch (FEAT_TRAINS, SELF, er1_m_direction_left,
  cargo_subtype +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_m_sprites_left;
    1: er1_v2_m_sprites_left;
  100: er1_v1_m_notpowered_sprites_left;
  101: er1_v2_m_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er1_m_direction_right,
  cargo_subtype +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_m_sprites_right;
    1: er1_v2_m_sprites_right;
  100: er1_v1_m_notpowered_sprites_right;
  101: er1_v2_m_notpowered_sprites_right;
       align_12_sprites;
}

EMU_46789101112_m_template(er1)

EMU_direction_calculation_template(er1, _m)

long_vehicle_dir(er1_m)

switch (FEAT_TRAINS, SELF, er1_c_direction_left,
  cargo_subtype  +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_c_sprites_left;
    1: er1_v2_c_sprites_left;
  100: er1_v1_c_notpowered_sprites_left;
  101: er1_v2_c_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er1_c_direction_right,
  cargo_subtype  +
  ((LOAD_TEMP(4) != er1_h) * 100) )
{
    0: er1_v1_c_sprites_right;
    1: er1_v2_c_sprites_right;
  100: er1_v1_c_notpowered_sprites_right;
  101: er1_v2_c_notpowered_sprites_right;
       align_12_sprites;
}

EMU_46789101112_c_template(er1)

EMU_direction_calculation_template(er1, _c)

long_vehicle_dir(er1_c)

EMU_46810_can_attach_wagon_template(er1)

EMU_attach_calculation_template(er1)

switch (FEAT_TRAINS, SELF, er1_can_attach_wagon1, vehicle_type_id)
{
  er1_h: er1_can_attach_wagon_template;
  er1_m: er1_can_attach_wagon_template;
  er1_c: er1_can_attach_wagon_template;
         return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, er1_can_attach_wagon, count_veh_id(er1_h))
{
  0: return string(STR_CAN_ATTACH_HEAD_ONLY);
  1: er1_can_attach_wagon1;
     return string(STR_CAN_ATTACH_NO_AFTER_REAR_HEAD);
}

switch (FEAT_TRAINS, PARENT, er1_h_start_stop,
  count_veh_id(er1_h))
{
  2: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

// Моторный самостоятельно выпускать из депо нельзя
switch (FEAT_TRAINS, SELF, er1_m_start_stop,
  0)
{
     return string(STR_START_STOP_MOTORIZED_CAR_CANNOT_LEAVE_DEPOT);
}

// Визуализируем эффекты тяги у er1_m, только если локомотив - er1_h

EMU_46789101112_m_create_effect_template(er1, -1, 5)

EMU_direction_calculation_template(er1, _m_create_effect)

switch (FEAT_TRAINS, PARENT, er1_m_create_effect,
  vehicle_type_id == er1_h)
{
  1: er1_m_create_effect_direction_template;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - er1_h
switch (FEAT_TRAINS, PARENT, er1_m_power,
  vehicle_type_id == er1_h)
{
  1: return get_power_in_cb(853);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - er1_h
switch (FEAT_TRAINS, PARENT, er1_h_cargo_capacity,
  (vehicle_type_id == er1_h))
{
  1: return 88;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er1_m_cargo_capacity,
  (vehicle_type_id == er1_h) 
)
{
  1: return 110;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er1_c_cargo_capacity,
  (vehicle_type_id == er1_h) 
)
{
  1: return 110;
     return 0;
}

switch (FEAT_TRAINS, SELF, er1_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY_6211, string(STR_REFIT_TTD_53));
  1: return string(STR_REFIT_FACTORY_6211, string(STR_REFIT_TTD_59));
     return CB_RESULT_NO_TEXT;
}

general_running_cost(er1_m, 78)
general_running_cost(er1_h, 67)
general_running_cost(er1_c, 42)

item (FEAT_TRAINS, er1_h) {
  property {
    name: string(STR_ER1_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: 9;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 130 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 88;
    weight: 38.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_NONE;
  }
  graphics {
    default: er1_h_sprites;
    purchase: er1_h_purchase_sprites;
    running_cost_factor: er1_h_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: er1_h_start_stop;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    additional_text: return string(STR_ER1_H_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    power: return 0;
    cargo_capacity: er1_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, er1_m) {
  property {
    name: string(STR_ER1_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: 11;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 853 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 110;
    weight: 52.5 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(35.0, 52.5);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: er1_m_sprites;
    purchase: er1_m_purchase_sprites;
    running_cost_factor: er1_m_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: er1_m_start_stop;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    additional_text: return string(STR_ER1_M_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    power: er1_m_power;
    cargo_capacity: er1_m_cargo_capacity;
    create_effect: er1_m_create_effect;
  }
}

item (FEAT_TRAINS, er1_c) {
  property {
    name: string(STR_ER1_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1957), 1, 1);
    model_life: get_model_life(1957,1962);
    retire_early: get_retire_early(1957,1962);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: 6;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_dc_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 110;
    weight: 36.9 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_NONE;
  }
  graphics {
    default: er1_c_sprites;
    purchase: er1_c_purchase_sprites;
    running_cost_factor: er1_c_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_ER1_C_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10));
    cargo_subtype_text: er1_cargo_subtype_text;
    speed: speed_after30_130to100;
    purchase_speed: return 130;
    power: return 0;
    can_attach_wagon: er1_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_capacity: er1_c_cargo_capacity;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, er1_h) {
    property { name: string(STR_ER1_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, er1_m) {
    property { name: string(STR_ER1_M_NAME_LONG); }
  }
  item (FEAT_TRAINS, er1_c) {
    property { name: string(STR_ER1_C_NAME_LONG); }
  }
}

