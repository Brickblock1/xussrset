 // er200
// 380 (17Ch) 381 (17Dh) 382 (17Eh)

#define IMAGEFILE  "src/emu/er200.png"
purchase_sprites(er200_h, 0, 0)
purchase_sprites(er200_m, 120, 0)
purchase_sprites(er200_c, 240, 0)

MU_3r_0_0_sprites(15, er200_v1, 32, 40)
MU_0_3_0_sprites(15, er200_v1, 32, 40)
MU_0_0_2_sprites(15, er200_v1, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, er200_h_sprites_left,
  LOAD_TEMP(4) != er200_h)
{
  0: er200_v1_h_sprites_left;
  1: er200_v1_h_notpowered_sprites_left;
     align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er200_h_sprites_right,
  LOAD_TEMP(4) != er200_h)
{
  0: er200_v1_h_sprites_right;
  1: er200_v1_h_notpowered_sprites_right;
     align_15_sprites;
}

engine_direction_template3m2(er200_h, er200_m, er200_c, )

long_vehicle(er200_h)

switch (FEAT_TRAINS, SELF, er200_m_sprites_3,
  ((LOAD_TEMP(0) - count_veh_id(er200_m)) >=
   (LOAD_TEMP(6) - count_veh_id(er200_c))) +
  ((LOAD_TEMP(4) != er200_h) * 100) )
{
    0: er200_v1_m_sprites_left;
    1: er200_v1_m_sprites_right;
  100: er200_v1_m_notpowered_sprites_left;
  101: er200_v1_m_notpowered_sprites_right;
       align_15_sprites;
}

switch (FEAT_TRAINS, PARENT, er200_m_sprites_start,
[ 
  STORE_TEMP(count_veh_id(er200_c), 6)
])                                                          
{ 
  er200_m_sprites_3; 
}                               

long_vehicle(er200_m)

switch (FEAT_TRAINS, SELF, er200_c_sprites_3,
  ((LOAD_TEMP(0) - count_veh_id(er200_c)) >=
   (LOAD_TEMP(6) - count_veh_id(er200_m))) +
  ((LOAD_TEMP(4) != er200_h) * 100) )
{
    0: er200_v1_c_sprites_left;
    1: er200_v1_c_sprites_right;
  100: er200_v1_c_notpowered_sprites_left;
  101: er200_v1_c_notpowered_sprites_right;
       align_15_sprites;
}

switch (FEAT_TRAINS, PARENT, er200_c_sprites_start,
[ 
  STORE_TEMP(count_veh_id(er200_m), 6)
])                                                          
{ 
  er200_c_sprites_3; 
}                               

long_vehicle(er200_c)

switch (FEAT_TRAINS, PARENT, er200_h_can_attach_wagon3, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er200_h: return string(STR_CAN_ATTACH_CAR_ONLY);
           return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er200_h_can_attach_wagon2, 
  ((count_veh_id(er200_h) + count_veh_id(er200_m) + count_veh_id(er200_c)) < 5) ||
  ((count_veh_id(er200_h) + count_veh_id(er200_m) + count_veh_id(er200_c)) % 2 == 0))
{
  1: return string(STR_CAN_ATTACH_CAR_ONLY);
     er200_h_can_attach_wagon3;
}

switch (FEAT_TRAINS, PARENT, er200_h_can_attach_wagon, 
  count_veh_id(er200_h) > 1)
{
  1: return string(STR_CAN_ATTACH_NO_MORE_THAN_2_HEAD);
     er200_h_can_attach_wagon2;
}

switch (FEAT_TRAINS, PARENT, er200_m_can_attach_wagon, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er200_m: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
           return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er200_c_can_attach_wagon, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er200_c: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
           return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, SELF, er200_can_attach_wagon3,
  vehicle_type_id)
{
  er200_h: er200_h_can_attach_wagon;
  er200_m: er200_m_can_attach_wagon;
  er200_c: er200_c_can_attach_wagon;
           return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, er200_can_attach_wagon2,
  ((count_veh_id(er200_h) + count_veh_id(er200_m) + count_veh_id(er200_c)) >= 13) && (LOAD_TEMP(0) != er200_h))
{
  1: return string(STR_CAN_ATTACH_HEAD_ONLY);
     er200_can_attach_wagon3;
}

switch (FEAT_TRAINS, PARENT, er200_can_attach_wagon1,
  count_veh_id(er200_h) > 1)
{
  1: return string(STR_CAN_ATTACH_NO_AFTER_REAR_HEAD);
     er200_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, er200_can_attach_wagon,
  STORE_TEMP(vehicle_type_id, 0))
{
  er200_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, er200_h_start_stop,
  count_veh_id(er200_h))
{
  2: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

// Моторный самостоятельно выпускать из депо нельзя
switch (FEAT_TRAINS, SELF, er200_m_start_stop,
  0)
{
     return string(STR_START_STOP_MOTORIZED_CAR_CANNOT_LEAVE_DEPOT);
}


switch (FEAT_TRAINS, SELF, er200_m_create_effect3, 
  electric_create_effect(-3)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, er200_m_create_effect2, 
  electric_create_effect(8)) 
{ 
  return 1; 
}    

// Визуализируем эффекты тяги у er200_m, только если локомотив - er200_h
switch (FEAT_TRAINS, SELF, er200_m_create_effect1,
  position_in_consist % 4 == 0)
{
  0: er200_m_create_effect2;
     er200_m_create_effect3;
}

switch (FEAT_TRAINS, PARENT, er200_m_create_effect,
  vehicle_type_id == er200_h)
{
  1: er200_m_create_effect1;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - er200_h
switch (FEAT_TRAINS, PARENT, er200_m_power,
  vehicle_type_id == er200_h)
{
  1: return get_power_in_cb(1146); 
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - er200_h
switch (FEAT_TRAINS, PARENT, er200_h_cargo_capacity,
  (vehicle_type_id == er200_h) )
{
  1: return 16;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er200_m_cargo_capacity,
  (vehicle_type_id == er200_h) )
{
  1: return 64;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er200_c_cargo_capacity,
  (vehicle_type_id == er200_h) )
{
  1: return 64;
     return 0;
}

general_running_cost(er200_m_v1, 313)

switch (FEAT_TRAINS, PARENT, er200_m_running_cost_factor,
  vehicle_type_id == er200_h)
{
  1: er200_m_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

general_running_cost(er200_h_v1, 310)

switch (FEAT_TRAINS, PARENT, er200_h_running_cost_factor,
  vehicle_type_id == er200_h)
{
  1: er200_h_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

general_running_cost(er200_c_v1, 279)

switch (FEAT_TRAINS, PARENT, er200_c_running_cost_factor,
  vehicle_type_id == er200_h)
{
  1: er200_c_v1_running_cost_factor;
     return RC_MU_AS_CAR;
}

switch (FEAT_TRAINS, PARENT, er200_h_additional_text,
  current_year >= USSREND)
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_H_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_111)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
     return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_H_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_111)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
}

switch (FEAT_TRAINS, PARENT, er200_m_additional_text,
  current_year >= USSREND)
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_MP_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_112)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
     return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_MP_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_112)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
}

switch (FEAT_TRAINS, PARENT, er200_c_additional_text,
  current_year >= USSREND)
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_MN_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_114)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
     return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_MN_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_114)),
                   string(STR_SECTIONS_EMU_AM6_8_10_12_14),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1992),
                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW_AUSSR));
}

engine_speed(er200, 200)

item (FEAT_TRAINS, er200_h, 380) {
  property {
    name: string(STR_NAME_ER200);
    vehicle_dates(1979, 1992, 30, 10)
    vehicle_dcemu(1, 48.7, 0, 16, 15)
    cost_factor: 36;
    length: 8;
  }
  graphics {
    default: er200_h_sprites;
    purchase: er200_h_purchase_sprites;
    cargo_age_period: return 120;
    running_cost_factor: er200_h_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: er200_can_attach_wagon;
    start_stop: er200_h_start_stop;
    additional_text: er200_h_additional_text;
    speed: er200_speed;
    purchase_speed: er200_purchase_speed;
    power: return 0;
    cargo_capacity: er200_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, er200_m, 381) {
  property {
    name: string(STR_NAME_ER200);
    vehicle_dates(1979, 1992, 30, 10)
    vehicle_dcemu(1146, 58.5, 35.6, 64, 15)
    cost_factor: 40;
    length: 8;
  }
  graphics {
    default: er200_m_sprites;
    purchase: er200_m_purchase_sprites;
    cargo_age_period: return 120;
    running_cost_factor: er200_m_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    can_attach_wagon: er200_can_attach_wagon;
    start_stop: er200_m_start_stop;
    speed: er200_speed;
    purchase_speed: er200_purchase_speed;
    additional_text: er200_m_additional_text;
    power: er200_m_power;
    cargo_capacity: er200_m_cargo_capacity;
    create_effect: er200_m_create_effect;
  }
}

item (FEAT_TRAINS, er200_c, 382) {
  property {
    name: string(STR_NAME_ER200);
    vehicle_dates(1979, 1992, 30, 10)
    vehicle_dcemu(1146, 56.5, 36.0, 64, 15)
    cost_factor: 40;
    length: 8;
  }
  graphics {
    default: er200_c_sprites;
    purchase: er200_c_purchase_sprites;
    cargo_age_period: return 120;
    running_cost_factor: er200_c_running_cost_factor;
    articulated_part: articulated_part_dummy7;
    additional_text: er200_c_additional_text;
    speed: er200_speed;
    purchase_speed: er200_purchase_speed;
    can_attach_wagon: er200_can_attach_wagon;
    start_stop: er200_m_start_stop;
    power: er200_m_power;
    cargo_capacity: er200_c_cargo_capacity;
  }
}

long_name_template(er200_h, STR_LONGNAME_ER200_H)
long_name_template(er200_m, STR_LONGNAME_ER200_M)
long_name_template(er200_c, STR_LONGNAME_ER200_C)
allow_dcemu(er200_h)
allow_dcemu(er200_m)
allow_dcemu(er200_c)
