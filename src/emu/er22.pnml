 // er22
// 426 (1AAh)

#define IMAGEFILE  "src/emu/er22_h.png"
purchase_sprites(er22_v1, 0, 0)
purchase_sprites(er22_v2, 100, 0)
purchase_sprites(er22_v3, 200, 0)
EMU_head_motor1_middle_sprites(15, er22_h_v1,  32, 40)
EMU_head_motor1_middle_sprites(15, er22_h_v2,  32, 40 + 268)    
EMU_head_motor1_middle_sprites(15, er22_h_v2u, 32, 40 + 268 * 2)
#undef IMAGEFILE                             
#define IMAGEFILE  "src/emu/er22_c.png"
MU_car_sprites(15, er22_c_v1, 32, 40)
MU_car_sprites(15, er22_c_v2, 32, 40 + 268)    
#undef IMAGEFILE
purchase_sprites_select2(er22, 1968, 1972) // TODO исправить, что построенные после 1968 года имеют цвет завода, а не зелёные

switch (FEAT_TRAINS, SELF, er22_a_sprites_left_gr,
  check_year(GREAT_CHANGE_YEAR))
{
  0: er22_h_v2_middle_sprites_left;
     er22_h_v2u_middle_sprites_left;
}

switch (FEAT_TRAINS, SELF, er22_a_sprites_left_1,
  cargo_subtype)
{
  0: er22_h_v1_middle_sprites_left;
  LV_RAL6002: er22_a_sprites_left_gr;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_a_sprites_left_2_gr,
  check_year(GREAT_CHANGE_YEAR))
{
  0: er22_h_v2_sprites_left;
     er22_h_v2u_sprites_left;
}

switch (FEAT_TRAINS, SELF, er22_a_sprites_left_2,
  cargo_subtype)
{
  0: er22_h_v1_sprites_left;
  LV_RAL6002: er22_a_sprites_left_2_gr;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_a_sprites_start,
  position_in_consist == 0)
{
  0: er22_a_sprites_left_1;
     er22_a_sprites_left_2;
}

switch (FEAT_TRAINS, SELF, er22_b_sprites_left_1,
[  set_offset_to(-2),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_c_v1_sprites_left;
  LV_RAL6002: er22_c_v2_sprites_left;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_c_notpowered_sprites_left,
[  set_offset_to(-2),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_c_v1_notpowered_sprites_left;
  LV_RAL6002: er22_c_v2_notpowered_sprites_left;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_b_sprites_start,
  LOAD_TEMP(4) != er22)
{
  0: er22_b_sprites_left_1;
     er22_c_notpowered_sprites_left;
}

switch (FEAT_TRAINS, SELF, er22_c_sprites_right_1,
[  set_offset_to(-4),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_c_v1_sprites_right;
  LV_RAL6002: er22_c_v2_sprites_right;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_c_notpowered_sprites_right,
[  set_offset_to(-4),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_c_v1_notpowered_sprites_right;
  LV_RAL6002: er22_c_v2_notpowered_sprites_right;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_c_sprites_start,
  LOAD_TEMP(4) != er22)
{
  0: er22_c_sprites_right_1;
     er22_c_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, er22_d_sprites_right_gr,
  check_year(GREAT_CHANGE_YEAR))
{
  0: er22_h_v2_middle_sprites_right;
     er22_h_v2u_middle_sprites_right;
}

switch (FEAT_TRAINS, SELF, er22_d_sprites_right_1,
[  set_offset_to(-6),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_h_v1_middle_sprites_right;
  LV_RAL6002: er22_d_sprites_right_gr;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_d_sprites_right_2_gr,
  check_year(GREAT_CHANGE_YEAR))
{
  0: er22_h_v2_sprites_right;
     er22_h_v2u_sprites_right;
}

switch (FEAT_TRAINS, SELF, er22_d_sprites_right_2,
[  set_offset_to(-6),
   (prev_vehicle_cargo_subtype()) ])
{
  0: er22_h_v1_sprites_right;
  LV_RAL6002: er22_d_sprites_right_2_gr;
  align_15_sprites;
}

switch (FEAT_TRAINS, SELF, er22_d_sprites_start,
  position_in_consist_from_end == 1)
{
  0: er22_d_sprites_right_1;
     er22_d_sprites_right_2;
}

switch (FEAT_TRAINS, SELF, er22_sprites_start,
  position_in_articulated_veh)
{
  0: er22_a_sprites_start;
  2: er22_b_sprites_start;
  4: er22_c_sprites_start;
  6: er22_d_sprites_start;
     dummy_sprites;
}

long_vehicle(er22)

// считаем уже имеющиеся вагоны

switch (FEAT_TRAINS, PARENT, er22_can_attach_wagon1,
  count_veh_id(er22) < 2)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, er22_can_attach_wagon,
  vehicle_type_id == er22)
{
  1: er22_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, er22_articulated_part,
  extra_callback_info1)
{
  1: return dummy7;
  2: return er22_part2;
  3: return dummy7;
  4: return er22_part2;
  5: return dummy7;
  6: return er22_part2;
  7: return dummy7;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Визуализируем эффекты тяги у er22, только если локомотив - er22
switch (FEAT_TRAINS, SELF, er22_create_effect1,
  electric_create_effect(9))
{
  return 1;
}

switch (FEAT_TRAINS, SELF, er22_create_effect2,
  electric_create_effect(-2))
{
  return 1;
}

switch (FEAT_TRAINS, SELF, er22_create_effect0,
  position_in_articulated_veh)
{
  0: er22_create_effect1;
  6: er22_create_effect2;
     return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, er22_create_effect,
  vehicle_type_id == er22)
{
  1: er22_create_effect0;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - er22
switch (FEAT_TRAINS, PARENT, er22_power,
  (vehicle_type_id == er22) *
  (1 + (build_year >= 1967)))
{
  1: return get_power_in_cb(2 * 948.5);
  2: return get_power_in_cb(2 * 1039);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - er22
switch (FEAT_TRAINS, PARENT, er22_cargo_capacity,
  vehicle_type_id == er22)
{
  1: return 494;
     return 0;
}

emu_running_cost(er22_v1, 114 + 114, 64 + 64)

switch (FEAT_TRAINS, PARENT, er22_running_cost_factor,
  vehicle_type_id == er22)
{
  1: er22_v1_running_cost_factor;
     return RC_MU_AS_CAR * 3;
}

livery_template(er22, FACTORY, LV_RAL6002, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)

item (FEAT_TRAINS, er22, 426) {
  property {
    name: string(STR_NAME_ER22);
    vehicle_dates(1964, 1968, 30, 10, 8, 15 + 8 + 8 + 15)
    vehicle_dcemu(2*948, 66 + 41 + 41 + 66, 117.6 + 117.6, 494, 24*3*4)
  }
  graphics {
    default: er22_sprites;
    purchase: er22_purchase_sprites;
    purchase_speed: return 130;
    loading_speed: return 255; // 288=24*3*4;
    cargo_age_period: return 60;
    speed: speed_after30_130to110;
    running_cost_factor: er22_running_cost_factor;
    articulated_part: er22_articulated_part;
    can_attach_wagon: er22_can_attach_wagon;
    cargo_capacity: er22_cargo_capacity;
    cargo_subtype_text: er22_cargo_subtype_text;
    power: er22_power;
    create_effect: er22_create_effect;
    colour_mapping: any_cc_colour;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_S_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_105)),
                                   string(STR_SECTIONS_MU_S1_2of4),
                                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1968),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
  }
}

long_name_template(er22, STR_LONGNAME_ER22)
allow_dcemu(er22)

item (FEAT_TRAINS, er22_part2, 454) {
  property {
    vehicle_no_dates(1964, 1968, 30, 10, 8, 0)
    vehicle_dcemu_empty()
  }
  graphics {
    default: er22_sprites;
    create_effect: er22_create_effect;
    colour_mapping: any_cc_colour;
  }
}
