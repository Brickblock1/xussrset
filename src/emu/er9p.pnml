 // er9p

// Спрайты

#define IMAGEFILE  "src/emu/er9p.png"
purchase_sprites(er9p_h_v1, 0, 0)
purchase_sprites(er9p_h_v2, 0, 19)

switch (FEAT_TRAINS, SELF, er9p_h_purchase_sprites,
  build_year >= 1974)
{
  0: er9p_h_v1_purchase_sprites;
     er9p_h_v2_purchase_sprites;
}

purchase_sprites(er9p_m, 120, 0)
purchase_sprites(er9p_c, 240, 0)

emu_3r_0_0_sprites(12, er9p_v1, 32, 40)
emu_0_3_0_sprites(12, er9p_v1, 32, 40)
emu_0_0_2_sprites(12, er9p_v1, 32, 40)
emu_3r_0_0_sprites(12, er9p_v1u, 32, 442)
emu_0_3_0_sprites(12, er9p_v1u, 32, 442)
emu_3r_0_0_sprites(12, er9p_v1p, 32, 844)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/er9p-gr.png"
emu_3r_0_0_sprites(12, er9p_v2, 32, 40)
emu_0_3_0_sprites(12, er9p_v2, 32, 40)
emu_0_0_2_sprites(12, er9p_v2, 32, 40)
emu_3r_0_0_sprites(12, er9p_v2u, 32, 442)
emu_0_3_0_sprites(12, er9p_v2u, 32, 442)
emu_3r_0_0_sprites(12, er9p_v2p, 32, 844)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/er9p-gzhd.png"
emu_3r_0_0_sprites(12, er9p_v3u, 32, 40)
emu_0_3_0_sprites(12, er9p_v3, 32, 40)
emu_0_0_2_sprites(12, er9p_v3, 32, 40)
emu_3r_0_0_sprites(12, er9p_v3p, 32, 442)
emu_0_3_0_sprites(12, er9p_v3u, 32, 442)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/er9p-uz.png"
emu_3r_0_0_sprites(12, er9p_v4u, 32, 40)
emu_0_3_0_sprites(12, er9p_v4, 32, 40)
emu_0_0_2_sprites(12, er9p_v4, 32, 40)
emu_3r_0_0_sprites(12, er9p_v4p, 32, 442)
emu_0_3_0_sprites(12, er9p_v4u, 32, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, er9p_h_sprites_l_b1974,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er9p_h) * 100) +
  cargo_subtype)
{
    0: er9p_v1_h_sprites_left;
    1: er9p_v2_h_sprites_left;
    2: er9p_v3u_h_sprites_left;
    3: er9p_v4u_h_sprites_left;
  100: er9p_v1_h_notpowered_sprites_left;
  101: er9p_v2_h_notpowered_sprites_left;
  102: er9p_v3u_h_notpowered_sprites_left;
  103: er9p_v4u_h_notpowered_sprites_left;
  200: er9p_v1u_h_sprites_left;
  201: er9p_v2u_h_sprites_left;
  202: er9p_v3u_h_sprites_left;
  203: er9p_v4u_h_sprites_left;
  300: er9p_v1u_h_notpowered_sprites_left;
  301: er9p_v2u_h_notpowered_sprites_left;
  302: er9p_v3u_h_notpowered_sprites_left;
  303: er9p_v4u_h_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_h_sprites_l_a1974,
  ((LOAD_TEMP(4) != er9p_h) * 100) +
  cargo_subtype)
{
    0: er9p_v1p_h_sprites_left;
    1: er9p_v2p_h_sprites_left;
    2: er9p_v3p_h_sprites_left;
    3: er9p_v4p_h_sprites_left;
  100: er9p_v1p_h_notpowered_sprites_left;
  101: er9p_v2p_h_notpowered_sprites_left;
  102: er9p_v3p_h_notpowered_sprites_left;
  103: er9p_v4p_h_notpowered_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_h_sprites_l,
  build_year >= 1974)
{
  1: er9p_h_sprites_l_a1974;
     er9p_h_sprites_l_b1974;
}

switch (FEAT_TRAINS, SELF, er9p_h_sprites_r_b1974,
  ((LOAD_TEMP(2) >= date(1972,1,1)) * 200) +
  ((LOAD_TEMP(4) != er9p_h) * 100) +
  cargo_subtype)
{
    0: er9p_v1_h_sprites_right;
    1: er9p_v2_h_sprites_right;
    2: er9p_v3u_h_sprites_right;
    3: er9p_v4u_h_sprites_right;
  100: er9p_v1_h_notpowered_sprites_right;
  101: er9p_v2_h_notpowered_sprites_right;
  102: er9p_v3u_h_notpowered_sprites_right;
  103: er9p_v4u_h_notpowered_sprites_right;
  200: er9p_v1u_h_sprites_right;
  201: er9p_v2u_h_sprites_right;
  202: er9p_v3u_h_sprites_right;
  203: er9p_v4u_h_sprites_right;
  300: er9p_v1u_h_notpowered_sprites_right;
  301: er9p_v2u_h_notpowered_sprites_right;
  302: er9p_v3u_h_notpowered_sprites_right;
  303: er9p_v4u_h_notpowered_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_h_sprites_r_a1974,
  ((LOAD_TEMP(4) != er9p_h) * 100) +
  cargo_subtype)
{
    0: er9p_v1p_h_sprites_right;
    1: er9p_v2p_h_sprites_right;
    2: er9p_v3p_h_sprites_right;
    3: er9p_v4p_h_sprites_right;
  100: er9p_v1p_h_notpowered_sprites_right;
  101: er9p_v2p_h_notpowered_sprites_right;
  102: er9p_v3p_h_notpowered_sprites_right;
  103: er9p_v4p_h_notpowered_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_h_sprites_r,
  build_year >= 1974)
{
  1: er9p_h_sprites_r_a1974;
     er9p_h_sprites_r_b1974;
}

engine_direction_template3m2(er9p_h, er9p_m, er9p_c)

long_vehicle(er9p_h)

switch (FEAT_TRAINS, SELF, er9p_m_sprites_b1968,
  ((count_veh_id(er9p_m) * 2) <= LOAD_TEMP(0)) +
  (cargo_subtype * 2) +
  ((LOAD_TEMP(4) != er9p_h) * 100) )
{
    0: er9p_v1_m_sprites_left;
    1: er9p_v1_m_sprites_right;
    2: er9p_v2_m_sprites_left;
    3: er9p_v2_m_sprites_right;
    4: er9p_v3_m_sprites_left;
    5: er9p_v3_m_sprites_right;
    6: er9p_v4_m_sprites_left;
    7: er9p_v4_m_sprites_right;

  100: er9p_v1_m_notpowered_sprites_left;
  101: er9p_v1_m_notpowered_sprites_right;
  102: er9p_v2_m_notpowered_sprites_left;
  103: er9p_v2_m_notpowered_sprites_right;
  104: er9p_v3_m_notpowered_sprites_left;
  105: er9p_v3_m_notpowered_sprites_right;
  106: er9p_v4_m_notpowered_sprites_left;
  107: er9p_v4_m_notpowered_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_m_sprites_a1968,
  ((count_veh_id(er9p_m) * 2) <= LOAD_TEMP(0)) +
  (cargo_subtype * 2) +
  ((LOAD_TEMP(4) != er9p_h) * 100) )
{
    0: er9p_v1u_m_sprites_left;
    1: er9p_v1u_m_sprites_right;
    2: er9p_v2u_m_sprites_left;
    3: er9p_v2u_m_sprites_right;
    4: er9p_v3u_m_sprites_left;
    5: er9p_v3u_m_sprites_right;
    6: er9p_v4u_m_sprites_left;
    7: er9p_v4u_m_sprites_right;

  100: er9p_v1u_m_notpowered_sprites_left;
  101: er9p_v1u_m_notpowered_sprites_right;
  102: er9p_v2u_m_notpowered_sprites_left;
  103: er9p_v2u_m_notpowered_sprites_right;
  104: er9p_v3u_m_notpowered_sprites_left;
  105: er9p_v3u_m_notpowered_sprites_right;
  106: er9p_v4u_m_notpowered_sprites_left;
  107: er9p_v4u_m_notpowered_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, er9p_m_sprites_start,
  build_year >= 1968)
{
  1: er9p_m_sprites_a1968;
     er9p_m_sprites_b1968;
}

long_vehicle(er9p_m)

switch (FEAT_TRAINS, SELF, er9p_c_sprites_start,
  ((count_veh_id(er9p_c) * 2) <= LOAD_TEMP(0)) +
  (cargo_subtype * 2)  +
  ((LOAD_TEMP(4) != er9p_h) * 100) )
{
    0: er9p_v1_c_sprites_left;
    1: er9p_v1_c_sprites_right;
    2: er9p_v2_c_sprites_left;
    3: er9p_v2_c_sprites_right;
    4: er9p_v3_c_sprites_left;
    5: er9p_v3_c_sprites_right;
    6: er9p_v4_c_sprites_left;
    7: er9p_v4_c_sprites_right;
  100: er9p_v1_c_notpowered_sprites_left;
  101: er9p_v1_c_notpowered_sprites_right;
  102: er9p_v2_c_notpowered_sprites_left;
  103: er9p_v2_c_notpowered_sprites_right;
  104: er9p_v3_c_notpowered_sprites_left;
  105: er9p_v3_c_notpowered_sprites_right;
  106: er9p_v4_c_notpowered_sprites_left;
  107: er9p_v4_c_notpowered_sprites_right;
       align_12_sprites;
}

long_vehicle(er9p_c)

switch (FEAT_TRAINS, PARENT, er9p_h_can_attach_wagon3, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er9p_c: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_CAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_h_can_attach_wagon2, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er9p_h: return string(STR_CAN_ATTACH_CAR_ONLY);
          return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er9p_h_can_attach_wagon, 
  (count_veh_id(er9p_h) > 1) ? 0 : 
  (((count_veh_id(er9p_h) + count_veh_id(er9p_m) + count_veh_id(er9p_c)) < 3) ||
  ((count_veh_id(er9p_h) + count_veh_id(er9p_m) + count_veh_id(er9p_c)) % 2 == 0 ) ) ? 1 : (2 + count_veh_id(er9p_m)) )
{
     0: return string(STR_CAN_ATTACH_NO_MORE_THAN_2_HEAD);
     1: return string(STR_CAN_ATTACH_CAR_ONLY);
     2: er9p_h_can_attach_wagon2;
  6..8: er9p_h_can_attach_wagon3;
        return CB_RESULT_ATTACH_ALLOW; 
}

switch (FEAT_TRAINS, PARENT, er9p_m_can_attach_wagon, 
  ((count_veh_id(er9p_m) - count_veh_id(er9p_c) - count_veh_id(er9p_h)) < 1 ) * (1 + count_veh_id(er9p_m)))
{
  0: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
     return CB_RESULT_ATTACH_ALLOW;
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon4, 
[  set_offset_to(num_vehs_in_consist-8),
   prev_vehicle_type_id()])
{
  er9p_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon3, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er9p_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon2, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er9p_m: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon1, 
[  set_offset_to(num_vehs_in_consist-4),
   prev_vehicle_type_id()])
{
  er9p_c: return CB_RESULT_ATTACH_ALLOW;
          return string(STR_CAN_ATTACH_HEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon0, 
  ((count_veh_id(er9p_m) - count_veh_id(er9p_c) - count_veh_id(er9p_h)) >= 0 ) * (count_veh_id(er9p_m)))
{
  0: return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
  1: return CB_RESULT_ATTACH_ALLOW;
  2: er9p_c_can_attach_wagon1;
  3: er9p_c_can_attach_wagon2;
  4: er9p_c_can_attach_wagon3;
  5: er9p_c_can_attach_wagon4;
     return string(STR_CAN_ATTACH_HEAD_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_c_can_attach_wagon, 
[  set_offset_to(num_vehs_in_consist-2),
   prev_vehicle_type_id()])
{
  er9p_m: er9p_c_can_attach_wagon0;
          return string(STR_CAN_ATTACH_OTHERCAR_ONLY);
}

switch (FEAT_TRAINS, SELF, er9p_can_attach_wagon2,
  vehicle_type_id)
{
  er9p_h: er9p_h_can_attach_wagon;
  er9p_m: er9p_m_can_attach_wagon;
  er9p_c: er9p_c_can_attach_wagon;
          return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, PARENT, er9p_can_attach_wagon1,
  (count_veh_id(er9p_h) > 1) ? 0 : 
  (((count_veh_id(er9p_h) + count_veh_id(er9p_m) + count_veh_id(er9p_c)) >= 11) && (LOAD_TEMP(0) != er9p_h)) ? 1 : 2)
{
  0: return string(STR_CAN_ATTACH_NO_AFTER_REAR_HEAD);
  1: return string(STR_CAN_ATTACH_HEAD_ONLY);
     er9p_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, er9p_can_attach_wagon,
  STORE_TEMP(vehicle_type_id, 0))
{
  er9p_can_attach_wagon1;
}

switch (FEAT_TRAINS, PARENT, er9p_h_start_stop,
  count_veh_id(er9p_h))
{
  2: return CB_RESULT_NO_TEXT;
     return string(STR_START_STOP_NUMBER_OF_HEAD_CAR_MUST_BE_EVEN);
}

// Моторный самостоятельно выпускать из депо нельзя
switch (FEAT_TRAINS, SELF, er9p_m_start_stop,
  0)
{
     return string(STR_START_STOP_MOTORIZED_CAR_CANNOT_LEAVE_DEPOT);
}

// Визуализируем эффекты тяги у er9p_m, только если локомотив - er9p_h
switch (FEAT_TRAINS, SELF, er9p_m_visual_effect_and_powered2,
  (count_veh_id(er9p_m) * 2) <= LOAD_TEMP(0) )
{
  0: return electric_visual_effect_and_powered(-3);
     return electric_visual_effect_and_powered(5);
}

switch (FEAT_TRAINS, PARENT, er9p_m_visual_effect_and_powered1,
[ STORE_TEMP(count_veh_id(er9p_m), 0) ])                                                          
{
  er9p_m_visual_effect_and_powered2;
}

switch (FEAT_TRAINS, PARENT, er9p_m_visual_effect_and_powered,
  vehicle_type_id == er9p_h)
{
  1: er9p_m_visual_effect_and_powered1;
     return disable_visual_effect_and_powered();
}

// Даём тягу, только если локомотив - er9p_h
switch (FEAT_TRAINS, PARENT, er9p_m_power,
  vehicle_type_id == er9p_h)
{
  1: return get_power_in_cb(826); 
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - er9p_h
switch (FEAT_TRAINS, PARENT, er9p_h_cargo_capacity,
  (vehicle_type_id == er9p_h) )
{
  1: return 88;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er9p_m_cargo_capacity,
  (vehicle_type_id == er9p_h) )
{
  1: return 110;
     return 0;
}

switch (FEAT_TRAINS, PARENT, er9p_c_cargo_capacity,
  (vehicle_type_id == er9p_h) )
{
  1: return 108;
     return 0;
}

switch (FEAT_TRAINS, SELF, er9p_cargo_subtype_text4,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_53));
  1: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_59));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, er9p_cargo_subtype_text3,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_53));
  1: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_59));
  2: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_GORKOV_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, er9p_cargo_subtype_text2,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_53));
  1: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_TTD_59));
  2: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_GORKOV_RAILWAY));
  3: string(STR_REFIT_FACTORY_62101, string(STR_REFIT_UKRAINIAN_RAILWAY));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, er9p_cargo_subtype_text,
  ((build_year >= 1972) || (current_year >= 1972)) + 
  ((build_year >= 1990) || (current_year >= 1990)))
{
  0: er9p_cargo_subtype_text4;
  1: er9p_cargo_subtype_text3;
     er9p_cargo_subtype_text2;
}

item (FEAT_TRAINS, er9p_h) 
{
  property
  {
    name: string(STR_ER9P_H_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1963), 1, 1);
    model_life: get_model_life(1963,1976);
    retire_early: get_retire_early(1963,1976);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12; 
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 130 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 88;
    weight: 39 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: er9p_h_sprites;
    purchase: er9p_h_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er9p_can_attach_wagon;
    start_stop: er9p_h_start_stop;
    speed: speed_after30_130to110;
    purchase_speed: return 130;
    additional_text: return string(STR_ER9P_H_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10_12));
    cargo_subtype_text: er9p_cargo_subtype_text;
    power: return 0;
    cargo_capacity: er9p_h_cargo_capacity;
  }
}

item (FEAT_TRAINS, er9p_m) 
{
  property
  {
    name: string(STR_ER9P_M_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: get_model_life(1963,1976);
    retire_early: get_retire_early(1963,1976);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 826 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 110;
    weight: 59 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(47.0, 59);
    length: 8;
  }
  graphics {
    default: er9p_m_sprites;
    purchase: er9p_m_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    can_attach_wagon: er9p_can_attach_wagon;
    start_stop: er9p_m_start_stop;
    speed: speed_after30_130to110;
    purchase_speed: return 130;
    additional_text: return string(STR_ER9P_M_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10_12));
    cargo_subtype_text: er9p_cargo_subtype_text;
    visual_effect_and_powered: er9p_m_visual_effect_and_powered;
    power: er9p_m_power;
    cargo_capacity: er9p_m_cargo_capacity;
  }
}

item (FEAT_TRAINS, er9p_c) 
{
  property
  {
    name: string(STR_ER9P_C_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1963), 1, 1);
    model_life: get_model_life(1963,1976);
    retire_early: get_retire_early(1963,1976);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    loading_speed: 12;
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: get_ac_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    cargo_capacity: 108;
    weight: 37 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: er9p_c_sprites;
    purchase: er9p_c_purchase_sprites;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_ER9P_C_PURCHASE_HINT, string(STR_SECTIONS_EMU_4_6_8_10_12));
    cargo_subtype_text: er9p_cargo_subtype_text;
    speed: speed_after30_130to110;
    purchase_speed: return 130;
    power: return 0;
    can_attach_wagon: er9p_can_attach_wagon;
    start_stop: return string(STR_START_STOP_HEAD_REQUIRED);
    cargo_capacity: er9p_c_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, er9p_h)
  {
    property
    { name: string(STR_ER9P_H_NAME_LONG); }
  }
  item (FEAT_TRAINS, er9p_m)
  {
    property
    { name: string(STR_ER9P_M_NAME_LONG); }
  }
  item (FEAT_TRAINS, er9p_c)
  {
    property
    { name: string(STR_ER9P_C_NAME_LONG); }
  }
}

