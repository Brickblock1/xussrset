 // evs1
// 400 (190h)

#define IMAGEFILE  "src/emu/evs.png"
purchase_sprites(evs1, 0, 0)
purchase_sprites(evs2, 120, 0)

MU_3r_0_0_sprites(15, evs1_gbt, 32, 40)
MU_0_4_0_sprites(14, evs1_dt,  32, 40)
MU_0_0_2_sprites(14, evs1_t,   32, 40)
MU_2_0_0_sprites(15, evs1_gbt_middle, 32, 576)
MU_0_4_0_sprites(14, evs1_ttr, 32, 576)
MU_0_0_2_sprites(14, evs1_tt,  32, 576)
MU_0_0_2_sprites(14, evs1_ta,  32, 1112)
MU_0_0_1_sprites(14, evs1_tab, 32, 1380)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, evs1_gbt_h_sprites_left_check,
  position_in_consist == 0)
{
  1: evs1_gbt_h_sprites_left;
     evs1_gbt_middle_h_sprites_left;
}

random_switch (FEAT_TRAINS, SELF, evs1_h_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: evs1_gbt_h_sprites_left_check;
  1: evs1_gbt_h_sprites_left_check;
}

switch (FEAT_TRAINS, SELF, evs1_gbt_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: evs1_gbt_h_sprites_right;
     evs1_gbt_middle_h_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs1_gbt_h_notpowered_sprites_right_check,
[  set_offset_to(2),
   prev_vehicle_type_id() == evs1])
{
  1: evs1_gbt_h_notpowered_sprites_right;
     evs1_gbt_middle_h_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs_never_train, 0)
{
   1: evs1_ttr_m_sprites_left;  
   2: evs1_ttr_m_normal_sprites;
      align_14_sprites;
}

switch (FEAT_TRAINS, SELF, evs1_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   ((LOAD_TEMP(4) != evs1) * 200) +
   (prev_vehicle_randombits() % 2) * 100 +
   position_in_articulated_veh])
{
   0: evs1_h_sprites_start;
   2: evs1_dt_m_sprites_left;
   4: evs1_t_c_sprites_left;
   6: evs1_tt_c_sprites_left;
   8: evs1_ta_c_sprites_left;
  10: evs1_tab_c_sprites_right;
  12: evs1_tt_c_sprites_right;
  14: evs1_t_c_sprites_right;
  16: evs1_dt_m_sprites_right;
  18: evs1_gbt_h_sprites_right_check;

 100: evs1_h_sprites_start;
 102: evs1_dt_m_sprites_left;
 104: evs1_t_c_sprites_left;
 106: evs1_tt_c_sprites_left;
 108: evs1_tab_c_sprites_left;
 110: evs1_ta_c_sprites_right;
 112: evs1_tt_c_sprites_right;
 114: evs1_t_c_sprites_right;
 116: evs1_dt_m_sprites_right;
 118: evs1_gbt_h_sprites_right_check;

 200: evs1_h_sprites_start;
 202: evs1_dt_m_notpowered_sprites_left;
 204: evs1_t_c_notpowered_sprites_left;
 206: evs1_tt_c_notpowered_sprites_left;
 208: evs1_ta_c_notpowered_sprites_left;
 210: evs1_tab_c_sprites_right;
 212: evs1_tt_c_notpowered_sprites_right;
 214: evs1_t_c_notpowered_sprites_right;
 216: evs1_dt_m_notpowered_sprites_right;
 218: evs1_gbt_h_notpowered_sprites_right_check;

 300: evs1_h_sprites_start;
 302: evs1_dt_m_notpowered_sprites_left;
 304: evs1_t_c_notpowered_sprites_left;
 306: evs1_tt_c_notpowered_sprites_left;
 308: evs1_tab_c_sprites_left;
 310: evs1_ta_c_notpowered_sprites_right;
 312: evs1_tt_c_notpowered_sprites_right;
 314: evs1_t_c_notpowered_sprites_right;
 316: evs1_dt_m_notpowered_sprites_right;
 318: evs1_gbt_h_notpowered_sprites_right_check;

 777: evs_never_train;

      align_14_sprites;
}

long_vehicle_RND(evs1, evs1_h_sprites_start)

switch (FEAT_TRAINS, SELF, evs1_articulated_part,
  extra_callback_info1)
{
  1: return dummy7;
  2: return evs1_part2;
  3: return dummy6;
  4: return evs1_part3;
  5: return dummy6;
  6: return evs1_part3;
  7: return dummy6;
  8: return evs1_part3;
  9: return dummy6;
 10: return evs1_part3;
 11: return dummy6;
 12: return evs1_part3;
 13: return dummy6;
 14: return evs1_part3;
 15: return dummy6;
 16: return evs1_part3;
 17: return dummy6;
 18: return evs1_part3;
 19: return dummy7;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// даём тягу, если локомотив evs1
switch (FEAT_TRAINS, PARENT, evs1_power,
  vehicle_type_id == evs1)
{
  1: return get_power_in_cb(10876); 
     return 0;
}

switch (FEAT_TRAINS, SELF, evs1_create_effect3, 
  electric_create_effect(-3)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, evs1_create_effect2, 
  electric_create_effect(7)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, evs1_create_effect1,
  position_in_articulated_veh)
{
   2: evs1_create_effect2;
  16: evs1_create_effect3;
      return disable_create_effect();
}

// показываем визуальные эффекты, если локомотив evs1
switch (FEAT_TRAINS, PARENT, evs1_create_effect,
  vehicle_type_id == evs1)
{
  1: evs1_create_effect1;
     return disable_create_effect();
}

// Допускается эксплуатация с пассажирами только если голова - evs1
switch (FEAT_TRAINS, SELF, evs1_cargo_capacity2,
  build_year >= 2014)
{
  0: return 564-75;
     return 508-75;
}

switch (FEAT_TRAINS, PARENT, evs1_cargo_capacity,
  vehicle_type_id == evs1)
{
  1: evs1_cargo_capacity2;
     return 0;
}

switch (FEAT_TRAINS, SELF, evs1_purchase_cargo_capacity,
  current_year >= 2014)
{
  0: return 564-75;
     return 508-75;
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon3,
  LOAD_TEMP(0) <= 1)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon2,
  (LOAD_TEMP(1) >= 2014) +
  ((build_year >= 2014) * 100) )
{
   0: return string(STR_CAN_ATTACH_NO_MORE);
   1: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-2013","2014-...");
 100: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-2013","2014-...");
 101: evs1_can_attach_wagon3;
      return string(STR_CAN_ATTACH_WRONG_YEAR,"2000","2000");
}

switch (FEAT_TRAINS, PARENT, evs1_can_attach_wagon1,
[  STORE_TEMP(build_year, 1),
   STORE_TEMP(count_veh_id(evs1), 0) ])
{
  evs1_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon,
  vehicle_type_id == evs1)
{
  1: evs1_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

emu_running_cost(evs1_v1, 2966, 2556)

switch (FEAT_TRAINS, PARENT, evs1_running_cost_factor,
  vehicle_type_id == evs1)
{
  1: evs1_v1_running_cost_factor;
     return RC_MU_AS_CAR * 10;
}

switch (FEAT_TRAINS, SELF, evs1_additional_text,
  current_year >= 2014)
{
  0: return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_2),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_VALERO)),
                   string(STR_SECTIONS_MU_S1of10),
                   string(STR_PURCHASE_HINT_FROM, 2009),
                   string(STR_PURCHASE_HINT_FACTORY_SIEMENS));
     return string(STR_PURCHASE_HINT_ENGINE_MU,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_2),
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_VALERO)),
                   string(STR_SECTIONS_MU_S1_2of10),
                   string(STR_PURCHASE_HINT_FROM, 2009),
                   string(STR_PURCHASE_HINT_FACTORY_SIEMENS));
}

livery_template(evs1, FACTORY, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
engine_CAP_change_build_after(evs1, 145, 
                                    135, 2014)

item (FEAT_TRAINS, evs1, 400) {
  property {
    name: string(STR_NAME_EVS1);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2009), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2009,2050);
    retire_early: get_retire_early(2009,2050); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 10;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
//    cost_factor: 1696;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 156 mph;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dc_track_type_emu();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 10876 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    loading_speed: (15*8);
    cargo_capacity: 255;
    weight: 576.8 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(328.0, 576.8);
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: evs1_sprites;
    purchase: evs1_purchase_sprites;
    cargo_age_period: evs1_cargo_age_period;
    running_cost_factor: evs1_running_cost_factor;
    articulated_part: evs1_articulated_part;
    can_attach_wagon: evs1_can_attach_wagon;
    cargo_subtype_text: evs1_cargo_subtype_text;
    additional_text: evs1_additional_text;
    power: evs1_power;
    cargo_capacity: evs1_cargo_capacity;
    purchase_cargo_capacity: evs1_purchase_cargo_capacity;
    cost_factor: return 1696;
    colour_mapping: return ttdall_cc + 0xC9;
    create_effect: evs1_create_effect;
  }
}

long_name_template(evs1, STR_LONGNAME_EVS1)
allow_dcemu(evs1)

switch (FEAT_TRAINS, PARENT, evs1_part2_cargo_capacity,
  vehicle_type_id == evs1)
{
  1: return 75;
     return 0;
}

item (FEAT_TRAINS, evs1_part2, 451) {
  property {
    climates_available: NO_CLIMATE;
    cargo_allow_refit: [TOUR, PASS];
    default_cargo_type: TOUR;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    loading_speed: 15*2;
    cargo_capacity: 75;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: evs1_sprites;
    cargo_age_period: return 145;
    create_effect: evs1_create_effect;
    cargo_capacity: evs1_part2_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, evs1_part3, 452) {
  property {
    climates_available: NO_CLIMATE;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    cargo_capacity: 0;
    length: 8;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: evs1_sprites;
    create_effect: evs1_create_effect;
  }
}
