// evs1

#define IMAGEFILE  "src/emu/evs_gbt_h.png"
purchase_sprites(evs1, 0, 0)
purchase_sprites(evs2, 100, 0)
MU_head_middle_sp_sprites(15, evs1_h_gbt, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_dt_m.png"
EMU_motor1_middle_sprites(14, evs1_m_dt, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_ttr_m.png"
EMU_motor1_middle_sprites(14, evs1_m_ttr, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_t_c.png"
MU_car_sprites(14, evs1_c_t, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_tt_c.png"
MU_car_sprites(14, evs1_c_tt, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_ta_c.png"
MU_car_sprites(14, evs1_c_ta, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/evs_tab_c.png"
MU_car_nd_sprites(14, evs1_c_tab, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, evs1_h_gbt_sprites_left_check,
  position_in_consist == 0)
{
  1: evs1_h_gbt_sprites_left;
     evs1_h_gbt_middle_sprites_left;
}

random_switch (FEAT_TRAINS, SELF, evs1_h_sprites_start,
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: evs1_h_gbt_sprites_left_check;
  1: evs1_h_gbt_sprites_left_check;
}

switch (FEAT_TRAINS, SELF, evs1_h_gbt_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: evs1_h_gbt_sprites_right;
     evs1_h_gbt_middle_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs1_h_gbt_notpowered_sprites_right_check,
[  set_offset_to(2),
   prev_vehicle_type_id() == evs1])
{
  1: evs1_h_gbt_notpowered_sprites_right;
     evs1_h_gbt_middle_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs_never_train, 0)
{
   1: evs1_m_ttr_sprites_left;
   2: evs1_m_ttr_normal_sprites;
      align_14_sprites;
}

switch (FEAT_TRAINS, SELF, evs1_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   ((LOAD_TEMP(4) != evs1) * 200) +
   (prev_vehicle_randombits() % 2) * 100 +
   position_in_articulated_veh])
{
   0: evs1_h_sprites_start;
   2: evs1_m_dt_sprites_left;
   4: evs1_c_t_sprites_left;
   6: evs1_c_tt_sprites_left;
   8: evs1_c_ta_sprites_left;
  10: evs1_c_tab_sprites_right;
  12: evs1_c_tt_sprites_right;
  14: evs1_c_t_sprites_right;
  16: evs1_m_dt_sprites_right;
  18: evs1_h_gbt_sprites_right_check;

 100: evs1_h_sprites_start;
 102: evs1_m_dt_sprites_left;
 104: evs1_c_t_sprites_left;
 106: evs1_c_tt_sprites_left;
 108: evs1_c_tab_sprites_left;
 110: evs1_c_ta_sprites_right;
 112: evs1_c_tt_sprites_right;
 114: evs1_c_t_sprites_right;
 116: evs1_m_dt_sprites_right;
 118: evs1_h_gbt_sprites_right_check;

 200: evs1_h_sprites_start;
 202: evs1_m_dt_notpowered_sprites_left;
 204: evs1_c_t_notpowered_sprites_left;
 206: evs1_c_tt_notpowered_sprites_left;
 208: evs1_c_ta_notpowered_sprites_left;
 210: evs1_c_tab_sprites_right;
 212: evs1_c_tt_notpowered_sprites_right;
 214: evs1_c_t_notpowered_sprites_right;
 216: evs1_m_dt_notpowered_sprites_right;
 218: evs1_h_gbt_notpowered_sprites_right_check;

 300: evs1_h_sprites_start;
 302: evs1_m_dt_notpowered_sprites_left;
 304: evs1_c_t_notpowered_sprites_left;
 306: evs1_c_tt_notpowered_sprites_left;
 308: evs1_c_tab_sprites_left;
 310: evs1_c_ta_notpowered_sprites_right;
 312: evs1_c_tt_notpowered_sprites_right;
 314: evs1_c_t_notpowered_sprites_right;
 316: evs1_m_dt_notpowered_sprites_right;
 318: evs1_h_gbt_notpowered_sprites_right_check;

 777: evs_never_train;

      align_14_sprites;
}

long_vehicle_RND(evs1, evs1_h_sprites_start)

switch (FEAT_TRAINS, SELF, evs1_articulated_part,
  extra_callback_info1)
{
  1: return dummy7;
  2: return evs1_part2;
  3: return dummy6;
  4: return evs1_part3;
  5: return dummy6;
  6: return evs1_part3;
  7: return dummy6;
  8: return evs1_part3;
  9: return dummy6;
 10: return evs1_part3;
 11: return dummy6;
 12: return evs1_part3;
 13: return dummy6;
 14: return evs1_part3;
 15: return dummy6;
 16: return evs1_part3;
 17: return dummy6;
 18: return evs1_part3;
 19: return dummy7;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, evs1_create_effect3,
  electric_create_effect(-3)) { return 1; }

switch (FEAT_TRAINS, SELF, evs1_create_effect2,
  electric_create_effect(7)) { return 1; }

switch (FEAT_TRAINS, SELF, evs1_create_effect1,
  position_in_articulated_veh)
{
   2: evs1_create_effect2;
  16: evs1_create_effect3;
      return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, evs1_create_effect,
  vehicle_type_id == evs1)
{
  1: evs1_create_effect1;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon3,
  LOAD_TEMP(0) <= 1)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon2,
  (LOAD_TEMP(1) >= 2014) +
  ((build_year >= 2014) * 100))
{
   0: return string(STR_CAN_ATTACH_NO_MORE);
   1: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-2013","2014-...");
 100: return string(STR_CAN_ATTACH_WRONG_YEAR,"...-2013","2014-...");
 101: evs1_can_attach_wagon3;
      return string(STR_CAN_ATTACH_WRONG_YEAR,"2000","2000");
}

switch (FEAT_TRAINS, PARENT, evs1_can_attach_wagon1,
[  STORE_TEMP(build_year, 1),
   STORE_TEMP(count_veh_id(evs1), 0) ])
{
  evs1_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, evs1_can_attach_wagon,
  vehicle_type_id == evs1)
{
  1: evs1_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

engine_power_MU(evs1, 10876, evs1)
engine_capacity_MU_change_build_after(evs1, 460,
                                            433, 2014, evs1)
engine_capacity_MU_change_build_after(evs1_part2, 104,
                                                   75, 2014, evs1)
engine_CAP_change_build_after(evs1_part2, 200,
                                          300, 2014)

RC_head_check_new(evs1, evs1)
switch (FEAT_TRAINS, SELF, evs1_running_cost_factor,
[  STORE_TEMP(3148, 0),  // Моторы
   STORE_TEMP(442, 1),   // Бригада
   STORE_TEMP(1104, 2),  // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(773, 4),   // ТО
   STORE_TEMP(55, 5),    // Сертификация

   STORE_TEMP(250, 6),                                     // Скорость
   STORE_TEMP(577, 7),                                     // Тара
   STORE_TEMP(653, 8)])                      /* 577+76 */  // Максимальная масса
{ evs1_check_running_cost_factor; }                        // 2966+2556

engine_speed(evs1, 250)
hint_MU_with_subtype_noto(evs1_b2014,
     calc_loading(max(460 / (7 * WAGON_DOOR), 104 / (2 * WAGON_DOOR))),
     STR_PURCHASE_HINT_AGING_PERIOD_3_2,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_VELARO,
     STR_SECTIONS_MU_S1of10,
     2009,
     string(STR_PURCHASE_HINT_FACTORY_SIEMENS))
hint_MU_with_subtype_noto_comment(evs1_a2014,
     calc_loading(max(433 / (7 * WAGON_DOOR), 75 / (2 * WAGON_DOOR))),
     STR_PURCHASE_HINT_AGING_PERIOD_3_2,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_VELARO,
     STR_SECTIONS_MU_S1_2of10,
     2009,
     string(STR_PURCHASE_HINT_FACTORY_SIEMENS),
     string(STR_PURCHASE_HINT_ATTACH_YEAR,"...-2013","2014-..."))
hint_change_after(evs1, 2014)

item (FEAT_TRAINS, evs1, 400) {
  property {
    name: string(STR_NAME_EVS1);
    vehicle_dates(2009, 2050, 30, 10, 8, 1696)
    vehicle_emu(dc, 10876, 576.8, 328.0, 460, 7 * WAGON_DOOR)
  }
  graphics {
    default: evs1_sprites;
    purchase: evs1_purchase_sprites;
    speed: evs1_speed;
    cargo_age_period: return 110;
    running_cost_factor: evs1_running_cost_factor;
    articulated_part: evs1_articulated_part;
    can_attach_wagon: evs1_can_attach_wagon;
    additional_text: evs1_additional_text;
    power: evs1_power;
    cargo_capacity: evs1_cargo_capacity;
    cost_factor: return 1696;
    colour_mapping: any_cc_colour;
    create_effect: evs1_create_effect;
    purchase_speed: return 250;
  }
}

long_name_template(evs1, STR_LONGNAME_EVS1)
allow_dcemu(evs1)

item (FEAT_TRAINS, evs1_part2, 451) {
  property {
    vehicle_no_dates(2009, 2050, 30, 10, 8, 0)
    vehicle_emu_c(dc, 0, 104, 2 * WAGON_DOOR)
    vehicle_allow_tourists()
    default_cargo_type: TOUR;
  }
  graphics {
    default: evs1_sprites;
    cargo_age_period: evs1_part2_cargo_age_period;
    create_effect: evs1_create_effect;
    cargo_capacity: evs1_part2_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, evs1_part3, 452) {
  property {
    vehicle_no_dates(2009, 2050, 30, 10, 8, 0)
    vehicle_emu_empty(dc)
  }
  graphics {
    default: evs1_sprites;
    create_effect: evs1_create_effect;
  }
}
