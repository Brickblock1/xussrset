 // evs1

// Спрайты

#define IMAGEFILE  "src/emu/evs.png"

purchase_sprites(evs2, 120, 0)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, evs2_sprites_2, 0)
{
  evs1_gbt_h_sprites_left;
}

long_vehicle_simple(evs2)

switch (FEAT_TRAINS, SELF, evs2_dt_m_sprites_right_railcheck, current_railtype)
{
  ERD1: evs1_dt_m_sprites_right;
  ERD2: evs1_dt_m_sprites_right;
  ERD3: evs1_dt_m_sprites_right;
        evs1_dt_m_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs2_ttr_m_sprites_right_railcheck, current_railtype)
{
  ERD1: evs1_ttr_m_notpowered_sprites_right;
  ERD2: evs1_ttr_m_notpowered_sprites_right;
  ERD3: evs1_ttr_m_notpowered_sprites_right;
        evs1_ttr_m_sprites_right;
}

switch (FEAT_TRAINS, SELF, evs2_dt_m_sprites_left_railcheck, current_railtype)
{
  ERD1: evs1_dt_m_sprites_left;
  ERD2: evs1_dt_m_sprites_left;
  ERD3: evs1_dt_m_sprites_left;
        evs1_dt_m_notpowered_sprites_left;
}

switch (FEAT_TRAINS, SELF, evs2_b_sprites_2,
  position_in_articulated_veh)
{
   2: evs2_dt_m_sprites_left_railcheck;
   4: evs1_ttr_m_notpowered_sprites_left;
   6: evs1_tt_c_sprites_left;
   8: evs1_ta_c_sprites_left;
  10: evs1_tab_c_sprites_right;
  12: evs1_tt_c_sprites_right;
  14: evs2_ttr_m_sprites_right_railcheck;
  16: evs2_dt_m_sprites_right_railcheck;
  18: evs1_gbt_h_sprites_right;
      align_15_sprites;
}

long_vehicle_simple(evs2_b)

switch (FEAT_TRAINS, SELF, evs2_articulated_part,
  extra_callback_info1)
{
  1: return dummy7;
  2: return unitb;
  3: return dummy7;
  4: return unitb;
  5: return dummy7;
  6: return unitb;
  7: return dummy7;
  8: return unitb;
  9: return dummy7;
 10: return unitb;
 11: return dummy7;
 12: return unitb;
 13: return dummy7;
 14: return unitb;
 15: return dummy7;
 16: return unitb;
 17: return dummy7;
 18: return unitb;
 19: return dummy6;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// даём тягу, если локомотив evs2
switch (FEAT_TRAINS, PARENT, evs2_power,
  vehicle_type_id == evs2)
{
  1: return get_power_in_cb(10876); 
     return 0;
}

switch (FEAT_TRAINS, SELF, evs2_b_visual_effect_and_powered16,
  current_railtype)
{
  ERD1: return electric_visual_effect_and_powered(-3);
  ERD2: return electric_visual_effect_and_powered(-3);
  ERD3: return electric_visual_effect_and_powered(-3);
        return disable_visual_effect_and_powered();
}

switch (FEAT_TRAINS, SELF, evs2_b_visual_effect_and_powered14,
  current_railtype)
{
  ERD1: return disable_visual_effect_and_powered();
  ERD2: return disable_visual_effect_and_powered();
  ERD3: return disable_visual_effect_and_powered();
        return electric_visual_effect_and_powered(-3);
}

switch (FEAT_TRAINS, SELF, evs2_b_visual_effect_and_powered2,
  current_railtype)
{
  ERD1: return electric_visual_effect_and_powered(7);
  ERD2: return electric_visual_effect_and_powered(7);
  ERD3: return electric_visual_effect_and_powered(7);
        return disable_visual_effect_and_powered();
}

switch (FEAT_TRAINS, SELF, evs2_b_visual_effect_and_powered0,
  position_in_articulated_veh)
{
   2: evs2_b_visual_effect_and_powered2;
  14: evs2_b_visual_effect_and_powered14;
  16: evs2_b_visual_effect_and_powered16;
      return disable_visual_effect_and_powered();
}

// показываем визуальные эффекты, если локомотив evs1
switch (FEAT_TRAINS, PARENT, evs2_b_visual_effect_and_powered, vehicle_type_id == evs2)
{
  1: evs2_b_visual_effect_and_powered0;
     return disable_visual_effect_and_powered();
}

// Допускается эксплуатация с пассажирами только если голова - evs2
switch (FEAT_TRAINS, PARENT, evs2_cargo_capacity,
  (vehicle_type_id == evs2) )
{
  1: return 604;
     return 0;
}

item (FEAT_TRAINS, evs2) 
{
  property
  {
    name: string(STR_EVS2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(2009), 1, 1);
    vehicle_life: 30; // срок службы
    model_life: get_model_life(2009,2050);
    retire_early: get_retire_early(2009,2050); // VEHICLE_NEVER_EXPIRES;
    reliability_decay: 20;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cost_factor: get_emu_cost_factor();
    running_cost_factor: get_emu_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 156 mph;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_dual_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 10876 hpM;
    running_cost_base: RUNNING_COST_ELECTRIC;
    loading_speed: (260/8);
    cargo_capacity: 255; // 260
    weight: 667 ton;
    engine_class: ENGINE_CLASS_ELECTRIC;
    tractive_effort_coefficient: get_tec(380.0, 667);
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
  }
  graphics {
    default: evs2_sprites;
    purchase: evs2_purchase_sprites;
    articulated_part: evs2_articulated_part;
    can_attach_wagon: string(STR_CAN_ATTACH_NO_MORE);
    additional_text: return string(STR_EVS2_PURCHASE_HINT);
    power: evs2_power;
    cargo_capacity: evs2_cargo_capacity;
    cost_factor: return 1344;
  }
  livery_override(unitb)
  {
    length: return 8;
    visual_effect_and_powered: evs2_b_visual_effect_and_powered;
            evs2_b_sprites;
  }  
}

if (enable_long_names)
{
  item (FEAT_TRAINS, evs2)
  {
    property
    { name: string(STR_EVS2_NAME_LONG); }
  }
}

