// emu_sn

#define IMAGEFILE  "src/emu/sn_h.png"
purchase_sprites(emu_sn, 0, 0)
MU_head_middle_sprites(12, emu_sn_h_v1, 32, 40)
MU_head_middle_sprites(12, emu_sn_h_v2, 32, 308)
MU_head_middle_sprites(12, emu_sn_h_v3, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sn_m.png"
EMU_motor2_sprites(12, emu_sn_m_v1, 32, 40)
EMU_motor2_sprites(12, emu_sn_m_v2, 32, 308)
EMU_motor2_sprites(12, emu_sn_m_v3, 32, 576)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, emu_sn_h_sprites_left_1,
  cargo_subtype)
{
           0: emu_sn_h_v1_sprites_left;
           1: emu_sn_h_v2_sprites_left;
  LV_RAL6020: emu_sn_h_v3_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_h_notpowered_normal_sprites_left,
  cargo_subtype)
{
           0: emu_sn_h_v1_notpowered_normal_sprites_left;
           1: emu_sn_h_v2_notpowered_normal_sprites_left;
  LV_RAL6020: emu_sn_h_v3_notpowered_normal_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_h_sprites_left_check,
  position_in_consist == 0)
{
  0: emu_sn_h_notpowered_normal_sprites_left;
     emu_sn_h_sprites_left_1;
}

switch (FEAT_TRAINS, SELF, emu_sn_m_sprites_left_1,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_m_v1_sprites_left;
           1: emu_sn_m_v2_sprites_left;
  LV_RAL6020: emu_sn_m_v3_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_m_sprites_right_1,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_m_v1_sprites_right;
           1: emu_sn_m_v2_sprites_right;
  LV_RAL6020: emu_sn_m_v3_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_m_notpowered_sprites_left,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_m_v1_notpowered_sprites_left;
           1: emu_sn_m_v2_notpowered_sprites_left;
  LV_RAL6020: emu_sn_m_v3_notpowered_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_m_notpowered_sprites_right,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_m_v1_notpowered_sprites_right;
           1: emu_sn_m_v2_notpowered_sprites_right;
  LV_RAL6020: emu_sn_m_v3_notpowered_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_b_sprites_start,
[ set_offset_to(-2),
  ((LOAD_TEMP(4) != emu_sn) * 10) +
  (prev_vehicle_randombits() & 0x01) ])
{
   0: emu_sn_m_sprites_left_1;
   1: emu_sn_m_sprites_right_1;
  10: emu_sn_m_notpowered_sprites_left;
  11: emu_sn_m_notpowered_sprites_right;
      align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_h_sprites_right_1,
[ set_offset_to(-4),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_h_v1_sprites_right;
           1: emu_sn_h_v2_sprites_right;
  LV_RAL6020: emu_sn_h_v3_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_h_notpowered_normal_sprites_right,
[ set_offset_to(-4),
  prev_vehicle_cargo_subtype() ])
{
           0: emu_sn_h_v1_notpowered_normal_sprites_right;
           1: emu_sn_h_v2_notpowered_normal_sprites_right;
  LV_RAL6020: emu_sn_h_v3_notpowered_normal_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sn_c_sprites_start,
  position_in_consist_from_end == 1)
{
  0: emu_sn_h_notpowered_normal_sprites_right;
     emu_sn_h_sprites_right_1;
}

switch (FEAT_TRAINS, SELF, emu_sn_sprites_start,
  position_in_articulated_veh)
{
  0: emu_sn_h_sprites_left_check;
  2: emu_sn_b_sprites_start;
  4: emu_sn_c_sprites_start;
     dummy_sprites;
}

long_vehicle(emu_sn)

switch (FEAT_TRAINS, PARENT, emu_sn_can_attach_wagon1,
  count_veh_id(emu_sn) < 3)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, emu_sn_can_attach_wagon,
  vehicle_type_id == emu_sn)
{
  1: emu_sn_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, emu_sn_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return emu_sn_part2;
  3: return dummy4;
  4: return emu_sn_part2;
  5: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, emu_sn_create_effect2,
  electric_create_effect(4)) { return 1; }

switch (FEAT_TRAINS, PARENT, emu_sn_create_effect1,
  vehicle_type_id == emu_sn)
{
  1: emu_sn_create_effect2;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, emu_sn_create_effect,
  position_in_articulated_veh)
{
  2: emu_sn_create_effect1;
     return disable_create_effect();
}

  livery_template_base_list(emu_sn_alt, LV_RAL6020)
livery_template_alt(emu_sn, STR_REFIT_TTD_6C)
engine_power_MU(emu_sn, 921, emu_sn)
engine_capacity_MU(emu_sn, 296, emu_sn)
engine_speed(emu_sn, 120)

RC_head_check_new(emu_sn, emu_sn)
switch (FEAT_TRAINS, SELF, emu_sn_running_cost_factor,
[  STORE_TEMP(144, 0),  // Моторы
   STORE_TEMP(20, 1),   // Бригада
   STORE_TEMP(51, 2),   // Износ
   STORE_TEMP(0, 3),    // Сопровождение
   STORE_TEMP(35, 4),   // ТО
   STORE_TEMP(3, 5),    // Сертификация

   STORE_TEMP(120, 6),                 // Скорость
   STORE_TEMP(148, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 37, 8)])  // Максимальная масса
{ emu_sn_check_running_cost_factor; }  // 93 + 80 + 80

hint_MU_with_subtype(emu_sn,
     calc_loading(296 / (3 * 2 * DOUBLE_DOOR)),
     STR_PURCHASE_HINT_AGING_PERIOD_1_1,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_S_DC_WITH_SUBTYPE, STR_PURCHASE_HINT_62_08,
     STR_SECTIONS_MU_S1_3of3,
     1954, 1956,
     fact_rigacw())

item (FEAT_TRAINS, emu_sn, 218) {
  property {
    name: string(STR_NAME_SN);
    vehicle_dates(1954, 1956, 10, 10, 8, 12 + 10 + 10)
    vehicle_emu(dc, 921, 148.2, 100.0, 296, 3 * 2 * DOUBLE_DOOR)
  }
  graphics {
        additional_text: emu_sn_additional_text;
       articulated_part: emu_sn_articulated_part;
       can_attach_wagon: emu_sn_can_attach_wagon;
       cargo_age_period: return 60;
         cargo_capacity: emu_sn_cargo_capacity;
     cargo_subtype_text: emu_sn_cargo_subtype_text;
         colour_mapping: any_cc_colour;
          create_effect: emu_sn_create_effect;
                default: emu_sn_sprites;
                  power: emu_sn_power;
               purchase: emu_sn_purchase_sprites;
         purchase_speed: return 120;
    running_cost_factor: emu_sn_running_cost_factor;
                  speed: emu_sn_speed;
  }
}

long_name_template(emu_sn, STR_LONGNAME_SN)
allow_dcemu(emu_sn)

item (FEAT_TRAINS, emu_sn_part2, 459) {
  property {
    vehicle_no_dates(1954, 1956, 10, 10, 8, 0)
    vehicle_emu_empty(dc)
  }
  graphics {
   colour_mapping: any_cc_colour;
    create_effect: emu_sn_create_effect;
          default: emu_sn_sprites;
  }
}
