 // sn
// 218 (DAh) 

#define IMAGEFILE  "src/emu/sn.png"
purchase_sprites(sn, 0, 0)

MU_4r_0_0_sprites(12, sn_v1, 32, 40)
MU_0_3r_0_sprites(12, sn_v1, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/sn-br.png"
MU_4r_0_0_sprites(12, sn_v2, 32, 40)
MU_0_3r_0_sprites(12, sn_v2, 32, 40)
#undef IMAGEFILE

#define IMAGEFILE  "src/emu/sn-gr.png"
MU_4r_0_0_sprites(12, sn_v3, 32, 40)
MU_0_3r_0_sprites(12, sn_v3, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, sn_h_sprites_left_1,
  cargo_subtype)
{
  0: sn_v1_h_sprites_left;
  1: sn_v2_h_sprites_left;
  LV_RAL6020: sn_v3_h_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_h_notpowered_normal_sprites_left,
  cargo_subtype)
{
  0: sn_v1_h_notpowered_normal_sprites_left;
  1: sn_v2_h_notpowered_normal_sprites_left;
  LV_RAL6020: sn_v3_h_notpowered_normal_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_h_sprites_left_check,
  position_in_consist == 0)
{
  0: sn_h_notpowered_normal_sprites_left;
     sn_h_sprites_left_1;
}

random_switch (FEAT_TRAINS, SELF, sn_a_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: sn_h_sprites_left_check;
  1: sn_h_sprites_left_check;
}

switch (FEAT_TRAINS, SELF, sn_m_sprites_left_1,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_m_sprites_left;
  1: sn_v2_m_sprites_left;
  LV_RAL6020: sn_v3_m_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_m_sprites_right_1,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_m_sprites_right;
  1: sn_v2_m_sprites_right;
  LV_RAL6020: sn_v3_m_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_m_notpowered_sprites_left,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_m_notpowered_sprites_left;
  1: sn_v2_m_notpowered_sprites_left;
  LV_RAL6020: sn_v3_m_notpowered_sprites_left;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_m_notpowered_sprites_right,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_m_notpowered_sprites_right;
  1: sn_v2_m_notpowered_sprites_right;
  LV_RAL6020: sn_v3_m_notpowered_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_b_sprites_start,
[ set_offset_to(-2),
  ((LOAD_TEMP(4) != sn) * 10) +
  (prev_vehicle_randombits() % 2) ])
{
   0: sn_m_sprites_left_1;
   1: sn_m_sprites_right_1;
  10: sn_m_notpowered_sprites_left;
  11: sn_m_notpowered_sprites_right;
      align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_h_sprites_right_1,
[ set_offset_to(-4),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_h_sprites_right;
  1: sn_v2_h_sprites_right;
  LV_RAL6020: sn_v3_h_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_h_notpowered_normal_sprites_right,
[ set_offset_to(-4),
  prev_vehicle_cargo_subtype() ])
{
  0: sn_v1_h_notpowered_normal_sprites_right;
  1: sn_v2_h_notpowered_normal_sprites_right;
  LV_RAL6020: sn_v3_h_notpowered_normal_sprites_right;
  align_12_sprites;
}

switch (FEAT_TRAINS, SELF, sn_c_sprites_start,
  position_in_consist_from_end == 1)
{
  0: sn_h_notpowered_normal_sprites_right;
     sn_h_sprites_right_1;
}

switch (FEAT_TRAINS, SELF, sn_sprites_start, 
  position_in_articulated_veh)    
{ 
  0: sn_a_sprites_start;
  2: sn_b_sprites_start;                                        
  4: sn_c_sprites_start;                                        
     dummy_sprites; 
}                                           

long_vehicle_RND(sn, sn_a_sprites_start)

// считаем уже имеющиеся вагоны

switch (FEAT_TRAINS, PARENT, sn_can_attach_wagon1,
  count_veh_id(sn) < 3)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, sn_can_attach_wagon,
  vehicle_type_id == sn)
{
  1: sn_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, sn_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return sn_part2;
  3: return dummy4;
  4: return sn_part2;
  5: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Визуализируем эффекты тяги у sn, только если локомотив - sn
switch (FEAT_TRAINS, SELF, sn_create_effect2, 
  electric_create_effect(4)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, PARENT, sn_create_effect1,
  vehicle_type_id == sn)
{
  1: sn_create_effect2;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, sn_create_effect,
  position_in_articulated_veh)
{
  2: sn_create_effect1;
     return disable_create_effect();
}

// Даём тягу, только если локомотив - sn
switch (FEAT_TRAINS, PARENT, sn_power,
  vehicle_type_id == sn)
{
  1: return get_power_in_cb(921);
     return 0;
}

// Допускается эксплуатация с пассажирами только если голова - sn
switch (FEAT_TRAINS, PARENT, sn_cargo_capacity,
  vehicle_type_id == sn)
{
  1: return 296;
     return 0;
}

switch (FEAT_TRAINS, SELF, sn_cargo_subtype_text_long,
  cargo_subtype)
{
  1:          return string(STR_REFIT_FACTORY_6208, string(STR_REFIT_TTD_6C));
  LV_RAL6020: return string(STR_REFIT_FACTORY_6208, string(STR_REFIT_TTD_59));
              return string(STR_REFIT_FACTORY_6208, string(STR_REFIT_TTD_7C));
}

livery_template(sn_short, TTD_7C, LV_RAL6020, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(sn_short, TTD_6C)

switch (FEAT_TRAINS, SELF, sn_cargo_subtype_text,
  enable_long_names)
{
  1: sn_cargo_subtype_text_long;
     sn_short_cargo_subtype_text_pre;
}       

emu_running_cost(sn_v1, 93, 80 + 80)

switch (FEAT_TRAINS, PARENT, sn_running_cost_factor,
  vehicle_type_id == sn)
{
  1: sn_v1_running_cost_factor;
     return RC_MU_AS_CAR * 3;
}

engine_speed(sn, 120)

item (FEAT_TRAINS, sn, 218) {
  property {
    name: string(STR_NAME_SN);
    vehicle_dates(1954, 1956, 10, 10, 8, 12 + 10 + 10)
    vehicle_dcemu(921, 148.2, 100.0, 296, 24*2*3)
  }
  graphics {
    default: sn_sprites;
    purchase: sn_purchase_sprites;
    speed: sn_speed;
    purchase_speed: sn_purchase_speed;
    cargo_age_period: return 60;
    running_cost_factor: sn_running_cost_factor;
    articulated_part: sn_articulated_part;
    can_attach_wagon: sn_can_attach_wagon;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE_MU,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1),
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_EMU_S_DC_WITH_SUBTYPE, string(STR_PURCHASE_HINT_62_08)),
                                   string(STR_SECTIONS_MU_S1_3of3),
                                   string(STR_PURCHASE_HINT_FROMTO, 1954,1956),
                                   string(STR_PURCHASE_HINT_FACTORY_RIGA_CW));
    cargo_subtype_text: sn_cargo_subtype_text;
    cargo_capacity: sn_cargo_capacity;
    power: sn_power;
    create_effect: sn_create_effect;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(sn, STR_LONGNAME_SN)
allow_dcemu(sn)

item (FEAT_TRAINS, sn_part2, 459) {
  property {
    vehicle_no_dates(1954, 1956, 10, 10, 8, 0)
    vehicle_dcemu_empty()
  }
  graphics {
    default: sn_sprites;
    create_effect: sn_create_effect;
    colour_mapping: any_cc_colour;
  }
}
