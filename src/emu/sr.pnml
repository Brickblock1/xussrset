// emu_sr

#define IMAGEFILE  "src/emu/sr_h.png"
purchase_sprites(emu_sr_v1, 0, 0)
MU_head_middle_sprites(12, emu_sr_h_v1, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sr_h-bag.png"
MU_head_middle_sprites(12, emu_sr_h_v1u, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sr_m.png"
EMU_motor2_sprites(12, emu_sr_m_v1, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sr_h-a1951.png"
purchase_sprites(emu_sr_v2, 0, 0)
MU_head_middle_sprites(12, emu_sr_h_v2, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sr_h-a1951-bag.png"
MU_head_middle_sprites(12, emu_sr_h_v2u, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/sr_m-a1951.png"
EMU_motor2_sprites(12, emu_sr_m_v2, 32, 40)
#undef IMAGEFILE
purchase_sprites_select(emu_sr, 1951)

switch (FEAT_TRAINS, SELF, emu_sr_h_sprites_left_check,
  (position_in_consist == 0) +
  ((random_bits & 0x01) * 10) +
  ((build_year >= 1951) * 100))
{
    0: emu_sr_h_v1u_notpowered_normal_sprites_left;
    1: emu_sr_h_v1u_sprites_left;
   10: emu_sr_h_v1_notpowered_normal_sprites_left;
   11: emu_sr_h_v1_sprites_left;
  100: emu_sr_h_v2u_notpowered_normal_sprites_left;
  101: emu_sr_h_v2u_sprites_left;
  110: emu_sr_h_v2_notpowered_normal_sprites_left;
  111: emu_sr_h_v2_sprites_left;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sr_b_sprites_start,
[  set_offset_to(-2),
   (no_emu_s_inR4() * 10) +
   ((build_year >= 1951) * 100) +
   (prev_vehicle_randombits() & 0x01) ])
{
    0: emu_sr_m_v1_sprites_left;
    1: emu_sr_m_v1_sprites_right;
   10: emu_sr_m_v1_notpowered_sprites_left;
   11: emu_sr_m_v1_notpowered_sprites_right;
  100: emu_sr_m_v2_sprites_left;
  101: emu_sr_m_v2_sprites_right;
  110: emu_sr_m_v2_notpowered_sprites_left;
  111: emu_sr_m_v2_notpowered_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sr_c_sprites_start,
[ set_offset_to(-4),
  (position_in_consist_from_end == 1) +
  ((prev_vehicle_randombits() & 0x01) * 10) +
  ((build_year >= 1951) * 100)])
{
    0: emu_sr_h_v1_notpowered_normal_sprites_right;
    1: emu_sr_h_v1_sprites_right;
   10: emu_sr_h_v1u_notpowered_normal_sprites_right;
   11: emu_sr_h_v1u_sprites_right;
  100: emu_sr_h_v2_notpowered_normal_sprites_right;
  101: emu_sr_h_v2_sprites_right;
  110: emu_sr_h_v2u_notpowered_normal_sprites_right;
  111: emu_sr_h_v2u_sprites_right;
       align_12_sprites;
}

switch (FEAT_TRAINS, SELF, emu_sr_sprites_start,
  position_in_articulated_veh)
{
  0: emu_sr_h_sprites_left_check;
  2: emu_sr_b_sprites_start;
  4: emu_sr_c_sprites_start;
     dummy_sprites;
}

long_vehicle(emu_sr)

switch (FEAT_TRAINS, PARENT, emu_sr_can_attach_wagon1,
  emu_s_count() < 3)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, emu_sr_can_attach_wagon,
  any_emu_s())
{
  1: emu_sr_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, emu_sr_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return emu_sr_part2;
  3: return dummy4;
  4: return emu_sr_part3;
  5: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, emu_sr_create_effect2,
  electric_create_effect(4)) { return 1; }

switch (FEAT_TRAINS, PARENT, emu_sr_create_effect1,
  any_emu_s())
{
  1: emu_sr_create_effect2;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, emu_sr_create_effect,
  position_in_articulated_veh)
{
  2: emu_sr_create_effect1;
     return disable_create_effect();
}

engine_speed(emu_sr, 85)
engine_capacity_MU_emu_s(emu_sr, 306)
engine_power_MU_emu_s(emu_sr, 702)

RC_head_check_emu_s_new(emu_sr)
switch (FEAT_TRAINS, SELF, emu_sr_running_cost_factor,
[  STORE_TEMP(96, 0),  // Моторы
   STORE_TEMP(14, 1),  // Бригада
   STORE_TEMP(34, 2),  // Износ
   STORE_TEMP(0, 3),   // Сопровождение
   STORE_TEMP(24, 4),  // ТО
   STORE_TEMP(2, 5),   // Сертификация

   STORE_TEMP(85, 6),                  // Скорость
   STORE_TEMP(140, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 41, 8)])  // Максимальная масса
{ emu_sr_check_running_cost_factor; }  // 63+53 + 53

hint_MU_with_subtype(emu_sr,
     calc_loading(306 / (3 * 2 * DOUBLE_DOOR)),
     STR_PURCHASE_HINT_AGING_PERIOD_1_1,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_S_DDC_WITH_SUBTYPE, STR_PURCHASE_HINT_62_01,
     STR_SECTIONS_MU_S1_3of3,
     1947, 1952,
     fact_rigacw())

item (FEAT_TRAINS, emu_sr, 442) {
  property {
    name: string(STR_NAME_SR);
    vehicle_dates(1947, 1952, 20, 10, 8, 8 + 7 + 7)
    vehicle_emu(ddc, 702, 139.5, 96.1, 306, 3 * 2 * DOUBLE_DOOR)
  }
  graphics {
                    default: emu_sr_sprites;
                   purchase: emu_sr_purchase_sprites;
                      speed: emu_sr_speed;
           cargo_age_period: return 60;
        running_cost_factor: emu_sr_running_cost_factor;
           articulated_part: emu_sr_articulated_part;
           can_attach_wagon: emu_sr_can_attach_wagon;
             cargo_capacity: emu_sr_cargo_capacity;
                      power: emu_sr_power;
              create_effect: emu_sr_create_effect;
             colour_mapping: any_cc_colour;
             purchase_speed: return 85;
            additional_text: emu_sr_additional_text;
  }
}

long_name_template(emu_sr, STR_LONGNAME_SR)
allow_dcemu(emu_sr)

item (FEAT_TRAINS, emu_sr_part2, 461) {
  property {
    vehicle_no_dates(1947, 1952, 20, 10, 8, 0)
    vehicle_emu_c_mail(ddc, 0, 20, 10)
  }
  graphics {
             default: emu_sr_sprites;
    cargo_age_period: return 60;
       create_effect: emu_sr_create_effect;
      colour_mapping: any_cc_colour;
  }
}

item (FEAT_TRAINS, emu_sr_part3, 755) {
  property {
    vehicle_no_dates(1947, 1952, 20, 10, 8, 0)
    vehicle_emu_empty(ddc)
  }
  graphics {
             default: emu_sr_sprites;
       create_effect: emu_sr_create_effect;
      colour_mapping: any_cc_colour;
  }
}
