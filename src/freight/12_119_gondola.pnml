 // 12_119_gondola

/// Вагон
// 12-119 полувагон (1986-1990)
// gondola

#define IMAGEFILE  "src/freight/12-119-gondola-1986-1990.png"

// покупка
spriteset (_12_119_gondola_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(102, 56, 0)
}

// пусто
spriteset (_12_119_gondola_empty_spriteset, IMAGEFILE)
{
  s8_template(102, 56, 0)
}

spriteset (_12_119_gondola_v2_empty_spriteset, IMAGEFILE)
{
  s8_template(102, 92, 0)
}

// шаблон груза, 2 ливреи, 3 вида загруженности
// 25-49%, 50-74%, полный
#define _12_119_sprite_layout_template(x, y, cargo) \
spriteset (_12_119_gondola_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (_12_119_gondola_v2_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+36, 0) \
} \
 \
spriteset (_12_119_gondola_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y, 0) \
} \
 \
spriteset (_12_119_gondola_v2_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+36, 0) \
} \
 \
spriteset (_12_119_gondola_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y, 0) \
} \
 \
spriteset (_12_119_gondola_v2_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+36, 0) \
} \
 \
spritegroup _12_119_gondola_##cargo##_spritegroup \
{ \
  loading: [_12_119_gondola_empty_spriteset, \
    _12_119_gondola_##cargo##_1_spriteset, \
    _12_119_gondola_##cargo##_2_spriteset, \
    _12_119_gondola_##cargo##_full_spriteset]; \
  loaded: [_12_119_gondola_empty_spriteset, \
    _12_119_gondola_##cargo##_1_spriteset, \
    _12_119_gondola_##cargo##_2_spriteset, \
    _12_119_gondola_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_119_gondola_v2_##cargo##_spritegroup \
{ \
  loading: [_12_119_gondola_v2_empty_spriteset, \
    _12_119_gondola_v2_##cargo##_1_spriteset, \
    _12_119_gondola_v2_##cargo##_2_spriteset, \
    _12_119_gondola_v2_##cargo##_full_spriteset]; \
  loaded: [_12_119_gondola_v2_empty_spriteset, \
    _12_119_gondola_v2_##cargo##_1_spriteset, \
    _12_119_gondola_v2_##cargo##_2_spriteset, \
    _12_119_gondola_v2_##cargo##_full_spriteset]; \
} \

_12_119_sprite_layout_template(400, 56, coal)    // уголь
_12_119_sprite_layout_template(400, 174, iore)   // жел. руда
_12_119_sprite_layout_template(400, 292, core)   // медн. руда
_12_119_sprite_layout_template(400, 410, aore)   // бокситы
_12_119_sprite_layout_template(400, 528, clay)   // глина
_12_119_sprite_layout_template(400, 646, fert)   // удобрения
_12_119_sprite_layout_template(400, 764, grvl)   // гравий
_12_119_sprite_layout_template(400, 882, lime)   // известняк
_12_119_sprite_layout_template(400, 1000, pota)  // поташ
_12_119_sprite_layout_template(400, 1118, sand)  // песок
_12_119_sprite_layout_template(400, 1236, sulp)  // сера
_12_119_sprite_layout_template(400, 1354, brck)  // кирпичи
_12_119_sprite_layout_template(400, 1472, wdpr)  // лесоматериалы
_12_119_sprite_layout_template(400, 1590, stel)  // сталь
_12_119_sprite_layout_template(400, 1708, stel2) // сталь
_12_119_sprite_layout_template(400, 1826, stel3) // сталь
_12_119_sprite_layout_template(400, 1944, stel4) // сталь

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, _12_119_gondola_get_spriteset4,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_119_gondola_v2_stel_spritegroup;
  32: _12_119_gondola_v2_stel2_spritegroup;
  32: _12_119_gondola_v2_stel3_spritegroup;
  32: _12_119_gondola_v2_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_119_gondola_get_spriteset3,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_119_gondola_stel_spritegroup;
  32: _12_119_gondola_stel2_spritegroup;
  32: _12_119_gondola_stel3_spritegroup;
  32: _12_119_gondola_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, _12_119_gondola_get_spriteset2,
  cargo_type_in_veh)
{
  COAL: _12_119_gondola_v2_coal_spritegroup;
  IORE: _12_119_gondola_v2_iore_spritegroup;
  CORE: _12_119_gondola_v2_core_spritegroup;
  AORE: _12_119_gondola_v2_aore_spritegroup;
  CLAY: _12_119_gondola_v2_clay_spritegroup;
  FERT: _12_119_gondola_v2_fert_spritegroup;
  GRVL: _12_119_gondola_v2_grvl_spritegroup;
  LIME: _12_119_gondola_v2_lime_spritegroup;
  POTA: _12_119_gondola_v2_pota_spritegroup;
  SAND: _12_119_gondola_v2_sand_spritegroup;
  SULP: _12_119_gondola_v2_sulp_spritegroup;
  BRCK: _12_119_gondola_v2_brck_spritegroup;
  WDPR: _12_119_gondola_v2_wdpr_spritegroup;
  STEL: _12_119_gondola_get_spriteset4;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_119_gondola_get_spriteset1,
  cargo_type_in_veh)
{
  COAL: _12_119_gondola_coal_spritegroup;
  IORE: _12_119_gondola_iore_spritegroup;
  CORE: _12_119_gondola_core_spritegroup;
  AORE: _12_119_gondola_aore_spritegroup;
  CLAY: _12_119_gondola_clay_spritegroup;
  FERT: _12_119_gondola_fert_spritegroup;
  GRVL: _12_119_gondola_grvl_spritegroup;
  LIME: _12_119_gondola_lime_spritegroup;
  POTA: _12_119_gondola_pota_spritegroup;
  SAND: _12_119_gondola_sand_spritegroup;
  SULP: _12_119_gondola_sulp_spritegroup;
  BRCK: _12_119_gondola_brck_spritegroup;
  WDPR: _12_119_gondola_wdpr_spritegroup;
  STEL: _12_119_gondola_get_spriteset3;
  align_8_spriteset;
}


switch (FEAT_TRAINS, SELF, _12_119_gondola_get_spriteset,
  cargo_subtype
)
{
  0: _12_119_gondola_get_spriteset1;
  1: _12_119_gondola_get_spriteset2;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_119_gondola_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_OCHER);
  1: string(STR_REFIT_DARK_GREEN_MALE);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _12_119_gondola) 
{
  property
  {
    name: string(STR_12_119_GONDOLA_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: 22;
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, BRCK, CORE, GRVL, AORE, CLAY, LIME,
      POTA, SAND, SULP, WDPR, STEL];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    speed: 120 km/h;
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 19;
    cargo_capacity: 69;
    weight: 23 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _12_119_gondola_get_spriteset;
    purchase: _12_119_gondola_purchase_spriteset;
    additional_text: return string(STR_12_119_GONDOLA_PURCHASE_HINT);
    cargo_subtype_text: _12_119_gondola_cargo_subtype_text;
  }
}
