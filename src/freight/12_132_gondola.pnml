 // 12_132_gondola

/// Вагон
// 12-132 полувагон (1992-)
// gondola

#define IMAGEFILE  "src/freight/12-132-gondola-1992.png"

// покупка
spriteset (_12_132_gondola_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(102, 56, 0)
}

// пусто
spriteset (_12_132_gondola_empty_spriteset, IMAGEFILE)
{
  s8_template(102, 56, 0)
}

spriteset (_12_132_gondola_v2_empty_spriteset, IMAGEFILE)
{
  s8_template(102, 92, 0)
}

spriteset (_12_132_gondola_v3_empty_spriteset, IMAGEFILE)
{
  s8_template(102, 128, 0)
}

// шаблон груза, 3 ливреи, 3 вида загруженности
// 25-49%, 50-74%, полный
#define _12_132_sprite_layout_template(x, y, cargo) \
spriteset (_12_132_gondola_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (_12_132_gondola_v2_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+36, 0) \
} \
 \
spriteset (_12_132_gondola_v3_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+72, 0) \
} \
 \
spriteset (_12_132_gondola_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y, 0) \
} \
 \
spriteset (_12_132_gondola_v2_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+36, 0) \
} \
 \
spriteset (_12_132_gondola_v3_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+72, 0) \
} \
 \
spriteset (_12_132_gondola_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y, 0) \
} \
 \
spriteset (_12_132_gondola_v2_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+36, 0) \
} \
 \
spriteset (_12_132_gondola_v3_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+72, 0) \
} \
 \
spritegroup _12_132_gondola_##cargo##_spritegroup \
{ \
  loading: [_12_132_gondola_empty_spriteset, \
    _12_132_gondola_##cargo##_1_spriteset, \
    _12_132_gondola_##cargo##_2_spriteset, \
    _12_132_gondola_##cargo##_full_spriteset]; \
  loaded: [_12_132_gondola_empty_spriteset, \
    _12_132_gondola_##cargo##_1_spriteset, \
    _12_132_gondola_##cargo##_2_spriteset, \
    _12_132_gondola_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_132_gondola_v2_##cargo##_spritegroup \
{ \
  loading: [_12_132_gondola_v2_empty_spriteset, \
    _12_132_gondola_v2_##cargo##_1_spriteset, \
    _12_132_gondola_v2_##cargo##_2_spriteset, \
    _12_132_gondola_v2_##cargo##_full_spriteset]; \
  loaded: [_12_132_gondola_v2_empty_spriteset, \
    _12_132_gondola_v2_##cargo##_1_spriteset, \
    _12_132_gondola_v2_##cargo##_2_spriteset, \
    _12_132_gondola_v2_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_132_gondola_v3_##cargo##_spritegroup \
{ \
  loading: [_12_132_gondola_v3_empty_spriteset, \
    _12_132_gondola_v3_##cargo##_1_spriteset, \
    _12_132_gondola_v3_##cargo##_2_spriteset, \
    _12_132_gondola_v3_##cargo##_full_spriteset]; \
  loaded: [_12_132_gondola_v3_empty_spriteset, \
    _12_132_gondola_v3_##cargo##_1_spriteset, \
    _12_132_gondola_v3_##cargo##_2_spriteset, \
    _12_132_gondola_v3_##cargo##_full_spriteset]; \
} \


_12_132_sprite_layout_template(400, 56, coal)    // уголь
_12_132_sprite_layout_template(400, 174, iore)   // жел. руда
_12_132_sprite_layout_template(400, 292, core)   // медн. руда
_12_132_sprite_layout_template(400, 410, aore)   // бокситы
_12_132_sprite_layout_template(400, 528, clay)   // глина
_12_132_sprite_layout_template(400, 646, fert)   // удобрения
_12_132_sprite_layout_template(400, 764, grvl)   // гравий
_12_132_sprite_layout_template(400, 882, lime)   // известняк
_12_132_sprite_layout_template(400, 1000, pota)  // поташ
_12_132_sprite_layout_template(400, 1118, sand)  // песок
_12_132_sprite_layout_template(400, 1236, sulp)  // сера
_12_132_sprite_layout_template(400, 1354, brck)  // кирпичи
_12_132_sprite_layout_template(400, 1472, wdpr)  // лесоматериалы
_12_132_sprite_layout_template(400, 1590, stel)  // сталь
_12_132_sprite_layout_template(400, 1708, stel2) // сталь
_12_132_sprite_layout_template(400, 1826, stel3) // сталь
_12_132_sprite_layout_template(400, 1944, stel4) // сталь

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset7,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_132_gondola_v3_stel_spritegroup;
  32: _12_132_gondola_v3_stel2_spritegroup;
  32: _12_132_gondola_v3_stel3_spritegroup;
  32: _12_132_gondola_v3_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset6,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_132_gondola_v2_stel_spritegroup;
  32: _12_132_gondola_v2_stel2_spritegroup;
  32: _12_132_gondola_v2_stel3_spritegroup;
  32: _12_132_gondola_v2_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset5,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_132_gondola_stel_spritegroup;
  32: _12_132_gondola_stel2_spritegroup;
  32: _12_132_gondola_stel3_spritegroup;
  32: _12_132_gondola_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset4,
  cargo_type_in_veh)
{
  COAL: _12_132_gondola_v3_coal_spritegroup;
  IORE: _12_132_gondola_v3_iore_spritegroup;
  CORE: _12_132_gondola_v3_core_spritegroup;
  AORE: _12_132_gondola_v3_aore_spritegroup;
  CLAY: _12_132_gondola_v3_clay_spritegroup;
  FERT: _12_132_gondola_v3_fert_spritegroup;
  GRVL: _12_132_gondola_v3_grvl_spritegroup;
  LIME: _12_132_gondola_v3_lime_spritegroup;
  POTA: _12_132_gondola_v3_pota_spritegroup;
  SAND: _12_132_gondola_v3_sand_spritegroup;
  SULP: _12_132_gondola_v3_sulp_spritegroup;
  BRCK: _12_132_gondola_v3_brck_spritegroup;
  WDPR: _12_132_gondola_v3_wdpr_spritegroup;
  STEL: _12_132_gondola_get_spriteset7;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset3,
  cargo_type_in_veh)
{
  COAL: _12_132_gondola_v2_coal_spritegroup;
  IORE: _12_132_gondola_v2_iore_spritegroup;
  CORE: _12_132_gondola_v2_core_spritegroup;
  AORE: _12_132_gondola_v2_aore_spritegroup;
  CLAY: _12_132_gondola_v2_clay_spritegroup;
  FERT: _12_132_gondola_v2_fert_spritegroup;
  GRVL: _12_132_gondola_v2_grvl_spritegroup;
  LIME: _12_132_gondola_v2_lime_spritegroup;
  POTA: _12_132_gondola_v2_pota_spritegroup;
  SAND: _12_132_gondola_v2_sand_spritegroup;
  SULP: _12_132_gondola_v2_sulp_spritegroup;
  BRCK: _12_132_gondola_v2_brck_spritegroup;
  WDPR: _12_132_gondola_v2_wdpr_spritegroup;
  STEL: _12_132_gondola_get_spriteset6;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset2,
  cargo_type_in_veh)
{
  COAL: _12_132_gondola_coal_spritegroup;
  IORE: _12_132_gondola_iore_spritegroup;
  CORE: _12_132_gondola_core_spritegroup;
  AORE: _12_132_gondola_aore_spritegroup;
  CLAY: _12_132_gondola_clay_spritegroup;
  FERT: _12_132_gondola_fert_spritegroup;
  GRVL: _12_132_gondola_grvl_spritegroup;
  LIME: _12_132_gondola_lime_spritegroup;
  POTA: _12_132_gondola_pota_spritegroup;
  SAND: _12_132_gondola_sand_spritegroup;
  SULP: _12_132_gondola_sulp_spritegroup;
  BRCK: _12_132_gondola_brck_spritegroup;
  WDPR: _12_132_gondola_wdpr_spritegroup;
  STEL: _12_132_gondola_get_spriteset5;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_132_gondola_get_spriteset,
  cargo_subtype
)
{
  0: _12_132_gondola_get_spriteset2;
  1: _12_132_gondola_get_spriteset3;
  2: _12_132_gondola_get_spriteset4;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_132_gondola_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RAL_6020);
  1: string(STR_REFIT_OCHER);
  2: string(STR_REFIT_GREEN);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _12_132_gondola) 
{
  property
  {
    name: string(STR_12_132_GONDOLA_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1992), 1, 1);
    model_life: 22;
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, BRCK, CORE, GRVL, AORE, CLAY, FERT, LIME,
      POTA, SAND, SULP, WDPR, STEL];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 70;
    weight: 24 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _12_132_gondola_get_spriteset;
    purchase: _12_132_gondola_purchase_spriteset;
    additional_text: return string(STR_12_132_GONDOLA_PURCHASE_HINT);
    cargo_subtype_text: _12_132_gondola_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
