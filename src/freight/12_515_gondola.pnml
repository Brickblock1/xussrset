 // 12_515_gondola

/// Вагон
// 12-515 полувагон (1964-1974)
// gondola

#define IMAGEFILE  "src/freight/12-515-gondola-1964-1974.png"

// покупка
spriteset (_12_515_gondola_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(125, 56, 0)
}

// пусто
spriteset (_12_515_gondola_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 56, 0)
}

spriteset (_12_515_gondola_v2_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 92, 0)
}

spriteset (_12_515_gondola_v3_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 128, 0)
}

spriteset (_12_515_gondola_v4_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 164, 0)
}

// шаблон груза, 4 ливреи, 3 вида загруженности
// 25-49%, 50-74%, полный
#define _12_515_sprite_layout_template(x, y, cargo) \
spriteset (_12_515_gondola_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (_12_515_gondola_v2_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+36, 0) \
} \
 \
spriteset (_12_515_gondola_v3_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+72, 0) \
} \
 \
spriteset (_12_515_gondola_v4_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+108, 0) \
} \
 \
spriteset (_12_515_gondola_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y, 0) \
} \
 \
spriteset (_12_515_gondola_v2_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+36, 0) \
} \
 \
spriteset (_12_515_gondola_v3_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+72, 0) \
} \
 \
spriteset (_12_515_gondola_v4_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+108, 0) \
} \
 \
spriteset (_12_515_gondola_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y, 0) \
} \
 \
spriteset (_12_515_gondola_v2_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+36, 0) \
} \
 \
spriteset (_12_515_gondola_v3_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+72, 0) \
} \
 \
spriteset (_12_515_gondola_v4_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+108, 0) \
} \
 \
spritegroup _12_515_gondola_##cargo##_spritegroup \
{ \
  loading: [_12_515_gondola_empty_spriteset, \
    _12_515_gondola_##cargo##_1_spriteset, \
    _12_515_gondola_##cargo##_2_spriteset, \
    _12_515_gondola_##cargo##_full_spriteset]; \
  loaded: [_12_515_gondola_empty_spriteset, \
    _12_515_gondola_##cargo##_1_spriteset, \
    _12_515_gondola_##cargo##_2_spriteset, \
    _12_515_gondola_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_515_gondola_v2_##cargo##_spritegroup \
{ \
  loading: [_12_515_gondola_v2_empty_spriteset, \
    _12_515_gondola_v2_##cargo##_1_spriteset, \
    _12_515_gondola_v2_##cargo##_2_spriteset, \
    _12_515_gondola_v2_##cargo##_full_spriteset]; \
  loaded: [_12_515_gondola_v2_empty_spriteset, \
    _12_515_gondola_v2_##cargo##_1_spriteset, \
    _12_515_gondola_v2_##cargo##_2_spriteset, \
    _12_515_gondola_v2_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_515_gondola_v3_##cargo##_spritegroup \
{ \
  loading: [_12_515_gondola_v3_empty_spriteset, \
    _12_515_gondola_v3_##cargo##_1_spriteset, \
    _12_515_gondola_v3_##cargo##_2_spriteset, \
    _12_515_gondola_v3_##cargo##_full_spriteset]; \
  loaded: [_12_515_gondola_v3_empty_spriteset, \
    _12_515_gondola_v3_##cargo##_1_spriteset, \
    _12_515_gondola_v3_##cargo##_2_spriteset, \
    _12_515_gondola_v3_##cargo##_full_spriteset]; \
} \
 \
spritegroup _12_515_gondola_v4_##cargo##_spritegroup \
{ \
  loading: [_12_515_gondola_v4_empty_spriteset, \
    _12_515_gondola_v4_##cargo##_1_spriteset, \
    _12_515_gondola_v4_##cargo##_2_spriteset, \
    _12_515_gondola_v4_##cargo##_full_spriteset]; \
  loaded: [_12_515_gondola_v4_empty_spriteset, \
    _12_515_gondola_v4_##cargo##_1_spriteset, \
    _12_515_gondola_v4_##cargo##_2_spriteset, \
    _12_515_gondola_v4_##cargo##_full_spriteset]; \
} \


_12_515_sprite_layout_template(423, 56, coal)    // уголь
_12_515_sprite_layout_template(423, 205, iore)   // жел. руда
_12_515_sprite_layout_template(423, 354, core)   // медн. руда
_12_515_sprite_layout_template(423, 503, aore)   // бокситы
_12_515_sprite_layout_template(423, 652, clay)   // глина
_12_515_sprite_layout_template(423, 801, fert)   // удобрения
_12_515_sprite_layout_template(423, 950, grvl)   // гравий
_12_515_sprite_layout_template(423, 1099, lime)  // известняк
_12_515_sprite_layout_template(423, 1248, pota)  // поташ
_12_515_sprite_layout_template(423, 1397, sand)  // песок
_12_515_sprite_layout_template(423, 1546, sulp)  // сера
_12_515_sprite_layout_template(423, 1695, brck)  // кирпичи
_12_515_sprite_layout_template(423, 1844, wdpr)  // лесоматериалы
_12_515_sprite_layout_template(423, 1993, stel)  // сталь
_12_515_sprite_layout_template(423, 2142, stel2) // сталь
_12_515_sprite_layout_template(423, 2291, stel3) // сталь
_12_515_sprite_layout_template(423, 2440, stel4) // сталь
_12_515_sprite_layout_template(423, 2589, wood)  // древесина

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset9,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_515_gondola_v4_stel_spritegroup;
  32: _12_515_gondola_v4_stel2_spritegroup;
  32: _12_515_gondola_v4_stel3_spritegroup;
  32: _12_515_gondola_v4_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset8,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_515_gondola_v3_stel_spritegroup;
  32: _12_515_gondola_v3_stel2_spritegroup;
  32: _12_515_gondola_v3_stel3_spritegroup;
  32: _12_515_gondola_v3_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset7,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_515_gondola_v2_stel_spritegroup;
  32: _12_515_gondola_v2_stel2_spritegroup;
  32: _12_515_gondola_v2_stel3_spritegroup;
  32: _12_515_gondola_v2_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset6,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: _12_515_gondola_stel_spritegroup;
  32: _12_515_gondola_stel2_spritegroup;
  32: _12_515_gondola_stel3_spritegroup;
  32: _12_515_gondola_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset5,
  cargo_type_in_veh)
{
  COAL: _12_515_gondola_v4_coal_spritegroup;
  IORE: _12_515_gondola_v4_iore_spritegroup;
  CORE: _12_515_gondola_v4_core_spritegroup;
  AORE: _12_515_gondola_v4_aore_spritegroup;
  CLAY: _12_515_gondola_v4_clay_spritegroup;
  FERT: _12_515_gondola_v4_fert_spritegroup;
  GRVL: _12_515_gondola_v4_grvl_spritegroup;
  LIME: _12_515_gondola_v4_lime_spritegroup;
  POTA: _12_515_gondola_v4_pota_spritegroup;
  SAND: _12_515_gondola_v4_sand_spritegroup;
  SULP: _12_515_gondola_v4_sulp_spritegroup;
  BRCK: _12_515_gondola_v4_brck_spritegroup;
  WDPR: _12_515_gondola_v4_wdpr_spritegroup;
  STEL: _12_515_gondola_get_spriteset9;
  WOOD: _12_515_gondola_v4_wood_spritegroup;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset4,
  cargo_type_in_veh)
{
  COAL: _12_515_gondola_v3_coal_spritegroup;
  IORE: _12_515_gondola_v3_iore_spritegroup;
  CORE: _12_515_gondola_v3_core_spritegroup;
  AORE: _12_515_gondola_v3_aore_spritegroup;
  CLAY: _12_515_gondola_v3_clay_spritegroup;
  FERT: _12_515_gondola_v3_fert_spritegroup;
  GRVL: _12_515_gondola_v3_grvl_spritegroup;
  LIME: _12_515_gondola_v3_lime_spritegroup;
  POTA: _12_515_gondola_v3_pota_spritegroup;
  SAND: _12_515_gondola_v3_sand_spritegroup;
  SULP: _12_515_gondola_v3_sulp_spritegroup;
  BRCK: _12_515_gondola_v3_brck_spritegroup;
  WDPR: _12_515_gondola_v3_wdpr_spritegroup;
  STEL: _12_515_gondola_get_spriteset8;
  WOOD: _12_515_gondola_v3_wood_spritegroup;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset3,
  cargo_type_in_veh)
{
  COAL: _12_515_gondola_v2_coal_spritegroup;
  IORE: _12_515_gondola_v2_iore_spritegroup;
  CORE: _12_515_gondola_v2_core_spritegroup;
  AORE: _12_515_gondola_v2_aore_spritegroup;
  CLAY: _12_515_gondola_v2_clay_spritegroup;
  FERT: _12_515_gondola_v2_fert_spritegroup;
  GRVL: _12_515_gondola_v2_grvl_spritegroup;
  LIME: _12_515_gondola_v2_lime_spritegroup;
  POTA: _12_515_gondola_v2_pota_spritegroup;
  SAND: _12_515_gondola_v2_sand_spritegroup;
  SULP: _12_515_gondola_v2_sulp_spritegroup;
  BRCK: _12_515_gondola_v2_brck_spritegroup;
  WDPR: _12_515_gondola_v2_wdpr_spritegroup;
  STEL: _12_515_gondola_get_spriteset7;
  WOOD: _12_515_gondola_v2_wood_spritegroup;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset2,
  cargo_type_in_veh)
{
  COAL: _12_515_gondola_coal_spritegroup;
  IORE: _12_515_gondola_iore_spritegroup;
  CORE: _12_515_gondola_core_spritegroup;
  AORE: _12_515_gondola_aore_spritegroup;
  CLAY: _12_515_gondola_clay_spritegroup;
  FERT: _12_515_gondola_fert_spritegroup;
  GRVL: _12_515_gondola_grvl_spritegroup;
  LIME: _12_515_gondola_lime_spritegroup;
  POTA: _12_515_gondola_pota_spritegroup;
  SAND: _12_515_gondola_sand_spritegroup;
  SULP: _12_515_gondola_sulp_spritegroup;
  BRCK: _12_515_gondola_brck_spritegroup;
  WDPR: _12_515_gondola_wdpr_spritegroup;
  STEL: _12_515_gondola_get_spriteset6;
  WOOD: _12_515_gondola_wood_spritegroup;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_get_spriteset,
  cargo_subtype
)
{
  0: _12_515_gondola_get_spriteset2;
  1: _12_515_gondola_get_spriteset3;
  2: _12_515_gondola_get_spriteset4;
  3: _12_515_gondola_get_spriteset5;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_cargo_subtype_text2,
  build_year >= 1975)
{
  1: string(STR_REFIT_OCHER);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, _12_515_gondola_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_WOOD_PANELING);
  1: string(STR_REFIT_WOOD_PANELING2);
  2: _12_515_gondola_cargo_subtype_text2;
  3: string(STR_REFIT_GREEN_GRASS);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _12_515_gondola) 
{
  property
  {
    name: string(STR_12_515_GONDOLA_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1964), 1, 1);
    model_life: 22;
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, BRCK, CORE, GRVL, AORE, CLAY, FERT, LIME,
      POTA, SAND, SULP, WDPR, STEL, WOOD];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 65;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _12_515_gondola_get_spriteset;
    purchase: _12_515_gondola_purchase_spriteset;
    additional_text: return string(STR_12_515_GONDOLA_PURCHASE_HINT);
    cargo_subtype_text: _12_515_gondola_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
