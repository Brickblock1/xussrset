 // 15_871_tanker

/// Вагон
// 15-871 8-осная цистерна для бензина (1974-1987)
// tanker petrol

#define IMAGEFILE  "src/freight/15-871-8axis-tanker-petrol-1974-1987.png"

// покупка
spriteset (_15_871_tanker_purchase_spriteset, IMAGEFILE)
{
  s12_purchase_template(0, 25, -8)
}

// вид 1
spriteset (_15_871_tanker_spriteset, IMAGEFILE)
{
  s12_template(0, 25, 0)
}

// вид 2
spriteset (_15_871_tanker_v2_spriteset, IMAGEFILE)
{
  s12_template(0, 92, 0)
}

// вид 3
spriteset (_15_871_tanker_v3_spriteset, IMAGEFILE)
{
  s12_template(0, 159, 0)
}

// вид 4
spriteset (_15_871_tanker_v4_spriteset, IMAGEFILE)
{
  s12_template(0, 226, 0)
}

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, _15_871_tanker_get_spriteset1,
  cargo_subtype
)
{
  0: _15_871_tanker_spriteset;
  1: _15_871_tanker_v2_spriteset;
  2: _15_871_tanker_v3_spriteset;
  3: _15_871_tanker_v4_spriteset;
  align_12_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _15_871_tanker_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _15_871_tanker_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _15_871_tanker_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_YELLOW_MALE);
  1: string(STR_REFIT_PALE_YELLOW_2_MALE);
  2: string(STR_REFIT_OCHER);
  3: string(STR_REFIT_BLUE_MALE);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _15_871_tanker) 
{
  property
  {
    name: string(STR_15_871_TANKER_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1974), 1, 1);
    model_life: 32;
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [PETR, RFPR];
    default_cargo_type: PETR;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    speed: 120 km/h;
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 30;
    cargo_capacity: 120;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _15_871_tanker_get_spriteset;
    purchase: _15_871_tanker_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_15_871_TANKER_PURCHASE_HINT);
    cargo_subtype_text: _15_871_tanker_cargo_subtype_text;
  }
}
