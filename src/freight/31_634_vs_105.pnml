 // 31_634_vs_105

/// Вагон
// 31-634 6-осный думпкар типа ВС-105
// dumpcar

#define IMAGEFILE  "src/freight/31-634-6axis-dumpcar-vs-105.png"

// покупка
spriteset (_31_634_vs_105_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(0, 18, 0)
}

// вид 1
spriteset (_31_634_vs_105_spriteset, IMAGEFILE)
{
  s9_template(0, 18, 0)
}

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _31_634_vs_105_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _31_634_vs_105_spriteset;
}

// 70 км/ч; (120 км/ч пустой)
switch (FEAT_TRAINS, SELF, _31_634_vs_105_speed,
  cargo_count)
{
  0: return 120;
  return 70;
}

// 90 тонн, построенным с 1974 г. - 105 тонн
switch (FEAT_TRAINS, SELF, _31_634_vs_105_cargo_capacity,
  build_year >= 1974)
{
  1: return 105;
  return 90;
}

item (FEAT_TRAINS, _31_634_vs_105) 
{
  property
  {
    name: string(STR_31_634_VS_105_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1967), 1, 1);
    model_life: 22;
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, CORE, GOLD, GRVL, AORE, CLAY, LIME,
      POTA, SAND, SULP];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 23;
    cargo_capacity: 90;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _31_634_vs_105_get_spriteset;
    purchase: _31_634_vs_105_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_31_634_VS_105_PURCHASE_HINT);
    speed: _31_634_vs_105_speed;
    cargo_capacity: _31_634_vs_105_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
