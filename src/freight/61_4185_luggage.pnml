 // 61_4185_luggage

/// Вагон
//  61-4185. Багажный вагон Тверского вагоностроительного завода,
//  выпускается с 1998 года.

#define IMAGEFILE  "src/freight/61-4185-luggage-1998.png"

// покупка
spriteset (_61_4185_luggage_purchase_spriteset, IMAGEFILE)
{
  s14_purchase_template(0, 39, -12)
}

// вид 1
spriteset (_61_4185_luggage_spriteset, IMAGEFILE)
{
  s14_template(0, 39, 0)
}

// вид 2
spriteset (_61_4185_luggage_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 106, 0)
}

// Реверс: (430, y, 0)

// Хвост: вид 1
spriteset (_61_4185_luggage_tail_spriteset, IMAGEFILE)
{
  s14_template(0, 183, 0)
}

// вид 2
spriteset (_61_4185_luggage_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 250, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _61_4185_luggage_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: _61_4185_luggage_spriteset;
  1: _61_4185_luggage_tail_spriteset;
  2: _61_4185_luggage_v2_spriteset;
  3: _61_4185_luggage_tail_v2_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, _61_4185_luggage_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  _61_4185_luggage_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, _61_4185_luggage_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_REFIT_RAL_6020);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, _61_4185_luggage) 
{
  property
  {
    name: string(STR_61_4185_LUGGAGE_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1998), 1, 1);
    model_life: 28;
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, GOLD, VALU];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 40;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: _61_4185_luggage_get_spriteset;
    purchase: _61_4185_luggage_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_61_4185_LUGGAGE_PURCHASE_HINT);
    cargo_subtype_text: _61_4185_luggage_cargo_subtype_text;
  }
}
