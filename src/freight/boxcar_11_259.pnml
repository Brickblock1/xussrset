 // boxcar_11_259

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-259-1984.png"

// покупка
spriteset (boxcar_11_259_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_259_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_259##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_259_1##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_259_2##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_259_3##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_259##v##_spritegroup \
{ \
  loading: [boxcar_11_259_1##v##_spriteset, \
    boxcar_11_259_1##v##_spriteset, \
    boxcar_11_259_1##v##_spriteset, \
    boxcar_11_259_2##v##_spriteset, \
    boxcar_11_259_2##v##_spriteset, \
    boxcar_11_259_2##v##_spriteset, \
    boxcar_11_259_3##v##_spriteset, \
    boxcar_11_259_3##v##_spriteset, \
    boxcar_11_259_3##v##_spriteset, \
    boxcar_11_259##v##_spriteset]; \
  loaded: [boxcar_11_259##v##_spriteset]; \
} \



// ливреи
boxcar_11_259_sprite_layout_template(0, 38,)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_259_get_spriteset2,
  cargo_subtype)
{
  0..4: boxcar_11_259_spritegroup;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_259_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_259_get_spriteset2;
}

// cb colour_mapping
random_switch (FEAT_TRAINS, SELF, boxcar_11_259_random, 0 )
{
  1: return ttd_cc + 25;
  7: return ttd_cc + 27;
  5: return ttd_cc + 32;
  1: return ttd_cc + 40;
}                      

switch (FEAT_TRAINS, SELF, boxcar_11_259_colour_mapping,
  cargo_subtype)
{
  0: boxcar_11_259_random;
  1: return ttd_cc + 25;
  2: return ttd_cc + 27;
  3: return ttd_cc + 32;
  4: return ttd_cc + 40;
  return CB_FAILED;
}

// cb cargo_subtype_text
switch (FEAT_TRAINS, SELF, boxcar_11_259_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_4C);
  2: string(STR_REFIT_TTD_53);
  3: string(STR_REFIT_TTD_62);
  4: string(STR_REFIT_TTD_72);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, boxcar_11_259) 
{
  property
  {
    name: string(STR_BOXCAR_11_259_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1984), 1, 1);
    retire_early: retire_early_default;
    model_life: get_model_life(1984,2050);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [PAPR];
    default_cargo_type: PAPR;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 17;
    cargo_capacity: 68;
    weight: 24 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_259_get_spriteset;
    purchase: boxcar_11_259_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_259_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_259_cargo_subtype_text;
    colour_mapping: boxcar_11_259_colour_mapping;
  }
}
