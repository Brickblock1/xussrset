 // boxcar_11_259

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-259-1984.png"

// покупка
spriteset (boxcar_11_259_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(0, 38, -2)
}

// вид 1
spriteset (boxcar_11_259_spriteset, IMAGEFILE)
{
  s9_template(0, 38, 0)
}

// вид 2
spriteset (boxcar_11_259_v2_spriteset, IMAGEFILE)
{
  s9_template(0, 113, 0)
}

// менее 50%
spriteset (boxcar_11_259_1_spriteset, IMAGEFILE)
{
  s9_template(0, 188, 0)
}

spriteset (boxcar_11_259_1_v2_spriteset, IMAGEFILE)
{
  s9_template(0, 263, 0)
}

// более 50%
spriteset (boxcar_11_259_2_spriteset, IMAGEFILE)
{
  s9_template(0, 338, 0)
}

spriteset (boxcar_11_259_2_v2_spriteset, IMAGEFILE)
{
  s9_template(0, 413, 0)
}

spritegroup boxcar_11_259_spritegroup
{
  loading: [boxcar_11_259_1_spriteset, boxcar_11_259_2_spriteset];
  loaded: [boxcar_11_259_spriteset];
}

spritegroup boxcar_11_259_v2_spritegroup
{
  loading: [boxcar_11_259_1_v2_spriteset, boxcar_11_259_2_v2_spriteset];
  loaded: [boxcar_11_259_v2_spriteset];
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_259_get_spriteset2,
  cargo_subtype)
{
  0: boxcar_11_259_spritegroup;
  1: boxcar_11_259_v2_spritegroup;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_259_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_259_get_spriteset2;
}

switch (FEAT_TRAINS, SELF, boxcar_11_259_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_V1);
  1: string(STR_REFIT_V2);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, boxcar_11_259) 
{
  property
  {
    name: string(STR_BOXCAR_11_259_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1984), 1, 1);
    model_life: 32;
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [PAPR];
    default_cargo_type: PAPR;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 17;
    cargo_capacity: 68;
    weight: 24 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_259_get_spriteset;
    purchase: boxcar_11_259_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_259_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_259_cargo_subtype_text;
  }
}
