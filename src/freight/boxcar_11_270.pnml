 // boxcar_11_270

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-270.png"

// покупка
spriteset (boxcar_11_270_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(0, 38, -2)
}

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_270_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_270##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_270_1##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_270_2##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_270_3##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_270##v##_spritegroup \
{ \
  loading: [boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270##v##_spriteset]; \
  loaded: [boxcar_11_270##v##_spriteset]; \
} \


// ливреи
boxcar_11_270_sprite_layout_template(0, 38,)
//boxcar_11_270_sprite_layout_template(0, 113, _v2)
//boxcar_11_270_sprite_layout_template(0, 188, _v3)
//boxcar_11_270_sprite_layout_template(0, 263, _v4)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_270_get_spriteset2,
  cargo_subtype)
{
  0..3: boxcar_11_270_spritegroup;
//  1: boxcar_11_270_v2_spritegroup;
//  2: boxcar_11_270_v3_spritegroup;
//  3: boxcar_11_270_v4_spritegroup;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_270_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_270_get_spriteset2;
}

switch (FEAT_TRAINS, SELF, boxcar_11_270_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_RAL_6020);
  1: string(STR_REFIT_V2);
  2: string(STR_REFIT_V3);
//  3: string(STR_REFIT_V4);
  return CB_RESULT_NO_TEXT;
}

// C6-CD
// RAL 6020 = 18 60 61 62 63 64 65 66
ral6020 = reserve_sprites(1);
replace(ral6020)
{
  recolour_sprite
  {
    0xC6: 0x18;
    0xC7: 0x60;
    0xC8: 0x61;
    0xC9: 0x62;
    0xCA: 0x63;
    0xCB: 0x64;
    0xCC: 0x65;
    0xCD: 0x66;
  }
}

// RAL 3011 = 01 46 B2 B3 B4 B5 B6 B7
ral3011 = reserve_sprites(1);
replace(ral3011)
{
  recolour_sprite
  {
    0xC6: 0x01;
    0xC7: 0x46;
    0xC8: 0xB2;
    0xC9: 0xB3;
    0xCA: 0xB4;
    0xCB: 0xB5;
    0xCC: 0xB6;
    0xCD: 0xB7;
  }
}

// RAL 3001 = 46 B2 B3 B4 B5 B6 B7 A4
ral3001 = reserve_sprites(1);
replace(ral3001)
{
  recolour_sprite
  {
    0xC6: 0x46;
    0xC7: 0xB2;
    0xC8: 0xB3;
    0xC9: 0xB4;
    0xCA: 0xB5;
    0xCB: 0xB6;
    0xCC: 0xB7;
    0xCD: 0xA4;
  }
}

switch (FEAT_TRAINS, SELF, boxcar_11_270_colour_mapping,
  cargo_subtype)
{
  0: return ral6020;
  1: return ral3011;
  2: return ral3001;
  return CB_FAILED;
}


item (FEAT_TRAINS, boxcar_11_270) 
{
  property
  {
    name: string(STR_BOXCAR_11_270_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: 32;
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, ENSP, MNSP, CERA, FICR, BDMT];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 35;
    cargo_capacity: 69*2;
    weight: 35 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_270_get_spriteset;
    purchase: boxcar_11_270_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_270_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_270_cargo_subtype_text;
    colour_mapping: boxcar_11_270_colour_mapping;
  }
}
