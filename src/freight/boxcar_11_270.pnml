 // boxcar_11_270

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-270.png"

// покупка
spriteset (boxcar_11_270_purchase_spriteset, IMAGEFILE)
{
  s9_purchase_template(0, 38, -2)
}

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_270_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_270##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_270_1##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_270_2##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_270_3##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_270##v##_spritegroup \
{ \
  loading: [boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_1##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_2##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270_3##v##_spriteset, \
    boxcar_11_270##v##_spriteset]; \
  loaded: [boxcar_11_270##v##_spriteset]; \
} \


// ливреи
boxcar_11_270_sprite_layout_template(0, 38,)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_270_get_spriteset2,
  cargo_subtype)
{
  0..3: boxcar_11_270_spritegroup;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_270_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_270_get_spriteset2;
}

// cb colour_mapping
switch (FEAT_TRAINS, SELF, boxcar_11_270_colour_mapping,
  cargo_subtype)
{
  0: return ttd_cc + 23;  
  1: return ttd_cc + 28;  
  2: return ttd_cc + 32;  
  3: return ttd_cc + 47;
  return CB_FAILED;
}

// cb cargo_subtype_text
switch (FEAT_TRAINS, SELF, boxcar_11_270_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_4A);
  1: string(STR_REFIT_TTD_54);
  2: string(STR_REFIT_TTD_62);
  3: string(STR_REFIT_TTD_7C);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, boxcar_11_270) 
{
  property
  {
    name: string(STR_BOXCAR_11_270_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: 32;
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, ENSP, MNSP, CERA, FICR, BDMT];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: RAIL;
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 35;
    cargo_capacity: 69*2;
    weight: 35 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_270_get_spriteset;
    purchase: boxcar_11_270_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_270_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_270_cargo_subtype_text;
    colour_mapping: boxcar_11_270_colour_mapping;
  }
}
