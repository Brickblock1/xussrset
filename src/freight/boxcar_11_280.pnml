 // boxcar_11_280

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-280.png"

// покупка
purchase_sprites(boxcar_11_280, 0, 0)

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_280_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_280##v##_spriteset, IMAGEFILE) \
{ \
  s10_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_280_1##v##_spriteset, IMAGEFILE) \
{ \
  s10_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_280_2##v##_spriteset, IMAGEFILE) \
{ \
  s10_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_280_3##v##_spriteset, IMAGEFILE) \
{ \
  s10_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_280##v##_spritegroup \
{ \
  loading: [boxcar_11_280_1##v##_spriteset, \
    boxcar_11_280_1##v##_spriteset, \
    boxcar_11_280_1##v##_spriteset, \
    boxcar_11_280_2##v##_spriteset, \
    boxcar_11_280_2##v##_spriteset, \
    boxcar_11_280_2##v##_spriteset, \
    boxcar_11_280_3##v##_spriteset, \
    boxcar_11_280_3##v##_spriteset, \
    boxcar_11_280_3##v##_spriteset, \
    boxcar_11_280##v##_spriteset]; \
  loaded: [boxcar_11_280##v##_spriteset]; \
} \



// ливреи
boxcar_11_280_sprite_layout_template(0, 38,)

#undef IMAGEFILE

#define IMAGEFILE  "src/freight/boxcar-11-280-baltic.png"
boxcar_11_280_sprite_layout_template(0, 38, _v2)
#undef IMAGEFILE

#define IMAGEFILE  "src/freight/boxcar-11-280-pid.png"
boxcar_11_280_sprite_layout_template(0, 38, _v3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_280_sprites_2,
  cargo_subtype)
{
  0..7: boxcar_11_280_spritegroup;
  8: boxcar_11_280_v2_spritegroup;
  9: boxcar_11_280_v3_spritegroup;
     align_10_spriteset;
}

long_vehicle(boxcar_11_280, )

// cb colour_mapping

random_switch (FEAT_TRAINS, SELF, boxcar_11_280_random, 0)
{
  2: return ttdall_cc + 0x5C;
  2: return ttdall_cc + 0x62;
  2: return ttdall_cc + 0x63;
  5: return ttdall_cc + 0x73;
  1: return ttdall_cc + 0x83;
  1: return ttdall_cc + 0x9C;
  1: return ttdall_cc + 0xCE;
}                      

switch (FEAT_TRAINS, SELF, boxcar_11_280_colour_mapping,
  cargo_subtype)
{
  0: boxcar_11_280_random;
  1: return ttdall_cc + 0x5C;    
  2: return ttdall_cc + 0x62;    
  3: return ttdall_cc + 0x63;    
  4: return ttdall_cc + 0x73;    
  5: return ttdall_cc + 0x83;    
  6: return ttdall_cc + 0x9C;    
  7: return ttdall_cc + 0xCE;    
                         
  8: return ttdall_cc + 0x9C;    
  9: return ttdall_cc + 0x05;    
     return CB_FAILED;
}

// cb cargo_subtype_text
switch (FEAT_TRAINS, SELF, boxcar_11_280_cargo_subtype_text2,
  build_year >= 2010)
{
  0: return CB_RESULT_NO_TEXT;
     return string(STR_REFIT_RZD_PID);
}

switch (FEAT_TRAINS, SELF, boxcar_11_280_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_5B); // это 5C
  2: string(STR_REFIT_TTD_62);
  3: string(STR_REFIT_TTD_63);
  4: string(STR_REFIT_TTD_73);
  5: string(STR_REFIT_TTD_83);
  6: string(STR_REFIT_TTD_9C);
  7: string(STR_REFIT_TTD_CE);
  8: string(STR_REFIT_BALTIC);
  9: boxcar_11_280_cargo_subtype_text2;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_11_280_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 136;
  WOOL: return 166;
  GLAS: return 181;
        return 68;
}

item (FEAT_TRAINS, boxcar_11_280) 
{
  property
  {
    name: string(STR_BOXCAR_11_280_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1991), 1, 1);
    model_life: get_model_life(1991,2050);
    retire_early: get_retire_early(1991,2050);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, WOOL, GLAS, PAPR, ENSP, MNSP, FICR,  CMNT, BDMT, CERA, SGBT];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 17;
    cargo_capacity: 68;
    weight: 26 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_280_sprites;
    purchase: boxcar_11_280_purchase_sprites;
    articulated_part: articulated_part_dummy2;
    additional_text: return string(STR_BOXCAR_11_280_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: boxcar_11_280_cargo_capacity;
    cargo_subtype_text: boxcar_11_280_cargo_subtype_text;
    colour_mapping: boxcar_11_280_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_280)
  {
    property
    { name: string(STR_BOXCAR_11_280_NAME_LONG); }
  }
}

