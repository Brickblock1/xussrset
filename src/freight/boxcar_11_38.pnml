 // boxcar_11_38

/// Вагон
// Крытый 11-38 (1936-1941, 1946-54), бывший 11-xxxx
// boxcar

#define IMAGEFILE  "src/freight/boxcar-11-38-1936-1948.png"

// покупка
spriteset (boxcar_11_38_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_38_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_38##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_38_1##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_38_2##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_38_3##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_38##v##_spritegroup \
{ \
  loading: [boxcar_11_38_1##v##_spriteset, \
    boxcar_11_38_1##v##_spriteset, \
    boxcar_11_38_1##v##_spriteset, \
    boxcar_11_38_2##v##_spriteset, \
    boxcar_11_38_2##v##_spriteset, \
    boxcar_11_38_2##v##_spriteset, \
    boxcar_11_38_3##v##_spriteset, \
    boxcar_11_38_3##v##_spriteset, \
    boxcar_11_38_3##v##_spriteset, \
    boxcar_11_38##v##_spriteset]; \
  loaded: [boxcar_11_38##v##_spriteset]; \
} \


// ливреи
boxcar_11_38_sprite_layout_template(0, 38,)

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_38_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_38_spritegroup;
}

item (FEAT_TRAINS, boxcar_11_38) 
{
  property
  {
    name: string(STR_BOXCAR_11_38_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1936), 1, 1);
    model_life: get_model_life(1936,1954);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [PAPR, GOOD, GRAI, WHEA, MAIZ, CERE, ENSP, MNSP,
      FICR, GLAS, OLSD, BDMT, SGBT];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 13;
    cargo_capacity: 104;
    weight: 23 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_38_get_spriteset;
    purchase: boxcar_11_38_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_38_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
