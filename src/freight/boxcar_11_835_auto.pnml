 // boxcar_11_835

/// Вагон
// 11-835 крытый для автомобилей (Rautaruukki, 1986-1990)
// boxcar 11-835

#define IMAGEFILE  "src/freight/boxcar-11-835-auto-1986-1990.png"

// покупка
purchase_spriteset(boxcar_11_835, 0, 0)

// пустой
spriteset (boxcar_11_835_empty_spriteset, IMAGEFILE)
{
  s14_template(0, 26, 0)
}

// с грузом
spriteset (boxcar_11_835_full_spriteset, IMAGEFILE)
{
  s14_template(0, 93, 0)
}

spritegroup boxcar_11_835_spritegroup
{
  loading: [boxcar_11_835_empty_spriteset, boxcar_11_835_full_spriteset];
  loaded: [boxcar_11_835_empty_spriteset, boxcar_11_835_full_spriteset];
}

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_835_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_835_spritegroup;
}

item (FEAT_TRAINS, boxcar_11_835) 
{
  property
  {
    name: string(STR_BOXCAR_11_835_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: get_model_life(1986,1990);
    retire_early: get_retire_early(1986,1990);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [VEHI];
    default_cargo_type: VEHI;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 5;
    cargo_capacity: 10;
    weight: 35 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_835_get_spriteset;
    purchase: boxcar_11_835_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_BOXCAR_11_835_PURCHASE_HINT);
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_835)
  {
    property
    { name: string(STR_BOXCAR_11_835_NAME_LONG); }
  }
}

