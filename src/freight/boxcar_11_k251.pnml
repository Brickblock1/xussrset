 // boxcar_11_k251

/// Вагон
// Крытый 11-К251 (1948-1960)
// boxcar 11-k251

#define IMAGEFILE  "src/freight/boxcar-11-k251-1948-1956.png"

// покупка
purchase_spriteset(boxcar_11_k251, 0, 0)

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_k251_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_k251##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_k251_1##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_k251_2##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_k251_3##v##_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_k251##v##_spritegroup \
{ \
  loading: [boxcar_11_k251_1##v##_spriteset, \
    boxcar_11_k251_1##v##_spriteset, \
    boxcar_11_k251_1##v##_spriteset, \
    boxcar_11_k251_2##v##_spriteset, \
    boxcar_11_k251_2##v##_spriteset, \
    boxcar_11_k251_2##v##_spriteset, \
    boxcar_11_k251_3##v##_spriteset, \
    boxcar_11_k251_3##v##_spriteset, \
    boxcar_11_k251_3##v##_spriteset, \
    boxcar_11_k251##v##_spriteset]; \
  loaded: [boxcar_11_k251##v##_spriteset]; \
} \



// ливреи
boxcar_11_k251_sprite_layout_template(0, 38,)

#undef IMAGEFILE
#define IMAGEFILE  "src/freight/boxcar-11-k251-1957-1960.png"

// покупка
purchase_spriteset(boxcar_11_k251_v2, 0, 0)

// ливреи
boxcar_11_k251_sprite_layout_template(0, 38, _v2)

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, boxcar_11_k251_purchase_sprites,
  current_year >= 1957)
{
  0: boxcar_11_k251_purchase_spriteset;
     boxcar_11_k251_v2_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k251_sprites_2,
  build_year >= 1957)
{
  0: boxcar_11_k251_spritegroup;
     boxcar_11_k251_v2_spritegroup;
}

long_vehicle(boxcar_11_k251, )

// < 1957
switch (FEAT_TRAINS, SELF, boxcar_11_k251_cargo_capacity3,
  cargo_type_in_veh)
{
  GOOD: return 120;
  WOOL: return 127;
  GLAS: return 160;
        return 60;
}

// >= 1957
switch (FEAT_TRAINS, SELF, boxcar_11_k251_cargo_capacity2,
  cargo_type_in_veh)
{
  GOOD: return 124;
  WOOL: return 127;
  GLAS: return 165;
        return 62;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k251_cargo_capacity,
  build_year >= 1957)
{
  1: boxcar_11_k251_cargo_capacity2;
     boxcar_11_k251_cargo_capacity3;
}

item (FEAT_TRAINS, boxcar_11_k251) 
{
  property
  {
    name: string(STR_BOXCAR_11_K251_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1948), 1, 1);
    model_life: get_model_life(1948,1960);
    retire_early: get_retire_early(1948,1960);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, WOOL, GLAS, PAPR, GRAI, WHEA, MAIZ, CERE, OLSD, ENSP, MNSP, FICR, CMNT, BDMT, CERA, SGBT];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 16;
    cargo_capacity: 60;
    weight: 24 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_k251_sprites;
    purchase: boxcar_11_k251_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_K251_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: boxcar_11_k251_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_k251)
  {
    property
    { name: string(STR_BOXCAR_11_K251_NAME_LONG); }
  }
}

