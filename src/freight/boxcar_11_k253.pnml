 // boxcar_11_k253

/// Вагон

#define IMAGEFILE  "src/freight/boxcar-11-k253.png"

// покупка
purchase_spriteset(boxcar_11_k253, 0, 0)

// шаблон: движение/100%, 33%, 66%, 99%
#define boxcar_11_k253_sprite_layout_template(x, y, v) \
spriteset (boxcar_11_k253##v##_spriteset, IMAGEFILE) \
{ \
  s11_template(x, y, 0) \
} \
 \
spriteset (boxcar_11_k253_1##v##_spriteset, IMAGEFILE) \
{ \
  s11_template(x, y+113-38, 0) \
} \
 \
spriteset (boxcar_11_k253_2##v##_spriteset, IMAGEFILE) \
{ \
  s11_template(x, y+188-38, 0) \
} \
 \
spriteset (boxcar_11_k253_3##v##_spriteset, IMAGEFILE) \
{ \
  s11_template(x, y+263-38, 0) \
} \
 \
spritegroup boxcar_11_k253##v##_spritegroup \
{ \
  loading: [boxcar_11_k253_1##v##_spriteset, \
    boxcar_11_k253_1##v##_spriteset, \
    boxcar_11_k253_1##v##_spriteset, \
    boxcar_11_k253_2##v##_spriteset, \
    boxcar_11_k253_2##v##_spriteset, \
    boxcar_11_k253_2##v##_spriteset, \
    boxcar_11_k253_3##v##_spriteset, \
    boxcar_11_k253_3##v##_spriteset, \
    boxcar_11_k253_3##v##_spriteset, \
    boxcar_11_k253##v##_spriteset]; \
  loaded: [boxcar_11_k253##v##_spriteset]; \
} \



// ливреи
boxcar_11_k253_sprite_layout_template(0, 38,)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_k253_get_spriteset2,
  cargo_subtype)
{
  0..3: boxcar_11_k253_spritegroup;
  align_11_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, boxcar_11_k253_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  boxcar_11_k253_get_spriteset2;
}

// cb colour_mapping
random_switch (FEAT_TRAINS, SELF, boxcar_11_k253_random, 0 )
{
  7: return ttd_cc + 81;
  3: return ttd_cc + 80;
  2: return ttd_cc + 82;
}                      

switch (FEAT_TRAINS, SELF, boxcar_11_k253_colour_mapping,
  cargo_subtype)
{
  0: boxcar_11_k253_random;
  1: return ttd_cc + 81;
  2: return ttd_cc + 80;
  3: return ttd_cc + 82;
  return CB_FAILED;
}

// cb cargo_subtype_text
switch (FEAT_TRAINS, SELF, boxcar_11_k253_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_1A);
  2: string(STR_REFIT_TTD_19);
  3: string(STR_REFIT_TTD_5A);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, boxcar_11_k253) 
{
  property
  {
    name: string(STR_BOXCAR_11_K253_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1956), 1, 1);
    model_life: get_model_life(1956,1960+15);
    retire_early: get_retire_early(1956,1960+15);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [LVST];
    default_cargo_type: LVST;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 6;
    cargo_capacity: 23;
    weight: 24 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_k253_get_spriteset;
    purchase: boxcar_11_k253_purchase_spriteset;
    articulated_part: articulated_part_dummy3;
    additional_text: return string(STR_BOXCAR_11_K253_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_k253_cargo_subtype_text;
    colour_mapping: boxcar_11_k253_colour_mapping;
  }
}
