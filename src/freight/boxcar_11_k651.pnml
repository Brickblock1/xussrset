 // boxcar_11_k651

/// Вагон
// Крытый для автомобилей и легковесных грузов 11-К651 (1971-)
// boxcar 11-k651
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/boxcar-11-k651-1971.png"

// покупка
purchase_sprites(boxcar_11_k651, 0, 0)

spriteset (boxcar_11_k651_v1_sprites_l, IMAGEFILE)
{ s14_template(7, 47, 0) }

spriteset (boxcar_11_k651_v1_sprites_r, IMAGEFILE)
{ s14_r_template(7, 47, 0) }

spriteset (boxcar_11_k651_v2_sprites_l, IMAGEFILE)
{ s14_template(7, 114, 0) }

spriteset (boxcar_11_k651_v2_sprites_r, IMAGEFILE)
{ s14_r_template(7, 114, 0) }

spriteset (boxcar_11_k651_v3_sprites_l, IMAGEFILE)
{ s14_template(7, 180, 0) }

spriteset (boxcar_11_k651_v3_sprites_r, IMAGEFILE)
{ s14_r_template(7, 180, 0) }

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_random_l, 0 )
{
  1: boxcar_11_k651_v1_sprites_l;
  1: boxcar_11_k651_v2_sprites_l;
  1: boxcar_11_k651_v3_sprites_l;
}                      

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_random_r, 0 )
{
  dependent: boxcar_11_k651_random_l;
  1: boxcar_11_k651_v1_sprites_r;
  1: boxcar_11_k651_v2_sprites_r;
  1: boxcar_11_k651_v3_sprites_r;
}                               

switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_l,
  cargo_subtype)
{
  0: boxcar_11_k651_random_l;
  1: boxcar_11_k651_v1_sprites_l;
  2: boxcar_11_k651_v2_sprites_l;
  3: boxcar_11_k651_v3_sprites_l;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_r,
  cargo_subtype)
{
  0: boxcar_11_k651_random_r;
  1: boxcar_11_k651_v1_sprites_r;
  2: boxcar_11_k651_v2_sprites_r;
  3: boxcar_11_k651_v3_sprites_r;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: boxcar_11_k651_sprites_l;
  1: boxcar_11_k651_sprites_r;
}

long_vehicle(boxcar_11_k651)

switch (FEAT_TRAINS, SELF, boxcar_11_k651_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_53);
  2: string(STR_REFIT_TTD_3F);
  3: string(STR_REFIT_TTD_73);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_loading_speed,
  cargo_type_in_veh)
{
  GOOD: return 22;
  WOOL: return 22;
  GLAS: return 22;
  VEHI: return 3;
        return 11;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 84;
  WOOL: return 168;
  GLAS: return 112;
  ENSP: return 64;
  FMSP: return 75;
  MNSP: return 134;
  FICR: return 42;
  WDPR: return 42;
  VEHI: return 10;
  PAPR: return 42;
  BDMT: return 42; 
        return 42;
}

item (FEAT_TRAINS, boxcar_11_k651) 
{
  property
  {
    name: string(STR_BOXCAR_11_K651_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1971), 1, 1);
    model_life: get_model_life(1971,2050);
    retire_early: get_retire_early(1971,2050);
    vehicle_life: 33;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, WOOL, GLAS, VEHI, PAPR, ENSP, MNSP, FMSP, FICR, WDPR, BDMT];
    default_cargo_type: VEHI;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 3;
    cargo_capacity: 10;
    weight: 42 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_k651_sprites;
    purchase: boxcar_11_k651_purchase_sprites;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_BOXCAR_11_K651_PURCHASE_HINT);
    cargo_subtype_text: boxcar_11_k651_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: boxcar_11_k651_cargo_capacity;
    loading_speed: boxcar_11_k651_loading_speed;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_k651)
  {
    property
    { name: string(STR_BOXCAR_11_K651_NAME_LONG); }
  }
}

