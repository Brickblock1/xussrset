 // boxcar_11_066

/// Вагон
// Крытый 11-066 (1960-1978)
// Крытый 11-066 (1979-1982)
// boxcar 11-066
// 1960-1978 один вид, 1979-1982 другой
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/boxcars/11-066.png"
purchase_sprites(boxcar_11_066_v1, 0, 0)
box_empty_layout_template(boxcar_11_066_v1, 9, 0, 38) 
box_layout_template(boxcar_11_066_v1, 9, 0, 188, goods1)
#undef IMAGEFILE

#define IMAGEFILE  "src/freight/boxcars/11-066-s1979.png"
purchase_sprites(boxcar_11_066_v2, 0, 0)
box_empty_layout_template(boxcar_11_066_v2, 9, 0, 38) 
box_layout_template(boxcar_11_066_v2, 9, 0, 188, goods1)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_066_purchase_sprites,
  current_year >= 1979)
{
  0: boxcar_11_066_v1_purchase_sprites;
     boxcar_11_066_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, boxcar_11_066_sprites_start,
  build_year >= 1979)
{
  0: boxcar_11_066_v1_goods1_sprites;
  1: boxcar_11_066_v2_goods1_sprites;
     align_9_sprites;
}

long_vehicle(boxcar_11_066)

wagon_running_cost(boxcar_11_066, 16)

// < 1979
switch (FEAT_TRAINS, SELF, boxcar_11_066_cargo_capacity_b1979,
  cargo_type_in_veh)
{
  GOOD: return 120;
  WOOL: return 144;
  GLAS: return 171;
  ENSP: return 58;
  FMSP: return 53;
  MNSP: return 115;
  SGCN: return 32;
  FICR: return 60;
  RCYC: return 60;
  WDPR: return 60;
        return 64;
}

// >= 1979
switch (FEAT_TRAINS, SELF, boxcar_11_066_cargo_capacity_a1979,
  cargo_type_in_veh)
{
  GOOD: return 120;
  WOOL: return 144;
  GLAS: return 181;
  ENSP: return 58;
  FMSP: return 53;
  MNSP: return 115;
  SGCN: return 32;
  FICR: return 60;
  RCYC: return 60;
  WDPR: return 60;
  JAVA: return 67;
        return 68;
}

switch (FEAT_TRAINS, SELF, boxcar_11_066_cargo_capacity,
  build_year >= 1979)
{
  1: boxcar_11_066_cargo_capacity_a1979;
     boxcar_11_066_cargo_capacity_b1979;
}

item (FEAT_TRAINS, boxcar_11_066) 
{
  property
  {
    name: string(STR_BOXCAR_11_066_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1960), 1, 1);
    model_life: get_model_life(1960,1982);
    retire_early: get_retire_early(1960,1982);
    vehicle_life: 32;
    reliability_decay: 10;
    cargo_allow_refit: [GOOD, WOOL, GLAS, ENSP, FMSP, MNSP, SGCN, FICR, RCYC, WDPR, PAPR, GRAI, WHEA, MAIZ, CERE, OLSD, CMNT, BDMT, CERA, SGBT, BEER, JAVA];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: 3;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 16;
    cargo_capacity: 64;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_066_sprites;
    purchase: boxcar_11_066_purchase_sprites;
    running_cost_factor: boxcar_11_066_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_BOXCAR_11_066_PURCHASE_HINT);
    cargo_capacity: boxcar_11_066_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_066)
  {
    property
    { name: string(STR_BOXCAR_11_066_NAME_LONG); }
  }
}

