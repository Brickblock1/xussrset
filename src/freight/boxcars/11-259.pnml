 // boxcar_11_259

#define IMAGEFILE  "src/freight/boxcars/11-259.png"
purchase_sprites(boxcar_11_259, 0, 0)
combo_layout_boxcar_template(boxcar_11_259, 9, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_259_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_11_259_sprites_back;
  1: cargo9all_sprites;
  2: boxcar_11_259_sprites_front;
     return CB_FAILED;
}

long_vehicle_RND(boxcar_11_259, boxcar_11_259_sprites_asm_back)

switch (FEAT_TRAINS, SELF, boxcar_11_259_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x0F])
{
   0..7: return ttdall_cc + 0x4C;
  8..13: return ttdall_cc + 0x53;
     14: return ttdall_cc + 0x62;
         return ttdall_cc + 0x72;
}

switch (FEAT_TRAINS, SELF, boxcar_11_259_colour_mapping,
  cargo_subtype)
{
  0: boxcar_11_259_random;
  1: return ttdall_cc + 0x4C;
  2: return ttdall_cc + 0x53;
  3: return ttdall_cc + 0x62;
  4: return ttdall_cc + 0x72;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_11_259_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_4C);
  2: return string(STR_REFIT_TTD_53);
  3: return string(STR_REFIT_TTD_62);
  4: return string(STR_REFIT_TTD_72);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_11_259_cargo_capacity,
[  STORE_TEMP(68, 0),  // т
   STORE_TEMP(120, 1), // м³
   STORE_TEMP(39, 2),  // м²
   STORE_TEMP(0, 3),   // палетомест
   STORE_TEMP(9, 4)])  // шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, boxcar_11_259_running_cost_factor,
[  STORE_TEMP(0, 0),            // Моторы
   STORE_TEMP(0, 1),            // Бригада
   STORE_TEMP(11, 2),       		/* 17*2/3 */	// Износ
   STORE_TEMP(0, 3),            // Сопровождение
   STORE_TEMP(6, 4),         		/* 17/3 */	// ТО
   STORE_TEMP(0, 5),            // Сертификация

   STORE_TEMP(120, 6),       // Скорость
   STORE_TEMP(24, 7),        // Тара
   STORE_TEMP(92, 8)])    		/* 24+68 */	// Максимальная масса
{ all_running_cost_factor; } // 17

switch (FEAT_TRAINS, SELF, boxcar_11_259_loading_speed,
  STORE_TEMP(20, 2))
{ all_wagons_box_loading_speed; }

hint_wagon_noto(boxcar_11_259,
                68, 120,
                STR_PURCHASE_HINT_LOADING_SPEED_4TIC,
                STR_PURCHASE_HINT_AGING_PERIOD_5_1,
                STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
                1984,
                string(STR_PURCHASE_HINT_FACTORY_ALTAI_CW))

item (FEAT_TRAINS, boxcar_11_259, 321) {
  property {
    name: string(STR_MODEL_NUMBER2, 11, 259);
    vehicle_dates(1984, 2050, 32, 10, 8, 44)
    vehicle_wagon(24, 120)
    cargo_allow_refit: [PAPR];
  }
  graphics {
    default: boxcar_11_259_sprites;
    purchase: boxcar_11_259_purchase_sprites;
    cargo_age_period: return 350;
    running_cost_factor: boxcar_11_259_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_259_cargo_subtype_text;
    colour_mapping: boxcar_11_259_colour_mapping;
    cargo_capacity: boxcar_11_259_cargo_capacity;
    loading_speed: boxcar_11_259_loading_speed;
    additional_text: boxcar_11_259_additional_text;
  }
}

long_name_template(boxcar_11_259, STR_LONGNAME_BOXCAR_11_259)
