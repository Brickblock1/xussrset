 // boxcar_11_270

/// Вагон
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/boxcars/11-270.png"
purchase_sprites(boxcar_11_270, 0, 0)
box_empty_layout_template(boxcar_11_270_v1, 9, 0, 38) 
box_layout_template(boxcar_11_270_v1, 9, 0, 188, goods1)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_11_270_sprites_start,
  cargo_subtype)
{
  0..7: boxcar_11_270_v1_goods1_sprites;
        align_9_sprites;
}

long_vehicle_RND(boxcar_11_270, boxcar_11_270_v1_goods1_sprites)

wagon_running_cost(boxcar_11_270, 18)

// cb colour_mapping
random_switch (FEAT_TRAINS, SELF, boxcar_11_270_random, 0 )
{
  independent: boxcar_11_270_v1_goods1_sprites;
  5: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x53;
  3: return ttdall_cc + 0x5B;
  3: return ttdall_cc + 0x62;
  5: return ttdall_cc + 0x71;
  5: return ttdall_cc + 0x73;
  1: return ttdall_cc + 0x9C;
}                      

switch (FEAT_TRAINS, SELF, boxcar_11_270_colour_mapping,
  cargo_subtype)
{
  0: boxcar_11_270_random;
  1: return ttdall_cc + 0x4B;   
  2: return ttdall_cc + 0x53;   
  3: return ttdall_cc + 0x5B;   
  4: return ttdall_cc + 0x62;   
  5: return ttdall_cc + 0x71;   
  6: return ttdall_cc + 0x73;   
  7: return ttdall_cc + 0x9C;   
     return CB_FAILED;
}

// cb cargo_subtype_text
switch (FEAT_TRAINS, SELF, boxcar_11_270_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_4B);
  2: return string(STR_REFIT_TTD_53);
  3: return string(STR_REFIT_TTD_5B);
  4: return string(STR_REFIT_TTD_62);
  5: return string(STR_REFIT_TTD_71);
  6: return string(STR_REFIT_TTD_73);
  7: return string(STR_REFIT_TTD_9C);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_11_270_cargo_capacity, 
  cargo_type_in_veh)
{
  GOOD: return 122;
  WOOL: return 146;
  GLAS: return 184;
  ENSP: return 59;
  FMSP: return 54;
  MNSP: return 117;
  SGCN: return 33;
  FICR: return 61;
  RCYC: return 61;
  WDPR: return 61;
  JAVA: return 68;
  WATR: return 34;
        return 69;
}

item (FEAT_TRAINS, boxcar_11_270) {
  property {
    name: string(STR_NAME_BOXCAR_11_270);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: get_model_life(1986,1991);
    retire_early: get_retire_early(1986,1991);
    vehicle_life: 32;
    reliability_decay: 10;
    cargo_allow_refit: [GOOD, WOOL, GLAS, ENSP, FMSP, MNSP, SGCN, FICR, RCYC, WDPR, CMNT, BDMT, CERA, SGBT, BEER, JAVA, WATR];
    default_cargo_type: GOOD; 
    cost_factor: 3;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 35;
    cargo_capacity: 69;
    weight: 35 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: boxcar_11_270_sprites;
    purchase: boxcar_11_270_purchase_sprites;
    running_cost_factor: boxcar_11_270_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_PURCHASE_HINT_WAGON,
                                   69, 122,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR_4AXLE_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1986, 1991), 
                                   string(STR_PURCHASE_HINT_FACTORY_ALTAI_CW));
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: boxcar_11_270_cargo_subtype_text;
    colour_mapping: boxcar_11_270_colour_mapping;
    cargo_capacity: boxcar_11_270_cargo_capacity;
  }
}

long_name_template(boxcar_11_270, STR_LONGNAME_BOXCAR_11_270)
