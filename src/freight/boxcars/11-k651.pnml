 // boxcar_11_k651

/// Вагон
// Крытый для автомобилей и легковесных грузов 11-К651 (1971-)
// boxcar 11-k651
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/boxcars/11-k651.png"
purchase_sprites(boxcar_11_k651, 0, 0)

spriteset (boxcar_11_k651_v1_sprites_left, IMAGEFILE)
{ s14_template(7, 47, 0) }

spriteset (boxcar_11_k651_v1_sprites_right, IMAGEFILE)
{ s14_r_template(7, 47, 0) }

spriteset (boxcar_11_k651_v2_sprites_left, IMAGEFILE)
{ s14_template(7, 114, 0) }

spriteset (boxcar_11_k651_v2_sprites_right, IMAGEFILE)
{ s14_r_template(7, 114, 0) }

spriteset (boxcar_11_k651_v3_sprites_left, IMAGEFILE)
{ s14_template(7, 180, 0) }

spriteset (boxcar_11_k651_v3_sprites_right, IMAGEFILE)
{ s14_r_template(7, 180, 0) }
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_random_left, 0 )
{
  1: boxcar_11_k651_v1_sprites_left;
  1: boxcar_11_k651_v2_sprites_left;
  1: boxcar_11_k651_v3_sprites_left;
}                      

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_random_right, 0 )
{
  dependent: boxcar_11_k651_random_left;
  1: boxcar_11_k651_v1_sprites_right;
  1: boxcar_11_k651_v2_sprites_right;
  1: boxcar_11_k651_v3_sprites_right;
}                               

switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_left,
  cargo_subtype)
{
  0: boxcar_11_k651_random_left;
  1: boxcar_11_k651_v1_sprites_left;
  2: boxcar_11_k651_v2_sprites_left;
  3: boxcar_11_k651_v3_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_right,
  cargo_subtype)
{
  0: boxcar_11_k651_random_right;
  1: boxcar_11_k651_v1_sprites_right;
  2: boxcar_11_k651_v2_sprites_right;
  3: boxcar_11_k651_v3_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, boxcar_11_k651_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: boxcar_11_k651_sprites_left;
  1: boxcar_11_k651_sprites_right;
}

long_vehicle(boxcar_11_k651)

wagon_running_cost(boxcar_11_k651, 87)

switch (FEAT_TRAINS, SELF, boxcar_11_k651_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_53);
  2: return string(STR_REFIT_TTD_3F);
  3: return string(STR_REFIT_TTD_73);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_loading_speed,
  cargo_type_in_veh)
{
  GOOD: return 22;
  WOOL: return 22;
  GLAS: return 22;
  VEHI: return 3;
        return 11;
}

switch (FEAT_TRAINS, SELF, boxcar_11_k651_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 84;
  WOOL: return 168;
  GLAS: return 112;
  ENSP: return 67;
  FMSP: return 75;
  MNSP: return 134;
  FICR: return 42;
  WDPR: return 42;
  VEHI: return 10;
  PAPR: return 42;
  BDMT: return 42; 
  WATR: return 21;
        return 42;
}

item (FEAT_TRAINS, boxcar_11_k651) 
{
  property
  {
    name: string(STR_BOXCAR_11_K651_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1971), 1, 1);
    model_life: get_model_life(1971,2050);
    retire_early: get_retire_early(1971,2050);
    vehicle_life: 33;
    reliability_decay: 10;
    cargo_allow_refit: [GOOD, WOOL, GLAS, VEHI, PAPR, ENSP, MNSP, FMSP, FICR, WDPR, BDMT, BEER, JAVA, WATR];
    default_cargo_type: VEHI;
    refit_cost: 0;
    cost_factor: 14;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 3;
    cargo_capacity: 10;
    weight: 42 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: boxcar_11_k651_sprites;
    purchase: boxcar_11_k651_purchase_sprites;
    running_cost_factor: boxcar_11_k651_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_BOXCAR_11_K651_PURCHASE_HINT);
    cargo_subtype_text: boxcar_11_k651_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: boxcar_11_k651_cargo_capacity;
    loading_speed: boxcar_11_k651_loading_speed;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, boxcar_11_k651)
  {
    property
    { name: string(STR_BOXCAR_11_K651_NAME_LONG); }
  }
}

