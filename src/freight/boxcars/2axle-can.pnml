 // boxcar_2axle_can

#define IMAGEFILE  "src/freight/boxcars/2axle-can.png"
purchase_sprites(boxcar_2axle_can, 0, 0)
combo_layout_boxcartop_template(boxcar_2axle_can, 5, 32, 40)
combo_layout_boxcar_template(boxcar_2axle_can_grain, 5, 32, 40 + 67 * 4)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_sprites_top_cc1,
  STORE_TEMP(ttdall_cc + 0x49, 0x100))
{ boxcar_2axle_can_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_sprites_top_cc2,
  STORE_TEMP(PALETTE_USE_DEFAULT, 0x100))
{ boxcar_2axle_can_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_sprites_top_cc,
  cargo_subtype)
{
  4: boxcar_2axle_can_sprites_top_cc2;
     boxcar_2axle_can_sprites_top_cc1;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_base_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_2axle_can_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_2axle_can_sprites_front;
  3: boxcar_2axle_can_sprites_top_cc;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_grain_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_2axle_can_grain_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_2axle_can_grain_sprites_front;
  3: boxcar_2axle_can_sprites_top_cc;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_sprites_start,
  cargo_type_in_veh)
{
  CERE: boxcar_2axle_can_grain_sprites_start;
  GRAI: boxcar_2axle_can_grain_sprites_start;
  MAIZ: boxcar_2axle_can_grain_sprites_start;
  OLSD: boxcar_2axle_can_grain_sprites_start;
  WHEA: boxcar_2axle_can_grain_sprites_start;
        boxcar_2axle_can_base_sprites_start;
}

long_vehicle_RND(boxcar_2axle_can, boxcar_2axle_can_sprites_asm_back)

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
   0..2: return ttdall_cc + 0x48;
   3..5: return ttdall_cc + 0x6A;
         return ttdall_cc + 0x6C;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: boxcar_2axle_can_random;
  1: return ttdall_cc + 0x48;
  2: return ttdall_cc + 0x6A;
  3: return ttdall_cc + 0x6C;
  4: return ttdall_cc + 0x61;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_48);
  2: return string(STR_REFIT_TTD_6A);
  3: return string(STR_REFIT_TTD_6C);
  4: return string(STR_REFIT_TTD_61);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_2axle_can_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 40;
  WOOL: return 54;
  GLAS: return 53;
  ENSP: return 22;
  FMSP: return 20;
  MNSP: return 44;
  VPTS: return 44;
  SGCN: return 12;
  WATR: return 10;
        return 20;
}

wagon_running_cost(boxcar_2axle_can, 3)
hint_wagon_3fact(boxcar_2axle_can,
                 20, 45,
                 STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
                 STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                 STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
                 1926, 1931,
                 STR_PURCHASE_HINT_FACTORY_AMERICAN_RC, 
                 STR_PURCHASE_HINT_FACTORY_CANADIAN_S,
                 STR_PURCHASE_HINT_FACTORY_PITTSBURGH_S)
 
item (FEAT_TRAINS, boxcar_2axle_can, 416) {
  property {
    name: string(STR_NAME_BOXCAR_2AXLE_CAN);
    vehicle_dates(1926, 1931, 30, 10, 4, 1)
    vehicle_wagon(11, 120)
    vehicle_wagon_cargo(40, 16)
    cargo_allow_refit: [cargo_all_lvst_grain_boxcarlist()];
  }
  graphics {
    default: boxcar_2axle_can_sprites;
    purchase: boxcar_2axle_can_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: boxcar_2axle_can_running_cost_factor;
    refit_cost: boxcar_refit_cost;
    cargo_capacity: boxcar_2axle_can_cargo_capacity;
    cargo_subtype_text: boxcar_2axle_can_cargo_subtype_text;
    colour_mapping: boxcar_2axle_can_colour_mapping;
    additional_text: boxcar_2axle_can_additional_text;
  }
}

long_name_template(boxcar_2axle_can, STR_LONGNAME_BOXCAR_2AXLE_CAN)
