 // boxcar_type1891

#define IMAGEFILE  "src/freight/boxcars/type1891.png"
purchase_sprites(boxcar_type1891_v1, 0, 0)
purchase_sprites(boxcar_type1891_v2, 103, 0)
combo_layout_boxcartop_template(boxcar_type1891_v1, 5, 32, 40)
combo_layout_boxcar_template(boxcar_type1891_v1_grain, 5, 32, 40 + 67 * 4)
combo_layout_boxcartop_template(boxcar_type1891_v2, 5, 32, 40 + 67 * 7)
combo_layout_boxcar_template(boxcar_type1891_v2_grain, 5, 32, 40 + 67 * 11)
#undef IMAGEFILE
purchase_sprites_select(boxcar_type1891, 1918)

switch (FEAT_TRAINS, SELF, boxcar_type1891_sprites_top1,
  STORE_TEMP(PALETTE_USE_DEFAULT, 0x100))
{ boxcar_type1891_v1_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_sprites_top2,
  STORE_TEMP(PALETTE_USE_DEFAULT, 0x100))
{ boxcar_type1891_v2_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_sprites_top,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: boxcar_type1891_sprites_top1;
     boxcar_type1891_sprites_top2;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v1_base_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1891_v1_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1891_v1_sprites_front;
  3: boxcar_type1891_sprites_top;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v1_grain_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1891_v1_grain_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1891_v1_grain_sprites_front;
  3: boxcar_type1891_sprites_top;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v1_sprites_start,
  cargo_type_in_veh)
{
  CERE: boxcar_type1891_v1_grain_sprites_start;
  GRAI: boxcar_type1891_v1_grain_sprites_start;
  MAIZ: boxcar_type1891_v1_grain_sprites_start;
  OLSD: boxcar_type1891_v1_grain_sprites_start;
  WHEA: boxcar_type1891_v1_grain_sprites_start;
        boxcar_type1891_v1_base_sprites_start;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v2_base_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1891_v2_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1891_v2_sprites_front;
  3: boxcar_type1891_sprites_top;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v2_grain_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1891_v2_grain_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1891_v2_grain_sprites_front;
  3: boxcar_type1891_sprites_top;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_v2_sprites_start,
  cargo_type_in_veh)
{
  CERE: boxcar_type1891_v2_grain_sprites_start;
  GRAI: boxcar_type1891_v2_grain_sprites_start;
  MAIZ: boxcar_type1891_v2_grain_sprites_start;
  OLSD: boxcar_type1891_v2_grain_sprites_start;
  WHEA: boxcar_type1891_v2_grain_sprites_start;
        boxcar_type1891_v2_base_sprites_start;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_sprites_start,
  check_year(1918))
{
  0: boxcar_type1891_v1_sprites_start;
     boxcar_type1891_v2_sprites_start;
}

long_vehicle_RND(boxcar_type1891, boxcar_type1891_v1_sprites_asm_back)

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity_1891,
[  STORE_TEMP(12, 0), // т
   STORE_TEMP(39, 1), // м³
   STORE_TEMP(22, 2), // м²
   STORE_TEMP(0, 3), // палетомест
   STORE_TEMP(5, 4)])// шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity_1905,
[  STORE_TEMP(15, 0), // т
   STORE_TEMP(39, 1), // м³
   STORE_TEMP(22, 2), // м²
   STORE_TEMP(0, 3), // палетомест
   STORE_TEMP(5, 4)])// шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity_1911,
[  STORE_TEMP(16, 0), // т
   STORE_TEMP(39, 1), // м³
   STORE_TEMP(22, 2), // м²
   STORE_TEMP(0, 3), // палетомест
   STORE_TEMP(5, 4)])// шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity_1933,
[  STORE_TEMP(18, 0), // т
   STORE_TEMP(39, 1), // м³
   STORE_TEMP(22, 2), // м²
   STORE_TEMP(0, 3), // палетомест
   STORE_TEMP(5, 4)])// шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity1,
  (build_year >= 1905) +
  (build_year >= 1911) +
  (build_year >= 1933))
{
  0: boxcar_type1891_cargo_capacity_1891;
  1: boxcar_type1891_cargo_capacity_1905;
  2: boxcar_type1891_cargo_capacity_1911;
     boxcar_type1891_cargo_capacity_1933;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_capacity,
  cargo_subtype)
{
  0: boxcar_type1891_cargo_capacity1;
  1: boxcar_type1891_cargo_capacity_1905;
  2: boxcar_type1891_cargo_capacity_1911;
     boxcar_type1891_cargo_capacity_1933;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_loading_speed,
  STORE_TEMP(9, 2))
{ all_wagons_box_loading_speed; }

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_up1905,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_WITH_INCREASED_CAPACITY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_up1911,
  cargo_subtype)
{
  0..1: return string(STR_REFIT_FACTORY);
  2: return string(STR_REFIT_WITH_INCREASED_CAPACITY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_up1933,
  cargo_subtype)
{
  0..2: return string(STR_REFIT_FACTORY);
  3: return string(STR_REFIT_WITH_INCREASED_CAPACITY);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_1891,
  (current_year >= 1905) +
  (current_year >= 1911) +
  (current_year >= 1933))
{
  1: boxcar_type1891_cargo_subtype_text_up1905;
  2: boxcar_type1891_cargo_subtype_text_up1911;
  3: boxcar_type1891_cargo_subtype_text_up1933;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_1905,
  (current_year >= 1911) +
  (current_year >= 1933))
{
  1: boxcar_type1891_cargo_subtype_text_up1911;
  2: boxcar_type1891_cargo_subtype_text_up1933;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text_1911,
  current_year >= 1933)
{
  1: boxcar_type1891_cargo_subtype_text_up1933;
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1891_cargo_subtype_text,
  (build_year >= 1905) +
  (build_year >= 1911) +
  (build_year >= 1933))
{
  0: boxcar_type1891_cargo_subtype_text_1891;
  1: boxcar_type1891_cargo_subtype_text_1905;
  2: boxcar_type1891_cargo_subtype_text_1911;
     return CB_RESULT_NO_TEXT;
}

wagon_running_cost(boxcar_type1891, 1)
hint_wagon(boxcar_type1891_b1905,
           12, 39,
           STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
           1891, 1925,
           STR_PURCHASE_HINT_FACTORY_PUTILOVSKY)
hint_wagon(boxcar_type1891_a1905,
           15, 39,
           STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
           1891, 1925,
           STR_PURCHASE_HINT_FACTORY_PUTILOVSKY)
hint_wagon(boxcar_type1891_a1911,
           16, 39,
           STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
           1891, 1925,
           STR_PURCHASE_HINT_FACTORY_PUTILOVSKY)
hint_wagon(boxcar_type1891_a1933,
           18, 39,
           STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
           1891, 1925,
           STR_PURCHASE_HINT_FACTORY_PUTILOVSKY)
hint_change_after3(boxcar_type1891, 1905, 1911, 1933)

item (FEAT_TRAINS, boxcar_type1891, 303) {
  property {
    name: string(STR_NAME_BOXCAR_ISSUE_OF_1891);
    vehicle_dates(1891, 1925, 30, 10, 4, 1)
    vehicle_wagon(7, 120)
    cargo_allow_refit: [cargo_all_lvst_grain_boxcarlist()];
  }
  graphics {
    default: boxcar_type1891_sprites;
    purchase: boxcar_type1891_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: boxcar_type1891_running_cost_factor;
    additional_text: boxcar_type1891_additional_text;
    refit_cost: boxcar_refit_cost;
    cargo_capacity: boxcar_type1891_cargo_capacity;
    loading_speed: boxcar_type1891_loading_speed;
    colour_mapping: any_cc_colour;
    cargo_subtype_text: boxcar_type1891_cargo_subtype_text;
  }
}
