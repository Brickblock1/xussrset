 // boxcar_type1926can

#define IMAGEFILE  "src/freight/boxcars/type1926can.png"
purchase_sprites(boxcar_type1926can, 0, 0)
combo_layout_boxcartop_template(boxcar_type1926can, 5, 32, 40)
combo_layout_boxcar_template(boxcar_type1926can_grain, 5, 32, 40 + 67 * 4)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, boxcar_type1926can_sprites_top_cc1,
  STORE_TEMP(ttdall_cc + 0x49, 0x100))
{ boxcar_type1926can_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_type1926can_sprites_top_cc2,
  STORE_TEMP(PALETTE_USE_DEFAULT, 0x100))
{ boxcar_type1926can_sprites_top; }

switch (FEAT_TRAINS, SELF, boxcar_type1926can_sprites_top_cc,
  cargo_subtype)
{
  4: boxcar_type1926can_sprites_top_cc2;
     boxcar_type1926can_sprites_top_cc1;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_base_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1926can_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1926can_sprites_front;
  3: boxcar_type1926can_sprites_top_cc;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_grain_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: boxcar_type1926can_grain_sprites_back;
  1: cargo5all_sprites;
  2: boxcar_type1926can_grain_sprites_front;
  3: boxcar_type1926can_sprites_top_cc;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_sprites_start,
  cargo_type_in_veh)
{
  CERE: boxcar_type1926can_grain_sprites_start;
  GRAI: boxcar_type1926can_grain_sprites_start;
  MAIZ: boxcar_type1926can_grain_sprites_start;
  OLSD: boxcar_type1926can_grain_sprites_start;
  WHEA: boxcar_type1926can_grain_sprites_start;
        boxcar_type1926can_base_sprites_start;
}

long_vehicle_RND(boxcar_type1926can, boxcar_type1926can_sprites_asm_back)

switch (FEAT_TRAINS, SELF, boxcar_type1926can_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
   0..2: return ttdall_cc + 0x48;
   3..5: return ttdall_cc + 0x6A;
         return ttdall_cc + 0x6C;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: boxcar_type1926can_random;
  1: return ttdall_cc + 0x48;
  2: return ttdall_cc + 0x6A;
  3: return ttdall_cc + 0x6C;
  4: return ttdall_cc + 0x61;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_48);
  2: return string(STR_REFIT_TTD_6A);
  3: return string(STR_REFIT_TTD_6C);
  4: return string(STR_REFIT_TTD_61);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_cargo_capacity,
[  STORE_TEMP(20, 0), // т
   STORE_TEMP(45, 1), // м³
   STORE_TEMP(22, 2), // м² 
   STORE_TEMP(0, 3), // палетомест 
   STORE_TEMP(5, 4), // шаблон
   cargo_type_in_veh])
{
  box_cargo_capacity;
}

switch (FEAT_TRAINS, SELF, boxcar_type1926can_loading_speed,
[  STORE_TEMP(9, 2),
   STORE_TEMP(9, 3)])
{ all_wagons_box_loading_speed; }

wagon_running_cost(boxcar_type1926can, 3)
hint_wagon_3fact(boxcar_type1926can,
                 20, 45,
                 STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
                 STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                 STR_PURCHASE_HINT_WAGON_TYPE_BOXCAR,
                 1926, 1931,
                 STR_PURCHASE_HINT_FACTORY_AMERICAN_RC, 
                 STR_PURCHASE_HINT_FACTORY_CANADIAN_S,
                 STR_PURCHASE_HINT_FACTORY_PITTSBURGH_S)
 
item (FEAT_TRAINS, boxcar_type1926can, 416) {
  property {
    name: string(STR_NAME_BOXCAR_ISSUE_OF_1926_CAN);
    vehicle_dates(1926, 1931, 30, 10, 4, 1)
    vehicle_wagon(11, 120)
    cargo_allow_refit: [cargo_all_lvst_grain_boxcarlist()];
  }
  graphics {
    default: boxcar_type1926can_sprites;
    purchase: boxcar_type1926can_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: boxcar_type1926can_running_cost_factor;
    refit_cost: boxcar_refit_cost;
    cargo_capacity: boxcar_type1926can_cargo_capacity;
    loading_speed: boxcar_type1926can_loading_speed;
    cargo_subtype_text: boxcar_type1926can_cargo_subtype_text;
    colour_mapping: boxcar_type1926can_colour_mapping;
    additional_text: boxcar_type1926can_additional_text;
  }
}
