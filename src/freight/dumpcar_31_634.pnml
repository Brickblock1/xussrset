 // dumpcar_31_634

/// Вагон
// 31-634 6-осный думпкар типа ВС-105
// dumpcar

#define IMAGEFILE  "src/freight/dumpcar-31-634.png"

// покупка
spriteset (dumpcar_31_634_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(32, 59)
}

// пустой
spriteset (dumpcar_31_634_empty_spriteset, IMAGEFILE)
{
  s9_template(0, 15, 0)
}

// шаблон груза, 3 вида загруженности
// 25-49%, 50-74%, полный
#define dumpcar_31_634_sprite_layout_template(x, y, cargo) \
spriteset (dumpcar_31_634_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (dumpcar_31_634_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+75, 0) \
} \
 \
spriteset (dumpcar_31_634_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+150, 0) \
} \
 \
spritegroup dumpcar_31_634_##cargo##_spritegroup \
{ \
  loading: [dumpcar_31_634_empty_spriteset, \
    dumpcar_31_634_##cargo##_1_spriteset, \
    dumpcar_31_634_##cargo##_2_spriteset, \
    dumpcar_31_634_##cargo##_full_spriteset]; \
  loaded: [dumpcar_31_634_empty_spriteset, \
    dumpcar_31_634_##cargo##_1_spriteset, \
    dumpcar_31_634_##cargo##_2_spriteset, \
    dumpcar_31_634_##cargo##_full_spriteset]; \
} \


dumpcar_31_634_sprite_layout_template(0, 90, coal)    // уголь
dumpcar_31_634_sprite_layout_template(0, 315, iore)   // жел. руда
dumpcar_31_634_sprite_layout_template(0, 540, core)   // медн. руда
dumpcar_31_634_sprite_layout_template(0, 765, aore)   // бокситы
dumpcar_31_634_sprite_layout_template(0, 990, clay)   // глина
dumpcar_31_634_sprite_layout_template(0, 1215, fert)  // удобрения
dumpcar_31_634_sprite_layout_template(0, 1440, grvl)  // гравий
dumpcar_31_634_sprite_layout_template(0, 1665, lime)  // известняк
dumpcar_31_634_sprite_layout_template(0, 1890, pota)  // поташ
dumpcar_31_634_sprite_layout_template(0, 2115, sand)  // песок
dumpcar_31_634_sprite_layout_template(0, 2340, sulp)  // сера

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dumpcar_31_634_get_spriteset1,
  cargo_type_in_veh)
{
  COAL: dumpcar_31_634_coal_spritegroup;
  IORE: dumpcar_31_634_iore_spritegroup;
  CORE: dumpcar_31_634_core_spritegroup;
  AORE: dumpcar_31_634_aore_spritegroup;
  CLAY: dumpcar_31_634_clay_spritegroup;
  FERT: dumpcar_31_634_fert_spritegroup;
  GRVL: dumpcar_31_634_grvl_spritegroup;
  LIME: dumpcar_31_634_lime_spritegroup;
  POTA: dumpcar_31_634_pota_spritegroup;
  SAND: dumpcar_31_634_sand_spritegroup;
  SULP: dumpcar_31_634_sulp_spritegroup;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, dumpcar_31_634_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  dumpcar_31_634_get_spriteset1;
}

// 70 км/ч; (120 км/ч пустой)
switch (FEAT_TRAINS, SELF, dumpcar_31_634_speed,
  cargo_count)
{
  0: return 120;
  return 70;
}

// 90 тонн, построенным с 1974 г. - 105 тонн
switch (FEAT_TRAINS, SELF, dumpcar_31_634_cargo_capacity,
  build_year >= 1974)
{
  1: return 105;
  return 90;
}

item (FEAT_TRAINS, dumpcar_31_634) 
{
  property
  {
    name: string(STR_DUMPCAR_31_634_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1967), 1, 1);
    retire_early: retire_early_default;
    model_life: get_model_life(1967,1990);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, CORE, GRVL, AORE, CLAY, LIME,
      POTA, SAND, SULP];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 23;
    cargo_capacity: 90;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: dumpcar_31_634_get_spriteset;
    purchase: dumpcar_31_634_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_DUMPCAR_31_634_PURCHASE_HINT);
    speed: dumpcar_31_634_speed;
    cargo_capacity: dumpcar_31_634_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
