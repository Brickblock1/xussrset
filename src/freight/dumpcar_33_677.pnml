 // dumpcar_33_677

/// Вагон
// 33-677 6-осный думпкар типа ВС-105
// dumpcar

#define IMAGEFILE  "src/freight/dumpcar-33-677.png"

// покупка
purchase_spriteset(dumpcar_33_677, 32, 59)

// пустой
spriteset (dumpcar_33_677_empty_spriteset, IMAGEFILE)
{
  s9_template(0, 15, 0)
}
// шаблон груза, 3 вида загруженности
// 25-49%, 50-74%, полный
#define dumpcar_33_677_sprite_layout_template(x, y, cargo) \
spriteset (dumpcar_33_677_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (dumpcar_33_677_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+75, 0) \
} \
 \
spriteset (dumpcar_33_677_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+150, 0) \
} \
 \
spritegroup dumpcar_33_677_##cargo##_spritegroup \
{ \
  loading: [dumpcar_33_677_empty_spriteset, \
    dumpcar_33_677_##cargo##_1_spriteset, \
    dumpcar_33_677_##cargo##_2_spriteset, \
    dumpcar_33_677_##cargo##_full_spriteset]; \
  loaded: [dumpcar_33_677_empty_spriteset, \
    dumpcar_33_677_##cargo##_1_spriteset, \
    dumpcar_33_677_##cargo##_2_spriteset, \
    dumpcar_33_677_##cargo##_full_spriteset]; \
} \


dumpcar_33_677_sprite_layout_template(0, 90, coal)    // уголь
dumpcar_33_677_sprite_layout_template(0, 315, iore)   // жел. руда
dumpcar_33_677_sprite_layout_template(0, 540, core)   // медн. руда
dumpcar_33_677_sprite_layout_template(0, 765, aore)   // бокситы
dumpcar_33_677_sprite_layout_template(0, 990, clay)   // глина
dumpcar_33_677_sprite_layout_template(0, 1215, fert)  // удобрения
dumpcar_33_677_sprite_layout_template(0, 1440, grvl)  // гравий
dumpcar_33_677_sprite_layout_template(0, 1665, lime)  // известняк
dumpcar_33_677_sprite_layout_template(0, 1890, pota)  // поташ
dumpcar_33_677_sprite_layout_template(0, 2115, sand)  // песок
dumpcar_33_677_sprite_layout_template(0, 2340, sulp)  // сера

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, dumpcar_33_677_get_spriteset_2,
  cargo_type_in_veh)
{
  COAL: dumpcar_33_677_coal_spritegroup;
  IORE: dumpcar_33_677_iore_spritegroup;
  CORE: dumpcar_33_677_core_spritegroup;
  AORE: dumpcar_33_677_aore_spritegroup;
  CLAY: dumpcar_33_677_clay_spritegroup;
  FERT: dumpcar_33_677_fert_spritegroup;
  GRVL: dumpcar_33_677_grvl_spritegroup;
  LIME: dumpcar_33_677_lime_spritegroup;
  POTA: dumpcar_33_677_pota_spritegroup;
  SAND: dumpcar_33_677_sand_spritegroup;
  SULP: dumpcar_33_677_sulp_spritegroup;
        align_9_spriteset;
}

long_vehicle(dumpcar_33_677, )

item (FEAT_TRAINS, dumpcar_33_677) 
{
  property
  {
    name: string(STR_DUMPCAR_33_677_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1992), 1, 1);
    model_life: get_model_life(1992,2050);
    retire_early: get_retire_early(1992,2050);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, IORE, CORE, GRVL, AORE, CLAY, LIME,
      POTA, SAND, SULP];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 80 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 23;
    cargo_capacity: 105;
    weight: 62 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: dumpcar_33_677_get_spriteset;
    purchase: dumpcar_33_677_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_DUMPCAR_33_677_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, dumpcar_33_677)
  {
    property
    { name: string(STR_DUMPCAR_33_677_NAME_LONG); }
  }
}

