 // flatbed_13_401

/// Платформа
// Платформа 13-401
// flatbed

#define IMAGEFILE  "src/freight/flatbed-13-401.png"

// покупка
spriteset (flatbed_13_401_purchase_spriteset_ico, IMAGEFILE)
{ new_purchase_template(0, 0) }
spriteset (flatbed_13_401_purchase_spriteset_noico, IMAGEFILE)
{ new_purchase_template_noico(0, 0) }
switch (FEAT_TRAINS, SELF, flatbed_13_401_purchase_spriteset, 
  enable_icons)
{  1: flatbed_13_401_purchase_spriteset_ico;
      flatbed_13_401_purchase_spriteset_noico;}

// пусто
spriteset (flatbed_13_401_empty_spriteset, IMAGEFILE)
{
  s9_template(45, 30, 0)
}

// шаблон груза, 3 вида загруженности
#define flatbed_13_401_sprite_layout3_template(x, y, cargo) \
spriteset (flatbed_13_401_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (flatbed_13_401_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+75, 0) \
} \
 \
spriteset (flatbed_13_401_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+150, 0) \
} \
 \
spritegroup flatbed_13_401_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_2_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_2_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
} \

// шаблон груза, 2 вида загруженности
#define flatbed_13_401_sprite_layout2_template(x, y, cargo) \
spriteset (flatbed_13_401_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spriteset (flatbed_13_401_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+75, 0) \
} \
 \
spritegroup flatbed_13_401_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
} \

// шаблон груза, 2 вида загруженности 2
#define flatbed_13_401_sprite_layout2_template2(x, y, cargo) \
spriteset (flatbed_13_401_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+150, 0) \
} \
 \
spriteset (flatbed_13_401_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y+75, 0) \
} \
 \
spritegroup flatbed_13_401_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_1_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
} \

// шаблон груза, 1 вид загруженности
#define flatbed_13_401_sprite_layout1_template(x, y, cargo) \
spriteset (flatbed_13_401_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s9_template(x, y, 0) \
} \
 \
spritegroup flatbed_13_401_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_401_empty_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset, \
    flatbed_13_401_##cargo##_full_spriteset]; \
} \

flatbed_13_401_sprite_layout2_template(45, 105, good1)  // товары
flatbed_13_401_sprite_layout2_template2(45, 105, good2)  // товары
flatbed_13_401_sprite_layout1_template(45, 330, good3) // товары

flatbed_13_401_sprite_layout2_template(45, 630, wdpr1)  // лесоматериалы
flatbed_13_401_sprite_layout2_template(45, 780, wdpr2)  // лесоматериалы
flatbed_13_401_sprite_layout2_template(45, 930, wdpr3)  // лесоматериалы
flatbed_13_401_sprite_layout2_template(45, 1080, wdpr4)  // лесоматериалы

flatbed_13_401_sprite_layout2_template(45, 1230, stel1)  // сталь
flatbed_13_401_sprite_layout2_template2(45, 1230, stel2)  // сталь
flatbed_13_401_sprite_layout3_template(45, 1455, stel3) // сталь
flatbed_13_401_sprite_layout3_template(45, 1680, stel4) // сталь

flatbed_13_401_sprite_layout1_template(45, 1905, wood) // лес

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, flatbed_13_401_get_spriteset3,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  3: flatbed_13_401_wdpr1_spritegroup;
  3: flatbed_13_401_wdpr2_spritegroup;
  5: flatbed_13_401_wdpr3_spritegroup;
  1: flatbed_13_401_wdpr4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, flatbed_13_401_get_spriteset2,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  4: flatbed_13_401_good1_spritegroup;
  4: flatbed_13_401_good2_spritegroup;
  5: flatbed_13_401_good3_spritegroup;
}


random_switch (FEAT_TRAINS, SELF, flatbed_13_401_get_spriteset1,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  1: flatbed_13_401_stel1_spritegroup;
  1: flatbed_13_401_stel2_spritegroup;
  2: flatbed_13_401_stel3_spritegroup;
  2: flatbed_13_401_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, flatbed_13_401_spriteset,
  cargo_type_in_veh)
{
  GOOD: flatbed_13_401_get_spriteset2;
  WDPR: flatbed_13_401_get_spriteset3;
  WOOD: flatbed_13_401_wood_spritegroup;
  STEL: flatbed_13_401_get_spriteset1;
  align_9_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, flatbed_13_401_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  flatbed_13_401_spriteset;
}

random_switch (FEAT_TRAINS, SELF, flatbed_13_401_random, 0 )
{
  1: return ttd_cc + 22;
  1: return ttd_cc + 23;
  1: return ttd_cc + 24;
  1: return ttd_cc + 27;
  1: return ttd_cc + 82;
  1: return ttd_cc + 39;
  1: return ttd_cc + 40;
  1: return ttd_cc + 41;
}                      

switch (FEAT_TRAINS, SELF, flatbed_13_401_colour_mapping,
  cargo_subtype)
{
  0: flatbed_13_401_random;
  1: return ttd_cc + 22;
  2: return ttd_cc + 23;
  3: return ttd_cc + 24;
  4: return ttd_cc + 27;
  5: return ttd_cc + 82;
  6: return ttd_cc + 39;
  7: return ttd_cc + 40;
  8: return ttd_cc + 41;
  return CB_FAILED;
}


switch (FEAT_TRAINS, SELF, flatbed_13_401_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_49);
  2: string(STR_REFIT_TTD_4A);
  3: string(STR_REFIT_TTD_4B);
  4: string(STR_REFIT_TTD_53);
  5: string(STR_REFIT_TTD_5A);
  6: string(STR_REFIT_TTD_71);
  7: string(STR_REFIT_TTD_72);
  8: string(STR_REFIT_TTD_73);
  return CB_RESULT_NO_TEXT;
}  


switch (FEAT_TRAINS, SELF, flatbed_13_401_cargo_capacity_1968, cargo_type_in_veh)
{
  GOOD: return 95;
  return 63;
}

switch (FEAT_TRAINS, SELF, flatbed_13_401_cargo_capacity_1979, cargo_type_in_veh)
{
  GOOD: return 99;
  return 66;
}

switch (FEAT_TRAINS, SELF, flatbed_13_401_cargo_capacity_1981, cargo_type_in_veh)
{
  GOOD: return 105;
  return 70;
}


switch (FEAT_TRAINS, SELF, flatbed_13_401_cargo_capacity,
  (build_year >= 1979) +
  (build_year >= 1981))
{
  0: flatbed_13_401_cargo_capacity_1968;
  1: flatbed_13_401_cargo_capacity_1979;
  flatbed_13_401_cargo_capacity_1981;
}

item (FEAT_TRAINS, flatbed_13_401) 
{
  property
  {
    name: string(STR_FLATBED_13_401_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1968), 1, 1);
    model_life: get_model_life(1968,1984);
    retire_early: get_retire_early(1968,1984);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, STEL, WDPR];
    default_cargo_type: STEL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 63;
    weight: 21 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: flatbed_13_401_get_spriteset;
    purchase: flatbed_13_401_purchase_spriteset;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_FLATBED_13_401_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: flatbed_13_401_cargo_subtype_text;
    colour_mapping: flatbed_13_401_colour_mapping;
    cargo_capacity: flatbed_13_401_cargo_capacity;
  }
}
