 // flatbed_13_xx

/// Платформа
// Платформа 13-XX (грузоп. 50 т, БМЗ)
// flatbed

#define IMAGEFILE  "src/freight/flatbed-13-xx-50t-bmz.png"

// покупка
spriteset (flatbed_13_xx_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// пусто
spriteset (flatbed_13_xx_empty_spriteset, IMAGEFILE)
{
  s8_template(7, 36, 0)
}

spriteset (flatbed_13_xx_empty_v2_spriteset, IMAGEFILE)
{
  s8_template(7, 493, 0)
}

// шаблон груза, 4 вида загруженности
#define flatbed_13_xx_sprite_layout4_template(x, y, cargo, ver) \
spriteset (flatbed_13_xx_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+35, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_3_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+70, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+105, 0) \
} \
 \
spritegroup flatbed_13_xx_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_2_spriteset, \
    flatbed_13_xx_##cargo##_3_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_2_spriteset, \
    flatbed_13_xx_##cargo##_3_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
} \

// шаблон груза, 3 вида загруженности
#define flatbed_13_xx_sprite_layout3_template(x, y, cargo, ver) \
spriteset (flatbed_13_xx_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+35, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+70, 0) \
} \
 \
spritegroup flatbed_13_xx_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_2_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_2_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
} \

// шаблон груза, 2 вида загруженности
#define flatbed_13_xx_sprite_layout2_template(x, y, cargo, ver) \
spriteset (flatbed_13_xx_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (flatbed_13_xx_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+35, 0) \
} \
 \
spritegroup flatbed_13_xx_##cargo##_spritegroup \
{ \
  loading: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
  loaded: [flatbed_13_xx_empty##ver##_spriteset, \
    flatbed_13_xx_##cargo##_1_spriteset, \
    flatbed_13_xx_##cargo##_full_spriteset]; \
} \

flatbed_13_xx_sprite_layout4_template(7,  71, good,)   // товары

flatbed_13_xx_sprite_layout2_template(7, 211, wdpr,)  // лесоматериалы
flatbed_13_xx_sprite_layout3_template(7, 281, stel,)  // сталь
flatbed_13_xx_sprite_layout3_template(7, 386, stel2,) // сталь
flatbed_13_xx_sprite_layout4_template(7, 528, stel3, _v2) // сталь
flatbed_13_xx_sprite_layout3_template(7, 668, stel4, _v2) // сталь

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, flatbed_13_xx_get_spriteset1,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: flatbed_13_xx_stel_spritegroup;
  32: flatbed_13_xx_stel2_spritegroup;
  32: flatbed_13_xx_stel3_spritegroup;
  32: flatbed_13_xx_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, flatbed_13_xx_get_spriteset,
  cargo_type_in_veh)
{
  GOOD: flatbed_13_xx_good_spritegroup;
  WDPR: flatbed_13_xx_wdpr_spritegroup;
  STEL: flatbed_13_xx_get_spriteset1;
  align_8_spriteset;
}

item (FEAT_TRAINS, flatbed_13_xx) 
{
  property
  {
    name: string(STR_FLATBED_13_XX_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1930), 1, 1);
    model_life: get_model_life(1930,1938);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, STEL, WDPR];
    default_cargo_type: STEL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 50;
    weight: 18 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: flatbed_13_xx_get_spriteset;
    purchase: flatbed_13_xx_purchase_spriteset;
    additional_text: return string(STR_FLATBED_13_XX_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
