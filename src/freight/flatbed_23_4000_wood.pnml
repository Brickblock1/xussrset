 // flatbed_23_4000_wood

/// Вагон
// 23-4000 платформа для лесоматериалов (1979-)
// Вагон симметричный, конструкция bitmask(TRIGGER_VEHICLE_SERVICE) не требуется

#define IMAGEFILE  "src/freight/flatbed-23-4000-wood-1979.png"
purchase_sprites(flatbed_23_4000_wood, 0, 0)

flatbed_empty_layout_template(flatbed_23_4000_wood_v1, 14, 0, 52)
flatbed_empty_layout_template(flatbed_23_4000_wood_v2, 14, 0, 119)
flatbed_empty_layout_template(flatbed_23_4000_wood_v3, 14, 0, 186)

flatbed_layout1_template(flatbed_23_4000_wood_v1, 14, 440, 52, wood)
flatbed_layout1_template(flatbed_23_4000_wood_v2, 14, 440, 119, wood)
flatbed_layout1_template(flatbed_23_4000_wood_v3, 14, 440, 186, wood)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, flatbed_23_4000_wood_random, 0 )
{
  independent: flatbed_23_4000_wood_v1_wood_sprites;
  1: flatbed_23_4000_wood_v1_wood_sprites;
  1: flatbed_23_4000_wood_v2_wood_sprites;
  1: flatbed_23_4000_wood_v3_wood_sprites;
}                      

switch (FEAT_TRAINS, SELF, flatbed_23_4000_wood_sprites_2,
  cargo_subtype)
{
  0: flatbed_23_4000_wood_random;
  1: flatbed_23_4000_wood_v1_wood_sprites;
  2: flatbed_23_4000_wood_v2_wood_sprites;
  3: flatbed_23_4000_wood_v3_wood_sprites;
     align_14_sprites;
}

long_vehicle(flatbed_23_4000_wood)

switch (FEAT_TRAINS, SELF, flatbed_23_4000_wood_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_59);
  2: string(STR_REFIT_TTD_62);
  3: string(STR_REFIT_TTD_53);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, flatbed_23_4000_wood) 
{
  property
  {
    name: string(STR_FLATBED_23_4000_WOOD_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1979), 1, 1);
    model_life: get_model_life(1979,2050);
    retire_early: get_retire_early(1979,2050);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [WOOD];
    default_cargo_type: WOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 14;
    cargo_capacity: 56;
    weight: 34 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: flatbed_23_4000_wood_sprites;
    purchase: flatbed_23_4000_wood_purchase_sprites;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_FLATBED_23_4000_WOOD_PURCHASE_HINT);
    cargo_subtype_text: flatbed_23_4000_wood_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, flatbed_23_4000_wood)
  {
    property
    { name: string(STR_FLATBED_23_4000_WOOD_NAME_LONG); }
  }
}

