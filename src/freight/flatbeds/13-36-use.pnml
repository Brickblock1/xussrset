 // flatbed_13_36

switch (FEAT_TRAINS, SELF, flatbed_13_36_sprites_start,
  cargo_type_in_veh)
{
  GOOD: flatbed_13_xx_good_sprites;
  WDPR: flatbed_13_xx_wdpr_sprites;
  WOOD: flatbed_13_xx_wood_sprites;
  STEL: flatbed_13_xx_stel_sprites;
        align_8_sprites;
}

short_vehicle_RND(flatbed_13_36, flatbed_13_xx_stel1_sprites)

switch (FEAT_TRAINS, SELF, flatbed_13_36_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 64;
  VEHI: return  3;
  ENSP: return 31;
  MNSP: return 62;
  VPTS: return 62;
  AORE: return 46;
  BDMT: return 57;
  BRCK: return 57;
  GRVL: return 57;
  LIME: return 50;
  SAND: return 57;
  SULP: return 46;
        return 60;
}

switch (FEAT_TRAINS, SELF, flatbed_13_36_stel_loading_speed,
  (random_bits / 2) % 16)
{
  0..5: return (cargo_capacity / 2 + 1);   // фермы
        return (cargo_capacity / 3 + 1);   // трубы
}

switch (FEAT_TRAINS, SELF, flatbed_13_36_loading_speed, 
  cargo_type_in_veh)
{ 
  STEL: flatbed_13_36_stel_loading_speed;
  GOOD: return (cargo_capacity / 2 + 1);
        all_wagons_bulk_loading_speed; 
}

wagon_running_cost(flatbed_13_36, 21)
hint_wagon_flat(flatbed_13_36,
                60, 36,
                STR_PURCHASE_HINT_LOADING_SPEED_5TIC,
                STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                STR_PURCHASE_HINT_WAGON_TYPE_FLATBED_4AXLE,
                1936, 1948,
                STR_PURCHASE_HINT_FACTORY_URALVAGONZAVOD)

item (FEAT_TRAINS, flatbed_13_36, 508) {
  property {
    name: string(STR_MODEL_NUMBER2, 13, 36);
    vehicle_dates(1936, 1948, 32, 10, 8, 3)
    vehicle_wagon(22, 120)
    vehicle_wagon_cargo(60, 10)
    cargo_allow_refit: [STEL, GOOD, WDPR, WOOD];
  }
  graphics {
    default: flatbed_13_36_sprites;
    purchase: flatbed_13_xx_purchase_sprites;
    running_cost_factor: flatbed_13_36_running_cost_factor;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: flatbed_13_xx_cargo_subtype_text;
    loading_speed: flatbed_13_36_loading_speed;
    colour_mapping: flatbed_13_xx_colour_mapping;
    cargo_capacity: flatbed_13_36_cargo_capacity;
    additional_text: flatbed_13_36_additional_text;
  }
}

long_name_template(flatbed_13_36, STR_LONGNAME_FLATBED_13_36)
