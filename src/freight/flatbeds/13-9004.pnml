// flatbed_13_9004

#define IMAGEFILE  "src/freight/flatbeds/13-9004.png"
purchase_sprites(flatbed_13_9004, 0, 0)
container_platform_layout_template(flatbed_13_9004_back, 12, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, flatbed_13_9004_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: flatbed_13_9004_back_sprites_start;
  1: cargo12_container_20and40_sprites;
     return CB_FAILED;
}

long_vehicle(flatbed_13_9004)

switch (FEAT_TRAINS, SELF, flatbed_13_9004_running_cost_factor,
[  STORE_TEMP(0, 0),                              // Моторы
   STORE_TEMP(0, 1),                              // Бригада
   STORE_TEMP(16 + 2 * (build_year >= 1994), 2),  // Износ
   STORE_TEMP(0, 3),                              // Сопровождение
   STORE_TEMP(8 + (build_year >= 1994), 4),       // ТО
   STORE_TEMP(0, 5),                              // Сертификация

   STORE_TEMP(120, 6),                                            // Скорость
   STORE_TEMP(26 - 2 * (build_year >= 1994), 7),                  // Тара
   STORE_TEMP(LOAD_TEMP(7) + 65 + 3 * (build_year >= 1994), 8)])  // Максимальная масса
{ all_running_cost_factor; }                                      // 24 - 27

switch (FEAT_TRAINS, SELF, flatbed_13_9004_weight,
  STORE_TEMP(26 - 2 * (build_year >= 1994), 7))
{
  container_20and40_weight;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9004_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..2: return ttdall_cc + 0x04;
        return ttdall_cc + 0xC9;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9004_colour_mapping,
  cargo_subtype)
{
  0: flatbed_13_9004_random;
  1: return ttdall_cc + 0x04;
  2: return ttdall_cc + 0xC9;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9004_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_04);
  2: return string(STR_REFIT_TTD_9C);
     return CB_RESULT_NO_TEXT;
}

engine_cost_change_build_after(flatbed_13_9004, 62,
                                                69, 1994)

hint_wagon_flat_2fact(flatbed_13_9004_b1994,
                      65, 53,
                      string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                      STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                      STR_PURCHASE_HINT_WAGON_TYPE_FLATBED,
                      1987, 1993,
                      string(STR_PURCHASE_HINT_FACTORY_RUZHIMMASH), string(STR_PURCHASE_HINT_FACTORY_ABAKANSKII_CW))
hint_wagon_flat_noto_2fact(flatbed_13_9004_a1994,
                     68, 53,
                     string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                     STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                     STR_PURCHASE_HINT_WAGON_TYPE_FLATBED,
                     1994,
                     string(STR_PURCHASE_HINT_FACTORY_RUZHIMMASH), string(STR_PURCHASE_HINT_FACTORY_ABAKANSKII_CW))
hint_change_after(flatbed_13_9004, 1994)

item (FEAT_TRAINS, flatbed_13_9004, 595) {
  property {
    name: string(STR_MODEL_NUMBER2, 13, 9004);
    vehicle_dates(1987, 2050, 32, 10, 8, 62)
    vehicle_wagon(22, 120)
    cargo_allow_refit: [cargo_all_containerlist()];
  }
  graphics {
         additional_text: flatbed_13_9004_additional_text;
        articulated_part: articulated_part_dummy4;
    bitmask_vehicle_info: container_bitmask_vehicle_info;
          cargo_capacity: container_20and40_cargo_capacity;
      cargo_subtype_text: flatbed_13_9004_cargo_subtype_text;
          colour_mapping: flatbed_13_9004_colour_mapping;
             cost_factor: flatbed_13_9004_cost_factor;
                 default: flatbed_13_9004_sprites;
           loading_speed: container_loading_speed;
                purchase: flatbed_13_9004_purchase_sprites;
         purchase_weight: return (26 - 2 * (build_year >= 1994));
              refit_cost: container_20and40_refit_cost;
     running_cost_factor: flatbed_13_9004_running_cost_factor;
                  weight: flatbed_13_9004_weight;
  }
}

long_name_template(flatbed_13_9004, STR_LONGNAME_FLATBED_13_9004)
