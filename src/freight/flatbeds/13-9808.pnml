 // flatbed_13_9808
// 417 (1A1h)

#define IMAGEFILE  "src/freight/flatbeds/13-9808-part1.png"
purchase_sprites(flatbed_13_9808, 0, 0)
flatbed_empty_layout_template(flatbed_13_9808, 9, 32, 40)
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*1,  good1) // товары
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*3,  good2) // товары
flatbed_layout1_template_fixed(flatbed_13_9808, 9, 32, 40+67*5,  good3) // товары
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*6,  wdpr1) // лесоматериалы
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*8,  wdpr2) // лесоматериалы
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*10, wdpr3) // лесоматериалы
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40+67*12, wdpr4) // лесоматериалы
#undef IMAGEFILE
#define IMAGEFILE  "src/freight/flatbeds/13-9808-part2.png"
flatbed_layout2_template_fixed(flatbed_13_9808, 9, 32, 40,       stel1) // сталь
flatbed_layout3_template_fixed(flatbed_13_9808, 9, 32, 40+67*2,  stel2) // сталь
flatbed_layout3_template_fixed(flatbed_13_9808, 9, 32, 40+67*5,  stel3) // сталь
flatbed_layout1_template_fixed(flatbed_13_9808, 9, 32, 40+67*8,  wood)  // лес
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, flatbed_13_9808_good_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  independent: flatbed_13_9808_good1_sprites;
  7: flatbed_13_9808_good1_sprites;
  4: flatbed_13_9808_good2_sprites;
  5: flatbed_13_9808_good3_sprites;
}

random_switch (FEAT_TRAINS, SELF, flatbed_13_9808_wdpr_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  dependent: flatbed_13_9808_good_sprites;
  4: flatbed_13_9808_wdpr1_sprites;
  4: flatbed_13_9808_wdpr2_sprites;
  7: flatbed_13_9808_wdpr3_sprites;
  1: flatbed_13_9808_wdpr4_sprites;
}

random_switch (FEAT_TRAINS, SELF, flatbed_13_9808_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  dependent: flatbed_13_9808_good_sprites;
  6: flatbed_13_9808_stel1_sprites;
  5: flatbed_13_9808_stel2_sprites;
  5: flatbed_13_9808_stel3_sprites;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_sprites_start,
  cargo_type_in_veh)
{
  GOOD: flatbed_13_9808_good_sprites;
  ENSP: flatbed_13_9808_good_sprites;
  MNSP: flatbed_13_9808_good_sprites;
  VPTS: flatbed_13_9808_good_sprites;
  WOOD: flatbed_13_9808_wood_sprites;
  WDPR: flatbed_13_9808_wdpr_sprites;
  AORE: flatbed_13_9808_good_sprites;
  CORE: flatbed_13_9808_good_sprites;
  IORE: flatbed_13_9808_good_sprites;
  BDMT: flatbed_13_9808_good_sprites;
  BRCK: flatbed_13_9808_good_sprites;
  CLAY: flatbed_13_9808_good_sprites;
  GRVL: flatbed_13_9808_good_sprites;
  LIME: flatbed_13_9808_good_sprites;
  POTA: flatbed_13_9808_good_sprites;
  SAND: flatbed_13_9808_good_sprites;
  SCMT: flatbed_13_9808_good_sprites;
  STEL: flatbed_13_9808_stel_sprites;
  SULP: flatbed_13_9808_good_sprites;
        align_9_sprites;
}

long_vehicle_RND(flatbed_13_9808, flatbed_13_9808_good1_sprites)

random_switch (FEAT_TRAINS, SELF, flatbed_13_9808_random, 0 )
{
  independent: flatbed_13_9808_good_sprites;
  1: return ttdall_cc + 0x5C;
  1: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x63;
  2: return ttdall_cc + 0x73;
  1: return ttdall_cc + 0x83;
  1: return ttdall_cc + 0x9C;
  1: return ttdall_cc + 0xCE;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: flatbed_13_9808_random;
  1: return ttdall_cc + 0x5C;
  2: return ttdall_cc + 0x62;
  3: return ttdall_cc + 0x63;
  4: return ttdall_cc + 0x73;
  5: return ttdall_cc + 0x83;
  6: return ttdall_cc + 0x9C;
  7: return ttdall_cc + 0xCE;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_5C);
  2: return string(STR_REFIT_TTD_62);
  3: return string(STR_REFIT_TTD_63);
  4: return string(STR_REFIT_TTD_73);
  5: return string(STR_REFIT_TTD_83);
  6: return string(STR_REFIT_TTD_9C);
  7: return string(STR_REFIT_TTD_CE);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_cargo_capacity,
  cargo_type_in_veh)
{
  GOOD: return 108;
  WOOD: return 64;
  WDPR: return 68;
  BRCK: return 61;
        return 72;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_stel_loading_speed,
  (random_bits / 2) % 16)
{
  0..5: return (cargo_capacity / 2 + 1);   // фермы
        return (cargo_capacity / 3 + 1);   // трубы
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_good_loading_speed,
  (random_bits / 2) % 16)
{
  0..10: return (cargo_capacity / 2 + 1);    // товары 2
         return 255;                         // товары 1
}

switch (FEAT_TRAINS, SELF, flatbed_13_9808_loading_speed, 
  cargo_type_in_veh)
{ 
  STEL: flatbed_13_9808_stel_loading_speed;
  GOOD: flatbed_13_9808_good_loading_speed;
        all_wagons_bulk_loading_speed; 
}

wagon_running_cost(flatbed_13_9808, 15)

item (FEAT_TRAINS, flatbed_13_9808, 417) {
  property {
    name: string(STR_MODEL_NUMBER2, 13, 9808);
    vehicle_dates(2008, 2050, 32, 10, 8, 2)
    vehicle_wagon(22, 120)
    vehicle_wagon_cargo(72, 10)
    cargo_allow_refit: [STEL, GOOD, WDPR, WOOD];
  }
  graphics {
    default: flatbed_13_9808_sprites;
    purchase: flatbed_13_9808_purchase_sprites;
    running_cost_factor: flatbed_13_9808_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: flatbed_13_9808_cargo_subtype_text;
    colour_mapping: flatbed_13_9808_colour_mapping;
    cargo_capacity: flatbed_13_9808_cargo_capacity;
    loading_speed: flatbed_13_9808_loading_speed;
    additional_text: return string(STR_PURCHASE_HINT_WAGON_FLATBED,
                                   72, 38,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_FLATBED_4AXLE),
                                   string(STR_PURCHASE_HINT_FROM, 2008),
                                   string(STR_PURCHASE_HINT_FACTORY_ENGELS));
  }
}

long_name_template(flatbed_13_9808, STR_LONGNAME_FLATBED_13_9808)
