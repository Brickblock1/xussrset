 // dumpcar_31_634

/// Вагон
// 31-634 6-осный думпкар типа ВС-105
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон
// dumpcar

#define IMAGEFILE  "src/freight/flatbeds/31-634.png"
purchase_sprites(dumpcar_31_634, 32, 59)
flatbed_empty_layout_template(dumpcar_31_634_v1, 9, 0, 15)
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0,   90, coal)  // уголь             
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0,  315, iore)  // жел. руда         
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0,  540, core)  // медн. руда        
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0,  765, aore)  // бокситы           
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0,  990, clay)  // глина             
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 1215, fert)  // удобрения         
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 1440, grvl)  // гравий            
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 1665, lime)  // известняк         
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 1890, pota)  // поташ             
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 2115, sand)  // песок             
flatbed_layout3_template(dumpcar_31_634_v1, 9, 0, 2340, sulp)  // сера              
#undef IMAGEFILE                       

switch (FEAT_TRAINS, SELF, dumpcar_31_634_sprites_start,
  cargo_type_in_veh)
{
  COAL: dumpcar_31_634_v1_coal_sprites;
  IORE: dumpcar_31_634_v1_iore_sprites;
  CORE: dumpcar_31_634_v1_core_sprites;
  AORE: dumpcar_31_634_v1_aore_sprites;
  CLAY: dumpcar_31_634_v1_clay_sprites;
  FERT: dumpcar_31_634_v1_fert_sprites;
  GRVL: dumpcar_31_634_v1_grvl_sprites;
  LIME: dumpcar_31_634_v1_lime_sprites;
  POTA: dumpcar_31_634_v1_pota_sprites;
  SAND: dumpcar_31_634_v1_sand_sprites;
  SULP: dumpcar_31_634_v1_sulp_sprites;
// temp
  SCMT: dumpcar_31_634_v1_iore_sprites;
        align_9_sprites;
}

long_vehicle_RND(dumpcar_31_634, dumpcar_31_634_v1_coal_sprites)

wagon_running_cost(dumpcar_31_634, 17)

wagon_speed_change_on_load(dumpcar_31_634, 120, 70, 0)

// 90 тонн, построенным с 1974 г. - 105 тонн
switch (FEAT_TRAINS, SELF, dumpcar_31_634_cargo_capacity_b1974, cargo_type_in_veh)
{
  COAL: return 40;
  IORE: return 90;
  CORE: return 90;
  GRVL: return 80;
  AORE: return 64;
  CLAY: return 85;
  LIME: return 70;
  POTA: return 90;
  SAND: return 80;
  SULP: return 65;
  SCMT: return 90;
        return 90;
}

switch (FEAT_TRAINS, SELF, dumpcar_31_634_cargo_capacity_a1974, cargo_type_in_veh)
{
  COAL: return 40;
  IORE: return 105;
  CORE: return 105;
  GRVL: return 80;
  AORE: return 64;
  CLAY: return 85;
  LIME: return 70;
  POTA: return 105;
  SAND: return 80;
  SULP: return 65;
  SCMT: return 105;
        return 105;
}
switch (FEAT_TRAINS, SELF, dumpcar_31_634_cargo_capacity,
  build_year >= 1974)
{
  1: dumpcar_31_634_cargo_capacity_a1974;
     dumpcar_31_634_cargo_capacity_b1974;
}

switch (FEAT_TRAINS, SELF, dumpcar_31_634_additional_text,
  current_year >= 1974)
{
  0: return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   90, 50,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                   string(STR_PURCHASE_HINT_WAGON_TYPE_DUMPCAR_6AXLE),
                   string(STR_PURCHASE_HINT_FROMTO, 1967, 1990), 
                   string(STR_PURCHASE_HINT_FACTORY_KALININGRAD_CW),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 70, 0));
     return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   105, 50,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                   string(STR_PURCHASE_HINT_WAGON_TYPE_DUMPCAR_6AXLE),
                   string(STR_PURCHASE_HINT_FROMTO, 1967, 1990), 
                   string(STR_PURCHASE_HINT_FACTORY_KALININGRAD_CW),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 70, 0));
}

item (FEAT_TRAINS, dumpcar_31_634) {
  property {
    name: string(STR_NAME_DUMPCAR_31_634);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1967), 1, 1);
    model_life: get_model_life(1967,1990);
    retire_early: get_retire_early(1967,1990);
    vehicle_life: 22;
    reliability_decay: 10;
    cargo_allow_refit: [COAL, IORE, CORE, GRVL, AORE, CLAY, LIME, POTA, SAND, SULP, SCMT];
    default_cargo_type: COAL; 
    cost_factor: 3;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 23;
    cargo_capacity: 90;
    weight: 48.5 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: dumpcar_31_634_sprites;
    purchase: dumpcar_31_634_purchase_sprites;
    purchase_speed: dumpcar_31_634_purchase_speed;
    running_cost_factor: dumpcar_31_634_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    additional_text: dumpcar_31_634_additional_text;
    speed: dumpcar_31_634_speed;
    cargo_capacity: dumpcar_31_634_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

long_name_template(dumpcar_31_634, STR_LONGNAME_DUMPCAR_31_634)
