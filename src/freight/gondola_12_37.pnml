 // gondola_12_37

#define IMAGEFILE  "src/freight/gondola-12-xx_12-37.png"

// покупка
purchase_spriteset(gondola_12_37, 0, 20)

// пусто
spriteset (gondola_12_37_empty_sprites, IMAGEFILE)
{
  s8_template(125, 56+36, 0)
}

// шаблон груза, 2 ливреи, 3 вида загруженности
// 25-49%, 50-74%, полный
#define gondola_12_37_sprite_layout_template(x, y, cargo)   \
spriteset (gondola_12_37_##cargo##_1_sprites, IMAGEFILE)    \
{ s8_template(x, y+36, 0) }                                 \
spriteset (gondola_12_37_##cargo##_2_sprites, IMAGEFILE)    \
{ s8_template(x+298, y+36, 0) }                             \
spriteset (gondola_12_37_##cargo##_full_sprites, IMAGEFILE) \
{ s8_template(x+596, y+36, 0) }                             \
spritegroup gondola_12_37_##cargo##_sprites                 \
{ loading: [gondola_12_37_empty_sprites,                    \
    gondola_12_37_##cargo##_1_sprites,                      \
    gondola_12_37_##cargo##_2_sprites,                      \
    gondola_12_37_##cargo##_full_sprites];                  \
  loaded: [gondola_12_37_empty_sprites,                     \
    gondola_12_37_##cargo##_1_sprites,                      \
    gondola_12_37_##cargo##_2_sprites,                      \
    gondola_12_37_##cargo##_full_sprites]; }                \

gondola_12_37_sprite_layout_template(423, 56, coal)    // уголь
gondola_12_37_sprite_layout_template(423, 205, iore)   // жел. руда
gondola_12_37_sprite_layout_template(423, 354, core)   // медн. руда
gondola_12_37_sprite_layout_template(423, 503, aore)   // бокситы
gondola_12_37_sprite_layout_template(423, 652, clay)   // глина
gondola_12_37_sprite_layout_template(423, 801, fert)   // удобрения
gondola_12_37_sprite_layout_template(423, 950, grvl)   // гравий
gondola_12_37_sprite_layout_template(423, 1099, lime)  // известняк
gondola_12_37_sprite_layout_template(423, 1248, pota)  // поташ
gondola_12_37_sprite_layout_template(423, 1397, sand)  // песок
gondola_12_37_sprite_layout_template(423, 1546, sulp)  // сера
gondola_12_37_sprite_layout_template(423, 1695, brck)  // кирпичи
gondola_12_37_sprite_layout_template(423, 1844, wdpr)  // лесоматериалы
gondola_12_37_sprite_layout_template(423, 1993, stel1) // сталь
gondola_12_37_sprite_layout_template(423, 2142, stel2) // сталь
gondola_12_37_sprite_layout_template(423, 2291, stel3) // сталь
gondola_12_37_sprite_layout_template(423, 2440, stel4) // сталь
gondola_12_37_sprite_layout_template(423, 2589, wood)  // древесина

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, gondola_12_37_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  1: gondola_12_37_stel1_sprites;
  1: gondola_12_37_stel2_sprites;
  1: gondola_12_37_stel3_sprites;
  1: gondola_12_37_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_sprites_2,
  cargo_type_in_veh)
{
  COAL: gondola_12_37_coal_sprites;
  IORE: gondola_12_37_iore_sprites;
  CORE: gondola_12_37_core_sprites;
  AORE: gondola_12_37_aore_sprites;
  CLAY: gondola_12_37_clay_sprites;
  FERT: gondola_12_37_fert_sprites;
  GRVL: gondola_12_37_grvl_sprites;
  LIME: gondola_12_37_lime_sprites;
  POTA: gondola_12_37_pota_sprites;
  SAND: gondola_12_37_sand_sprites;
  SULP: gondola_12_37_sulp_sprites;
  BRCK: gondola_12_37_brck_sprites;
  WDPR: gondola_12_37_wdpr_sprites;
  STEL: gondola_12_37_stel_sprites;
  WOOD: gondola_12_37_wood_sprites;
        align_8_spriteset;
}

short_vehicle(gondola_12_37, )

random_switch (FEAT_TRAINS, SELF, gondola_12_37_RND, 0 )
{
  independent: gondola_12_37_stel_sprites;
  1: return ttdall_cc + 0x1A;
  1: return ttdall_cc + 0x6C;
  1: return ttdall_cc + 0x6D;
  1: return ttdall_cc + 0x71;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_colour_mapping,
  cargo_subtype)
{
  0: gondola_12_37_RND;  
  1: return ttdall_cc + 0x1A;
  2: return ttdall_cc + 0x6C;
  3: return ttdall_cc + 0x6D;
  4: return ttdall_cc + 0x71;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_1A));
  2: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6C));
  3: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6D));
  4: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_71));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 52;
  FERT: return 52;
        return 63;
}

item (FEAT_TRAINS, gondola_12_37) 
{
  property
  {
    name: string(STR_GONDOLA_12_37_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1947), 1, 1);
    model_life: get_model_life(1947,1964);
    retire_early: get_retire_early(1947,1964);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, AORE, CORE, IORE, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP, WOOD, WDPR];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 20;
    cargo_capacity: 63;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_12_37_sprites;
    purchase: gondola_12_37_purchase_spriteset;
    additional_text: return string(STR_GONDOLA_12_37_PURCHASE_HINT);
    cargo_subtype_text: gondola_12_37_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_37_cargo_capacity;
    colour_mapping: gondola_12_37_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, gondola_12_37)
  {
    property
    { name: string(STR_GONDOLA_12_37_NAME_LONG); }
  }
}

