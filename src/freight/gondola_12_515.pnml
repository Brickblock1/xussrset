 // gondola_12_515

/// Вагон
// 12-515 полувагон (1964-1974)
// gondola

#define IMAGEFILE  "src/freight/gondola-12-515-1964-1974.png"

// покупка
purchase_sprites(gondola_12_515, 0, 0)

// пусто
spriteset (gondola_12_515_empty_sprites, IMAGEFILE)
{
  s8_template(125, 56, 0)
}

// шаблон груза 3 вида загруженности
// 25-49%, 50-74%, полный
#define gondola_12_515_sprite_layout_template(x, y, cargo)    \
spriteset (gondola_12_515_##cargo##_1_sprites, IMAGEFILE)     \
{ s8_template(x, y, 0) }                                      \
spriteset (gondola_12_515_##cargo##_2_sprites, IMAGEFILE)     \
{ s8_template(x+298, y, 0) }                                  \
spriteset (gondola_12_515_##cargo##_full_sprites, IMAGEFILE)  \
{ s8_template(x+596, y, 0) }                                  \
spritegroup gondola_12_515_##cargo##_sprites                  \
{ loading: [gondola_12_515_empty_sprites,                     \
    gondola_12_515_##cargo##_1_sprites,                       \
    gondola_12_515_##cargo##_2_sprites,                       \
    gondola_12_515_##cargo##_full_sprites];                   \
  loaded: [gondola_12_515_empty_sprites,                      \
    gondola_12_515_##cargo##_1_sprites,                       \
    gondola_12_515_##cargo##_2_sprites,                       \
    gondola_12_515_##cargo##_full_sprites]; }                 \

gondola_12_515_sprite_layout_template(423, 56, coal)    // уголь
gondola_12_515_sprite_layout_template(423, 205, iore)   // жел. руда
gondola_12_515_sprite_layout_template(423, 354, core)   // медн. руда
gondola_12_515_sprite_layout_template(423, 503, aore)   // бокситы
gondola_12_515_sprite_layout_template(423, 652, clay)   // глина
gondola_12_515_sprite_layout_template(423, 801, fert)   // удобрения
gondola_12_515_sprite_layout_template(423, 950, grvl)   // гравий
gondola_12_515_sprite_layout_template(423, 1099, lime)  // известняк
gondola_12_515_sprite_layout_template(423, 1248, pota)  // поташ
gondola_12_515_sprite_layout_template(423, 1397, sand)  // песок
gondola_12_515_sprite_layout_template(423, 1546, sulp)  // сера
gondola_12_515_sprite_layout_template(423, 1695, bdmt)  // кирпичи
gondola_12_515_sprite_layout_template(423, 1844, wdpr)  // лесоматериалы
gondola_12_515_sprite_layout_template(423, 1993, stel1) // сталь
gondola_12_515_sprite_layout_template(423, 2142, stel2) // сталь
gondola_12_515_sprite_layout_template(423, 2291, stel3) // сталь
gondola_12_515_sprite_layout_template(423, 2440, stel4) // сталь
gondola_12_515_sprite_layout_template(423, 2589, wood)  // древесина

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, gondola_12_515_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  1: gondola_12_515_stel1_sprites;
  1: gondola_12_515_stel2_sprites;
  1: gondola_12_515_stel3_sprites;
  1: gondola_12_515_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_sprites_2,
  cargo_type_in_veh)
{
  COAL: gondola_12_515_coal_sprites;
  IORE: gondola_12_515_iore_sprites;
  CORE: gondola_12_515_core_sprites;
  AORE: gondola_12_515_aore_sprites;
  CLAY: gondola_12_515_clay_sprites;
  FERT: gondola_12_515_fert_sprites;
  GRVL: gondola_12_515_grvl_sprites;
  LIME: gondola_12_515_lime_sprites;
  POTA: gondola_12_515_pota_sprites;
  SAND: gondola_12_515_sand_sprites;
  SULP: gondola_12_515_sulp_sprites;
  BRCK: gondola_12_515_bdmt_sprites;
  BDMT: gondola_12_515_bdmt_sprites;
  WDPR: gondola_12_515_wdpr_sprites;
  STEL: gondola_12_515_stel_sprites;
  WOOD: gondola_12_515_wood_sprites;
        align_8_sprites;
}

short_vehicle(gondola_12_515, )

random_switch (FEAT_TRAINS, SELF, gondola_12_515_RND_b1975, 0 )
{
  independent: gondola_12_515_stel_sprites;
  6: return ttdall_cc + 0x6C;
  6: return ttdall_cc + 0x71;
  2: return ttdall_cc + 0x1A;
  2: return ttdall_cc + 0x6D;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_515_RND_a1975, 0 )
{
  independent: gondola_12_515_stel_sprites;
  3: return ttdall_cc + 0x7C;
  3: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  2: return ttdall_cc + 0x73;
  2: return ttdall_cc + 0x74;
  1: return ttdall_cc + 0x72;
  1: return ttdall_cc + 0x53;
  1: return ttdall_cc + 0x5A;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping_b1975,
  cargo_subtype)
{
  0: gondola_12_515_RND_b1975;  
  1: return ttdall_cc + 0x6C;
  2: return ttdall_cc + 0x71;
  3: return ttdall_cc + 0x1A;
  4: return ttdall_cc + 0x6D;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping_a1975,
  cargo_subtype)
{
  0: gondola_12_515_RND_a1975;  
  1: return ttdall_cc + 0x7C;
  2: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  4: return ttdall_cc + 0x73;
  5: return ttdall_cc + 0x74;
  6: return ttdall_cc + 0x72;
  7: return ttdall_cc + 0x53;
  8: return ttdall_cc + 0x5A;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping,
  build_year >= 1975)
{
  1: gondola_12_515_colour_mapping_a1975;
     gondola_12_515_colour_mapping_b1975;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text_b1975,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6C));
  2: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_71));
  3: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_1A));
  4: string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6D));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text_a1975,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_7C);
  2: string(STR_REFIT_TTD_4B);
  3: string(STR_REFIT_TTD_7B);
  4: string(STR_REFIT_TTD_73);
  5: string(STR_REFIT_TTD_74);
  6: string(STR_REFIT_TTD_72);
  7: string(STR_REFIT_TTD_53);
  8: string(STR_REFIT_TTD_5A);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text,
  build_year >= 1975)
{
  1: gondola_12_515_cargo_subtype_text_a1975;
     gondola_12_515_cargo_subtype_text_b1975;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 56;
  GOOD: return 71;
  RCYC: return 34;
  FERT: return 56;
  WOOD: return 58;
  WDPR: return 62;
        return 65;
}

item (FEAT_TRAINS, gondola_12_515) 
{
  property
  {
    name: string(STR_GONDOLA_12_515_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1964), 1, 1);
    model_life: get_model_life(1964,1979);
    retire_early: get_retire_early(1964,1979);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, WOOD, WDPR, AORE, CORE, IORE, BDMT, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP];
// GOOD, RCYC, SCMT,
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 65;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_12_515_sprites;
    purchase: gondola_12_515_purchase_sprites;
    additional_text: return string(STR_GONDOLA_12_515_PURCHASE_HINT);
    cargo_subtype_text: gondola_12_515_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_515_cargo_capacity;
    colour_mapping: gondola_12_515_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, gondola_12_515)
  {
    property
    { name: string(STR_GONDOLA_12_515_NAME_LONG); }
  }
}

