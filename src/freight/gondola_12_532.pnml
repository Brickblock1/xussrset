 // gondola_12_532

/// Вагон
// 12-532 полувагон
// металлические стенки, торцевые двери (с 1981 года заглушены); 
// до 1979 года два варианта вышламповок, после - обычные горизонтальные
// v1 1973-1979
// v2 1974-1981
// v3 1981-1988
// gondola

#define IMAGEFILE  "src/freight/gondola-12-532.png"

// покупка
purchase_sprites(gondola_12_532_v1, 0, 0)
purchase_sprites(gondola_12_532_v2, 0, 20)

// пусто
spriteset (gondola_12_532_v1_empty_sprites, IMAGEFILE)
{ s8_template(125, 56, 0) }
spriteset (gondola_12_532_v2_empty_sprites, IMAGEFILE)
{ s8_template(125, 56+36, 0) }
spriteset (gondola_12_532_v3_empty_sprites, IMAGEFILE)
{ s8_template(125, 56+72, 0) }

// шаблон груза 3 вида загруженности
// 25-49%, 50-74%, полный
#define gondola_12_532_sprite_layout_template(x, y, cargo)       \
spriteset (gondola_12_532_v1_##cargo##_1_sprites, IMAGEFILE)     \
{ s8_template(x, y, 0) }                                         \
spriteset (gondola_12_532_v1_##cargo##_2_sprites, IMAGEFILE)     \
{ s8_template(x+298, y, 0) }                                     \
spriteset (gondola_12_532_v1_##cargo##_full_sprites, IMAGEFILE)  \
{ s8_template(x+596, y, 0) }                                     \
spritegroup gondola_12_532_v1_##cargo##_sprites                  \
{ loading: [gondola_12_532_v1_empty_sprites,                     \
    gondola_12_532_v1_##cargo##_1_sprites,                       \
    gondola_12_532_v1_##cargo##_2_sprites,                       \
    gondola_12_532_v1_##cargo##_full_sprites];                   \
  loaded: [gondola_12_532_v1_empty_sprites,                      \
    gondola_12_532_v1_##cargo##_1_sprites,                       \
    gondola_12_532_v1_##cargo##_2_sprites,                       \
    gondola_12_532_v1_##cargo##_full_sprites]; }                 \
spriteset (gondola_12_532_v2_##cargo##_1_sprites, IMAGEFILE)     \
{ s8_template(x, y+36, 0) }                                      \
spriteset (gondola_12_532_v2_##cargo##_2_sprites, IMAGEFILE)     \
{ s8_template(x+298, y+36, 0) }                                  \
spriteset (gondola_12_532_v2_##cargo##_full_sprites, IMAGEFILE)  \
{ s8_template(x+596, y+36, 0) }                                  \
spritegroup gondola_12_532_v2_##cargo##_sprites                  \
{ loading: [gondola_12_532_v2_empty_sprites,                     \
    gondola_12_532_v2_##cargo##_1_sprites,                       \
    gondola_12_532_v2_##cargo##_2_sprites,                       \
    gondola_12_532_v2_##cargo##_full_sprites];                   \
  loaded: [gondola_12_532_v2_empty_sprites,                      \
    gondola_12_532_v2_##cargo##_1_sprites,                       \
    gondola_12_532_v2_##cargo##_2_sprites,                       \
    gondola_12_532_v2_##cargo##_full_sprites]; }                 \
spriteset (gondola_12_532_v3_##cargo##_1_sprites, IMAGEFILE)     \
{ s8_template(x, y+72, 0) }                                      \
spriteset (gondola_12_532_v3_##cargo##_2_sprites, IMAGEFILE)     \
{ s8_template(x+298, y+72, 0) }                                  \
spriteset (gondola_12_532_v3_##cargo##_full_sprites, IMAGEFILE)  \
{ s8_template(x+596, y+72, 0) }                                  \
spritegroup gondola_12_532_v3_##cargo##_sprites                  \
{ loading: [gondola_12_532_v3_empty_sprites,                     \
    gondola_12_532_v3_##cargo##_1_sprites,                       \
    gondola_12_532_v3_##cargo##_2_sprites,                       \
    gondola_12_532_v3_##cargo##_full_sprites];                   \
  loaded: [gondola_12_532_v3_empty_sprites,                      \
    gondola_12_532_v3_##cargo##_1_sprites,                       \
    gondola_12_532_v3_##cargo##_2_sprites,                       \
    gondola_12_532_v3_##cargo##_full_sprites]; }                 \

gondola_12_532_sprite_layout_template(423, 56, coal)    // уголь
gondola_12_532_sprite_layout_template(423, 205, iore)   // жел. руда
gondola_12_532_sprite_layout_template(423, 354, core)   // медн. руда
gondola_12_532_sprite_layout_template(423, 503, aore)   // бокситы
gondola_12_532_sprite_layout_template(423, 652, clay)   // глина
gondola_12_532_sprite_layout_template(423, 801, fert)   // удобрения
gondola_12_532_sprite_layout_template(423, 950, grvl)   // гравий
gondola_12_532_sprite_layout_template(423, 1099, lime)  // известняк
gondola_12_532_sprite_layout_template(423, 1248, pota)  // поташ
gondola_12_532_sprite_layout_template(423, 1397, sand)  // песок
gondola_12_532_sprite_layout_template(423, 1546, sulp)  // сера
gondola_12_532_sprite_layout_template(423, 1695, bdmt)  // кирпичи
gondola_12_532_sprite_layout_template(423, 1844, wdpr)  // лесоматериалы
gondola_12_532_sprite_layout_template(423, 1993, stel1) // сталь
gondola_12_532_sprite_layout_template(423, 2142, stel2) // сталь
gondola_12_532_sprite_layout_template(423, 2291, stel3) // сталь
gondola_12_532_sprite_layout_template(423, 2440, stel4) // сталь
gondola_12_532_sprite_layout_template(423, 2589, wood)  // Древесина

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, gondola_12_532_purchase_sprites,
  build_year >= 1981)
{
  0: gondola_12_532_v1_purchase_sprites;
     gondola_12_532_v2_purchase_sprites;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_532_v1_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  1: gondola_12_532_v1_stel1_sprites;
  1: gondola_12_532_v1_stel2_sprites;
  1: gondola_12_532_v1_stel3_sprites;
  1: gondola_12_532_v1_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_v1_sprites,
  cargo_type_in_veh)
{
  COAL: gondola_12_532_v1_coal_sprites;
  IORE: gondola_12_532_v1_iore_sprites;
  CORE: gondola_12_532_v1_core_sprites;
  AORE: gondola_12_532_v1_aore_sprites;
  CLAY: gondola_12_532_v1_clay_sprites;
  FERT: gondola_12_532_v1_fert_sprites;
  GRVL: gondola_12_532_v1_grvl_sprites;
  LIME: gondola_12_532_v1_lime_sprites;
  POTA: gondola_12_532_v1_pota_sprites;
  SAND: gondola_12_532_v1_sand_sprites;
  SULP: gondola_12_532_v1_sulp_sprites;
  BRCK: gondola_12_532_v1_bdmt_sprites;
  BDMT: gondola_12_532_v1_bdmt_sprites;
  WDPR: gondola_12_532_v1_wdpr_sprites;
  STEL: gondola_12_532_v1_stel_sprites;
  WOOD: gondola_12_532_v1_wood_sprites;
        align_8_sprites;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_532_v2_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  dependent: gondola_12_532_v1_stel_sprites;
  1: gondola_12_532_v2_stel1_sprites;
  1: gondola_12_532_v2_stel2_sprites;
  1: gondola_12_532_v2_stel3_sprites;
  1: gondola_12_532_v2_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_v2_sprites,
  cargo_type_in_veh)
{
  COAL: gondola_12_532_v2_coal_sprites;
  IORE: gondola_12_532_v2_iore_sprites;
  CORE: gondola_12_532_v2_core_sprites;
  AORE: gondola_12_532_v2_aore_sprites;
  CLAY: gondola_12_532_v2_clay_sprites;
  FERT: gondola_12_532_v2_fert_sprites;
  GRVL: gondola_12_532_v2_grvl_sprites;
  LIME: gondola_12_532_v2_lime_sprites;
  POTA: gondola_12_532_v2_pota_sprites;
  SAND: gondola_12_532_v2_sand_sprites;
  SULP: gondola_12_532_v2_sulp_sprites;
  BRCK: gondola_12_532_v2_bdmt_sprites;
  BDMT: gondola_12_532_v2_bdmt_sprites;
  WDPR: gondola_12_532_v2_wdpr_sprites;
  STEL: gondola_12_532_v2_stel_sprites;
  WOOD: gondola_12_532_v2_wood_sprites;
        align_8_sprites;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_532_v3_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  dependent: gondola_12_532_v1_stel_sprites;
  1: gondola_12_532_v3_stel1_sprites;
  1: gondola_12_532_v3_stel2_sprites;
  1: gondola_12_532_v3_stel3_sprites;
  1: gondola_12_532_v3_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_v3_sprites,
  cargo_type_in_veh)
{
  COAL: gondola_12_532_v3_coal_sprites;
  IORE: gondola_12_532_v3_iore_sprites;
  CORE: gondola_12_532_v3_core_sprites;
  AORE: gondola_12_532_v3_aore_sprites;
  CLAY: gondola_12_532_v3_clay_sprites;
  FERT: gondola_12_532_v3_fert_sprites;
  GRVL: gondola_12_532_v3_grvl_sprites;
  LIME: gondola_12_532_v3_lime_sprites;
  POTA: gondola_12_532_v3_pota_sprites;
  SAND: gondola_12_532_v3_sand_sprites;
  SULP: gondola_12_532_v3_sulp_sprites;
  BRCK: gondola_12_532_v3_bdmt_sprites;
  BDMT: gondola_12_532_v3_bdmt_sprites;
  WDPR: gondola_12_532_v3_wdpr_sprites;
  STEL: gondola_12_532_v3_stel_sprites;
  WOOD: gondola_12_532_v3_wood_sprites;
        align_8_sprites;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_532_sprites_RND, 0)
{
  independent: gondola_12_532_v1_stel_sprites;
  1: gondola_12_532_v1_sprites;
  1: gondola_12_532_v2_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_sprites_2,
  (build_year>=1974) +
  (build_year>=1980) +
  (build_year>=1981))                       
{
  0: gondola_12_532_v1_sprites;
  1: gondola_12_532_sprites_RND;
  2: gondola_12_532_v2_sprites;
     gondola_12_532_v3_sprites;
}

short_vehicle(gondola_12_532)

random_switch (FEAT_TRAINS, SELF, gondola_12_532_RND, 0 )
{
  independent: gondola_12_532_v1_stel_sprites;
  independent: gondola_12_532_sprites_RND;
  3: return ttdall_cc + 0x7C;
  3: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  2: return ttdall_cc + 0x73;
  2: return ttdall_cc + 0x74;
  1: return ttdall_cc + 0x72;
  1: return ttdall_cc + 0x53;
  1: return ttdall_cc + 0x5A;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_colour_mapping,
  cargo_subtype)
{
  0: gondola_12_532_RND;  
  1: return ttdall_cc + 0x7C;
  2: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  4: return ttdall_cc + 0x73;
  5: return ttdall_cc + 0x74;
  6: return ttdall_cc + 0x72;
  7: return ttdall_cc + 0x53;
  8: return ttdall_cc + 0x5A;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_7C);
  2: string(STR_REFIT_TTD_4B);
  3: string(STR_REFIT_TTD_7B);
  4: string(STR_REFIT_TTD_73);
  5: string(STR_REFIT_TTD_74);
  6: string(STR_REFIT_TTD_72);
  7: string(STR_REFIT_TTD_53);
  8: string(STR_REFIT_TTD_5A);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_cargo_capacity_b1974, cargo_type_in_veh)
{
  COAL: return 58;
  GOOD: return 73;
  RCYC: return 37;
  FERT: return 58;
  WOOD: return 60;
  WDPR: return 64;
        return 64;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_cargo_capacity_a1974, cargo_type_in_veh)
{
  COAL: return 58;
  GOOD: return 73;
  RCYC: return 37;
  FERT: return 58;
  WOOD: return 60;
  WDPR: return 64;
        return 69;

}

switch (FEAT_TRAINS, SELF, gondola_12_532_cargo_capacity_a1981, cargo_type_in_veh)
{
  COAL: return 58;
  GOOD: return 73;
  RCYC: return 37;
  FERT: return 58;
  WOOD: return 59;
  WDPR: return 64;
        return 69;
}

switch (FEAT_TRAINS, SELF, gondola_12_532_cargo_capacity, 
  (build_year>=1974) +
  (build_year>=1981))
{
  0: gondola_12_532_cargo_capacity_b1974;
  1: gondola_12_532_cargo_capacity_a1974;
     gondola_12_532_cargo_capacity_a1981;
}

item (FEAT_TRAINS, gondola_12_532) 
{
  property
  {
    name: string(STR_GONDOLA_12_532_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1973), 1, 1);
    model_life: get_model_life(1973,1988);
    retire_early: get_retire_early(1973,1988);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, WOOD, WDPR, AORE, CORE, IORE, BDMT, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP];    
// GOOD, RCYC, SCMT,
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 69;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_12_532_sprites;
    purchase: gondola_12_532_purchase_sprites;
    additional_text: return string(STR_GONDOLA_12_532_PURCHASE_HINT);
    cargo_subtype_text: gondola_12_532_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_532_cargo_capacity;
    colour_mapping: gondola_12_532_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, gondola_12_532)
  {
    property
    { name: string(STR_GONDOLA_12_532_NAME_LONG); }
  }
}

