 // gondola_12_xx

/// Полувагон
// полувагон 12-XX с деревянными стенками и боковой дверью
// выпуска 1935-1941 годов. 
// gondola

#define IMAGEFILE  "src/freight/gondola-12-xx-1935-1941.png"

// покупка
purchase_spriteset(gondola_12_xx, 0, 0)

// пусто
spriteset (gondola_12_xx_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 56, 0)
}

spriteset (gondola_12_xx_v2_empty_spriteset, IMAGEFILE)
{
  s8_template(125, 92, 0)
}

// шаблон груза, 2 ливреи, 3 вида загруженности
// 25-49%, 50-74%, полный
#define gondola_12_xx_sprite_layout_template(x, y, cargo) \
spriteset (gondola_12_xx_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (gondola_12_xx_v2_##cargo##_1_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+36, 0) \
} \
 \
spriteset (gondola_12_xx_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y, 0) \
} \
 \
spriteset (gondola_12_xx_v2_##cargo##_2_spriteset, IMAGEFILE) \
{ \
  s8_template(x+298, y+36, 0) \
} \
 \
spriteset (gondola_12_xx_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y, 0) \
} \
 \
spriteset (gondola_12_xx_v2_##cargo##_full_spriteset, IMAGEFILE) \
{ \
  s8_template(x+596, y+36, 0) \
} \
 \
spritegroup gondola_12_xx_##cargo##_spritegroup \
{ \
  loading: [gondola_12_xx_empty_spriteset, \
    gondola_12_xx_##cargo##_1_spriteset, \
    gondola_12_xx_##cargo##_2_spriteset, \
    gondola_12_xx_##cargo##_full_spriteset]; \
  loaded: [gondola_12_xx_empty_spriteset, \
    gondola_12_xx_##cargo##_1_spriteset, \
    gondola_12_xx_##cargo##_2_spriteset, \
    gondola_12_xx_##cargo##_full_spriteset]; \
} \
 \
spritegroup gondola_12_xx_v2_##cargo##_spritegroup \
{ \
  loading: [gondola_12_xx_v2_empty_spriteset, \
    gondola_12_xx_v2_##cargo##_1_spriteset, \
    gondola_12_xx_v2_##cargo##_2_spriteset, \
    gondola_12_xx_v2_##cargo##_full_spriteset]; \
  loaded: [gondola_12_xx_v2_empty_spriteset, \
    gondola_12_xx_v2_##cargo##_1_spriteset, \
    gondola_12_xx_v2_##cargo##_2_spriteset, \
    gondola_12_xx_v2_##cargo##_full_spriteset]; \
} \

gondola_12_xx_sprite_layout_template(423, 56, coal)    // уголь
gondola_12_xx_sprite_layout_template(423, 205, iore)   // жел. руда
gondola_12_xx_sprite_layout_template(423, 354, core)   // медн. руда
gondola_12_xx_sprite_layout_template(423, 503, aore)   // бокситы
gondola_12_xx_sprite_layout_template(423, 652, clay)   // глина
gondola_12_xx_sprite_layout_template(423, 801, fert)   // удобрения
gondola_12_xx_sprite_layout_template(423, 950, grvl)   // гравий
gondola_12_xx_sprite_layout_template(423, 1099, lime)  // известняк
gondola_12_xx_sprite_layout_template(423, 1248, pota)  // поташ
gondola_12_xx_sprite_layout_template(423, 1397, sand)  // песок
gondola_12_xx_sprite_layout_template(423, 1546, sulp)  // сера
gondola_12_xx_sprite_layout_template(423, 1695, brck)  // кирпичи
gondola_12_xx_sprite_layout_template(423, 1844, wdpr)  // лесоматериалы
gondola_12_xx_sprite_layout_template(423, 1993, stel)  // сталь
gondola_12_xx_sprite_layout_template(423, 2142, stel2) // сталь
gondola_12_xx_sprite_layout_template(423, 2291, stel3) // сталь
gondola_12_xx_sprite_layout_template(423, 2440, stel4) // сталь
gondola_12_xx_sprite_layout_template(423, 2589, wood)  // древесина

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, gondola_12_xx_get_spriteset4,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: gondola_12_xx_v2_stel_spritegroup;
  32: gondola_12_xx_v2_stel2_spritegroup;
  32: gondola_12_xx_v2_stel3_spritegroup;
  32: gondola_12_xx_v2_stel4_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_xx_get_spriteset3,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD)
)
{
  32: gondola_12_xx_stel_spritegroup;
  32: gondola_12_xx_stel2_spritegroup;
  32: gondola_12_xx_stel3_spritegroup;
  32: gondola_12_xx_stel4_spritegroup;
}

switch (FEAT_TRAINS, SELF, gondola_12_xx_get_spriteset2,
  cargo_type_in_veh)
{
  COAL: gondola_12_xx_v2_coal_spritegroup;
  IORE: gondola_12_xx_v2_iore_spritegroup;
  CORE: gondola_12_xx_v2_core_spritegroup;
  AORE: gondola_12_xx_v2_aore_spritegroup;
  CLAY: gondola_12_xx_v2_clay_spritegroup;
  FERT: gondola_12_xx_v2_fert_spritegroup;
  GRVL: gondola_12_xx_v2_grvl_spritegroup;
  LIME: gondola_12_xx_v2_lime_spritegroup;
  POTA: gondola_12_xx_v2_pota_spritegroup;
  SAND: gondola_12_xx_v2_sand_spritegroup;
  SULP: gondola_12_xx_v2_sulp_spritegroup;
  BRCK: gondola_12_xx_v2_brck_spritegroup;
  WDPR: gondola_12_xx_v2_wdpr_spritegroup;
  STEL: gondola_12_xx_get_spriteset4;
  WOOD: gondola_12_xx_v2_wood_spritegroup;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, gondola_12_xx_get_spriteset1,
  cargo_type_in_veh)
{
  COAL: gondola_12_xx_coal_spritegroup;
  IORE: gondola_12_xx_iore_spritegroup;
  CORE: gondola_12_xx_core_spritegroup;
  AORE: gondola_12_xx_aore_spritegroup;
  CLAY: gondola_12_xx_clay_spritegroup;
  FERT: gondola_12_xx_fert_spritegroup;
  GRVL: gondola_12_xx_grvl_spritegroup;
  LIME: gondola_12_xx_lime_spritegroup;
  POTA: gondola_12_xx_pota_spritegroup;
  SAND: gondola_12_xx_sand_spritegroup;
  SULP: gondola_12_xx_sulp_spritegroup;
  BRCK: gondola_12_xx_brck_spritegroup;
  WDPR: gondola_12_xx_wdpr_spritegroup;
  STEL: gondola_12_xx_get_spriteset3;
  WOOD: gondola_12_xx_wood_spritegroup;
  align_8_spriteset;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_xx_random, 0 )
{
  3: gondola_12_xx_get_spriteset1;
  2: gondola_12_xx_get_spriteset2;
}


switch (FEAT_TRAINS, SELF, gondola_12_xx_get_spriteset,
  cargo_subtype
)
{
  0: gondola_12_xx_random;
  1: gondola_12_xx_get_spriteset1;
  2: gondola_12_xx_get_spriteset2;
  align_8_spriteset;
}

switch (FEAT_TRAINS, SELF, gondola_12_xx_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_WOOD_PANELING);
  2: string(STR_REFIT_WOOD_PANELING2);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_xx_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 53;
  FERT: return 53;
  return 60;
}


item (FEAT_TRAINS, gondola_12_xx) 
{
  property
  {
    name: string(STR_GONDOLA_12_XX_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1935), 1, 1);
    model_life: get_model_life(1935,1941);
    retire_early: get_retire_early(1935,1941);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, AORE, CORE, IORE, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP, WOOD, WDPR];
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 20;
    cargo_capacity: 60;
    weight: 23 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_12_xx_get_spriteset;
    purchase: gondola_12_xx_purchase_spriteset;
    additional_text: return string(STR_GONDOLA_12_XX_PURCHASE_HINT);
    cargo_subtype_text: gondola_12_xx_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_xx_cargo_capacity;
  }
}
