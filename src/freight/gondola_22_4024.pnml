 // gondola_12_532

/// Вагон
// 8-осный полувагон для медной руды	22-4024 (ДВЗ)
// gondola

#define IMAGEFILE  "src/freight/gondola-22-4024.png"

// покупка
purchase_sprites(gondola_22_4024, 0, 0)

// пусто
spriteset (gondola_22_4024_v1_empty_sprites, IMAGEFILE)
{ s9_template(0, 38, 0) }

// шаблон груза 3 вида загруженности
// 25-49%, 50-74%, полный
#define gondola_22_4024_sprite_layout_template(x, y, cargo)       \
spriteset (gondola_22_4024_v1_##cargo##_1_sprites, IMAGEFILE)     \
{ s9_template(x, y, 0) }                                          \
spriteset (gondola_22_4024_v1_##cargo##_2_sprites, IMAGEFILE)     \
{ s9_template(x, y+75, 0) }                                       \
spriteset (gondola_22_4024_v1_##cargo##_full_sprites, IMAGEFILE)  \
{ s9_template(x, y+150, 0) }                                      \
spritegroup gondola_22_4024_v1_##cargo##_sprites                  \
{ loading: [gondola_22_4024_v1_empty_sprites,                     \
    gondola_22_4024_v1_##cargo##_1_sprites,                       \
    gondola_22_4024_v1_##cargo##_2_sprites,                       \
    gondola_22_4024_v1_##cargo##_full_sprites];                   \
  loaded: [gondola_22_4024_v1_empty_sprites,                      \
    gondola_22_4024_v1_##cargo##_1_sprites,                       \
    gondola_22_4024_v1_##cargo##_2_sprites,                       \
    gondola_22_4024_v1_##cargo##_full_sprites]; }                 \

gondola_22_4024_sprite_layout_template(0, 113, coal)   // уголь
gondola_22_4024_sprite_layout_template(0, 338, iore)   // жел. руда
gondola_22_4024_sprite_layout_template(0, 563, core)   // медн. руда
gondola_22_4024_sprite_layout_template(0, 788, aore)   // бокситы

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, gondola_22_4024_sprites_2,
  cargo_type_in_veh)
{
  COAL: gondola_22_4024_v1_coal_sprites;
  IORE: gondola_22_4024_v1_iore_sprites;
  CORE: gondola_22_4024_v1_core_sprites;
  AORE: gondola_22_4024_v1_aore_sprites;
        align_9_sprites;
}

short_vehicle(gondola_22_4024)

random_switch (FEAT_TRAINS, SELF, gondola_22_4024_RND, 0 )
{
  1: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x71;
  1: return ttdall_cc + 0x73;
  1: return ttdall_cc + 0x4B;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_colour_mapping,
  cargo_subtype)
{
  0: gondola_22_4024_RND;  
  1: return ttdall_cc + 0x62;
  2: return ttdall_cc + 0x71;
  3: return ttdall_cc + 0x73;
  4: return ttdall_cc + 0x4B;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_62);
  2: string(STR_REFIT_TTD_71);
  3: string(STR_REFIT_TTD_73);
  4: string(STR_REFIT_TTD_4B);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 61;
  AORE: return 97;
        return 115;
}

item (FEAT_TRAINS, gondola_22_4024) 
{
  property
  {
    name: string(STR_GONDOLA_22_4024_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1989), 1, 1);
    model_life: get_model_life(1989,2050);
    retire_early: get_retire_early(1989,2050);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, AORE, CORE, IORE];
    default_cargo_type: IORE;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 115;
    weight: 46 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_22_4024_sprites;
    purchase: gondola_22_4024_purchase_sprites;
    additional_text: return string(STR_GONDOLA_22_4024_PURCHASE_HINT);
    cargo_subtype_text: gondola_22_4024_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_22_4024_cargo_capacity;
    colour_mapping: gondola_22_4024_colour_mapping;
    articulated_part: articulated_part_dummy1;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, gondola_22_4024)
  {
    property
    { name: string(STR_GONDOLA_22_4024_NAME_LONG); }
  }
}

