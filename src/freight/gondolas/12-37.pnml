 // gondola_12_37
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/gondolas/12-xx-12-37.png"
purchase_sprites(gondola_12_37, 0, 20)
flatbed_empty_layout_template(gondola_12_37_v1, 8, 125, 56+36)
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+56, coal)    // уголь         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+205, iore)   // жел. руда     
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+354, core)   // медн. руда    
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+503, aore)   // бокситы       
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+652, clay)   // глина         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+801, fert)   // удобрения     
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+950, grvl)   // гравий        
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1099, lime)  // известняк     
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1248, pota)  // поташ         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1397, sand)  // песок         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1546, sulp)  // сера          
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1695, bdmt)  // кирпичи       
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1844, wdpr)  // лесоматериалы 
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+1993, stel1) // сталь          
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+2142, stel2) // сталь         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+2291, stel3) // сталь         
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+2440, stel4) // сталь        
flatbed_horisontal_short_layout3_template(gondola_12_37_v1, 8, 423, 36+2589, wood)  // древесина    
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, gondola_12_37_v1_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  independent: gondola_12_37_v1_coal_sprites;
  1: gondola_12_37_v1_stel1_sprites;
  1: gondola_12_37_v1_stel2_sprites;
  1: gondola_12_37_v1_stel3_sprites;
  1: gondola_12_37_v1_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_sprites_start,
  cargo_type_in_veh)
{
  COAL: gondola_12_37_v1_coal_sprites;
  IORE: gondola_12_37_v1_iore_sprites;
  CORE: gondola_12_37_v1_core_sprites;
  AORE: gondola_12_37_v1_aore_sprites;
  CLAY: gondola_12_37_v1_clay_sprites;
  FERT: gondola_12_37_v1_fert_sprites;
  GRVL: gondola_12_37_v1_grvl_sprites;
  LIME: gondola_12_37_v1_lime_sprites;
  POTA: gondola_12_37_v1_pota_sprites;
  SAND: gondola_12_37_v1_sand_sprites;
  SULP: gondola_12_37_v1_sulp_sprites;
  BRCK: gondola_12_37_v1_bdmt_sprites;
  BDMT: gondola_12_37_v1_bdmt_sprites;
  WDPR: gondola_12_37_v1_wdpr_sprites;
  STEL: gondola_12_37_v1_stel_sprites;
  WOOD: gondola_12_37_v1_wood_sprites;
        align_8_sprites;
}

short_vehicle(gondola_12_37)

random_switch (FEAT_TRAINS, SELF, gondola_12_37_random, 0 )
{
  independent: gondola_12_37_v1_coal_sprites;
  independent: gondola_12_37_v1_stel_sprites;
  1: return ttdall_cc + 0x1A;
  1: return ttdall_cc + 0x6C;
  1: return ttdall_cc + 0x6D;
  1: return ttdall_cc + 0x71;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_colour_mapping,
  cargo_subtype)
{
  0: gondola_12_37_random;  
  1: return ttdall_cc + 0x1A;
  2: return ttdall_cc + 0x6C;
  3: return ttdall_cc + 0x6D;
  4: return ttdall_cc + 0x71;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_1A));
  2: return string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6C));
  3: return string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_6D));
  4: return string(STR_REFIT_WOOD_PANELING, string(STR_REFIT_TTD_71));
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_37_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 52;
  GOOD: return 65;
  RCYC: return 32;
  FERT: return 52;
  WOOD: return 60;
  WDPR: return 63;
        return 63;
}

item (FEAT_TRAINS, gondola_12_37) 
{
  property
  {
    name: string(STR_GONDOLA_12_37_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1947), 1, 1);
    model_life: get_model_life(1947,1964);
    retire_early: get_retire_early(1947,1964);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, WOOD, WDPR, AORE, CORE, IORE, BDMT, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP];
// GOOD, RCYC, SCMT,
    default_cargo_type: COAL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 20;
    cargo_capacity: 63;
    weight: 22 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: gondola_12_37_sprites;
    purchase: gondola_12_37_purchase_sprites;
    additional_text: return string(STR_GONDOLA_12_37_PURCHASE_HINT);
    cargo_subtype_text: gondola_12_37_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_37_cargo_capacity;
    colour_mapping: gondola_12_37_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, gondola_12_37)
  {
    property
    { name: string(STR_GONDOLA_12_37_NAME_LONG); }
  }
}

