 // gondola_12_515
// 285 (11Dh)

#define IMAGEFILE  "src/freight/gondolas/12-515.png"
purchase_sprites(gondola_12_515, 0, 0)
flatbed_empty_layout_template(gondola_12_515_v1, 8, 125, 56)
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 56, coal)     // уголь                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 205, iore)    // жел. руда            
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 354, core)    // медн. руда           
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 503, aore)    // бокситы              
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 652, clay)    // глина                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 801, fert)    // удобрения            
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 950, grvl)    // гравий               
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1099, lime)   // известняк            
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1248, pota)   // поташ                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1397, sand)   // песок                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1546, sulp)   // сера                 
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1695, bdmt)   // кирпичи              
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1844, wdpr)   // лесоматериалы        
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 1993, stel1)  // сталь                 
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 2142, stel2)  // сталь                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 2291, stel3)  // сталь                
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 2440, stel4)  // сталь          
flatbed_horisontal_short_layout3_template(gondola_12_515_v1, 8, 423, 2589, wood)   // древесина      
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, gondola_12_515_v1_stel_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD))
{
  independent: gondola_12_515_v1_coal_sprites;
  1: gondola_12_515_v1_stel1_sprites;
  1: gondola_12_515_v1_stel2_sprites;
  1: gondola_12_515_v1_stel3_sprites;
  1: gondola_12_515_v1_stel4_sprites;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_sprites_start,
  cargo_type_in_veh)
{
  COAL: gondola_12_515_v1_coal_sprites;
  IORE: gondola_12_515_v1_iore_sprites;
  CORE: gondola_12_515_v1_core_sprites;
  AORE: gondola_12_515_v1_aore_sprites;
  CLAY: gondola_12_515_v1_clay_sprites;
  FERT: gondola_12_515_v1_fert_sprites;
  GRVL: gondola_12_515_v1_grvl_sprites;
  LIME: gondola_12_515_v1_lime_sprites;
  POTA: gondola_12_515_v1_pota_sprites;
  SAND: gondola_12_515_v1_sand_sprites;
  SULP: gondola_12_515_v1_sulp_sprites;
  BRCK: gondola_12_515_v1_bdmt_sprites;
  BDMT: gondola_12_515_v1_bdmt_sprites;
  WDPR: gondola_12_515_v1_wdpr_sprites;
  STEL: gondola_12_515_v1_stel_sprites;
  WOOD: gondola_12_515_v1_wood_sprites;
        align_8_sprites;
}

short_vehicle_RND(gondola_12_515, gondola_12_515_v1_coal_sprites)

wagon_running_cost(gondola_12_515, 11)

random_switch (FEAT_TRAINS, SELF, gondola_12_515_random_b1975, 0 )
{
  independent: gondola_12_515_v1_coal_sprites;
  independent: gondola_12_515_v1_stel_sprites;
  6: return ttdall_cc + 0x6C;
  6: return ttdall_cc + 0x71;
  2: return ttdall_cc + 0x1A;
  2: return ttdall_cc + 0x6D;
}

random_switch (FEAT_TRAINS, SELF, gondola_12_515_random_a1975, 0 )
{
  dependent: gondola_12_515_random_b1975;
  3: return ttdall_cc + 0x7C;
  3: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  2: return ttdall_cc + 0x73;
  2: return ttdall_cc + 0x74;
  1: return ttdall_cc + 0x72;
  1: return ttdall_cc + 0x53;
  1: return ttdall_cc + 0x5A;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping_b1975,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: gondola_12_515_random_b1975;  
  1: return ttdall_cc + 0x6C;
  2: return ttdall_cc + 0x71;
  3: return ttdall_cc + 0x1A;
  4: return ttdall_cc + 0x6D;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping_a1975,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: gondola_12_515_random_a1975;  
  1: return ttdall_cc + 0x7C;
  2: return ttdall_cc + 0x4B;
  3: return ttdall_cc + 0x7B;
  4: return ttdall_cc + 0x73;
  5: return ttdall_cc + 0x74;
  6: return ttdall_cc + 0x72;
  7: return ttdall_cc + 0x53;
  8: return ttdall_cc + 0x5A;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_colour_mapping,
  build_year >= 1975)
{
  1: gondola_12_515_colour_mapping_a1975;
     gondola_12_515_colour_mapping_b1975;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text_b1975,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_6C);
  2: return string(STR_REFIT_TTD_71);
  3: return string(STR_REFIT_TTD_1A);
  4: return string(STR_REFIT_TTD_6D);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text_a1975,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_7C);
  2: return string(STR_REFIT_TTD_4B);
  3: return string(STR_REFIT_TTD_7B);
  4: return string(STR_REFIT_TTD_73);
  5: return string(STR_REFIT_TTD_74);
  6: return string(STR_REFIT_TTD_72);
  7: return string(STR_REFIT_TTD_53);
  8: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_subtype_text,
  build_year >= 1975)
{
  1: gondola_12_515_cargo_subtype_text_a1975;
     gondola_12_515_cargo_subtype_text_b1975;
}

switch (FEAT_TRAINS, SELF, gondola_12_515_cargo_capacity,
  cargo_type_in_veh)
{
  COAL: return 56;
  GOOD: return 71;
  RCYC: return 34;
  FERT: return 56;
  WOOD: return 58;
  WDPR: return 62;
        return 65;
}

item (FEAT_TRAINS, gondola_12_515, 285) {
  property {
    name: string(STR_NAME_GONDOLA_12_515);
    vehicle_dates(1964, 1979, 22, 10, 8, 2)
    vehicle_wagon(22, 120)
    vehicle_wagon_cargo(65, 10)
    cargo_allow_refit: [COAL, FERT, WOOD, WDPR, AORE, CORE, IORE, BDMT, BRCK, CLAY, GRVL, LIME, POTA, SAND, STEL, SULP]; // GOOD, RCYC, SCMT,
  }
  graphics {
    default: gondola_12_515_sprites;
    purchase: gondola_12_515_purchase_sprites;
    running_cost_factor: gondola_12_515_running_cost_factor;
    additional_text: return string(STR_PURCHASE_HINT_WAGON,
                                   65, 71,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_GONDOLA_4AXLE_WITH_DOORS_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1964, 1979), 
                                   string(STR_PURCHASE_HINT_FACTORY_URALVAGONZAVOD));
    cargo_subtype_text: gondola_12_515_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_12_515_cargo_capacity;
    colour_mapping: gondola_12_515_colour_mapping;
  }
}

long_name_template(gondola_12_515, STR_LONGNAME_GONDOLA_12_515)
