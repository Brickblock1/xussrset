 // gondola_12_532

/// Вагон
// 8-осный полувагон для медной руды	22-4024 (ДВЗ)
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/gondolas/22-4024.png"
purchase_sprites(gondola_22_4024, 0, 0)
flatbed_empty_layout_template(gondola_22_4024_v1, 9, 0, 38)
flatbed_layout3_template(gondola_22_4024_v1, 9, 0, 113, coal)   // уголь              
flatbed_layout3_template(gondola_22_4024_v1, 9, 0, 338, iore)   // жел. руда          
flatbed_layout3_template(gondola_22_4024_v1, 9, 0, 563, core)   // медн. руда         
flatbed_layout3_template(gondola_22_4024_v1, 9, 0, 788, aore)   // бокситы            
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, gondola_22_4024_sprites_start,
  cargo_type_in_veh)
{
  COAL: gondola_22_4024_v1_coal_sprites;
  IORE: gondola_22_4024_v1_iore_sprites;
  CORE: gondola_22_4024_v1_core_sprites;
  AORE: gondola_22_4024_v1_aore_sprites;
        align_9_sprites;
}

long_vehicle_RND(gondola_22_4024, gondola_22_4024_v1_coal_sprites)

wagon_running_cost(gondola_22_4024, 65)

random_switch (FEAT_TRAINS, SELF, gondola_22_4024_random, 0 )
{
  independent: gondola_22_4024_v1_coal_sprites;
  1: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x71;
  1: return ttdall_cc + 0x73;
  1: return ttdall_cc + 0x4B;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: gondola_22_4024_random;  
  1: return ttdall_cc + 0x62;
  2: return ttdall_cc + 0x71;
  3: return ttdall_cc + 0x73;
  4: return ttdall_cc + 0x4B;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_62);
  2: return string(STR_REFIT_TTD_71);
  3: return string(STR_REFIT_TTD_73);
  4: return string(STR_REFIT_TTD_4B);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_cargo_capacity, cargo_type_in_veh)
{
  COAL: return 61;
  AORE: return 97;
        return 115;
}

switch (FEAT_TRAINS, SELF, gondola_22_4024_additional_text,
  current_year >= USSREND)
{
  0: return string(STR_PURCHASE_HINT_WAGON,
                   115, 76,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                   string(STR_PURCHASE_HINT_WAGON_TYPE_GONDOLA_8AXLE_TSMW),
                   string(STR_PURCHASE_HINT_FROM, 1989), 
                   string(STR_PURCHASE_HINT_FACTORY_DNEPRODZERZHINSKY_CW));
     return string(STR_PURCHASE_HINT_WAGON,
                   115, 76,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                   string(STR_PURCHASE_HINT_WAGON_TYPE_GONDOLA_8AXLE_TSMW),
                   string(STR_PURCHASE_HINT_FROM, 1989), 
                   string(STR_PURCHASE_HINT_FACTORY_DNEPRODZERZHINSKY_CW_AUSSR));
}

item (FEAT_TRAINS, gondola_22_4024) {
  property {
    name: string(STR_NAME_GONDOLA_22_4024);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1989), 1, 1);
    model_life: get_model_life(1989,2050);
    retire_early: get_retire_early(1989,2050);
    vehicle_life: 22;
    reliability_decay: 10;
    cargo_allow_refit: [COAL, AORE, CORE, IORE];
    default_cargo_type: IORE; 
    cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 12;
    cargo_capacity: 115;
    weight: 46 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: gondola_22_4024_sprites;
    purchase: gondola_22_4024_purchase_sprites;
    running_cost_factor: gondola_22_4024_running_cost_factor;
    additional_text: gondola_22_4024_additional_text;
    cargo_subtype_text: gondola_22_4024_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: gondola_22_4024_cargo_capacity;
    colour_mapping: gondola_22_4024_colour_mapping;
    articulated_part: articulated_part_dummy1;
  }
}

long_name_template(gondola_22_4024, STR_LONGNAME_GONDOLA_22_4024)
