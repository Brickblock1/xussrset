 // 11_740_hopper

#define IMAGEFILE  "src/freight/hoppers/11-740.png"
purchase_sprites(hopper_11_740, 0, 0)
tanker_layout_template(hopper_11_740, 8, 32, 40)
#undef IMAGEFILE
long_vehicle_RND(hopper_11_740, hopper_11_740_sprites_start)

random_switch (FEAT_TRAINS, SELF, hopper_11_740_random, 0 )
{
  independent: hopper_11_740_sprites_start;
  2: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x53;
  1: return ttdall_cc + 0x59;
}

switch (FEAT_TRAINS, SELF, hopper_11_740_colour_mapping,
  cargo_subtype)
{
  0: hopper_11_740_random;
  1: return ttdall_cc + 0x62;
  2: return ttdall_cc + 0x53;
  3: return ttdall_cc + 0x59;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, hopper_11_740_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_62);
  2: return string(STR_REFIT_TTD_53);
  3: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, hopper_11_740_cargo_capacity,
[  STORE_TEMP(64, 0), // т
   STORE_TEMP(73, 1), // м³
   STORE_TEMP(0, 2),  // м²
   STORE_TEMP(0, 3),  // палетомест
   STORE_TEMP(8, 4)]) // шаблон
{ hopper_cargo_capacity; }

switch (FEAT_TRAINS, SELF, hopper_11_740_running_cost_factor,
[  STORE_TEMP(0, 0),            // Моторы
   STORE_TEMP(0, 1),            // Бригада
   STORE_TEMP(20, 2),       		/* 30*2/3 */	// Износ
   STORE_TEMP(0, 3),            // Сопровождение
   STORE_TEMP(10, 4),         		/* 30/3 */	// ТО
   STORE_TEMP(0, 5),            // Сертификация

   STORE_TEMP(120, 6),       // Скорость
   STORE_TEMP(22, 7),        // Тара
   STORE_TEMP(86, 8)])    		/* 22+64 */	// Максимальная масса
{ all_running_cost_factor; } // 30

switch (FEAT_TRAINS, SELF, hopper_11_740_loading_speed,
[  STORE_TEMP(0, 0),    // определяется элеватором (4 больших люка)
   STORE_TEMP(3, 1),    // 4 больших разгрузочных люка
   STORE_TEMP(0, 2),    // верхняя загрузка не ограничена люками
   STORE_TEMP(5*4, 3)]) // нижняя разгрузка ограничена 5 единицами груза на люк
{ all_wagons_hopper_loading_speed; }

hint_wagon_with_uspeed(hopper_11_740,
                       64, 73,
                       STR_PURCHASE_HINT_LOADING_SPEED_4TIC,
                       STR_PURCHASE_HINT_LOADING_SPEED_4TIC,
                       STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                       STR_PURCHASE_HINT_WAGON_TYPE_HOPPER,
                       1976, 1990,
                       STR_PURCHASE_HINT_FACTORY_KRYUKOVO_WW)

item (FEAT_TRAINS, hopper_11_740, 568) {
  property {
    name: string(STR_MODEL_NUMBER2, 11, 740);
    vehicle_dates(1976, 1990, 26, 10, 7, 78)
    vehicle_wagon(22, 120)
    cargo_allow_refit: [cargo_all_fert_hopperlist()];
  }
  graphics {
    default: hopper_11_740_sprites;
    purchase: hopper_11_740_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: hopper_11_740_running_cost_factor;
    colour_mapping: hopper_11_740_colour_mapping;
    cargo_subtype_text: hopper_11_740_cargo_subtype_text;
    cargo_capacity: hopper_11_740_cargo_capacity;
    loading_speed: hopper_11_740_loading_speed;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    additional_text: hopper_11_740_additional_text;
  }
}

long_name_template(hopper_11_740, STR_LONGNAME_HOPPER_11_740)
