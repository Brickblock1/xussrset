 // 19_7017_hopper

#define IMAGEFILE  "src/freight/hoppers/19-7017.png"
purchase_sprites(hopper_19_7017, 0, 0)
tanker_layout_template(hopper_19_7017, 8, 32, 40)
#undef IMAGEFILE
long_vehicle_RND(hopper_19_7017, hopper_19_7017_sprites_start)

random_switch (FEAT_TRAINS, SELF, hopper_19_7017_random, 0 )
{
  independent: hopper_19_7017_sprites_start;
  1: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x53;
  1: return ttdall_cc + 0x59;
  1: return ttdall_cc + 0x95;
}

switch (FEAT_TRAINS, SELF, hopper_19_7017_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: hopper_19_7017_random;
  1: return ttdall_cc + 0x62;
  2: return ttdall_cc + 0x53;
  3: return ttdall_cc + 0x59;
  4: return ttdall_cc + 0x95;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, hopper_19_7017_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_62);
  2: return string(STR_REFIT_TTD_53);
  3: return string(STR_REFIT_TTD_59);
  4: return string(STR_REFIT_TTD_96);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, hopper_19_7017_cargo_capacity,
[  STORE_TEMP(71, 0), // т
   STORE_TEMP(91, 1), // м³
   STORE_TEMP(0, 2),  // м²
   STORE_TEMP(0, 3),  // палетомест
   STORE_TEMP(8, 4)]) // шаблон
{ hopper_cargo_capacity; }

switch (FEAT_TRAINS, SELF, hopper_19_7017_running_cost_factor,
[  STORE_TEMP(0, 0),            // Моторы
   STORE_TEMP(0, 1),            // Бригада
   STORE_TEMP(39*2/3, 2),       // Износ
   STORE_TEMP(0, 3),            // Сопровождение
   STORE_TEMP(39/3, 4),         // ТО
   STORE_TEMP(0, 5),            // Сертификация

   STORE_TEMP(120, 6),       // Скорость
   STORE_TEMP(23, 7),        // Тара
   STORE_TEMP(23+71, 8)])    // Максимальная масса
{ all_running_cost_factor; } // 39

switch (FEAT_TRAINS, SELF, hopper_19_7017_loading_speed,
[  STORE_TEMP(0, 0),    // определяется элеватором (4 больших люка)
   STORE_TEMP(4, 1),    // 3 разгрузочных люка
   STORE_TEMP(0, 2),    // верхняя загрузка не ограничена люками
   STORE_TEMP(6*3, 3)]) // нижняя разгрузка ограничена 6 единицами груза на люк
{ all_wagons_hopper_loading_speed; }

hint_wagon_with_uspeed_noto(hopper_19_7017,
                            71, 91,
                            STR_PURCHASE_HINT_LOADING_SPEED_5TIC,
                            STR_PURCHASE_HINT_LOADING_SPEED_5TIC,
                            STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                            STR_PURCHASE_HINT_WAGON_TYPE_HOPPER,
                            2005,
                            STR_PURCHASE_HINT_FACTORY_KRYUKOVO_WW)

item (FEAT_TRAINS, hopper_19_7017, 569) {
  property {
    name: string(STR_MODEL_NUMBER2, 19, 7017);
    vehicle_dates(2005, 2050, 26, 10, 7, 6)
    vehicle_wagon(23, 120)
    cargo_allow_refit: [cargo_all_fert_hopperlist()];
  }
  graphics {
    default: hopper_19_7017_sprites;
    purchase: hopper_19_7017_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: hopper_19_7017_running_cost_factor;
    colour_mapping: hopper_19_7017_colour_mapping;
    cargo_subtype_text: hopper_19_7017_cargo_subtype_text;
    cargo_capacity: hopper_19_7017_cargo_capacity;
    loading_speed: hopper_19_7017_loading_speed;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    additional_text: hopper_19_7017_additional_text;
  }
}

long_name_template(hopper_19_7017, STR_LONGNAME_HOPPER_19_7017)
