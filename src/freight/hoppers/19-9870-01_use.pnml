// 19_9870_01_hopper

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_random,
[ set_offset_to(1),
   prev_vehicle_randombits() & 0x0F])
{
   0..5: return ttdall_cc + 0x71;
  6..10: return ttdall_cc + 0x7B;
         return ttdall_cc + 0x4B;
// 0x06 и 0x62 случайным образом не выбираются
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_colour_mapping,
  cargo_subtype)
{
  0: hopper_19_9870_01_random;
  1: return ttdall_cc + 0x71;
  2: return ttdall_cc + 0x7B;
  3: return ttdall_cc + 0x4B;
  4: return ttdall_cc + 0x06;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_71);
  2: return string(STR_REFIT_TTD_7B);
  3: return string(STR_REFIT_TTD_4B);
  4: return string(STR_REFIT_TTD_06);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_cargo_capacity,
[  STORE_TEMP(77, 0),   // т
   STORE_TEMP(101, 1),  // м³
   STORE_TEMP(0, 2),    // м²
   STORE_TEMP(0, 3),    // палетомест
   STORE_TEMP(9, 4)])   // шаблон
{ hopper_cargo_capacity; }

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_running_cost_factor,
[  STORE_TEMP(0, 0),   // Моторы
   STORE_TEMP(0, 1),   // Бригада
   STORE_TEMP(31, 2),  // Износ
   STORE_TEMP(0, 3),   // Сопровождение
   STORE_TEMP(16, 4),  // ТО
   STORE_TEMP(0, 5),   // Сертификация

   STORE_TEMP(120, 6),                                  // Скорость
   STORE_TEMP(24, 7),                                   // Тара
   STORE_TEMP(101, 8)])                    /* 24+77 */  // Максимальная масса
{ all_running_cost_factor; }                            // 47

switch (FEAT_TRAINS, SELF, hopper_19_9870_01_loading_speed,
[  STORE_TEMP(0, 0),     // определяется элеватором (4 больших люка)
   STORE_TEMP(4, 1),     // 6 малых разгрузочных люков
   STORE_TEMP(0, 2),     // верхняя загрузка не ограничена люками
   STORE_TEMP(3*6, 3)])  // нижняя разгрузка ограничена 3 единицами груза на люк
{ all_wagons_hopper_loading_speed; }

hint_wagon_with_uspeed_noto(hopper_19_9870_01,
                            77, 101,
                            string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                            string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                            STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                            STR_PURCHASE_HINT_WAGON_TYPE_HOPPER,
                            2012,
                            string(STR_PURCHASE_HINT_FACTORY_TIKHVIN_CBP))

item (FEAT_TRAINS, hopper_19_9870_01, 571) {
  property {
    name: string(STR_MODEL_NUMBER3, 19, 9870, 1);
    vehicle_dates(2012, 2050, 32, 10, 8, 132)
    vehicle_wagon(24, 120)
    cargo_allow_refit: [cargo_all_grain_hopperlist()];
  }
  graphics {
                default: hopper_19_9870_sprites;
               purchase: hopper_19_9870_purchase_sprites;
    running_cost_factor: hopper_19_9870_01_running_cost_factor;
       articulated_part: articulated_part_dummy1;
         colour_mapping: hopper_19_9870_01_colour_mapping;
     cargo_subtype_text: hopper_19_9870_01_cargo_subtype_text;
         cargo_capacity: hopper_19_9870_01_cargo_capacity;
          loading_speed: hopper_19_9870_01_loading_speed;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
        additional_text: hopper_19_9870_01_additional_text;
  }
}

long_name_template(hopper_19_9870_01, STR_LONGNAME_HOPPER_19_9870_01)
