 // 19_9870_hopper
// 427 (1ABh)

#define IMAGEFILE  "src/freight/hoppers/19-9870.png"
purchase_sprites(hopper_19_9870, 0, 0)
tanker_layout_template(hopper_19_9870_v1, 9, 0, 18)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, hopper_19_9870_sprites_start, 0)
{
  hopper_19_9870_v1_sprites;
}

long_vehicle_RND(hopper_19_9870, hopper_19_9870_v1_sprites)

random_switch (FEAT_TRAINS, SELF, hopper_19_9870_random, 0 )
{
  independent: hopper_19_9870_v1_sprites;
  6: return ttdall_cc + 0x71;
  5: return ttdall_cc + 0x7B;
  5: return ttdall_cc + 0x4B;
// 0x06 и 0x62 случайным образом не выбираются
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_colour_mapping2,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: hopper_19_9870_random;
  1: return ttdall_cc + 0x71;
  2: return ttdall_cc + 0x7B;
  3: return ttdall_cc + 0x4B;
  4: return ttdall_cc + 0x06;
  5: return ttdall_cc + 0x62;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_colour_mapping,
  cargo_type_in_veh)
{
  FERT: return ttdall_cc + 0x62;
        hopper_19_9870_colour_mapping2;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_cargo_subtype_text2,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_71);
  2: return string(STR_REFIT_TTD_7B);
  3: return string(STR_REFIT_TTD_4B);
  4: return string(STR_REFIT_TTD_06);
  5: return string(STR_REFIT_TTD_62);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_cargo_subtype_text,
  cargo_type_in_veh)
{
  FERT: return CB_RESULT_NO_TEXT;
        hopper_19_9870_cargo_subtype_text2;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_refit_cost,
  ((getbits(extra_callback_info1, 0, 8) == FERT) || (getbits(extra_callback_info1, 0, 8) == PHOS)) +
  (((cargo_type_in_veh == FERT) || (cargo_type_in_veh == PHOS)) * 2))
{
  1: return 1;
  2: return 3;
     return 0 | CB_RESULT_AUTOREFIT;
}

switch (FEAT_TRAINS, SELF, hopper_19_9870_cargo_capacity,
  cargo_type_in_veh)
{
  WHEA: return 71;
        return 77;
}

wagon_running_cost(hopper_19_9870, 47)

item (FEAT_TRAINS, hopper_19_9870, 427) {
  property {
    name: string(STR_MODEL_NUMBER2, 19, 9870);
    vehicle_dates(2012, 2050, 26, 10, 8, 7)
    vehicle_wagon(23.5, 120)
    vehicle_wagon_cargo(77, 20)
    default_cargo_type: FERT;
    cargo_allow_refit: [FERT, PHOS, GRAI, WHEA, MAIZ, CERE, OLSD];
  }
  graphics {
    default: hopper_19_9870_sprites;
    purchase: hopper_19_9870_purchase_sprites;
    running_cost_factor: hopper_19_9870_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    colour_mapping: hopper_19_9870_colour_mapping;
    cargo_subtype_text: hopper_19_9870_cargo_subtype_text;
    cargo_capacity: hopper_19_9870_cargo_capacity;
    loading_speed: all_wagons_bulk_auu_loading_speed;
    refit_cost: hopper_19_9870_refit_cost;
    additional_text: return string(STR_PURCHASE_HINT_WAGON_WITH_USPEED,
                                   77, 101,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_HOPPER_4AXLE_TSMW),
                                   string(STR_PURCHASE_HINT_FROM, 2012),
                                   string(STR_PURCHASE_HINT_FACTORY_TIKHVIN_CBP));
  }
}

long_name_template(hopper_19_9870, STR_LONGNAME_HOPPER_19_9870)
