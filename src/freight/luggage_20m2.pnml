 // luggage_20m2

/// Вагон
// Егоровец 20,2 м багажный (1929-1941)
// luggage

#define IMAGEFILE  "src/freight/luggage-20m2-1929-1941.png"

// покупка
purchase_spriteset(luggage_20m2, 0, 0)

// вид 1
spriteset (luggage_20m2_spriteset, IMAGEFILE)
{
  s12_template(0, 30, 0)
}

// вид 2
spriteset (luggage_20m2_v2_spriteset, IMAGEFILE)
{
  s12_template(0, 100, 0)
}

// вид 3
spriteset (luggage_20m2_v3_spriteset, IMAGEFILE)
{
  s12_template(0, 170, 0)
}

// Реверс: (431, y, 0)

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, luggage_20m2_get_spriteset1,
  cargo_subtype
)
{
  0: luggage_20m2_spriteset;
  1: luggage_20m2_v2_spriteset;
  2: luggage_20m2_v3_spriteset;
  align_12_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, luggage_20m2_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  luggage_20m2_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, luggage_20m2_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_5A);
  1: string(STR_REFIT_RED_ARROW);
  2: string(STR_REFIT_TTD_AD);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, luggage_20m2) 
{
  property
  {
    name: string(STR_LUGGAGE_20M2_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1929), 1, 1);
    model_life: get_model_life(1929,1941);
    retire_early: get_retire_early(1929,1941);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, GOLD, VALU];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 5;
    cargo_capacity: 40;
    weight: 32 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: luggage_20m2_get_spriteset;
    purchase: luggage_20m2_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_LUGGAGE_20M2_PURCHASE_HINT);
    cargo_subtype_text: luggage_20m2_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
