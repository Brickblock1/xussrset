 // mail_22sb

/// Вагон
// 22 сб (1960-1970), почтовый вагон производства ЛВЗ им. Егорова
// mail

#define IMAGEFILE  "src/freight/luggages/22sb.png"
purchase_sprites(mail_22sb_v1, 0, 0)
purchase_sprites(mail_22sb_v2, 100, 0)

pass_wagon_sprites(14, mail_22sb_v1, 32, 40)
pass_wagon_sprites(14, mail_22sb_v2, 32, 308)
pass_wagon_sprites(14, mail_22sb_v3, 32, 576)
pass_wagon_sprites(14, mail_22sb_v4, 32, 844)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, mail_22sb_purchase_sprites,
  current_year >= 1967)
{
  1: mail_22sb_v2_purchase_sprites;
     mail_22sb_v1_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, mail_22sb_sprites_left,
  (LOAD_TEMP(2) >= date(1967,1,1)) +
  (cargo_subtype *2) )
{
  0: mail_22sb_v1_sprites_left;
  1: mail_22sb_v2_sprites_left;
  2: mail_22sb_v3_sprites_left;
  3: mail_22sb_v4_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, mail_22sb_sprites_right,
  (LOAD_TEMP(2) >= date(1967,1,1)) +
  (cargo_subtype *2) )
{
  0: mail_22sb_v1_sprites_right;
  1: mail_22sb_v2_sprites_right;
  2: mail_22sb_v3_sprites_right;
  3: mail_22sb_v4_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, mail_22sb_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: mail_22sb_sprites_left;
  1: mail_22sb_sprites_right;
}

long_vehicle_RND(mail_22sb, mail_22sb_sprites_start)

wagon_running_cost(mail_22sb, 120)

switch (FEAT_TRAINS, SELF, mail_22sb_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, mail_22sb) {
  property {
    name: string(STR_NAME_MAIL_22SB);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1960), 1, 1);
    model_life: get_model_life(1960,1970);
    retire_early: get_retire_early(1960,1970);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    refit_cost: 0;
    cost_factor: 12;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 10;
    cargo_capacity: 80;
    weight: 46 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: mail_22sb_sprites;
    purchase: mail_22sb_purchase_sprites;
    running_cost_factor: mail_22sb_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_PURCHASE_HINT_WAGON_MAIL,
                                   20,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_MAIL_4AXLE_WITH_MLLP_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1960, 1970), 
                                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD_CW));
    cargo_subtype_text: mail_22sb_cargo_subtype_text;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, mail_22sb) {
    property { name: string(STR_LONGNAME_MAIL_22SB); }
  }
}

