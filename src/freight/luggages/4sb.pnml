 // mail_4sb

/// Вагон
// 4 сб (1950-1957), ЛВЗ им. Егорова
// mail

#define IMAGEFILE  "src/freight/luggages/4sb.png"
purchase_sprites(mail_4sb, 0, 0)

pass_wagon_sprites(14, mail_4sb_v1, 32, 40)
pass_wagon_sprites(14, mail_4sb_v2, 32, 308)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, mail_4sb_sprites_left,
  cargo_subtype )
{
  0: mail_4sb_v1_sprites_left;
  1: mail_4sb_v2_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, mail_4sb_sprites_right,
  cargo_subtype )
{
  0: mail_4sb_v1_sprites_right;
  1: mail_4sb_v2_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, mail_4sb_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: mail_4sb_sprites_left;
  1: mail_4sb_sprites_right;
}

long_vehicle_RND(mail_4sb, mail_4sb_sprites_start)

wagon_running_cost(mail_4sb, 96)

switch (FEAT_TRAINS, SELF, mail_4sb_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, mail_4sb) {
  property {
    name: string(STR_NAME_MAIL_4SB);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1950), 1, 1);
    model_life: get_model_life(1950,1957);
    retire_early: get_retire_early(1950,1957);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    refit_cost: 0;
    cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 8;
    cargo_capacity: 64;
    weight: 55 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: mail_4sb_sprites;
    purchase: mail_4sb_purchase_sprites;
    running_cost_factor: mail_4sb_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_PURCHASE_HINT_WAGON, 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_MAIL_4AXLE_WITH_ML_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1950, 1957), 
                                   string(STR_PURCHASE_HINT_FACTORY_LENINGRAD_CW));
    cargo_subtype_text: mail_4sb_cargo_subtype_text;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, mail_4sb) {
    property { name: string(STR_LONGNAME_MAIL_4SB); }
  }
}

