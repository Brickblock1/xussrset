 // luggage_61_4185

/// Вагон
//  61-4185. Багажный вагон Тверского вагоностроительного завода,
//  выпускается с 1998 года.

#define IMAGEFILE  "src/freight/luggages/61-4185.png"
purchase_sprites(luggage_61_4185, 0, 0)

pass_wagon_sprites(14, luggage_61_4185_v1, 32, 40)
pass_wagon_sprites(14, luggage_61_4185_v2, 32, 308)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, luggage_61_4185_sprites_left,
  cargo_subtype )
{
  0: luggage_61_4185_v1_sprites_left;
  1: luggage_61_4185_v2_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, luggage_61_4185_sprites_right,
  cargo_subtype )
{
  0: luggage_61_4185_v1_sprites_right;
  1: luggage_61_4185_v2_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, luggage_61_4185_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: luggage_61_4185_sprites_left;
  1: luggage_61_4185_sprites_right;
}

long_vehicle_RND(luggage_61_4185, luggage_61_4185_sprites_start)

wagon_running_cost(luggage_61_4185, 137)

switch (FEAT_TRAINS, SELF, luggage_61_4185_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}  

item (FEAT_TRAINS, luggage_61_4185) {
  property {
    name: string(STR_NAME_LUGGAGE_61_4185);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1998), 1, 1);
    model_life: get_model_life(1998,2050);
    retire_early: get_retire_early(1998,2050);
    vehicle_life: 28;
    reliability_decay: 10;
    cargo_allow_refit: [GOOD, GOLD, VALU];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: 14;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 10;
    cargo_capacity: 40;
    weight: 49.2 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: luggage_61_4185_sprites;
    purchase: luggage_61_4185_purchase_sprites;
    running_cost_factor: luggage_61_4185_running_cost_factor;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_PURCHASE_HINT_WAGON_MAIL,
                                   20,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_1_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_LUGGAGE_4AXLE_TSMW),
                                   string(STR_PURCHASE_HINT_FROM, 1998), 
                                   string(STR_PURCHASE_HINT_FACTORY_TVER_CW));
    cargo_subtype_text: luggage_61_4185_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: return ttdall_cc + 0xC6;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, luggage_61_4185) {
    property { name: string(STR_LONGNAME_LUGGAGE_61_4185); }
  }
}

