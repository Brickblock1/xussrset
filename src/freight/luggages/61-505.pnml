 // mail_61_505

/// Вагон
// 61-505 (1971-1979), почтовый вагон производства ЛВЗ им. Егорова
// mail

#define IMAGEFILE  "src/freight/luggages/61-505.png"

purchase_sprites(mail_61_505, 0, 0)

pass_wagon_sprites(14, mail_61_505_v1, 32, 40)
pass_wagon_sprites(14, mail_61_505_v2, 32, 308)

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, mail_61_505_sprites_left,
  cargo_subtype )
{
  0: mail_61_505_v1_sprites_left;
  1: mail_61_505_v2_sprites_left;
     align_14_sprites;
}

switch (FEAT_TRAINS, SELF, mail_61_505_sprites_right,
  cargo_subtype )
{
  0: mail_61_505_v1_sprites_right;
  1: mail_61_505_v2_sprites_right;
     align_14_sprites;
}

random_switch (FEAT_TRAINS, SELF, mail_61_505_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: mail_61_505_sprites_left;
  1: mail_61_505_sprites_right;
}

long_vehicle(mail_61_505)

switch (FEAT_TRAINS, SELF, mail_61_505_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, mail_61_505) 
{
  property
  {
    name: string(STR_MAIL_61_505_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1971), 1, 1);
    model_life: get_model_life(1971,1979);
    retire_early: get_retire_early(1971,1979);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 11;
    cargo_capacity: 44;
    weight: 47 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: mail_61_505_sprites;
    purchase: mail_61_505_purchase_sprites;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_MAIL_61_505_PURCHASE_HINT);
    cargo_subtype_text: mail_61_505_cargo_subtype_text;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, mail_61_505)
  {
    property
    { name: string(STR_MAIL_61_505_NAME_LONG); }
  }
}

