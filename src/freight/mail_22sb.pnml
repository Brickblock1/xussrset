 // mail_22sb

/// Вагон
// 22 сб (1960-1970), почтовый вагон производства ЛВЗ им. Егорова
// mail

#define IMAGEFILE  "src/freight/mail-22sb-1960-1970.png"

// покупка
spriteset (mail_22sb_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

spriteset (mail_22sb_v3_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(100, 0)
}

// вид 1
spriteset (mail_22sb_spriteset, IMAGEFILE)
{
  s14_template(0, 46, 0)
}

// вид 2
spriteset (mail_22sb_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 118, 0)
}

// вид 3
spriteset (mail_22sb_v3_spriteset, IMAGEFILE)
{
  s14_template(0, 200, 0)
}

// вид 4
spriteset (mail_22sb_v4_spriteset, IMAGEFILE)
{
  s14_template(0, 272, 0)
}

// хвост
spriteset (mail_22sb_tail_spriteset, IMAGEFILE)
{
  s14_template(0, 364, 0)
}

spriteset (mail_22sb_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 436, 0)
}

spriteset (mail_22sb_tail_v3_spriteset, IMAGEFILE)
{
  s14_template(0, 518, 0)
}

spriteset (mail_22sb_tail_v4_spriteset, IMAGEFILE)
{
  s14_template(0, 590, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, mail_22sb_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2) +
  ((current_year >= 1967) * 4)
)
{
  0: mail_22sb_spriteset;
  1: mail_22sb_tail_spriteset;
  2: mail_22sb_v2_spriteset;
  3: mail_22sb_tail_v2_spriteset;
  4: mail_22sb_v3_spriteset;
  5: mail_22sb_tail_v3_spriteset;
  6: mail_22sb_v4_spriteset;
  7: mail_22sb_tail_v4_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, mail_22sb_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  mail_22sb_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, mail_22sb_get_purchase_spriteset,
  current_year >= 1967)
{
  1: mail_22sb_v3_purchase_spriteset;
  mail_22sb_purchase_spriteset;
}

switch (FEAT_TRAINS, SELF, mail_22sb_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_REFIT_RAL_6020);
  return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, mail_22sb) 
{
  property
  {
    name: string(STR_MAIL_22SB_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1960), 1, 1);
    model_life: get_model_life(1960,1970);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 160 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 10;
    cargo_capacity: 40;
    weight: 46 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: mail_22sb_get_spriteset;
    purchase: mail_22sb_get_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_MAIL_22SB_PURCHASE_HINT);
    cargo_subtype_text: mail_22sb_cargo_subtype_text;
  }
}
