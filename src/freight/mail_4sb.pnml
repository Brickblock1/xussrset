 // mail_4sb

/// Вагон
// 4 сб (1950-1957), ЛВЗ им. Егорова
// mail

#define IMAGEFILE  "src/freight/mail-4sb-1950-1957.png"

// покупка
spriteset (mail_4sb_purchase_spriteset, IMAGEFILE)
{
  s14_purchase_template(0, 52, -12)
}

// вид 1
spriteset (mail_4sb_spriteset, IMAGEFILE)
{
  s14_template(0, 52, 0)
}

// вид 2
spriteset (mail_4sb_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 124, 0)
}

// хвост
spriteset (mail_4sb_tail_spriteset, IMAGEFILE)
{
  s14_template(0, 206, 0)
}

spriteset (mail_4sb_tail_v2_spriteset, IMAGEFILE)
{
  s14_template(0, 278, 0)
}

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, mail_4sb_get_spriteset1,
  (position_in_consist_from_end == 1) + // последний вагон?
  (cargo_subtype * 2)
)
{
  0: mail_4sb_spriteset;
  1: mail_4sb_tail_spriteset;
  2: mail_4sb_v2_spriteset;
  3: mail_4sb_tail_v2_spriteset;
  align_14_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, mail_4sb_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  mail_4sb_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, mail_4sb_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_GREEN_GRASS);
  1: string(STR_REFIT_RAL_6020);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, mail_4sb) 
{
  property
  {
    name: string(STR_MAIL_4SB_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1950), 1, 1);
    model_life: get_model_life(1950,1957);
    vehicle_life: 28;
    reliability_decay: 20;
    cargo_allow_refit: [MAIL];
    default_cargo_type: MAIL;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 8;
    cargo_capacity: 32;
    weight: 55 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: mail_4sb_get_spriteset;
    purchase: mail_4sb_purchase_spriteset;
    articulated_part: articulated_part_dummy6;
    additional_text: return string(STR_MAIL_4SB_PURCHASE_HINT);
    cargo_subtype_text: mail_4sb_cargo_subtype_text;
  }
}
