 // ntv-s

/// Вагон
// 2-осный полувагон с боковой дверью на раме НТВ 1914-1917


#define IMAGEFILE  "src/freight/ntv-s.png"

// покупка
purchase_spriteset(ntv_s, 0, 0)

// пусто
spriteset (ntv_s_empty_spriteset, IMAGEFILE)
{
  s4_template(2, 45, 0)
}

// шаблон груза, 
#define ntv_s_sprite_layout_template(y, cargo) \
spriteset (ntv_s_##cargo##_1_spriteset, IMAGEFILE) \
{ s4_template(241, y, 0) } \
spriteset (ntv_s_##cargo##_full_spriteset, IMAGEFILE) \
{ s4_template(481, y, 0) } \
spritegroup ntv_s_##cargo##_spritegroup \
{ loading: [ntv_s_empty_spriteset, \
    ntv_s_##cargo##_1_spriteset, \
    ntv_s_##cargo##_full_spriteset]; \
  loaded: [ntv_s_empty_spriteset, \
    ntv_s_##cargo##_1_spriteset, \
    ntv_s_##cargo##_full_spriteset]; } \

ntv_s_sprite_layout_template(45,  coal)  
ntv_s_sprite_layout_template(85,  iore)  
ntv_s_sprite_layout_template(125, core) 
ntv_s_sprite_layout_template(165, aore) 
ntv_s_sprite_layout_template(205, clay) 
ntv_s_sprite_layout_template(245, fert) 
ntv_s_sprite_layout_template(285, grvl) 
ntv_s_sprite_layout_template(325, lime) 
ntv_s_sprite_layout_template(365, pota)
ntv_s_sprite_layout_template(405, sand)
ntv_s_sprite_layout_template(445, sulp)
ntv_s_sprite_layout_template(485, bdmt)
ntv_s_sprite_layout_template(525, wdpr)
ntv_s_sprite_layout_template(565, stel) 
ntv_s_sprite_layout_template(605, wood) 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ntv_s_sprites_2,
  cargo_type_in_veh)
{
  COAL: ntv_s_coal_spritegroup;
  IORE: ntv_s_iore_spritegroup;
  CORE: ntv_s_core_spritegroup;
  AORE: ntv_s_aore_spritegroup;
  CLAY: ntv_s_clay_spritegroup;
  FERT: ntv_s_fert_spritegroup;
  GRVL: ntv_s_grvl_spritegroup;
  LIME: ntv_s_lime_spritegroup;
  POTA: ntv_s_pota_spritegroup;
  SAND: ntv_s_sand_spritegroup;
  SULP: ntv_s_sulp_spritegroup;
  BDMT: ntv_s_bdmt_spritegroup;
  WDPR: ntv_s_wdpr_spritegroup;
  STEL: ntv_s_stel_spritegroup;
  WOOD: ntv_s_wood_spritegroup;

  BRCK: ntv_s_bdmt_spritegroup;
      
        align_4_spriteset;
}

short_vehicle(ntv_s, )

item (FEAT_TRAINS, ntv_s) 
{
  property
  {
    name: string(STR_NTV_S_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1914), 1, 1);
    model_life: get_model_life(1914,1917);
    retire_early: get_retire_early(1914,1917);
    vehicle_life: 22;
    reliability_decay: 20;
    cargo_allow_refit: [COAL, FERT, AORE, CORE, IORE, BRCK, BDMT, CLAY,  GRVL, LIME, POTA, SAND, STEL, SULP, WOOD, WDPR];
    default_cargo_type: WOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 5;
    cargo_capacity: 17;
    weight: 7 ton;
    length: 4;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ntv_s_sprites;
    purchase: ntv_s_purchase_spriteset;
    additional_text: string(STR_NTV_S_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ntv_s)
  {
    property
    { name: string(STR_NTV_S_NAME_LONG); }
  }
}

