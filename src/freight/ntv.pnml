 // ntv

/// Вагон
// нормальный товарный вагон (НТВ) 1875-1931.
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/ntv.png"
purchase_sprites(ntv_v1, 0, 0)
flatbed_empty_layout_template(ntv_v1, 4, 0, 31)
purchase_sprites(ntv_v2, 100, 0)
flatbed_empty_layout_template(ntv_v2, 4, 235, 31)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, ntv_purchase_sprites,
  current_year >= 1918)
{
  0: ntv_v1_purchase_sprites;
     ntv_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, ntv_sprites_2,
  LOAD_TEMP(2) >= date(1918,1,1) )
{
  0: ntv_v1_empty_spriteset;
  1: ntv_v2_empty_spriteset;
     align_4_sprites;
}

short_vehicle(ntv)

switch (FEAT_TRAINS, SELF, ntv_additional_text,
  (current_year >= 1905) +
  (current_year >= 1911) )
{
  0: return string(STR_NTV_1891_PURCHASE_HINT);
  1: return string(STR_NTV_1905_PURCHASE_HINT);
     return string(STR_NTV_1911_PURCHASE_HINT);
}

switch (FEAT_TRAINS, SELF, ntv_cargo_capacity_1891, cargo_type_in_veh)
{
  GOOD: return 25;
  WOOL: return 47;
  GLAS: return 33;
  LVST: return 7;
  ENSP: return 19;
  FMSP: return 17;
  MNSP: return 37;
  SGCN: return 11;
        return 12;
}

switch (FEAT_TRAINS, SELF, ntv_cargo_capacity_1905, cargo_type_in_veh)
{
  GOOD: return 29;
  WOOL: return 47;
  GLAS: return 39;
  LVST: return 7;
  ENSP: return 19;
  FMSP: return 17;
  MNSP: return 37;
  SGCN: return 11;
        return 15;
}

switch (FEAT_TRAINS, SELF, ntv_cargo_capacity_1911, cargo_type_in_veh)
{
  GOOD: return 33;
  WOOL: return 47;
  GLAS: return 43;
  LVST: return 7;
  ENSP: return 19;
  FMSP: return 17;
  MNSP: return 37;
  SGCN: return 11;
        return 16;
}

switch (FEAT_TRAINS, SELF, ntv_cargo_capacity_1933, cargo_type_in_veh)
{
  GOOD: return 36;
  WOOL: return 47;
  GLAS: return 48;
  LVST: return 7;
  ENSP: return 19;
  FMSP: return 17;
  MNSP: return 37;
  SGCN: return 11;
        return 18;
}

switch (FEAT_TRAINS, SELF, ntv_cargo_capacity,
  (current_year >= 1905) +
  (current_year >= 1911) +
  (current_year >= 1933))
{
  0: ntv_cargo_capacity_1891;
  1: ntv_cargo_capacity_1905;
  2: ntv_cargo_capacity_1911;
     ntv_cargo_capacity_1933;
}

item (FEAT_TRAINS, ntv) 
{
  property
  {
    name: string(STR_NTV_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1892), 1, 1);
    model_life: get_model_life(1892,1925);
    retire_early: get_retire_early(1892,1925);
    vehicle_life: 30;
    reliability_decay: 20;
    cargo_allow_refit: [GOOD, WOOL, GLAS, LVST, ENSP, FMSP, MNSP, SGCN, PAPR, GRAI, WHEA, MAIZ, CERE, OLSD, FICR, CMNT, BDMT, CERA, SGBT, RCYC, WDPR, ];
    default_cargo_type: GOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 5;
    cargo_capacity: 12.3;
    weight: 7 ton;
    length: 4;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: ntv_sprites;
    purchase: ntv_purchase_sprites;
    additional_text: ntv_additional_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: ntv_cargo_capacity;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, ntv)
  {
    property
    { name: string(STR_NTV_NAME_LONG); }
  }
}

