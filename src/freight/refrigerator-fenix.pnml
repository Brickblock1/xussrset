 // refrigerator_fenix

/// Вагон
// Вагон-ледник	Riga Fenikss	1928-1932
// refrigerator

#define IMAGEFILE  "src/freight/refrigerator-fenix.png"

// покупка
spriteset (refrigerator_fenix_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// шаблон: движение/100%, 25%, 75%
#define refrigerator_fenix_sprite_layout_template(x, y, v) \
spriteset (refrigerator_fenix##v##_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y, 0) \
} \
 \
spriteset (refrigerator_fenix_1##v##_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+41, 0) \
} \
 \
spriteset (refrigerator_fenix_2##v##_spriteset, IMAGEFILE) \
{ \
  s8_template(x, y+82, 0) \
} \
 \
 \
spritegroup refrigerator_fenix##v##_spritegroup \
{ \
  loading: [refrigerator_fenix_1##v##_spriteset, \
    refrigerator_fenix_2##v##_spriteset, \
    refrigerator_fenix_2##v##_spriteset, \
    refrigerator_fenix##v##_spriteset]; \
  loaded: [refrigerator_fenix##v##_spriteset]; \
} \


// ливреи
refrigerator_fenix_sprite_layout_template(2, 39,)

#undef IMAGEFILE

item (FEAT_TRAINS, refrigerator_fenix) 
{
  property
  {
    name: string(STR_REFRIGERATOR_FENIX_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1928), 1, 1);
    model_life: get_model_life(1928,1932);
    retire_early: get_retire_early(1928,1932);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FRVG, FISH, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 13;
    cargo_capacity: 23;
    weight: 37 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: refrigerator_fenix_spritegroup;
    purchase: refrigerator_fenix_purchase_spriteset;
    additional_text: return string(STR_REFRIGERATOR_FENIX_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
