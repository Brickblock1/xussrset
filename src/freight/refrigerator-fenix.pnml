 // refrigerator_fenix/// Вагон
// Вагон-ледник	Riga Fenikss	1928-1932
// refrigerator

#define IMAGEFILE  "src/freight/refrigerator-fenix.png"

// покупка
purchase_spriteset(refrigerator_fenix, 0, 0)

#define refrigerator_fenix_sprite_layout_template(x, y, cargo)  \
spriteset (refrigerator_fenix_##cargo##_spriteset, IMAGEFILE)   \
{ s8_template(64, 136, 0) }                                     \
spriteset (refrigerator_fenix_1_##cargo##_spriteset, IMAGEFILE) \
{ s8_template(64, 176, 0) }                                     \
spriteset (refrigerator_fenix_2_##cargo##_spriteset, IMAGEFILE) \
{ s8_template(x, y, 0) }                                        \
spriteset (refrigerator_fenix_3_##cargo##_spriteset, IMAGEFILE) \
{ s8_template(x, y+40, 0) }                                     \
spritegroup refrigerator_fenix_##cargo##_spritegroup            \
{ loading: [refrigerator_fenix_1_##cargo##_spriteset,           \
    refrigerator_fenix_1_##cargo##_spriteset,                   \
    refrigerator_fenix_1_##cargo##_spriteset,                   \
    refrigerator_fenix_2_##cargo##_spriteset,                   \
    refrigerator_fenix_2_##cargo##_spriteset,                   \
    refrigerator_fenix_2_##cargo##_spriteset,                   \
    refrigerator_fenix_3_##cargo##_spriteset,                   \
    refrigerator_fenix_3_##cargo##_spriteset,                   \
    refrigerator_fenix_3_##cargo##_spriteset,                   \
    refrigerator_fenix_##cargo##_spriteset];                    \
  loaded: [refrigerator_fenix_##cargo##_spriteset]; }           \

refrigerator_fenix_sprite_layout_template(64, 256, fruit1)
refrigerator_fenix_sprite_layout_template(64, 376, fruit2)
refrigerator_fenix_sprite_layout_template(64, 496, fruit3)
refrigerator_fenix_sprite_layout_template(64, 616, fish)
refrigerator_fenix_sprite_layout_template(64, 736, food1)
refrigerator_fenix_sprite_layout_template(64, 856, food2)
refrigerator_fenix_sprite_layout_template(64, 976, sgbt)

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, refrigerator_fenix_fruit_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  2: refrigerator_fenix_fruit1_spritegroup;
  1: refrigerator_fenix_fruit2_spritegroup;
  2: refrigerator_fenix_fruit3_spritegroup;
}

random_switch (FEAT_TRAINS, SELF, refrigerator_fenix_food_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  1: refrigerator_fenix_food1_spritegroup;
  1: refrigerator_fenix_food2_spritegroup;
}

switch (FEAT_TRAINS, SELF, refrigerator_fenix_sprites_2,
  cargo_type_in_veh)
{
  FRVG: refrigerator_fenix_fruit_sprites;
  FRUT: refrigerator_fenix_fruit_sprites;
  FISH: refrigerator_fenix_fish_spritegroup;
  FOOD: refrigerator_fenix_food_sprites;
  SGBT: refrigerator_fenix_sgbt_spritegroup;
        align_9_spriteset;
}

short_vehicle(refrigerator_fenix, )

item (FEAT_TRAINS, refrigerator_fenix) 
{
  property
  {
    name: string(STR_REFRIGERATOR_FENIX_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1928), 1, 1);
    model_life: get_model_life(1928,1932);
    retire_early: get_retire_early(1928,1932);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FRVG, FISH, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 13;
    cargo_capacity: 23;
    weight: 37 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: refrigerator_fenix_sprites;
    purchase: refrigerator_fenix_purchase_spriteset;
    additional_text: return string(STR_REFRIGERATOR_FENIX_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, refrigerator_fenix)
  {
    property
    { name: string(STR_REFRIGERATOR_FENIX_NAME_LONG); }
  }
}

