 // refrigerator_ntv

/// Вагон
// 2-осный вагон-ледник на базе НТВ 1880-1930.
// 

#define IMAGEFILE  "src/freight/refrigerator-ntv.png"

// покупка
spriteset (refrigerator_ntv_purchase_spriteset, IMAGEFILE)
{
  s4_purchase_template(0, 31, 8)
}

// вид 1
spriteset (refrigerator_ntv_spriteset, IMAGEFILE)
{
  s4_template(0, 31, 0)
}

// вид 2
spriteset (refrigerator_ntv_v2_spriteset, IMAGEFILE)
{
  s4_template(0, 73, 0)
}

// вид 3
spriteset (refrigerator_ntv_v3_spriteset, IMAGEFILE)
{
  s4_template(0, 115, 0)
}

#undef IMAGEFILE


switch (FEAT_TRAINS, SELF, refrigerator_ntv_get_spriteset,
  cargo_subtype
)
{
  0: refrigerator_ntv_spriteset;
  1: refrigerator_ntv_v2_spriteset;
  2: refrigerator_ntv_v3_spriteset;
  align_4_spriteset;
}

switch (FEAT_TRAINS, SELF, refrigerator_ntv_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_WITHOUT_CORRUGATION);
  1: string(STR_REFIT_LIGHT_CORRUGATION);
  2: string(STR_REFIT_DARK_CORRUGATION);
  return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, refrigerator_ntv_cargo_capacity,
  cargo_type_in_veh == FOOD)
{
  1: return 7;
  return 10;
}

item (FEAT_TRAINS, refrigerator_ntv) 
{
  property
  {
    name: string(STR_REFRIGERATOR_NTV_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1880), 1, 1);
    model_life: 25;
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FRVG, FISH, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 5;
    cargo_capacity: 1;
    weight: 10 ton;
    length: 4;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: refrigerator_ntv_get_spriteset;
    purchase: refrigerator_ntv_purchase_spriteset;
    additional_text: return string(STR_REFRIGERATOR_NTV_PURCHASE_HINT);
    cargo_subtype_text: refrigerator_ntv_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_capacity: refrigerator_ntv_cargo_capacity;
  }
}
