 // refrigerator_mk4_420

/// Вагон-ледник
// refrigerator van
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/refrigerator-mk4-420.png"
purchase_sprites(refrigerator_mk4_420, 0, 0)
box_empty_layout_template(refrigerator_mk4_420, 13, 44, 113) 
box_layout_template(refrigerator_mk4_420, 13, 44,  298, fruit1)
box_layout_template(refrigerator_mk4_420, 13, 44,  482, fruit2)
box_layout_template(refrigerator_mk4_420, 13, 44,  666, fruit3)
box_layout_template(refrigerator_mk4_420, 13, 44,  850, fish)
box_layout_template(refrigerator_mk4_420, 13, 44, 1035, food)
#undef IMAGEFILE                   	

random_switch (FEAT_TRAINS, SELF, refrigerator_mk4_420_fruit_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  independent: refrigerator_mk4_420_fruit1_sprites;
  2: refrigerator_mk4_420_fruit1_sprites;
  1: refrigerator_mk4_420_fruit2_sprites;
  2: refrigerator_mk4_420_fruit3_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_mk4_420_sprites_2,
  cargo_type_in_veh)
{
  FRVG: refrigerator_mk4_420_fruit_sprites;
  FRUT: refrigerator_mk4_420_fruit_sprites;
  FISH: refrigerator_mk4_420_fish_sprites;
  FOOD: refrigerator_mk4_420_food_sprites;
        align_13_sprites;
}

long_vehicle(refrigerator_mk4_420)

item (FEAT_TRAINS, refrigerator_mk4_420) 
{
  property
  {
    name: string(STR_REFRIGERATOR_MK4_420_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1971), 1, 1);
    model_life: get_model_life(1971,1972);
    retire_early: get_retire_early(1971,1972);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FRUT, FRVG, FISH];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 42;
    weight: 42 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: refrigerator_mk4_420_sprites;
    purchase: refrigerator_mk4_420_purchase_sprites;
    articulated_part: articulated_part_dummy5;
    additional_text: return string(STR_REFRIGERATOR_MK4_420_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, refrigerator_mk4_420)
  {
    property
    { name: string(STR_REFRIGERATOR_MK4_420_NAME_LONG); }
  }
}

