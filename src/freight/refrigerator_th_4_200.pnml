 // refrigerator_th_4_200

/// Вагон-ледник
// refrigerator van
// вагон-термос TH 4-200 dessau (1987-1992).

#define IMAGEFILE  "src/freight/refrigerator-th-4-200.png"

// покупка
spriteset (refrigerator_th_4_200_purchase_spriteset, IMAGEFILE)
{
  s13_purchase_template(0, 45, -10)
}

// шаблон: движение/100%, 33%, 66%, 99%
#define refrigerator_th_4_200_sprite_layout_template(x, y, v) \
spriteset (refrigerator_th_4_200##v##_spriteset, IMAGEFILE) \
{ \
  s13_template(x, y, 0) \
} \
 \
spriteset (refrigerator_th_4_200_1##v##_spriteset, IMAGEFILE) \
{ \
  s13_template(x, y+123-45, 0) \
} \
 \
spriteset (refrigerator_th_4_200_2##v##_spriteset, IMAGEFILE) \
{ \
  s13_template(x, y+202-45, 0) \
} \
 \
spriteset (refrigerator_th_4_200_3##v##_spriteset, IMAGEFILE) \
{ \
  s13_template(x, y+282-45, 0) \
} \
 \
spritegroup refrigerator_th_4_200##v##_spritegroup \
{ \
  loading: [refrigerator_th_4_200_1##v##_spriteset, \
    refrigerator_th_4_200_1##v##_spriteset, \
    refrigerator_th_4_200_1##v##_spriteset, \
    refrigerator_th_4_200_2##v##_spriteset, \
    refrigerator_th_4_200_2##v##_spriteset, \
    refrigerator_th_4_200_2##v##_spriteset, \
    refrigerator_th_4_200_3##v##_spriteset, \
    refrigerator_th_4_200_3##v##_spriteset, \
    refrigerator_th_4_200_3##v##_spriteset, \
    refrigerator_th_4_200##v##_spriteset]; \
  loaded: [refrigerator_th_4_200##v##_spriteset]; \
} \



// ливреи
refrigerator_th_4_200_sprite_layout_template(0, 45,)

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  refrigerator_th_4_200_spritegroup;
}

item (FEAT_TRAINS, refrigerator_th_4_200) 
{
  property
  {
    name: string(STR_REFRIGERATOR_TH_4_200_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1987), 1, 1);
    model_life: get_model_life(1987,1992);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 33 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: refrigerator_th_4_200_get_spriteset;
    purchase: refrigerator_th_4_200_purchase_spriteset;
    articulated_part: articulated_part_dummy5;
    additional_text: return string(STR_REFRIGERATOR_TH_4_200_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
