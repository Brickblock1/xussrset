 // refrigerator_bmz_for_fruit

/// Вагон-ледник
// refrigerator van
// Вагон-ледник для фруктов (БМЗ) 1937-1954
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/refrigerators/bmz-type1937-fruit.png"
purchase_sprites(refrigerator_bmz_for_fruit_v1, 0, 0)
purchase_sprites(refrigerator_bmz_for_fruit_v2, 0, 21)

box_empty_layout_template(refrigerator_bmz_for_fruit_v1, 9, 186,  306) 
box_empty_layout_template(refrigerator_bmz_for_fruit_v2, 9, 860,  306) 

box_layout_template(refrigerator_bmz_for_fruit_v1, 9, 186,  498, fruit1)
box_layout_template(refrigerator_bmz_for_fruit_v1, 9, 186,  688, fruit2)
box_layout_template(refrigerator_bmz_for_fruit_v1, 9, 186,  880, fruit3)

box_layout_template(refrigerator_bmz_for_fruit_v2, 9, 860,  498, fruit1)
box_layout_template(refrigerator_bmz_for_fruit_v2, 9, 860,  688, fruit2)
box_layout_template(refrigerator_bmz_for_fruit_v2, 9, 860,  880, fruit3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_purchase_sprites,
  current_year >= 1948)
{
  0: refrigerator_bmz_for_fruit_v1_purchase_sprites;
     refrigerator_bmz_for_fruit_v2_purchase_sprites;
}

random_switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_v1_fruit_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  independent: refrigerator_bmz_for_fruit_v1_fruit1_sprites;
  2: refrigerator_bmz_for_fruit_v1_fruit1_sprites;
  1: refrigerator_bmz_for_fruit_v1_fruit2_sprites;
  2: refrigerator_bmz_for_fruit_v1_fruit3_sprites;
}

random_switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_v2_fruit_sprites,
  bitmask(TRIGGER_VEHICLE_NEW_LOAD) )
{
  dependent: refrigerator_bmz_for_fruit_v1_fruit_sprites;
  2: refrigerator_bmz_for_fruit_v2_fruit1_sprites;
  1: refrigerator_bmz_for_fruit_v2_fruit2_sprites;
  2: refrigerator_bmz_for_fruit_v2_fruit3_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_v1_sprites,
  cargo_type_in_veh)
{
  FRVG: refrigerator_bmz_for_fruit_v1_fruit_sprites;
  FRUT: refrigerator_bmz_for_fruit_v1_fruit_sprites;
        align_9_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_v2_sprites,
  cargo_type_in_veh)
{
  FRVG: refrigerator_bmz_for_fruit_v2_fruit_sprites;
  FRUT: refrigerator_bmz_for_fruit_v2_fruit_sprites;
        align_9_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_sprites_start,
  build_year >= 1948)
{
  0: refrigerator_bmz_for_fruit_v1_sprites;
     refrigerator_bmz_for_fruit_v2_sprites;
}

long_vehicle_RND(refrigerator_bmz_for_fruit, refrigerator_bmz_for_fruit_v1_fruit1_sprites)

wagon_running_cost(refrigerator_bmz_for_fruit, 28)

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_weight, 
  cargo_count > 0)
{
  1: return 32+6;
     return 32;
}

switch (FEAT_TRAINS, SELF, refrigerator_bmz_for_fruit_cargo_capacity, cargo_type_in_veh)
{
  FRUT: return 24;
  SGBT: return 24;
        return 24;
}

item (FEAT_TRAINS, refrigerator_bmz_for_fruit) {
  property {
    name: string(STR_NAME_REFRIGERATOR_BMZ_FOR_FRUIT);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1937), 1, 1);
    model_life: get_model_life(1937,1954);
    retire_early: get_retire_early(1937,1954);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [FRUT, SGBT];
    default_cargo_type: FRUT; 
    cost_factor: 4;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 24;
    weight: 32 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: refrigerator_bmz_for_fruit_sprites;
    purchase: refrigerator_bmz_for_fruit_purchase_sprites;
    cargo_age_period: return 300;
    running_cost_factor: refrigerator_bmz_for_fruit_running_cost_factor;
    articulated_part: articulated_part_dummy1;
    additional_text: return string(STR_PURCHASE_HINT_WAGON,
                                   24, 65,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_2TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_5_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AXLE),
                                   string(STR_PURCHASE_HINT_FROMTO, 1937, 1954), 
                                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK));
    cargo_capacity: refrigerator_bmz_for_fruit_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    weight: refrigerator_bmz_for_fruit_weight;
  }
}

long_name_template(refrigerator_bmz_for_fruit, STR_LONGNAME_REFRIGERATOR_BMZ_FOR_FRUIT)
