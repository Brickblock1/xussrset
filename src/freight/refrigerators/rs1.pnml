 // rs1

/// Вагон
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) закодирована особым образом

#define IMAGEFILE  "src/freight/refrigerators/rs1.png"
purchase_sprites(rs1, 0, 0)
refsection_layout_template(rs1, 13, 113, 65, food) 
flatbed_empty_layout_template(rs1_diesel, 10, 113, 333)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs1_food_sprites_left, 
  cargo_count)
{
     0..59: rs1_food_sprites_left1;
   60..119: rs1_food_sprites_left2;
  120..179: rs1_food_sprites_left3;
            rs1_food_sprites_left0;
} 

switch (FEAT_TRAINS, SELF, rs1_food_sprites_right, 
  cargo_count)
{
     0..59: rs1_food_sprites_right1;
   60..119: rs1_food_sprites_right2;
  120..179: rs1_food_sprites_right3;
            rs1_food_sprites_right0;
} 

random_switch (FEAT_TRAINS, SELF, rs1_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: rs1_food_sprites_left;
  1: rs1_food_sprites_right;
}

long_vehicle_RND(rs1, rs1_sprites_start)

switch (FEAT_TRAINS, SELF, rs1_diesel_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs1_diesel_empty_sprites_left;
     rs1_diesel_empty_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs1_food_sprites_left_prev, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() )])
{
     0..59: rs1_food_sprites_left1;
   60..119: rs1_food_sprites_left2;
  120..179: rs1_food_sprites_left3;
            rs1_food_sprites_left0;
} 

switch (FEAT_TRAINS, SELF, rs1_food_sprites_right_prev, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() )])
{
     0..59: rs1_food_sprites_right1;
   60..119: rs1_food_sprites_right2;
  120..179: rs1_food_sprites_right3;
            rs1_food_sprites_right0;
} 

switch (FEAT_TRAINS, SELF, rs1int_prev_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs1_food_sprites_left_prev;
     rs1_food_sprites_right_prev;
}

switch (FEAT_TRAINS, SELF, rs1int_sprites_start,
  position_in_vehid_chain)
{
  1: rs1int_prev_sprites;
  3: rs1_diesel_sprites;
  5: rs1int_prev_sprites;
  7: rs1int_prev_sprites;
     dummy_sprites;
}

long_vehicle(rs1int)

engine_diesel_1_effect(rs1int, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs1int_create_effect2, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs1int_create_effect_sprites_left;
     rs1int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs1int_create_effect,
  position_in_vehid_chain)
{
  3: rs1int_create_effect2;
     return 0;
}

// секция из 5-ти вагонов
switch (FEAT_TRAINS, SELF, rs1_articulated_part,
  extra_callback_info1)
{
  1..9: return rs1int;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs1int_length,
  position_in_vehid_chain)
{
  0: return 5;
//  1: return 8;
  2: return 5;
//  3: return 8;
  4: return 2;
//  5: return 8;
  6: return 5;
//  7: return 8;
  8: return 5;
     return 8;
}

item (FEAT_TRAINS, rs1int)
{
  property
  {
    climates_available: NO_CLIMATE;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    model_life: VEHICLE_NEVER_EXPIRES;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    visual_effect_and_powered: disable_visual_effect_and_powered();
    track_type: get_normal_track_type();
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics
  {
    default: rs1int_sprites;
    length: rs1int_length;
    create_effect: rs1int_create_effect;
  }
}

wagon_running_cost(rs1, 1)

item (FEAT_TRAINS, rs1) 
{
  property
  {
    name: string(STR_RS1_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1965), 1, 1);
    model_life: get_model_life(1965,1978);
    retire_early: get_retire_early(1965,1978);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [FOOD, FISH, FRUT, FRVG];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: 255;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 60;
    cargo_capacity: 184;
    weight: 209 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: rs1_sprites;
    purchase: rs1_purchase_sprites;
    cargo_age_period: return 500;
    running_cost_factor: rs1_running_cost_factor;
    articulated_part: rs1_articulated_part;
    additional_text: return string(STR_RS1_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, rs1int)
  {
    property
    { name: string(STR_RS1_NAME_LONG); }
  }
}

