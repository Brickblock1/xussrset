 // rs4

#define IMAGEFILE  "src/freight/refrigerators/rs4.png"
purchase_sprites(rs4, 0, 0)
combo_layout_refsection_template(rs4, 13, 32, 40)
ref_diesel_layout_template(rs4_diesel, 10, 32, 40 + 67*3)
#undef IMAGEFILE          

switch (FEAT_TRAINS, SELF, rs4_sprites_left,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs4_sprites_left_back;
  1: cargo13_frut_bc_sprites;
  2: rs4_sprites_left_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs4_sprites_right,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs4_sprites_right_back;
  1: cargo13_frut_bc_sprites;
  2: rs4_sprites_right_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs4_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4_sprites_left;
     rs4_sprites_right;
}

long_vehicle_RND(rs4, rs4_sprites_asm_left_back)

switch (FEAT_TRAINS, SELF, rs4int_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4_diesel_sprites_left;
     rs4_diesel_sprites_right;
}

long_vehicle(rs4int)

engine_diesel_1r_effect(rs4int, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs4int_create_effect2,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4int_create_effect_sprites_left;
     rs4int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs4int_create_effect,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_cargo_load() > 0)])
{
  1: rs4int_create_effect2;
     return 0;
}

item (FEAT_TRAINS, rs4int, 395) {
  property {
    vehicle_no_dates(1979, 2050, 25, 10, 8, 0)
    vehicle_wagon(0, 140)
    vehicle_wagon_cargo(0, 0)
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: rs4int_sprites;
    create_effect: rs4int_create_effect;
    colour_mapping: any_cc_colour;
  }
}

switch (FEAT_TRAINS, SELF, rs4_articulated_part,
  extra_callback_info1)
{
  1: return dummy5;	
  2: return rs4;	
  3: return dummy5;	
  4: return rs4int;	
  5: return dummy2;	
  6: return rs4;	
  7: return dummy5;	
  8: return rs4;	
  9: return dummy5;	
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity_b1983,
  cargo_type_in_veh)
{
        return 43;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity_a1983,
  cargo_type_in_veh)
{
  FOOD: return 45;
  FRUT: return 45;
        return 46;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity,
  build_year >= 1983)
{
  1: rs4_cargo_capacity_a1983;
     rs4_cargo_capacity_b1983;
}

switch (FEAT_TRAINS, SELF, rs4_additional_text,
  current_year >= 1973)
{
  0: return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   43*4, 112*4,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 380), string(STR_MODEL_NUMBER2, 16, 381)),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1985),
                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148));
     return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   46*4, 112*4,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 380), string(STR_MODEL_NUMBER2, 16, 381)),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1985),
                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148));
}

wagon_running_cost(rs4, 684)
wagon_speed_change_on_load(rs4, 140, 120, 148)

item (FEAT_TRAINS, rs4, 396) {
  property {
    name: string(STR_NAME_RS4);
    vehicle_dates(1979, 1985, 25, 10, 8, 108)
    vehicle_wagon(222, 140)
    vehicle_wagon_cargo(43, 15)
    cargo_allow_refit: [cargo_all_reflist()];
  }
  graphics {
    default: rs4_sprites;
    purchase: rs4_purchase_sprites;
    purchase_speed: rs4_purchase_speed;
    speed: rs4_speed;
    cargo_age_period: return 9000;
    cargo_capacity: rs4_cargo_capacity;
    running_cost_factor: rs4_running_cost_factor;
    articulated_part: rs4_articulated_part;
    additional_text: rs4_additional_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: any_cc_colour;
    cargo_subtype_text: return string(STR_REFIT_FACTORY);
  }
}

long_name_template(rs4, STR_LONGNAME_RS4)
