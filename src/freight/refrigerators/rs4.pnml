// rs4

#define IMAGEFILE  "src/freight/refrigerators/rs4.png"
purchase_sprites(rs4, 0, 0)
combo_layout_refsection_template(rs4, 13, 32, 40)
ref_diesel_layout_template(rs4_diesel, 10, 32, 40 + 67*3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs4_sprites_left,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs4_sprites_left_back;
  1: cargo13_frut_bc_sprites;
  2: rs4_sprites_left_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs4_sprites_right,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs4_sprites_right_back;
  1: cargo13_frut_bc_sprites;
  2: rs4_sprites_right_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs4_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4_sprites_left;
     rs4_sprites_right;
}

long_vehicle(rs4)

switch (FEAT_TRAINS, SELF, rs4int_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4_diesel_sprites_left;
     rs4_diesel_sprites_right;
}

long_vehicle(rs4int)

engine_diesel_1r_effect(rs4int_create_effect_sprites, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs4int_create_effect2,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs4int_create_effect_sprites_left;
     rs4int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs4int_create_effect,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_cargo_load() > 0)])
{
  1: rs4int_create_effect2;
     return 0;
}

item (FEAT_TRAINS, rs4int, 395) {
  property {
    vehicle_no_dates(1979, 2050, 25, 10, 8, 0)
    vehicle_wagon(0, 140)
    vehicle_wagon_cargo(0, 0)
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
           default: rs4int_sprites;
     create_effect: rs4int_create_effect;
    colour_mapping: any_cc_colour;
  }
}

switch (FEAT_TRAINS, SELF, rs4_articulated_part,
  extra_callback_info1)
{
  1: return dummy5;
  2: return rs4;
  3: return dummy5;
  4: return rs4int;
  5: return dummy2;
  6: return rs4;
  7: return dummy5;
  8: return rs4;
  9: return dummy5;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity_2,
[  STORE_TEMP((43 + 3 * (build_year >= 1983)) * 4, 0),  // т
   STORE_TEMP(112 * 4, 1),                              // м³
   STORE_TEMP(0, 2),                                    // м²
   STORE_TEMP(0, 3),                                    // палетомест
   STORE_TEMP(13 * 4, 4)])                              // шаблон
{ ref_cargo_capacity; }

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity,
  position_in_articulated_veh)
{
  0: rs4_cargo_capacity_2;
     return 0;
}

switch (FEAT_TRAINS, SELF, rs4_running_cost_factor,
[  STORE_TEMP(0, 0),    // Моторы
   STORE_TEMP(0, 1),    // Бригада
   STORE_TEMP(342, 2),  // Износ
   STORE_TEMP(114, 3),  // Сопровождение
   STORE_TEMP(228, 4),  // ТО
   STORE_TEMP(2, 5),    // Сертификация

   STORE_TEMP(140, 6),                                                 // Скорость
   STORE_TEMP(222, 7),                                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 4 * 43 + 12 * (build_year >= 1983), 8)])  // Максимальная масса
{ all_running_cost_factor; }                                           // 684

switch (FEAT_TRAINS, SELF, rs4_loading_speed,
  STORE_TEMP(12, 2))
{ all_wagons_box_loading_speed; }

wagon_speed_change_on_load(rs4, 140, 120, 148)
engine_cost_change_build_after(rs4, 4*360+315,
                                    4*398+315, 1983)

hint_wagon_with_spectype_comment(rs4_b1983,
     43*4, 112*4,
     string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
     STR_PURCHASE_HINT_AGING_PERIOD_8_1,
     string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 380), string(STR_MODEL_NUMBER2, 16, 381)),
     1979, 1982,
     string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
     string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148))
hint_wagon_with_spectype_comment(rs4_a1983,
     46*4, 112*4,
     string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
     STR_PURCHASE_HINT_AGING_PERIOD_8_1,
     string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 380), string(STR_MODEL_NUMBER2, 16, 381)),
     1983, 1985,
     string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
     string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148))
hint_change_after(rs4, 1983)

item (FEAT_TRAINS, rs4, 396) {
  property {
    name: string(STR_NAME_RS4);
    vehicle_dates(1979, 1985, 25, 10, 8, 4*360+315)
    vehicle_wagon(222, 140)
    cargo_allow_refit: [cargo_all_reflist()];
  }
  graphics {
                    default: rs4_sprites;
                   purchase: rs4_purchase_sprites;
             purchase_speed: return 140;
                      speed: rs4_speed;
    purchase_cargo_capacity: return (43+3*(current_year >= 1983));
           cargo_age_period: return 9000;
             cargo_capacity: rs4_cargo_capacity;
              loading_speed: rs4_loading_speed;
        running_cost_factor: rs4_running_cost_factor;
                cost_factor: rs4_cost_factor;
           articulated_part: rs4_articulated_part;
            additional_text: rs4_additional_text;
                 refit_cost: return 0 | CB_RESULT_AUTOREFIT;
             colour_mapping: any_cc_colour;
  }
}

long_name_template(rs4, STR_LONGNAME_RS4)
