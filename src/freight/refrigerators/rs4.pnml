 // rs4
// 395 (18Bh) 396 (18Ch)

#define IMAGEFILE  "src/freight/refrigerators/rs4.png"
purchase_sprites(rs4, 0, 0)
refsection_layout_template(rs4, 13, 113, 65, food)
flatbed_empty_layout_template(rs4_diesel, 10, 113, 333)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs4_food_sprites_left,
  cargo_count)
{
     0..59: rs4_food_sprites_left1;
   60..119: rs4_food_sprites_left2;
  120..179: rs4_food_sprites_left3;
            rs4_food_sprites_left0;
}

switch (FEAT_TRAINS, SELF, rs4_food_sprites_right,
  cargo_count)
{
     0..59: rs4_food_sprites_right1;
   60..119: rs4_food_sprites_right2;
  120..179: rs4_food_sprites_right3;
            rs4_food_sprites_right0;
}

random_switch (FEAT_TRAINS, SELF, rs4_sprites_start,
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: rs4_food_sprites_left;
  1: rs4_food_sprites_right;
}

long_vehicle_RND(rs4, rs4_sprites_start)

switch (FEAT_TRAINS, SELF, rs4_diesel_sprites,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs4_diesel_empty_sprites_left;
     rs4_diesel_empty_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs4_food_sprites_left_prev,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load())])
{
     0..59: rs4_food_sprites_left1;
   60..119: rs4_food_sprites_left2;
  120..179: rs4_food_sprites_left3;
            rs4_food_sprites_left0;
}

switch (FEAT_TRAINS, SELF, rs4_food_sprites_right_prev,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load())])
{
     0..59: rs4_food_sprites_right1;
   60..119: rs4_food_sprites_right2;
  120..179: rs4_food_sprites_right3;
            rs4_food_sprites_right0;
}

switch (FEAT_TRAINS, SELF, rs4int_prev_sprites,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs4_food_sprites_left_prev;
     rs4_food_sprites_right_prev;
}

switch (FEAT_TRAINS, SELF, rs4int_sprites_start,
  position_in_vehid_chain)
{
  1: rs4int_prev_sprites;
  3: rs4_diesel_sprites;
  5: rs4int_prev_sprites;
  7: rs4int_prev_sprites;
     dummy_sprites;
}

long_vehicle(rs4int)

engine_diesel_1r_effect(rs4int, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs4int_create_effect2,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs4int_create_effect_sprites_left;
     rs4int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs4int_create_effect,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() > 0) && (position_in_vehid_chain == 3)])
{
  1: rs4int_create_effect2;
     return 0;
}

// секция из 5-ти вагонов
switch (FEAT_TRAINS, SELF, rs4_articulated_part,
  extra_callback_info1)
{
  1..9: return rs4int;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs4int_length,
  position_in_vehid_chain)
{
  0: return 5;
//  1: return 8;
  2: return 5;
//  3: return 8;
  4: return 2;
//  5: return 8;
  6: return 5;
//  7: return 8;
  8: return 5;
     return 8;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity_b1983,
  cargo_type_in_veh)
{
  FOOD: return 172;
  FISH: return 172;
  FRUT: return 172;
  MILK: return 172;
  SGBT: return 172;
        return 172;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity_a1983,
  cargo_type_in_veh)
{
  FOOD: return 180;
  FISH: return 184;
  FRUT: return 180;
  MILK: return 184;
  SGBT: return 184;
        return 184;
}

switch (FEAT_TRAINS, SELF, rs4_cargo_capacity,
  build_year >= 1983)
{
  1: rs4_cargo_capacity_a1983;
     rs4_cargo_capacity_b1983;
}

item (FEAT_TRAINS, rs4int, 395) {
  property {
    vehicle_no_dates(1979, 2050, 25, 10, 8, 0)
    vehicle_wagon(0, 140)
    vehicle_wagon_cargo(0, 0)
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: rs4int_sprites;
    length: rs4int_length;
    create_effect: rs4int_create_effect;
    colour_mapping: any_cc_colour;
  }
}

wagon_running_cost(rs4, 684)

wagon_speed_change_on_load(rs4, 140, 120, 148)

switch (FEAT_TRAINS, SELF, rs4_additional_text,
  current_year >= 1973)
{
  0: return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   172, 447,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1_SECTION_4AXLE_TSMW, string(STR_PURCHASE_HINT_16_380), string(STR_PURCHASE_HINT_16_381)),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1985),
                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148));
     return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                   184, 447,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1_SECTION_4AXLE_TSMW, string(STR_PURCHASE_HINT_16_380), string(STR_PURCHASE_HINT_16_381)),
                   string(STR_PURCHASE_HINT_FROMTO, 1979, 1985),
                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 148));
}

item (FEAT_TRAINS, rs4, 396) {
  property {
    name: string(STR_NAME_RS4);
    vehicle_dates(1979, 1985, 25, 10, 8, 108)
    vehicle_wagon(222, 140)
    vehicle_wagon_cargo(172, 60)
    cargo_allow_refit: [FOOD, FISH, FRUT, FRVG];
  }
  graphics {
    default: rs4_sprites;
    purchase: rs4_purchase_sprites;
    purchase_speed: rs4_purchase_speed;
    speed: rs4_speed;
    cargo_age_period: return 9000;
    cargo_capacity: rs4_cargo_capacity;
    running_cost_factor: rs4_running_cost_factor;
    articulated_part: rs4_articulated_part;
    additional_text: rs4_additional_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: any_cc_colour;
    cargo_subtype_text: return string(STR_REFIT_FACTORY);
  }
}

long_name_template(rs4, STR_LONGNAME_RS4)
