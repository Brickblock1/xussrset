 // rs5

/// Вагон
// РС-5 рефрижераторная секция 5-вагонная (1986-2000)
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) закодирована особым образом

#define IMAGEFILE  "src/freight/refrigerators/rs5.png"
purchase_sprites(rs5, 0, 0)
refsection_layout_template(rs5, 13, 113, 65, food) 
flatbed_empty_layout_template(rs5_diesel, 10, 113, 333)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs5_food_sprites_left, 
  cargo_count)
{
     0..59: rs5_food_sprites_left1;
   60..119: rs5_food_sprites_left2;
  120..179: rs5_food_sprites_left3;
            rs5_food_sprites_left0;
} 

switch (FEAT_TRAINS, SELF, rs5_food_sprites_right, 
  cargo_count)
{
     0..59: rs5_food_sprites_right1;
   60..119: rs5_food_sprites_right2;
  120..179: rs5_food_sprites_right3;
            rs5_food_sprites_right0;
} 

random_switch (FEAT_TRAINS, SELF, rs5_sprites_start, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: rs5_food_sprites_left;
  1: rs5_food_sprites_right;
}

long_vehicle_RND(rs5, rs5_sprites_start)

switch (FEAT_TRAINS, SELF, rs5_diesel_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs5_diesel_empty_sprites_left;
     rs5_diesel_empty_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs5_food_sprites_left_prev, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() )])
{
     0..59: rs5_food_sprites_left1;
   60..119: rs5_food_sprites_left2;
  120..179: rs5_food_sprites_left3;
            rs5_food_sprites_left0;
} 

switch (FEAT_TRAINS, SELF, rs5_food_sprites_right_prev, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() )])
{
     0..59: rs5_food_sprites_right1;
   60..119: rs5_food_sprites_right2;
  120..179: rs5_food_sprites_right3;
            rs5_food_sprites_right0;
} 

switch (FEAT_TRAINS, SELF, rs5int_prev_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs5_food_sprites_left_prev;
     rs5_food_sprites_right_prev;
}

switch (FEAT_TRAINS, SELF, rs5int_sprites_start,
  position_in_vehid_chain)
{
  1: rs5int_prev_sprites;
  3: rs5_diesel_sprites;
  5: rs5int_prev_sprites;
  7: rs5int_prev_sprites;
     dummy_sprites;
}

long_vehicle(rs5int)

engine_diesel_1r_effect(rs5int, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs5int_create_effect2, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs5int_create_effect_sprites_left;
     rs5int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs5int_create_effect,
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_cargo_load() > 0) && (position_in_vehid_chain == 3)])
{
  1: rs5int_create_effect2;
     return 0;
}

// секция из 5-ти вагонов
switch (FEAT_TRAINS, SELF, rs5_articulated_part,
  extra_callback_info1)
{
  1..9: return rs5int;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs5int_length,
  position_in_vehid_chain)
{
  0: return 5;
//  1: return 8;
  2: return 5;
//  3: return 8;
  4: return 2;
//  5: return 8;
  6: return 5;
//  7: return 8;
  8: return 5;
     return 8;
}

item (FEAT_TRAINS, rs5int) {
  property {
    climates_available: NO_CLIMATE;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    model_life: VEHICLE_NEVER_EXPIRES;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    visual_effect_and_powered: disable_visual_effect_and_powered();
    track_type: get_normal_track_type();
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: rs5int_sprites;
    length: rs5int_length;
    create_effect: rs5int_create_effect;
  }
}

switch (FEAT_TRAINS, SELF, rs5_cargo_capacity, cargo_type_in_veh)
{
  FOOD: return 180;
  FISH: return 188;
  FRUT: return 180;
  MILK: return 188;   
  SGBT: return 188;
        return 188;
}

wagon_running_cost(rs5, 739)

wagon_speed_change_on_load(rs5, 140, 120, 145)

item (FEAT_TRAINS, rs5) {
  property {
    name: string(STR_NAME_RS5);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: get_model_life(1986,2000);
    retire_early: get_retire_early(1986,2000);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [FOOD, FISH, FRUT, MILK, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: 117;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 60;
    cargo_capacity: 188;
    weight: 232.9 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: rs5_sprites;
    purchase: rs5_purchase_sprites;
    cargo_age_period: return 9000;
    purchase_speed: rs5_purchase_speed;
    speed: rs5_speed;
    running_cost_factor: rs5_running_cost_factor;
    articulated_part: rs5_articulated_part;
    additional_text: return string(STR_PURCHASE_HINT_WAGON_WITH_COMMENT,
                                   188, 447,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_8_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1_SECTION_4AXLE_TSMW, string(STR_PURCHASE_HINT_16_3000), string(STR_PURCHASE_HINT_16_3001)),
                                   string(STR_PURCHASE_HINT_FROMTO, 1986, 2000), 
                                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK),
                                   string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 145));
    cargo_capacity: rs5_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, rs5int) {
    property { name: string(STR_LONGNAME_RS5); }
  }
}

