 // rs5

#define IMAGEFILE  "src/freight/refrigerators/rs5.png"
purchase_sprites(rs5, 0, 0)
combo_layout_refsection_template(rs5, 13, 32, 40)
ref_diesel_layout_template(rs5_diesel, 10, 32, 40 + 67*3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs5_sprites_left,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs5_sprites_left_back;
  1: cargo13_frut_bc_sprites;
  2: rs5_sprites_left_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_right,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs5_sprites_right_back;
  1: cargo13_frut_bc_sprites;
  2: rs5_sprites_right_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs5_sprites_left;
     rs5_sprites_right;
}

long_vehicle_RND(rs5, rs5_sprites_asm_left_back)

switch (FEAT_TRAINS, SELF, rs5int_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs5_diesel_sprites_left;
     rs5_diesel_sprites_right;
}

long_vehicle(rs5int)

engine_diesel_1r_effect(rs5int, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs5int_create_effect2,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01)])
{
  0: rs5int_create_effect_sprites_left;
     rs5int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs5int_create_effect,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_cargo_load() > 0)])
{
  1: rs5int_create_effect2;
     return 0;
}

item (FEAT_TRAINS, rs5int, 265) {
  property {
    vehicle_no_dates(1986, 2050, 25, 10, 8, 0)
    vehicle_wagon(0, 140)
    vehicle_wagon_cargo(0, 0)
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    default: rs5int_sprites;
    create_effect: rs5int_create_effect;
    colour_mapping: any_cc_colour;
  }
}

switch (FEAT_TRAINS, SELF, rs5_cargo_capacity,
[  STORE_TEMP(47, 0),  // т
   STORE_TEMP(112, 1), // м³
   STORE_TEMP(0, 2),   // м²
   STORE_TEMP(0, 3),   // палетомест
   STORE_TEMP(13, 4)]) // шаблон
{ ref_cargo_capacity; }

switch (FEAT_TRAINS, SELF, rs5_running_cost_factor, // Стоимость обслуживания дизельной секции разложена в стоимость обслуживания вагонов
[  STORE_TEMP(0, 0),            // Моторы
   STORE_TEMP(0, 1),            // Бригада
   STORE_TEMP(739/4/2, 2),      // Износ
   STORE_TEMP(739/4/6, 3),      // Сопровождение (охлаждение)
   STORE_TEMP(739/4/3, 4),      // ТО
   STORE_TEMP(2, 5),            // Сертификация

   STORE_TEMP(140, 6),       // Скорость
   STORE_TEMP(40, 7),        // Тара
   STORE_TEMP(40+47, 8)])    // Максимальная масса
{ all_running_cost_factor; } // 739

switch (FEAT_TRAINS, SELF, rs5_loading_speed,
  STORE_TEMP(12, 2))
{ all_wagons_box_loading_speed; }

switch (FEAT_TRAINS, SELF, rs5_articulated_part,
  extra_callback_info1)
{
  1: return dummy5;
  2: return rs5;
  3: return dummy5;
  4: return rs5int;
  5: return dummy2;
  6: return rs5;
  7: return dummy5;
  8: return rs5;
  9: return dummy5;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

wagon_speed_change_on_load(rs5, 140, 120, 145)

hint_wagon_with_spectype_comment(rs5,
     47*4, 112*4,
     STR_PURCHASE_HINT_LOADING_SPEED_4TIC,
     STR_PURCHASE_HINT_AGING_PERIOD_8_1, 
     string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 3000), string(STR_MODEL_NUMBER2, 16, 3001)),
     1986, 2000,
     STR_PURCHASE_HINT_FACTORY_BRYANSK,
     string(STR_PURCHASE_HINT_SPEED_CHANGE_ON_LOAD, 120, 145))

item (FEAT_TRAINS, rs5, 266) {
  property {
    name: string(STR_NAME_RS5);
    vehicle_dates(1986, 2000, 25, 10, 8, 117)
    vehicle_wagon(233, 140)
    cargo_allow_refit: [cargo_all_reflist()];
  }
  graphics {
    default: rs5_sprites;
    purchase: rs5_purchase_sprites;
    cargo_age_period: return 9000;
    purchase_speed: rs5_purchase_speed;
    speed: rs5_speed;
    running_cost_factor: rs5_running_cost_factor;
    articulated_part: rs5_articulated_part;
    cargo_capacity: rs5_cargo_capacity;
    loading_speed: rs5_loading_speed;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: any_cc_colour;
    additional_text: rs5_additional_text;
  }
}

long_name_template(rs5, STR_LONGNAME_RS5)
