 // refrigerator_th_4_200

/// Вагон-термос
// refrigerator van
// вагон-термос TH 4-200 dessau (1987-1992).
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/refrigerators/th-4-200.png"
purchase_sprites(refrigerator_th_4_200, 0, 0)
box_empty_layout_template(refrigerator_th_4_200_v1, 13, 166,   55) 
box_empty_layout_template(refrigerator_th_4_200_v2, 13, 166,  621) 
box_empty_layout_template(refrigerator_th_4_200_v3, 13, 166, 1187) 
box_empty_layout_template(refrigerator_th_4_200_v4, 13, 166, 1753) 
box_layout_template(refrigerator_th_4_200_v1, 13, 166,  194, fruit)
box_layout_template(refrigerator_th_4_200_v1, 13, 166,  333, food)
box_layout_template(refrigerator_th_4_200_v1, 13, 166,  472, sgbt)
box_layout_template(refrigerator_th_4_200_v2, 13, 166,  760, fruit)
box_layout_template(refrigerator_th_4_200_v2, 13, 166,  899, food)
box_layout_template(refrigerator_th_4_200_v2, 13, 166, 1038, sgbt)
box_layout_template(refrigerator_th_4_200_v3, 13, 166, 1326, fruit)
box_layout_template(refrigerator_th_4_200_v3, 13, 166, 1465, food)
box_layout_template(refrigerator_th_4_200_v3, 13, 166, 1604, sgbt)
box_layout_template(refrigerator_th_4_200_v4, 13, 166, 1892, food)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v1_sprites, cargo_type_in_veh)
{
  FRVG: refrigerator_th_4_200_v1_fruit_sprites;
  FRUT: refrigerator_th_4_200_v1_fruit_sprites;
  SGBT: refrigerator_th_4_200_v1_sgbt_sprites;
  FOOD: refrigerator_th_4_200_v1_food_sprites;
        align_13_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v2_sprites, cargo_type_in_veh)
{
  FRVG: refrigerator_th_4_200_v2_fruit_sprites;
  FRUT: refrigerator_th_4_200_v2_fruit_sprites;
  SGBT: refrigerator_th_4_200_v2_sgbt_sprites;
  FOOD: refrigerator_th_4_200_v2_food_sprites;
        align_13_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v3_sprites, cargo_type_in_veh)
{
  FRVG: refrigerator_th_4_200_v3_fruit_sprites;
  FRUT: refrigerator_th_4_200_v3_fruit_sprites;
  SGBT: refrigerator_th_4_200_v3_sgbt_sprites;
  FOOD: refrigerator_th_4_200_v3_food_sprites;
        align_13_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v4_sprites, cargo_type_in_veh)
{
  FOOD: refrigerator_th_4_200_v4_food_sprites;
        align_13_sprites;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_sprites_start, cargo_subtype)
{
  0: refrigerator_th_4_200_v1_sprites;
  1: refrigerator_th_4_200_v2_sprites;
  2: refrigerator_th_4_200_v3_sprites;
  3: refrigerator_th_4_200_v4_sprites;
     align_13_sprites;
}

long_vehicle_RND(refrigerator_th_4_200, refrigerator_th_4_200_v1_fruit_sprites)

wagon_running_cost(refrigerator_th_4_200, 114)

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_subtype_text_3, cargo_subtype)
{
  0: return string(STR_REFIT_TTD_0C);
  1: return string(STR_REFIT_TTD_0A);
  2: return string(STR_REFIT_TTD_73);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_subtype_text_2, cargo_subtype)
{
  0: return string(STR_REFIT_TTD_0C);
  1: return string(STR_REFIT_TTD_0A);
  2: return string(STR_REFIT_TTD_73);
  3: return string(STR_REFIT_BALTIC);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_subtype_text, cargo_type_in_veh)
{
  FOOD: refrigerator_th_4_200_cargo_subtype_text_2;
        refrigerator_th_4_200_cargo_subtype_text_3; 
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_capacity, cargo_type_in_veh)
{
  FOOD: return 50;
  FISH: return 60;
  FRUT: return 50;
  MILK: return 60;   
  SGBT: return 60;
        return 60;
}

item (FEAT_TRAINS, refrigerator_th_4_200) {
  property {
    name: string(STR_NAME_REFRIGERATOR_TH_4_200);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1987), 1, 1);
    model_life: get_model_life(1987,1992);
    retire_early: get_retire_early(1987,1992);
    vehicle_life: 25;
    reliability_decay: 10;
    cargo_allow_refit: [FOOD, FISH, FRUT, MILK, SGBT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: 18;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 33 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: refrigerator_th_4_200_sprites;
    purchase: refrigerator_th_4_200_purchase_sprites;
    cargo_age_period: return 400;
    running_cost_factor: refrigerator_th_4_200_running_cost_factor;
    articulated_part: articulated_part_dummy5;
    additional_text: return string(STR_PURCHASE_HINT_WAGON,
                                   60, 126, 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AXLE_THERMOS_TSMW),
                                   string(STR_PURCHASE_HINT_FROMTO, 1987, 1992), 
                                   string(STR_PURCHASE_HINT_FACTORY_DESSAU_W));
    cargo_capacity: refrigerator_th_4_200_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: refrigerator_th_4_200_cargo_subtype_text;
    colour_mapping: return ttdall_cc + 0x9C;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, refrigerator_th_4_200) {
    property { name: string(STR_LONGNAME_REFRIGERATOR_TH_4_200); }
  }
}

