// refrigerator_th_4_200

#define IMAGEFILE  "src/freight/refrigerators/th-4-200.png"
purchase_sprites(refrigerator_th_4_200, 0, 0)
combo_layout_boxcar_template(refrigerator_th_4_200_v1, 13, 32, 40)
combo_layout_boxcar_template(refrigerator_th_4_200_v2, 13, 32, 241)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v1_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: refrigerator_th_4_200_v1_sprites_back;
  1: cargo13_frut_bc_sprites;
  2: refrigerator_th_4_200_v1_sprites_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_v2_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: refrigerator_th_4_200_v2_sprites_back;
  1: cargo13_frut_bc_sprites;
  2: refrigerator_th_4_200_v2_sprites_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_sprites_start,
  cargo_subtype)
{
  0..3: refrigerator_th_4_200_v1_sprites_start;
     4: refrigerator_th_4_200_v2_sprites_start;
        align_13_sprites;
}

long_vehicle_RND(refrigerator_th_4_200, refrigerator_th_4_200_v1_sprites_asm_back)

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x07])
{
  0..3: return ttdall_cc + 0x0C;
  4..6: return ttdall_cc + 0x0A;
        return ttdall_cc + 0x73;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_colour_mapping,
  cargo_subtype)
{
  0: refrigerator_th_4_200_random;
  1: return ttdall_cc + 0x0C;
  2: return ttdall_cc + 0x0A;
  3: return ttdall_cc + 0x73;
  4: return ttdall_cc + 0x9C;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_any_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_0C);
  2: return string(STR_REFIT_TTD_0A);
  3: return string(STR_REFIT_TTD_73);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_food_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_0C);
  2: return string(STR_REFIT_TTD_0A);
  3: return string(STR_REFIT_TTD_73);
  4: return string(STR_REFIT_BALTIC);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_subtype_text,
  cargo_type_in_veh)
{
  FOOD: refrigerator_th_4_200_food_cargo_subtype_text;
  BEER: refrigerator_th_4_200_food_cargo_subtype_text;
        refrigerator_th_4_200_any_cargo_subtype_text;
}

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_cargo_capacity,
[  STORE_TEMP(60, 0),   // т
   STORE_TEMP(126, 1),  // м³
   STORE_TEMP(0, 2),    // м²
   STORE_TEMP(0, 3),    // палетомест
   STORE_TEMP(13, 4)])  // шаблон
{ ref_cargo_capacity; }

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_running_cost_factor,
[  STORE_TEMP(0, 0),   // Моторы
   STORE_TEMP(0, 1),   // Бригада
   STORE_TEMP(57, 2),  // Износ
   STORE_TEMP(19, 3),  // Сопровождение (охлаждение)
   STORE_TEMP(38, 4),  // ТО
   STORE_TEMP(2, 5),   // Сертификация

   STORE_TEMP(120, 6),                                 // Скорость
   STORE_TEMP(33, 7),                                  // Тара
   STORE_TEMP(93, 8)])                    /* 33+60 */  // Максимальная масса
{ all_running_cost_factor; }                           // 114

switch (FEAT_TRAINS, SELF, refrigerator_th_4_200_loading_speed,
  STORE_TEMP(11, 2))
{ all_wagons_box_loading_speed; }

hint_wagon(refrigerator_th_4_200,
           60, 126,
           string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
           STR_PURCHASE_HINT_AGING_PERIOD_5_1,
           STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_THERMOS,
           1987, 1992,
           string(STR_PURCHASE_HINT_FACTORY_DESSAU_W))

item (FEAT_TRAINS, refrigerator_th_4_200, 334) {
  property {
    name: string(STR_NAME_REFRIGERATOR_TH_4_200);
    vehicle_dates(1987, 1992, 25, 10, 8, 393)
    vehicle_wagon(33, 120)
    cargo_allow_refit: [cargo_all_reflist()];
  }
  graphics {
    default: refrigerator_th_4_200_sprites;
    purchase: refrigerator_th_4_200_purchase_sprites;
    cargo_age_period: return 400;
    running_cost_factor: refrigerator_th_4_200_running_cost_factor;
    cost_factor: return 393;
    articulated_part: articulated_part_dummy5;
    cargo_capacity: refrigerator_th_4_200_cargo_capacity;
    loading_speed: refrigerator_th_4_200_loading_speed;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    cargo_subtype_text: refrigerator_th_4_200_cargo_subtype_text;
    colour_mapping: refrigerator_th_4_200_colour_mapping;
    additional_text: refrigerator_th_4_200_additional_text;
  }
}

long_name_template(refrigerator_th_4_200, STR_LONGNAME_REFRIGERATOR_TH_4_200)
