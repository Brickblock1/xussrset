 // rs1

/// Вагон

#define IMAGEFILE  "src/freight/rs-refrigerator-section.png"

// покупка
purchase_sprites(rs1, 0, 0)

// шаблон: движение/100%, 33%, 66%, 99%
#define rs1_sprite_layout_template(x, y, v)    \
spriteset (rs1##v##_sprites, IMAGEFILE)        \
{ s13_template(x, y, 0) }                      \
spriteset (rs1_1##v##_sprites, IMAGEFILE)      \
{ s13_template(x, y+67, 0) }                   \
spriteset (rs1_2##v##_sprites, IMAGEFILE)      \
{ s13_template(x, y+134, 0) }                  \
spriteset (rs1_3##v##_sprites, IMAGEFILE)      \
{ s13_template(x, y+201, 0) }                  \
spritegroup rs1##v##_sprites_l                 \
{ loading: [rs1_1##v##_sprites,                \
    rs1_1##v##_sprites,                        \
    rs1_1##v##_sprites,                        \
    rs1_2##v##_sprites,                        \
    rs1_2##v##_sprites,                        \
    rs1_2##v##_sprites,                        \
    rs1_3##v##_sprites,                        \
    rs1_3##v##_sprites,                        \
    rs1_3##v##_sprites,                        \
    rs1##v##_sprites];                         \
  loaded: [rs1##v##_sprites]; }                \
                                               \
spriteset (rs1##v##_r_sprites, IMAGEFILE)      \
{ s13_r_template(x, y, 0) }                    \
spriteset (rs1_1##v##_r_sprites, IMAGEFILE)    \
{ s13_r_template(x, y+67, 0) }                 \
spriteset (rs1_2##v##_r_sprites, IMAGEFILE)    \
{ s13_r_template(x, y+134, 0) }                \
spriteset (rs1_3##v##_r_sprites, IMAGEFILE)    \
{ s13_r_template(x, y+201, 0) }                \
spritegroup rs1##v##_sprites_r                 \
{ loading: [rs1_1##v##_r_sprites,              \
    rs1_1##v##_r_sprites,                      \
    rs1_1##v##_r_sprites,                      \
    rs1_2##v##_r_sprites,                      \
    rs1_2##v##_r_sprites,                      \
    rs1_2##v##_r_sprites,                      \
    rs1_3##v##_r_sprites,                      \
    rs1_3##v##_r_sprites,                      \
    rs1_3##v##_r_sprites,                      \
    rs1##v##_r_sprites];                       \
  loaded: [rs1##v##_r_sprites]; }              \

// ливреи
rs1_sprite_layout_template(113, 447, _v1)

random_switch (FEAT_TRAINS, SELF, rs1_sprites_2, 
  bitmask(TRIGGER_VEHICLE_SERVICE))
{
  1: rs1_v1_sprites_l;
  1: rs1_v1_sprites_r;
}

// дизельный вагон
spriteset (rs1_diesel_sprites_l, IMAGEFILE)
{
  s10_template(113, 715, 0)
}
spriteset (rs1_diesel_sprites_r, IMAGEFILE)
{
  s10_r_template(113, 715, 0)
}

switch (FEAT_TRAINS, SELF, rs1_diesel_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs1_diesel_sprites_l;
     rs1_diesel_sprites_r;
}

#undef IMAGEFILE

long_vehicle_RND(rs1, rs1_sprites_2)

// секция из 5-ти вагонов
switch (FEAT_TRAINS, SELF, rs1_articulated_part,
  extra_callback_info1)
{
  1..9: return rs1int;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs1int_length,
  position_in_vehid_chain)
{
  0: return 5;
//  1: return 8;
  2: return 5;
//  3: return 8;
  4: return 2;
//  5: return 8;
  6: return 5;
//  7: return 8;
  8: return 5;
     return 8;
}

switch (FEAT_TRAINS, SELF, rs1int_prev_sprites, 
[  set_offset_to(-position_in_vehid_chain-1),
   (prev_vehicle_randombits() % 2)])
{
  0: rs1_v1_sprites_l;
     rs1_v1_sprites_r;
}

switch (FEAT_TRAINS, SELF, rs1int_sprites_2,
  position_in_vehid_chain)
{
  1: rs1int_prev_sprites;
  3: rs1_diesel_sprites;
  5: rs1int_prev_sprites;
  7: rs1int_prev_sprites;
     dummy_sprites;
}

long_vehicle(rs1int)

// Для задания нужных размеров частей и их рисования
item (FEAT_TRAINS, rs1int)
{
  property
  {
    climates_available: NO_CLIMATE;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    model_life: VEHICLE_NEVER_EXPIRES;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    visual_effect_and_powered: disable_visual_effect_and_powered();
    track_type: get_normal_track_type();
  }
  graphics
  {
    default: rs1int_sprites;
    length: rs1int_length;
  }
}

// Сама секция
item (FEAT_TRAINS, rs1) 
{
  property
  {
    name: string(STR_RS1_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1965), 1, 1);
    model_life: get_model_life(1965,1978);
    retire_early: get_retire_early(1965,1978);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FISH, FRUT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 120 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 25;
    cargo_capacity: 184;
    weight: 209 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: rs1_sprites;
    purchase: rs1_purchase_sprites;
    articulated_part: rs1_articulated_part;
    additional_text: return string(STR_RS1_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, rs1int)
  {
    property
    { name: string(STR_RS1_NAME_LONG); }
  }
}

