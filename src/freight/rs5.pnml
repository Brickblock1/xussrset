 // rs5

/// Вагон
// РС-5 рефрижераторная секция 5-вагонная (1986-2000)

#define IMAGEFILE  "src/freight/rs5-refrigerator-section-1986-2000.png"

// покупка
spriteset (rs5_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// грузовой вагон
spriteset (rs5_cargo_spriteset, IMAGEFILE)
{
  s13_template(112, 64, 0)
}

// дизельный вагон
spriteset (rs5_diesel_spriteset, IMAGEFILE)
{
  s10_template(112, 131, 0)
}

// Реверс: (552, y, 0)

#undef IMAGEFILE

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, rs5_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  rs5_cargo_spriteset;
}

// секция из 5-ти вагонов
switch (FEAT_TRAINS, SELF, rs5_articulated_part,
  extra_callback_info1)
{
  1..9: return rs5int;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, rs5int_length,
  position_in_vehid_chain)
{
  0: return 5;
//  1: return 8;
  2: return 5;
//  3: return 8;
  4: return 2;
//  5: return 8;
  6: return 5;
//  7: return 8;
  8: return 5;
  return 8;
}

switch (FEAT_TRAINS, SELF, rs5int_get_spriteset1,
  position_in_vehid_chain)
{
  1: rs5_cargo_spriteset;
  3: rs5_diesel_spriteset;
  5: rs5_cargo_spriteset;
  7: rs5_cargo_spriteset;
  dummy_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, rs5int_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  rs5int_get_spriteset1;
}

// Для задания нужных размеров частей и их рисования
item (FEAT_TRAINS, rs5int)
{
  property
  {
    climates_available: NO_CLIMATE;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    model_life: VEHICLE_NEVER_EXPIRES;
    misc_flags: bitmask(TRAIN_FLAG_TILT);
    visual_effect_and_powered: disable_visual_effect_and_powered();
    track_type: get_normal_track_type();
  }
  graphics
  {
    default: rs5int_get_spriteset;
    length: rs5int_length;
  }
}

// Сама секция
item (FEAT_TRAINS, rs5) 
{
  property
  {
    name: string(STR_RS5_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1986), 1, 1);
    model_life: get_model_life(1986,2000);
    vehicle_life: 25;
    reliability_decay: 20;
    cargo_allow_refit: [FOOD, FISH, FRUT];
    default_cargo_type: FOOD;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 140 km/h;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 25;
    cargo_capacity: 188;
    weight: 215 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: rs5_get_spriteset;
    purchase: rs5_purchase_spriteset;
    articulated_part: rs5_articulated_part;
    additional_text: return string(STR_RS5_PURCHASE_HINT);
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
