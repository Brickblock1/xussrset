 // tanker_15_871

/// Вагон
// 15-871 8-осная цистерна для бензина (1974-1987)
// tanker petrol

#define IMAGEFILE  "src/freight/tanker-15-871-8axis-petrol-1974-1987.png"

// покупка
spriteset (tanker_15_871_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(0, 0)
}

// вид 1
spriteset (tanker_15_871_spriteset, IMAGEFILE)
{
  s12_template(0, 25, 0)
}

// вид 2
spriteset (tanker_15_871_v2_spriteset, IMAGEFILE)
{
  s12_template(0, 92, 0)
}

// вид 3
spriteset (tanker_15_871_v3_spriteset, IMAGEFILE)
{
  s12_template(0, 159, 0)
}

// вид 4
spriteset (tanker_15_871_v4_spriteset, IMAGEFILE)
{
  s12_template(0, 226, 0)
}

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_15_871_random, 0 )
{
  3: tanker_15_871_spriteset;
  6: tanker_15_871_v2_spriteset;
  5: tanker_15_871_v3_spriteset;
  1: tanker_15_871_v4_spriteset;
}


switch (FEAT_TRAINS, SELF, tanker_15_871_get_spriteset1,
  cargo_subtype
)
{
  0: tanker_15_871_random;
  1: tanker_15_871_spriteset;
  2: tanker_15_871_v2_spriteset;
  3: tanker_15_871_v3_spriteset;
  4: tanker_15_871_v4_spriteset;
  align_12_spriteset;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, tanker_15_871_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  tanker_15_871_get_spriteset1;
}

switch (FEAT_TRAINS, SELF, tanker_15_871_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_YELLOW_MALE);
  2: string(STR_REFIT_PALE_YELLOW_2_MALE);
  3: string(STR_REFIT_OCHER);
  4: string(STR_REFIT_BLUE_MALE);
  return CB_RESULT_NO_TEXT;
}


item (FEAT_TRAINS, tanker_15_871) 
{
  property
  {
    name: string(STR_TANKER_15_871_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1974), 1, 1);
    retire_early: retire_early_default;
    model_life: get_model_life(1974,1987);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [PETR, RFPR];
    default_cargo_type: PETR;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 30;
    cargo_capacity: 120;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: tanker_15_871_get_spriteset;
    purchase: tanker_15_871_purchase_spriteset;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_TANKER_15_871_PURCHASE_HINT);
    cargo_subtype_text: tanker_15_871_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}
