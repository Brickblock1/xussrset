 // tanker_of_1931_25m3

/// Цистерна
// Двухосная цистерна образца 1931 г. (1931-1937).
// 25 кубов

#define IMAGEFILE  "src/freight/tanker-of-1931-25m3-1931-1937.png"

// покупка
purchase_sprites(tanker_of_1931_25m3, 0, 0)

// вид 1
spriteset (tanker_of_1931_25m3_spriteset, IMAGEFILE)
{
  s5_template(0, 30, 0)
}

// вид 2
spriteset (tanker_of_1931_25m3_v2_spriteset, IMAGEFILE)
{
  s5_template(0, 69, 0)
}

// вид 3
spriteset (tanker_of_1931_25m3_v3_spriteset, IMAGEFILE)
{
  s5_template(0, 108, 0)
}

// вид 4
spriteset (tanker_of_1931_25m3_v4_spriteset, IMAGEFILE)
{
  s5_template(0, 147, 0)
}

#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_random, 0 )
{
  2: tanker_of_1931_25m3_spriteset;
  3: tanker_of_1931_25m3_v2_spriteset;
}  

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_random, 0 )
{
  3: tanker_of_1931_25m3_v2_spriteset;
  2: tanker_of_1931_25m3_v3_spriteset;
}  

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_random, 0 )
{
  2: tanker_of_1931_25m3_spriteset;
  3: tanker_of_1931_25m3_v4_spriteset;
}  

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_OIL_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_v3_spriteset;
  align_5_spriteset;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_PETR_random;
  1: tanker_of_1931_25m3_spriteset;
  2: tanker_of_1931_25m3_v2_spriteset;
     align_5_spriteset;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_RFPR_random;
  1: tanker_of_1931_25m3_v2_spriteset;
  2: tanker_of_1931_25m3_v3_spriteset;
     align_5_spriteset;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_WATR_random;
  1: tanker_of_1931_25m3_spriteset;
  2: tanker_of_1931_25m3_v4_spriteset;
     align_5_spriteset;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_DYES_RUBR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_v4_spriteset;
     align_5_spriteset;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_sprites_2, 
  cargo_type_in_veh) 
{
  OIL_: tanker_of_1931_25m3_OIL_sprites;
  PETR: tanker_of_1931_25m3_PETR_sprites;
  RFPR: tanker_of_1931_25m3_RFPR_sprites;
  WATR: tanker_of_1931_25m3_WATR_sprites;
        tanker_of_1931_25m3_DYES_RUBR_sprites;
}

short_vehicle(tanker_of_1931_25m3, )

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_26);
  2: string(STR_REFIT_TTD_39);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_39);
  2: string(STR_REFIT_TTD_3F);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_FACTORY);
  1: string(STR_REFIT_TTD_26);
  2: string(STR_REFIT_TTD_08);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_cargo_subtype_text, 
  cargo_type_in_veh) 
{
  OIL_: string(STR_REFIT_TTD_3F);
  PETR: tanker_of_1931_25m3_PETR_cargo_subtype_text;
  RFPR: tanker_of_1931_25m3_RFPR_cargo_subtype_text;
  WATR: tanker_of_1931_25m3_WATR_cargo_subtype_text;
        string(STR_REFIT_TTD_08);
}

item (FEAT_TRAINS, tanker_of_1931_25m3)
{
  property
  {
    name: string(STR_TANKER_ISSUE_OF_1931_25M3_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1931), 1, 1);
    model_life: get_model_life(1931,1937);
    retire_early: get_retire_early(1931,1937);
    vehicle_life: 32;
    reliability_decay: 20;
    cargo_allow_refit: [OIL_, PETR, RFPR, RUBR, DYES, WATR];
    default_cargo_type: OIL_;
    refit_cost: 0;
    cost_factor: get_car_cost_factor();
    running_cost_factor: get_car_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_DIESEL;
    loading_speed: 9;
    cargo_capacity: 25;
    weight: 11 ton;
    length: 5;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: tanker_of_1931_25m3_sprites;
    purchase: tanker_of_1931_25m3_purchase_sprites;
    additional_text: return string(STR_TANKER_ISSUE_OF_1931_25M3_PURCHASE_HINT);
    cargo_subtype_text: tanker_of_1931_25m3_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, tanker_of_1931_25m3)
  {
    property
    { name: string(STR_TANKER_ISSUE_OF_1931_25M3_NAME_LONG); }
  }
}

