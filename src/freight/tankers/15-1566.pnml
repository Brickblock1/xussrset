 // tanker_15_1566
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/tankers/15-1566.png"
purchase_sprites(tanker_15_1566, 0, 0)
tanker_layout_template(tanker_15_1566_v1, 7, 0, 39)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tanker_15_1566_sprites_start, 0)
{
  tanker_15_1566_v1_sprites;
}

short_vehicle_RND(tanker_15_1566, tanker_15_1566_v1_sprites)

wagon_running_cost(tanker_15_1566, 23)

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_capacity_1974, cargo_type_in_veh)
{
  OIL_: return 62;
  RFPR: return 62;
  RUBR: return 62;
  PLST: return 62;
  PLAS: return 62;
        return 62;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_capacity_1981, cargo_type_in_veh)
{
  OIL_: return 66;
  RFPR: return 66;
  RUBR: return 67;
  PLST: return 67;
  PLAS: return 67;
        return 67;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_capacity_1989, cargo_type_in_veh)
{
  OIL_: return 66;
  RFPR: return 66;
  RUBR: return 67;
  PLST: return 69;
  PLAS: return 69;
        return 69;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_capacity,
  (build_year <= 1980) +
  (build_year >= 1989))
{
  1: tanker_15_1566_cargo_capacity_1974;
  2: tanker_15_1566_cargo_capacity_1989;  
     tanker_15_1566_cargo_capacity_1981;
}

random_switch (FEAT_TRAINS, SELF, tanker_15_1566_random_b1993, 0 )
{
  independent: tanker_15_1566_v1_sprites;
  2: return ttdall_cc + 0x49;
  1: return ttdall_cc + 0x7C;
  1: return ttdall_cc + 0x4A;
}                      

random_switch (FEAT_TRAINS, SELF, tanker_15_1566_random_a1993, 0)
{
  independent: tanker_15_1566_v1_sprites;
  3: return ttdall_cc + 0x49;
  3: return ttdall_cc + 0x7C;
  3: return ttdall_cc + 0x4A;
  2: return ttdall_cc + 0x08;
  2: return ttdall_cc + 0x95;
  1: return ttdall_cc + 0x0A;
  1: return ttdall_cc + 0x9D;
  1: return ttdall_cc + 0xB3;
}                      

switch (FEAT_TRAINS, SELF, tanker_15_1566_random,
  build_year >= 1993)
{
  0: tanker_15_1566_random_b1993;
     tanker_15_1566_random_a1993;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_colour_mapping,
  cargo_subtype)
{
  0: tanker_15_1566_random;  
  1: return ttdall_cc + 0x49;
  2: return ttdall_cc + 0x7C;
  3: return ttdall_cc + 0x4A;
  4: return ttdall_cc + 0x08;
  5: return ttdall_cc + 0x95;
  6: return ttdall_cc + 0x0A;
  7: return ttdall_cc + 0x9D;
  8: return ttdall_cc + 0xB3;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_subtype_text_b1993, 
    cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_49);
  2: return string(STR_REFIT_TTD_7C);
  3: return string(STR_REFIT_TTD_4A);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_subtype_text_a1993, 
    cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_49);
  2: return string(STR_REFIT_TTD_7C);
  3: return string(STR_REFIT_TTD_4A);
  4: return string(STR_REFIT_TTD_08);
  5: return string(STR_REFIT_TTD_95);
  6: return string(STR_REFIT_TTD_0A);
  7: return string(STR_REFIT_TTD_9D);
  8: return string(STR_REFIT_TTD_B3);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_15_1566_cargo_subtype_text,
  build_year >= 1993)
{
  0: tanker_15_1566_cargo_subtype_text_b1993;
     tanker_15_1566_cargo_subtype_text_a1993;
}

item (FEAT_TRAINS, tanker_15_1566) 
{
  property
  {
    name: string(STR_TANKER_15_1566_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1974), 1, 1);
    model_life: get_model_life(1974,1996);
    retire_early: get_retire_early(1974,1996);
    vehicle_life: 32;
    reliability_decay: 10;
    cargo_allow_refit: [OIL_, RFPR, RUBR, PLST, PLAS];
    default_cargo_type: OIL_;
    refit_cost: 0;
    cost_factor: 4;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 62;
    weight: 25 ton;
    length: 7;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: tanker_15_1566_sprites;
    purchase: tanker_15_1566_purchase_sprites;
    running_cost_factor: tanker_15_1566_running_cost_factor;
    cargo_capacity: tanker_15_1566_cargo_capacity;
    additional_text: return string(STR_TANKER_15_1566_PURCHASE_HINT);
    cargo_subtype_text: tanker_15_1566_cargo_subtype_text;
    cargo_capacity: tanker_15_1566_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: tanker_15_1566_colour_mapping;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, tanker_15_1566)
  {
    property
    { name: string(STR_TANKER_15_1566_NAME_LONG); }
  }
}

