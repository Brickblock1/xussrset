 // tanker_15_1593

#define IMAGEFILE  "src/freight/tankers/15-1593.png"
purchase_sprites(tanker_15_1593, 0, 0)
tanker_layout_template(tanker_15_1593_v1, 7, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tanker_15_1593_sprites_start, 0)
{
  tanker_15_1593_v1_sprites;
}

short_vehicle_RND(tanker_15_1593, tanker_15_1593_v1_sprites)

switch (FEAT_TRAINS, SELF, tanker_15_1593_cargo_subtype_text,
    cargo_subtype )
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_25);
  2: return string(STR_REFIT_TTD_39);
  3: return string(STR_REFIT_TTD_62);
  4: return string(STR_REFIT_TTD_63);
  5: return string(STR_REFIT_TTD_08);
  6: return string(STR_REFIT_TTD_49);
     return CB_RESULT_NO_TEXT;
}

random_switch (FEAT_TRAINS, SELF, tanker_15_1593_random, 0 )
{
  independent: tanker_15_1593_v1_sprites;
  2: return ttdall_cc + 0x25;
  1: return ttdall_cc + 0x39;
  2: return ttdall_cc + 0x62;
  1: return ttdall_cc + 0x63;
  1: return ttdall_cc + 0x08;
  1: return ttdall_cc + 0x49;
}

switch (FEAT_TRAINS, SELF, tanker_15_1593_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: tanker_15_1593_random;
  1: return ttdall_cc + 0x25;
  2: return ttdall_cc + 0x39;
  3: return ttdall_cc + 0x62;
  4: return ttdall_cc + 0x63;
  5: return ttdall_cc + 0x08;
  6: return ttdall_cc + 0x49;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tanker_1593_cargo_capacity,
  cargo_type_in_veh)
{
  FOOD: return 27;
  MILK: return 64;
  WATR: return 64;
  EOIL: return 64;
  COLA: return 64;
        return 67;
}

switch (FEAT_TRAINS, SELF, tanker_15_1593_additional_text,
  current_year >= USSREND)
{
  0: return string(STR_PURCHASE_HINT_WAGON,
                   67, 64,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_TANK_4AXLE_MILK),
                   string(STR_PURCHASE_HINT_FROM, 1988),
                   string(STR_PURCHASE_HINT_FACTORY_AZOVMASH_ZHDANOV));
     return string(STR_PURCHASE_HINT_WAGON,
                   67, 64,
                   string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1),
                   string(STR_PURCHASE_HINT_WAGON_TYPE_TANK_4AXLE_MILK),
                   string(STR_PURCHASE_HINT_FROM, 1988),
                   string(STR_PURCHASE_HINT_FACTORY_AZOVMASH_AUSSR));
}   

wagon_running_cost(tanker_15_1593, 36)

item (FEAT_TRAINS, tanker_15_1593, 529) {
  property {
    name: string(STR_NAME_TANKER_15_1593);
    vehicle_dates(1988, 2050, 30, 10, 7, 6)
    vehicle_wagon(26, 120)
    vehicle_wagon_cargo(67, 15)
    cargo_allow_refit: [WATR, FOOD, COLA, EOIL, MILK];
  }
  graphics {
    default: tanker_15_1593_sprites;
    purchase: tanker_15_1593_purchase_sprites;
    running_cost_factor: tanker_15_1593_running_cost_factor;
    cargo_subtype_text: tanker_15_1593_cargo_subtype_text;
    cargo_capacity: tanker_1593_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: tanker_15_1593_colour_mapping;
    additional_text: tanker_15_1593_additional_text;
  }
}

long_name_template(tanker_15_1593, STR_LONGNAME_TANKER_15_1593)
