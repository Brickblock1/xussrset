 // tanker_15_871

/// Вагон
// 15-871 8-осная цистерна для бензина (1974-1987)
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон
// tanker petrol

#define IMAGEFILE  "src/freight/tankers/15-871.png"
purchase_sprites(tanker_15_871, 0, 0)

tanker_layout_template(tanker_15_871_v1, 12, 0,  25)
tanker_layout_template(tanker_15_871_v2, 12, 0,  92)
tanker_layout_template(tanker_15_871_v3, 12, 0, 159)
tanker_layout_template(tanker_15_871_v4, 12, 0, 226)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_15_871_random, 0 )
{
  independent: tanker_15_871_v1_sprites;
  3: tanker_15_871_v1_sprites;
  6: tanker_15_871_v2_sprites;
  5: tanker_15_871_v3_sprites;
  1: tanker_15_871_v4_sprites;
}


switch (FEAT_TRAINS, SELF, tanker_15_871_sprites_start,
  cargo_subtype)
{
  0: tanker_15_871_random;
  1: tanker_15_871_v1_sprites;
  2: tanker_15_871_v2_sprites;
  3: tanker_15_871_v3_sprites;
  4: tanker_15_871_v4_sprites;
     align_12_sprites;
}

long_vehicle_RND(tanker_15_871, tanker_15_871_v1_sprites)

wagon_running_cost(tanker_15_871, 73)

switch (FEAT_TRAINS, SELF, tanker_15_871_cargo_capacity, cargo_type_in_veh)
{
  PETR: return 111;
  RFPR: return 120;
        return 120;
}

switch (FEAT_TRAINS, SELF, tanker_15_871_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_42);
  2: return string(STR_REFIT_TTD_39);
  3: return string(STR_REFIT_TTD_3F);
  4: return string(STR_REFIT_TTD_9C);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, tanker_15_871) 
{
  property
  {
    name: string(STR_TANKER_15_871_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1974), 1, 1);
    model_life: get_model_life(1974,1987);
    retire_early: get_retire_early(1974,1987);
    vehicle_life: 32;
    reliability_decay: 10;
    cargo_allow_refit: [PETR, RFPR];
    default_cargo_type: PETR;
    refit_cost: 0;
    cost_factor: 12;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 30;
    cargo_capacity: 120;
    weight: 49 ton;
    length: 8;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics
  {
    default: tanker_15_871_sprites;
    purchase: tanker_15_871_purchase_sprites;
    running_cost_factor: tanker_15_871_running_cost_factor;
    articulated_part: articulated_part_dummy4;
    additional_text: return string(STR_TANKER_15_871_PURCHASE_HINT);
    cargo_subtype_text: tanker_15_871_cargo_subtype_text;
    cargo_capacity: tanker_15_871_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

if (enable_long_names)
{
  item (FEAT_TRAINS, tanker_15_871)
  {
    property
    { name: string(STR_TANKER_15_871_NAME_LONG); }
  }
}

