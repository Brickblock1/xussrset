 // tanker_15_897
// конструкция bitmask(TRIGGER_VEHICLE_SERVICE) внесена в шаблон

#define IMAGEFILE  "src/freight/tankers/15-897.png"
purchase_sprites(tanker_15_897, 0, 0)
tanker_layout_template(tanker_15_897_v1, 7, 0, 39)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, tanker_15_897_sprites_start, 0)
{
  tanker_15_897_v1_sprites;
}

short_vehicle_RND(tanker_15_897, tanker_15_897_v1_sprites)

wagon_running_cost(tanker_15_897, 20)

switch (FEAT_TRAINS, SELF, tanker_15_897_cargo_capacity, cargo_type_in_veh)
{
  OIL_: return 56;
  RFPR: return 56;
        return 60;
}

random_switch (FEAT_TRAINS, SELF, tanker_15_897_random, 0 )
{
  independent: tanker_15_897_v1_sprites;
  2: return ttdall_cc + 0x49;
  1: return ttdall_cc + 0x4A;
  1: return ttdall_cc + 0x7C;
}                      

switch (FEAT_TRAINS, SELF, tanker_15_897_colour_mapping,
  cargo_subtype)
{
  0: tanker_15_897_random;  
  1: return ttdall_cc + 0x49;
  2: return ttdall_cc + 0x4A;
  3: return ttdall_cc + 0x7C;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tanker_15_897_cargo_subtype_text, 
    cargo_subtype )
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_49);
  2: return string(STR_REFIT_TTD_4A);
  3: return string(STR_REFIT_TTD_7C);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, tanker_15_897) {
  property {
    name: string(STR_NAME_TANKER_15_897);
    climates_available: get_climates_available();
    introduction_date: date(get_car_year(1960), 1, 1);
    model_life: get_model_life(1960,1976);
    retire_early: get_retire_early(1960,1976);
    vehicle_life: 32;
    reliability_decay: 10;
    cargo_allow_refit: [OIL_, RFPR];
    default_cargo_type: OIL_;
    refit_cost: 0;
    cost_factor: 3;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
    speed: 120 km/h;
    track_type: get_normal_track_type();
    power: 0 hpM;
    running_cost_base: RUNNING_COST_ROADVEH;
    loading_speed: 15;
    cargo_capacity: 60;
    weight: 23 ton;
    length: 7;
    visual_effect_and_powered: disable_visual_effect_and_powered();
    bitmask_vehicle_info: 0;
  }
  graphics {
    default: tanker_15_897_sprites;
    purchase: tanker_15_897_purchase_sprites;
    running_cost_factor: tanker_15_897_running_cost_factor;
    additional_text: return string(STR_TANKER_15_897_PURCHASE_HINT);
    cargo_subtype_text: tanker_15_897_cargo_subtype_text;
    cargo_capacity: tanker_15_897_cargo_capacity;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: tanker_15_897_colour_mapping;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, tanker_15_897) {
    property { name: string(STR_LONGNAME_TANKER_15_897); }
  }
}

