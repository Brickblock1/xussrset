 // tanker_15_c863

#define IMAGEFILE  "src/freight/tankers/15-c863.png"
purchase_sprites(tanker_15_c863, 0, 0)

tanker_layout_template(tanker_15_c863_v1, 7, 0,  39)
tanker_layout_template(tanker_15_c863_v2, 7, 0,  88)
tanker_layout_template(tanker_15_c863_v3, 7, 0, 137)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_15_c863_PETR_random, 0 )
{
  independent: tanker_15_c863_v1_sprites_start;
  2: tanker_15_c863_v1_sprites_start;
  3: tanker_15_c863_v2_sprites_start;
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_PETR_sprites,
  cargo_subtype)
{
  0: tanker_15_c863_PETR_random;
  1: tanker_15_c863_v1_sprites_start;
  2: tanker_15_c863_v2_sprites_start;
     align_7_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_OIL_sprites,
  cargo_subtype)
{
  0: tanker_15_c863_v3_sprites_start;
     align_7_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_other_sprites,
  cargo_subtype)
{
  0: tanker_15_c863_v1_sprites_start;
     align_7_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_sprites_start,
  cargo_type_in_veh)
{
  OIL_: tanker_15_c863_OIL_sprites;
  PETR: tanker_15_c863_PETR_sprites;
        tanker_15_c863_other_sprites;
}

long_vehicle_RND(tanker_15_c863, tanker_15_c863_v1_sprites_start)

switch (FEAT_TRAINS, SELF, tanker_15_c863_PETR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_26);
  2: return string(STR_REFIT_TTD_39);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_cargo_subtype_text,

    (cargo_type_in_veh == OIL_) +
    (cargo_type_in_veh == PETR)*2 )
{

  1: return string(STR_REFIT_TTD_3F);
  2: tanker_15_c863_PETR_cargo_subtype_text;
  string(STR_REFIT_TTD_08);
}

switch (FEAT_TRAINS, SELF, tanker_15_c863_cargo_capacity,
[  STORE_TEMP(60, 0), // т
   STORE_TEMP(60, 1), // м³
   STORE_TEMP(0, 2), // м² 
   STORE_TEMP(0, 3), // палетомест 
   STORE_TEMP(7, 4), // шаблон
   cargo_type_in_veh])
{
  tank_cargo_capacity;
}    

wagon_running_cost(tanker_15_c863, 20)
hint_wagon(tanker_15_c863,
           60, 60,
           STR_PURCHASE_HINT_LOADING_SPEED_4TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_TANK,
           1960, 1971,
           STR_PURCHASE_HINT_FACTORY_AZOVMASH_ZHDANOV)

item (FEAT_TRAINS, tanker_15_c863, 271) {
  property {
    name: string(STR_NAME_TANKER_15_C863);
    vehicle_dates(1960, 1971, 32, 10, 6, 3)
    vehicle_wagon(23, 120)
    vehicle_wagon_cargo(60, 15)
    cargo_allow_refit: [cargo_all_oil_products_tanker()];
  }
  graphics {
    default: tanker_15_c863_sprites;
    purchase: tanker_15_c863_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: tanker_15_c863_running_cost_factor;
    cargo_capacity: tanker_15_c863_cargo_capacity;
    cargo_subtype_text: tanker_15_c863_cargo_subtype_text;
    refit_cost: tanker_refit_cost;
    colour_mapping: any_cc_colour;
    additional_text: tanker_15_c863_additional_text;
  }
}

long_name_template(tanker_15_c863, STR_LONGNAME_TANKER_15_C863)
