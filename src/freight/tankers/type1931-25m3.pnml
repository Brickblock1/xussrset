 // tanker_of_1931_25m3
// 287 (11Fh)

#define IMAGEFILE  "src/freight/tankers/type1931-25m3.png"
purchase_sprites(tanker_of_1931_25m3, 0, 0)

tanker_layout_template(tanker_of_1931_25m3_v1, 5, 0,  30)
tanker_layout_template(tanker_of_1931_25m3_v2, 5, 0,  69)
tanker_layout_template(tanker_of_1931_25m3_v3, 5, 0, 108)
tanker_layout_template(tanker_of_1931_25m3_v4, 5, 0, 147)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_random, 0 )
{
  independent: tanker_of_1931_25m3_v1_sprites;
  2: tanker_of_1931_25m3_v1_sprites;
  3: tanker_of_1931_25m3_v2_sprites;
}  

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_random, 0 )
{
  dependent: tanker_of_1931_25m3_PETR_random;
  3: tanker_of_1931_25m3_v2_sprites;
  2: tanker_of_1931_25m3_v3_sprites;
}  

random_switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_random, 0 )
{
  dependent: tanker_of_1931_25m3_PETR_random;
  2: tanker_of_1931_25m3_v1_sprites;
  3: tanker_of_1931_25m3_v4_sprites;
}  

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_OIL_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_v3_sprites;
  align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_PETR_random;
  1: tanker_of_1931_25m3_v1_sprites;
  2: tanker_of_1931_25m3_v2_sprites;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_RFPR_random;
  1: tanker_of_1931_25m3_v2_sprites;
  2: tanker_of_1931_25m3_v3_sprites;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_WATR_random;
  1: tanker_of_1931_25m3_v1_sprites;
  2: tanker_of_1931_25m3_v4_sprites;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_DYES_RUBR_sprites,
  cargo_subtype)
{
  0: tanker_of_1931_25m3_v4_sprites;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_sprites_start, 
  cargo_type_in_veh) 
{
  OIL_: tanker_of_1931_25m3_OIL_sprites;
  PETR: tanker_of_1931_25m3_PETR_sprites;
  RFPR: tanker_of_1931_25m3_RFPR_sprites;
  WATR: tanker_of_1931_25m3_WATR_sprites;
        tanker_of_1931_25m3_DYES_RUBR_sprites;
}

short_vehicle_RND(tanker_of_1931_25m3, tanker_of_1931_25m3_v1_sprites)

wagon_running_cost(tanker_of_1931_25m3, 5)

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_cargo_capacity, cargo_type_in_veh)
{
  OIL_: return 23;
  PETR: return 20;
  RFPR: return 23;
  RUBR: return 24;
  DYES: return 25;
  WATR: return 25;
  COLA: return 25;
  PLAS: return 25;
  PLST: return 25;
        return 25;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_PETR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_26);
  2: return string(STR_REFIT_TTD_39);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_RFPR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_39);
  2: return string(STR_REFIT_TTD_3F);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_WATR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_26);
  2: return string(STR_REFIT_TTD_08);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_of_1931_25m3_cargo_subtype_text, 
  cargo_type_in_veh) 
{
  OIL_: string(STR_REFIT_TTD_3F);
  PETR: tanker_of_1931_25m3_PETR_cargo_subtype_text;
  RFPR: tanker_of_1931_25m3_RFPR_cargo_subtype_text;
  WATR: tanker_of_1931_25m3_WATR_cargo_subtype_text;
        string(STR_REFIT_TTD_08);
}

item (FEAT_TRAINS, tanker_of_1931_25m3, 287) {
  property {
    name: string(STR_NAME_TANKER_ISSUE_OF_1931_25M3);
    vehicle_dates(1931, 1937, 32, 10, 5, 1)
    vehicle_wagon(11, 120)
    vehicle_wagon_cargo(25, 9)
    cargo_allow_refit: [OIL_, PETR, RFPR, RUBR, DYES, WATR, COLA, PLAS, PLST];
  }
  graphics {
    default: tanker_of_1931_25m3_sprites;
    purchase: tanker_of_1931_25m3_purchase_sprites;
    running_cost_factor: tanker_of_1931_25m3_running_cost_factor;
    additional_text: return string(STR_PURCHASE_HINT_WAGON,
                                   25, 25,
                                   string(STR_PURCHASE_HINT_LOADING_SPEED_3TIC),
                                   string(STR_PURCHASE_HINT_AGING_PERIOD_3_1), 
                                   string(STR_PURCHASE_HINT_WAGON_TYPE_TANK_2AXLE),
                                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1937), 
                                   string(STR_PURCHASE_HINT_FACTORY_AZOVMASH_OLD));
    cargo_capacity: tanker_of_1931_25m3_cargo_capacity;
    cargo_subtype_text: tanker_of_1931_25m3_cargo_subtype_text;
    refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    colour_mapping: any_cc_colour;
  }
}

long_name_template(tanker_of_1931_25m3, STR_LONGNAME_TANKER_ISSUE_OF_1931_25M3)
