 // tanker_type1945

#define IMAGEFILE  "src/freight/tankers/type1945.png"
purchase_sprites(tanker_type1945, 0, 0)

tanker_layout_template(tanker_type1945_v1, 5, 0,  30)
tanker_layout_template(tanker_type1945_v2, 5, 0,  69)
tanker_layout_template(tanker_type1945_v3, 5, 0, 108)
tanker_layout_template(tanker_type1945_v4, 5, 0, 147)
#undef IMAGEFILE

random_switch (FEAT_TRAINS, SELF, tanker_type1945_PETR_random, 0 )
{
  independent: tanker_type1945_v1_sprites_start;
  2: tanker_type1945_v1_sprites_start;
  3: tanker_type1945_v2_sprites_start;
}

random_switch (FEAT_TRAINS, SELF, tanker_type1945_RFPR_random, 0 )
{
  dependent: tanker_type1945_PETR_random;
  3: tanker_type1945_v2_sprites_start;
  2: tanker_type1945_v3_sprites_start;
}

random_switch (FEAT_TRAINS, SELF, tanker_type1945_WATR_random, 0 )
{
  dependent: tanker_type1945_PETR_random;
  2: tanker_type1945_v1_sprites_start;
  3: tanker_type1945_v4_sprites_start;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_OIL_sprites,
  cargo_subtype)
{
  0: tanker_type1945_v3_sprites_start;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_PETR_sprites,
  cargo_subtype)
{
  0: tanker_type1945_PETR_random;
  1: tanker_type1945_v1_sprites_start;
  2: tanker_type1945_v2_sprites_start;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_RFPR_sprites,
  cargo_subtype)
{
  0: tanker_type1945_RFPR_random;
  1: tanker_type1945_v2_sprites_start;
  2: tanker_type1945_v3_sprites_start;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_WATR_sprites,
  cargo_subtype)
{
  0: tanker_type1945_WATR_random;
  1: tanker_type1945_v1_sprites_start;
  2: tanker_type1945_v4_sprites_start;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_DYES_RUBR_sprites,
  cargo_subtype)
{
  0: tanker_type1945_v4_sprites_start;
     align_5_sprites;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_sprites_start,
  cargo_type_in_veh)
{
  OIL_: tanker_type1945_OIL_sprites;
  PETR: tanker_type1945_PETR_sprites;
  RFPR: tanker_type1945_RFPR_sprites;
  WATR: tanker_type1945_WATR_sprites;
        tanker_type1945_DYES_RUBR_sprites;
}

long_vehicle_RND(tanker_type1945, tanker_type1945_v1_sprites_start)

switch (FEAT_TRAINS, SELF, tanker_type1945_PETR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_26);
  2: return string(STR_REFIT_TTD_39);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_RFPR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_39);
  2: return string(STR_REFIT_TTD_3F);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_WATR_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_26);
  2: return string(STR_REFIT_TTD_08);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tanker_type1945_cargo_subtype_text,
  cargo_type_in_veh)
{
  OIL_: string(STR_REFIT_TTD_3F);
  PETR: tanker_type1945_PETR_cargo_subtype_text;
  RFPR: tanker_type1945_RFPR_cargo_subtype_text;
  WATR: tanker_type1945_WATR_cargo_subtype_text;
        string(STR_REFIT_TTD_08);
}

switch (FEAT_TRAINS, SELF, tanker_type1945_loading_speed,
[  STORE_TEMP(0, 0),
   STORE_TEMP(0, 1),
   STORE_TEMP(15, 2),
   STORE_TEMP(10, 3)])
{ all_wagons_tanker_loading_speed; }

switch (FEAT_TRAINS, SELF, tanker_type1945_cargo_capacity,
[  STORE_TEMP(25, 0), // т
   STORE_TEMP(25, 1), // м³
   STORE_TEMP(0, 2), // м²
   STORE_TEMP(0, 3), // палетомест
   STORE_TEMP(5, 4)])// шаблон
{ tank_cargo_capacity; }

switch (FEAT_TRAINS, SELF, tanker_type1945_running_cost_factor,
[  STORE_TEMP(0, 0),       // Моторы
   STORE_TEMP(0, 1),       // Бригада
   STORE_TEMP(3, 2),       // Износ
   STORE_TEMP(0, 3),       // Сопровождение
   STORE_TEMP(2, 4),       // ТО
   STORE_TEMP(1, 5),       // Сертификация

   STORE_TEMP(120, 6),     // Скорость
   STORE_TEMP(13, 7),      // Тара
   STORE_TEMP(13+25, 8)])  // Максимальная масса
{ all_running_cost_factor; } // 5

hint_wagon_with_uspeed_changeable_factory_name(tanker_type1945,
           25, 25,
           STR_PURCHASE_HINT_LOADING_SPEED_3TIC, STR_PURCHASE_HINT_LOADING_SPEED_3TIC,
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_TANK,
           1945, 1948,
           fact_azovmach())

item (FEAT_TRAINS, tanker_type1945, 288) {
  property {
    name: string(STR_NAME_TANKER_ISSUE_OF_1945);
    vehicle_dates(1945, 1948, 32, 10, 4, 1)
    vehicle_wagon(13.5, 120)
    cargo_allow_refit: [cargo_all_tanker()];
  }
  graphics {
    default: tanker_type1945_sprites;
    purchase: tanker_type1945_purchase_sprites;
    articulated_part: articulated_part_dummy1;
    running_cost_factor: tanker_type1945_running_cost_factor;
    cargo_capacity: tanker_type1945_cargo_capacity;
    loading_speed: tanker_type1945_loading_speed;
    cargo_subtype_text: tanker_type1945_cargo_subtype_text;
    refit_cost: tanker_refit_cost;
    colour_mapping: any_cc_colour;
    additional_text: tanker_type1945_additional_text;
  }
}
