 // l

// Спрайты

#define IMAGEFILE  "src/l/l-lp.png"

// покупка
spriteset (l_e_purchase_spriteset, IMAGEFILE)
{
  s8_purchase_template(0, 2, 0)
}

// паровоз, анимация из 4-х фреймов
spriteset (l_e_1_spriteset, IMAGEFILE)
{
  s8_template(0, 2, 0)
} 

spriteset (l_e_2_spriteset, IMAGEFILE)
{
  s8_template(0, 40, 0)
} 

spriteset (l_e_3_spriteset, IMAGEFILE)
{
  s8_template(0, 78, 0)
} 

spriteset (l_e_4_spriteset, IMAGEFILE)
{
  s8_template(0, 116, 0)
} 

// тендер

spriteset (l_t_spriteset, IMAGEFILE)
{
  s5l_template(0, 173+4, 0) //y+4
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, l_e_get_spriteset,
  motion_counter & 3)
{
  0: l_e_1_spriteset;
  1: l_e_2_spriteset;
  2: l_e_3_spriteset;
  l_e_4_spriteset;
}

// Присоединяем
switch (FEAT_TRAINS, SELF, l_e_can_attach_wagon,
  vehicle_type_id)
{
  return CB_RESULT_ATTACH_ALLOW;
//  return CB_RESULT_ATTACH_DISALLOW;
}

// С 1920 года увеличиваем скорость
switch (FEAT_TRAINS, SELF, l_e_speed,
  current_year >= 1920
)
{
  1: return 120;
  return 105;
}


item (FEAT_TRAINS, l_e) 
{
  property
  {
    name: string(STR_L_NAME);
    climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
    introduction_date: date(1914, 1, 1);
    model_life: 40;
    vehicle_life: 40; // срок службы
    reliability_decay: 20;
    cost_factor: 2;
    running_cost_factor: 10;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 105 km/h;
    misc_flags: bitmask(TRAIN_FLAG_MU);
    refit_cost: 0;
    track_type: RAIL;
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1500 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    dual_headed: 0;
    cargo_capacity: 0; // пассажиров
    weight: (97+74) ton;
    ai_engine_rank: 0;
    engine_class: ENGINE_CLASS_STEAM;
    extra_power_per_wagon: 0 kW;
    tractive_effort_coefficient: 0.05967299;
    air_drag_coefficient: 0.06;
    shorten_vehicle: SHORTEN_TO_8_8;
    extra_weight_per_wagon: 0;
    bitmask_vehicle_info: 0;
    visual_effect_and_powered:
      visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, DISABLE_WAGON_POWER);
  }

  graphics {
    default: l_e_get_spriteset;
    purchase: l_e_purchase_spriteset;
    articulated_part: articulated_part_tender;
    can_attach_wagon: l_e_can_attach_wagon;
    additional_text: return string(STR_L_PURCHASE_HINT);
    speed: l_e_speed;
  }

  livery_override(tender)
  {
    shorten_vehicle: return SHORTEN_TO_5_8;
//    weight: return 74;
    l_t_spriteset;
  }

}

