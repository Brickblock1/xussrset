 // 9p

// Спрайты

#define IMAGEFILE  "src/steam/9p.png"

// покупка
spriteset (_9p_purchase_spriteset, IMAGEFILE)
{
  s6_purchase_template(0, 0, 4)
}

// паровоз, анимация из 4-х фреймов
spriteset (_9p_1_spriteset, IMAGEFILE)
{
  s6_template(0, 0, 0)
} 

spriteset (_9p_2_spriteset, IMAGEFILE)
{
  s6_template(0, 42, 0)
} 

spriteset (_9p_3_spriteset, IMAGEFILE)
{
  s6_template(0, 84, 0)
} 

spriteset (_9p_4_spriteset, IMAGEFILE)
{
  s6_template(0, 126, 0)
} 

// для флага FLIP
spriteset (_9p_flipped_1_spriteset, IMAGEFILE)
{
  s6_template(0, 0, -2)
} 

spriteset (_9p_flipped_2_spriteset, IMAGEFILE)
{
  s6_template(0, 42, -2)
} 

spriteset (_9p_flipped_3_spriteset, IMAGEFILE)
{
  s6_template(0, 84, -2)
} 

spriteset (_9p_flipped_4_spriteset, IMAGEFILE)
{
  s6_template(0, 126, -2)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, _9p_get_spriteset2,
  last_computed_result +
  (is_flipped() * 4)
)
{
  0: _9p_1_spriteset;
  1: _9p_2_spriteset;
  2: _9p_3_spriteset;
  3: _9p_4_spriteset;
  4: _9p_flipped_4_spriteset;
  5: _9p_flipped_3_spriteset;
  6: _9p_flipped_2_spriteset;
  7: _9p_flipped_1_spriteset;
  align_6_spriteset;
}

switch (FEAT_TRAINS, PARENT, _9p_get_spriteset,
  motion_counter & 3)
{
  _9p_get_spriteset2;
}

// Присоединяем
switch (FEAT_TRAINS, SELF, _9p_can_attach_wagon,
  vehicle_type_id)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Маневровый всегда штрафует других по скорости
switch (FEAT_TRAINS, SELF, _9p_bitmask_vehicle_info,
  0)
{
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, _9p) 
{
  property
  {
    name: string(STR_9P_NAME);
    climates_available: get_climates_available();
    introduction_date: date(1935, 1, 1);
    model_life: 30;
    vehicle_life: 30; // срок службы
    reliability_decay: 20;
    cost_factor: 2;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    speed: 35 km/h;
    misc_flags: bitmask(TRAIN_FLAG_FLIP);
    track_type: get_normal_track_type();
    power: 320 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: 55 ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: 0.15398886;
    length: 6;
    visual_effect_and_powered: steam_visual_effect_and_powered(-4);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }

  graphics {
    default: _9p_get_spriteset;
    purchase: _9p_purchase_spriteset;
    can_attach_wagon: _9p_can_attach_wagon;
    additional_text: return string(STR_9P_PURCHASE_HINT);
    bitmask_vehicle_info: _9p_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }

}

