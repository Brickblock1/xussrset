 // e

#define IMAGEFILE  "src/steam/e.png"
purchase_sprites(e_all_v1, 0, 0)
purchase_sprites(e_all_v2, 110, 0)
purchase_sprites(e_all_v3, 220, 0)
purchase_sprites(e_all_v4, 330, 0)

steam_sprites(7, e_all_v1, 32,   40)
steam_sprites(7, e_all_v2, 32,  844)
steam_sprites(7, e_all_v3, 32, 1648)
steam_sprites(7, e_all_v4, 32, 2452)

tender_sprites(5, e_all_t_v1, 447,  40) 
tender_sprites(6, e_all_t_v2, 447, 107)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, e_all_purchase_sprites,
  (build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1935))
{
  0: e_all_v1_purchase_sprites;
  1: e_all_v2_purchase_sprites;
  2: e_all_v3_purchase_sprites;
     e_all_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, e_all_tender_sprites,
  build_year >= 1948)
{
  0: e_all_t_v1_sprites_left;
     e_all_t_v2_sprites_left;
}

switch (FEAT_TRAINS, PARENT, e_e_sprites_start,
  ((build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1935)) * (models_details_level == 0))
{
  0: e_all_v1_sprites_left;
  1: e_all_v2_sprites_left;
  2: e_all_v3_sprites_left;
     e_all_v4_sprites_left;
}

short_vehicle(e_e)

switch (FEAT_TRAINS, SELF, e_all_create_effect, 
  steam_create_effect(-3, 12)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, e_all_weight,
  (build_year >= 1920) +
  (build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1933) +
  (build_year >= 1934) +
  (build_year >= 1935) +
  (build_year >= 1946) +
  (build_year >= 1948) )
{
  0: return (78+64); // Э
  1: return (82+64);
  2: return (86+72); // Эу
  3: return (85+68); // Эм
  4: return (78+68);
  5: return (83+68);
  6: return (84+68); // Эр
  7: return (85+68); 
     return (85+82); // новый тендр 
}

switch (FEAT_TRAINS, SELF, e_all_tractive_effort_coefficient,
  (build_year >= 1913) +
  (build_year >= 1916) +
  (build_year >= 1920) +
  (build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1933) +
  (build_year >= 1934) +
  (build_year >= 1935) +
  (build_year >= 1946) +
  (build_year >= 1948) )
{
  0: return get_tec_int(127.0, 78+64); // Э
  1: return get_tec_int(140.0, 78+64);
  2: return get_tec_int(149.0, 78+64);
  3: return get_tec_int(149.0, 82+64);
  4: return get_tec_int(159.0, 86+72); // Эу
  5: return get_tec_int(158.0, 85+68); // Эм
  6: return get_tec_int(145.0, 78+68); 
  7: return get_tec_int(154.0, 83+68); 
  8: return get_tec_int(186.0, 84+68); // Эр
  9: return get_tec_int(186.0, 85+68);                
     return get_tec_int(186.0, 85+82); // новый тендр   
}

switch (FEAT_TRAINS, SELF, e_all_power,
  (build_year >= 1916) +
  (build_year >= 1920) +
  (build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1935))
{
  0: return get_power_in_cb(780);  // Э
  1: return get_power_in_cb(790);  
  2: return get_power_in_cb(1000); 
  3: return get_power_in_cb(1250); // Эу
  4: return get_power_in_cb(1200); // Эм
     return get_power_in_cb(1370); // Эр
}

switch (FEAT_TRAINS, SELF, e_all_tender_length,
  build_year >= 1948)
{
  0: return 4;
     return 5;
}

switch (FEAT_TRAINS, SELF, e_all_cargo_subtype_text,
  (build_year >= 1926) +
  (build_year >= 1931) +
  (build_year >= 1935))
{
  0: return string(STR_REFIT_FACTORY_E);
  1: return string(STR_REFIT_FACTORY_EU);
  2: return string(STR_REFIT_FACTORY_EM);
  3: return string(STR_REFIT_FACTORY_ER);
     return CB_RESULT_NO_TEXT;
}

 // e

switch (FEAT_TRAINS, SELF, e_e_weight,
  build_year >= 1920)
{
  0: return (78+64);
     return (82+64);
}

switch (FEAT_TRAINS, SELF, e_e_tractive_effort_coefficient,
  (build_year >= 1913) +
  (build_year >= 1916) +
  (build_year >= 1920))
{
  0: return get_tec_int(127.0, 78+64);
  1: return get_tec_int(140.0, 78+64);
  2: return get_tec_int(149.0, 78+64);
     return get_tec_int(149.0, 82+64);
}

switch (FEAT_TRAINS, SELF, e_e_power2,
  build_year >= 1916)
{
  0: return 780;  
     return 790;  
}

switch (FEAT_TRAINS, SELF, e_e_power,
  (current_year >= 1920) +
  (current_year >= 1936))
{
  0: e_e_power2;  
  1: return get_power_in_cb(1000); 
     return get_power_in_cb(1100);
}

engine_speed_change_after(e_e, 55, 65, 1936)

engine_penalise_speed0(e_e)

general_running_cost(e_all, 183)

item (FEAT_TRAINS, e_e) {
  property {
    name: string(STR_E_NAME);
    climates_available: get_climates_available(); 
    introduction_date: date(get_loc_year(1912), 1, 1);
    model_life: get_model_life(1912,1926);
    retire_early: get_retire_early(1912,1926);
    vehicle_life: 40;
    reliability_decay: 20;
    cost_factor: 8;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 780 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (78+64) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(127.0, 78+64);
    length: 6;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: e_e_sprites;
    purchase: e_all_v1_purchase_sprites;
    articulated_part: articulated_part_tender1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_E_PURCHASE_HINT);
    speed: e_e_speed;
    purchase_speed: e_e_purchase_speed;
    bitmask_vehicle_info: e_e_bitmask_vehicle_info;
    cargo_capacity: return 0;
    power: e_e_power;
    tractive_effort_coefficient: e_e_tractive_effort_coefficient;
    weight: e_e_weight;
    create_effect: e_all_create_effect;
    running_cost_factor: e_all_running_cost_factor;
    purchase_running_cost_factor: e_all_purchase_running_cost_factor;
  }
  livery_override(unitb) {
    length: return 4;
            e_all_t_v1_sprites_left;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, e_e) {
    property { 
      name: string(STR_E_NAME_LONG); }
  }
}

if (models_details_level == 0) {
  item (FEAT_TRAINS, e_e) {
    property { 
      name: string(STR_E_ALL_NAME);
      model_life: get_model_life(1912,1957);
      retire_early: get_retire_early(1912,1957);
    }
    graphics {
      default: e_e_sprites;
      purchase: e_all_purchase_sprites;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: any_start_stop;
      additional_text: return string(STR_E_ALL_PURCHASE_HINT);
      speed: e_e_speed;
      purchase_speed: e_e_purchase_speed;
      bitmask_vehicle_info: e_e_bitmask_vehicle_info;
      cargo_capacity: return 0;
      create_effect: e_all_create_effect;
      running_cost_factor: e_all_running_cost_factor;
      purchase_running_cost_factor: e_all_purchase_running_cost_factor;
      articulated_part: articulated_part_tender1;
      power: e_all_power;
      tractive_effort_coefficient: e_all_tractive_effort_coefficient;
      weight: e_all_weight;
      cargo_subtype_text: e_all_cargo_subtype_text;
    }
    livery_override(unitb) {
      length: e_all_tender_length;
              e_all_tender_sprites;
    }
  }
  if (enable_long_names) {
    item (FEAT_TRAINS, e_e) {
      property { 
        name: string(STR_E_ALL_NAME_LONG); }
    }
  }
}

 // eu

switch (FEAT_TRAINS, PARENT, e_eu_sprites_start, 0)
{
  e_all_v2_sprites_left;
}

short_vehicle(e_eu)

switch (FEAT_TRAINS, SELF, e_eu_power,
  (current_year >= 1936))
{
  0: return get_power_in_cb(1250);
     return get_power_in_cb(1320);
}

engine_speed_change_after(e_eu, 55, 65, 1936)

engine_penalise_speed0(e_eu)

general_running_cost(e_eu, 269)

item (FEAT_TRAINS, e_eu) {
  property {
    name: string(STR_EU_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1926), 1, 1);
    model_life: get_model_life(1926,1931);
    retire_early: get_retire_early(1926,1931);
    vehicle_life: 30;
    reliability_decay: 20;
    cost_factor: 12;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1250 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (86+72) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(159.0, 86+72);
    length: 6;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }
  graphics {
    default: e_eu_sprites;
    purchase: e_all_v2_purchase_sprites;
    articulated_part: articulated_part_tender1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_EU_PURCHASE_HINT);
    speed: e_eu_speed;
    purchase_speed: e_eu_purchase_speed;
    bitmask_vehicle_info: e_eu_bitmask_vehicle_info;
    cargo_capacity: return 0;
    power: e_eu_power;
    create_effect: e_all_create_effect;
    running_cost_factor: e_eu_running_cost_factor;
    purchase_running_cost_factor: e_eu_purchase_running_cost_factor;
  }
  livery_override(unitb) {
    length: return 4;
            e_all_t_v1_sprites_left;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, e_eu) {
    property { 
      name: string(STR_EU_NAME_LONG); }
  }
}

if ((models_details_level == 1) || (models_details_level == 2)) {
  item (FEAT_TRAINS, e_eu) {
    property { climates_available: get_climates_available(); }
  }
}

 // em

switch (FEAT_TRAINS, PARENT, e_em_sprites_start, 0)
{
  e_all_v3_sprites_left;
}

short_vehicle(e_em)

switch (FEAT_TRAINS, SELF, e_em_weight,
  (build_year >= 1933) +
  (build_year >= 1934))
{
  0: return (85+68);
  1: return (78+68);
     return (83+68);
}

switch (FEAT_TRAINS, SELF, e_em_tractive_effort_coefficient,
  (build_year >= 1933) +
  (build_year >= 1934))
{
  0: return get_tec_int(158.0, 85+68);
  1: return get_tec_int(145.0, 78+68);
     return get_tec_int(154.0, 83+68);
}

switch (FEAT_TRAINS, SELF, e_em_power,
  (build_year >= 1936))
{
  0: return get_power_in_cb(1200);
     return get_power_in_cb(1250);
}

engine_speed_change_after(e_em, 55, 65, 1936)

engine_penalise_speed0(e_em)

general_running_cost(e_em, 259)

item (FEAT_TRAINS, e_em) {
  property {
    name: string(STR_EM_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1931), 1, 1);
    model_life: get_model_life(1931,1935);
    retire_early: get_retire_early(1931,1935);
    vehicle_life: 30;
    reliability_decay: 20;
    cost_factor: 11;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1200 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (85+68) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(158.0, 85+68);
    length: 6;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: e_em_sprites;
    purchase: e_all_v3_purchase_sprites;
    articulated_part: articulated_part_tender1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_EM_PURCHASE_HINT);
    speed: e_em_speed;
    purchase_speed: e_em_purchase_speed;
    bitmask_vehicle_info: e_em_bitmask_vehicle_info;
    cargo_capacity: return 0;
    power: e_em_power;
    tractive_effort_coefficient: e_em_tractive_effort_coefficient;
    weight: e_em_weight;
    create_effect: e_all_create_effect;
    running_cost_factor: e_em_running_cost_factor;
    purchase_running_cost_factor: e_em_purchase_running_cost_factor;
  }
  livery_override(unitb) {
    length: return 4;
            e_all_t_v1_sprites_left;
  }

}

if (enable_long_names) {
  item (FEAT_TRAINS, e_em) {
    property { 
      name: string(STR_EM_NAME_LONG); }
  }
}

if (models_details_level == 1) {
  item (FEAT_TRAINS, e_em) {
    property { 
      climates_available: get_climates_available(); }
  }
}

 // er

switch (FEAT_TRAINS, PARENT, e_er_sprites_start, 0)
{
  e_all_v4_sprites_left;
}

short_vehicle(e_er)

switch (FEAT_TRAINS, SELF, e_er_weight,
  (build_year >= 1946) +
  (build_year >= 1948))
{
  0: return (84+68);
  1: return (85+68);
     return (85+82);
}

switch (FEAT_TRAINS, SELF, e_er_tractive_effort_coefficient,
  build_year >= 1948)
{
  0: return get_tec_int(186.0, 84+68);
  1: return get_tec_int(186.0, 85+68);
     return get_tec_int(186.0, 85+82);
}

engine_speed_change_after(e_er, 55, 65, 1936)

engine_penalise_speed0(e_er)

general_running_cost(e_er, 300)

item (FEAT_TRAINS, e_er) {
  property {
    name: string(STR_ER_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1933), 1, 1);
    model_life: get_model_life(1933,1957);
    retire_early: get_retire_early(1933,1957);
    vehicle_life: 40;
    reliability_decay: 20;
    cost_factor: 13;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1370 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (84+82) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(186.0, 84+68);
    length: 6;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: e_er_sprites;
    purchase: e_all_v4_purchase_sprites;
    articulated_part: articulated_part_tender1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_ER_PURCHASE_HINT);
    tractive_effort_coefficient: e_er_tractive_effort_coefficient;
    speed: e_er_speed;
    purchase_speed: e_er_purchase_speed;
    bitmask_vehicle_info: e_er_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: e_er_weight;
    create_effect: e_all_create_effect;
    running_cost_factor: e_er_running_cost_factor;
    purchase_running_cost_factor: e_er_purchase_running_cost_factor;
  }
  livery_override(unitb) {
    length: e_all_tender_length;
            e_all_tender_sprites;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, e_er) {
    property { name: string(STR_ER_NAME_LONG); }
  }
}

if (models_details_level == 1) {
  item (FEAT_TRAINS, e_er) {
    property { climates_available: get_climates_available(); }
  }
}
