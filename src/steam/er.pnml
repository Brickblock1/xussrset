 // er

// Спрайты

#define IMAGEFILE  "src/steam/e.png"

// покупка
purchase_spriteset(er_e, 315, 60)

// паровоз, анимация из 4-х фреймов
spriteset (er_e_1_spriteset, IMAGEFILE)
{
  s7_template(3, 558, 0)
} 

spriteset (er_e_2_spriteset, IMAGEFILE)
{
  s7_template(3, 603, 0)
} 

spriteset (er_e_3_spriteset, IMAGEFILE)
{
  s7_template(3, 649, 0)
} 

spriteset (er_e_4_spriteset, IMAGEFILE)
{
  s7_template(3, 696, 0)
} 

// тендер

spriteset (er_t_spriteset, IMAGEFILE)
{
  s6_template(3, 778, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, PARENT, er_e_get_spriteset,
  motion_counter & 3)
{
  0: er_e_1_spriteset;
  1: er_e_2_spriteset;
  2: er_e_3_spriteset;
  er_e_4_spriteset;
}

switch (FEAT_TRAINS, SELF, er_e_weight,
  (build_year >= 1946))
{
  0: return (84+82);
  return (85+82);
}

// Макс. скорость: 55 км/ч до 1936, 65 км/ч после
#define MAX_SPEED current_year >= 1936 ? 65 : 55

switch (FEAT_TRAINS, SELF, er_e_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, er_e_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, er_e_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, SELF, er_e_articulated_part,
  extra_callback_info1)
{
  1: return dummy1;
  2: return unitb;
  3: return dummy1;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item (FEAT_TRAINS, er_e) 
{
  property
  {
    name: string(STR_ER_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1933), 1, 1);
    model_life: get_model_life(1933,1957);
    retire_early: get_retire_early(1933,1957);
    vehicle_life: 40; // срок службы
    reliability_decay: 20;
    cost_factor: 15;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1370 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (84+82) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(186.0, 84+82);
    length: 6;
    visual_effect_and_powered: steam_visual_effect_and_powered(-4);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }

  graphics {
    default: er_e_get_spriteset;
    purchase: er_e_purchase_spriteset;
    articulated_part: er_e_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_ER_PURCHASE_HINT);
    speed: er_e_speed;
    purchase_speed: er_e_purchase_speed;
    bitmask_vehicle_info: er_e_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
    weight: er_e_weight;
  }

  livery_override(unitb)
  {
    length: return 5;
    er_t_spriteset;
  }

}
