 // eu

// Спрайты

#define IMAGEFILE  "src/steam/e.png"

// покупка
purchase_sprites(eu_e, 315, 20)

// паровоз, анимация из 4-х фреймов
spriteset (eu_e_1_sprites, IMAGEFILE)
{
  s7_template(3, 190, 0)
} 

spriteset (eu_e_2_sprites, IMAGEFILE)
{
  s7_template(3, 235, 0)
} 

spriteset (eu_e_3_sprites, IMAGEFILE)
{
  s7_template(3, 281, 0)
} 

spriteset (eu_e_4_sprites, IMAGEFILE)
{
  s7_template(3, 328, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, PARENT, eu_e_sprites_2,
  LOAD_TEMP(3))
{
  0: eu_e_1_sprites;
  1: eu_e_2_sprites;
  2: eu_e_3_sprites;
     eu_e_4_sprites;
}

short_vehicle(eu_e)

switch (FEAT_TRAINS, SELF, eu_e_power,
  (build_year >= 1936))
{
  0: return 1250;
     return 1320;
}

engine_speed_change_after(eu_e, 55, 65, 1936)

engine_penalise_speed0(eu_e)

switch (FEAT_TRAINS, SELF, eu_e_articulated_part,
  extra_callback_info1)
{
  1: return dummy1;
  2: return unitb;
  3: return dummy1;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item (FEAT_TRAINS, eu_e) 
{
  property
  {
    name: string(STR_EU_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1926), 1, 1);
    model_life: get_model_life(1926,1931);
    retire_early: get_retire_early(1926,1931);
    vehicle_life: 30; // срок службы
    reliability_decay: 20;
    cost_factor: 15;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1250 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (86+72) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(159.0, 86+72);
    length: 6;
    visual_effect_and_powered: steam_visual_effect_and_powered(-4);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }

  graphics {
    default: eu_e_sprites;
    purchase: eu_e_purchase_sprites;
    articulated_part: eu_e_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_EU_PURCHASE_HINT);
    speed: eu_e_speed;
    purchase_speed: eu_e_purchase_speed;
    bitmask_vehicle_info: eu_e_bitmask_vehicle_info;
    cargo_capacity: return 0;
    power: eu_e_power;
  }

  livery_override(unitb)
  {
    length: return 4;
            e_t_sprites;
  }

}

if (enable_long_names)
{
  item (FEAT_TRAINS, eu_e)
  {
    property
    { name: string(STR_EU_NAME_LONG); }
  }
}

