 // fd20
// 283 (11Bh)

#define IMAGEFILE  "src/steam/fd.png"
purchase_sprites(fd20_v1, 0, 0)
purchase_sprites(fd20_v2, 110, 0)
purchase_sprites(fd20_v3, 220, 0)
purchase_sprites(fd20_v4, 330, 0)
purchase_sprites_select3(fd20, 1932, 1934, 1936)
steam_sprites(10, fd20_v1, 32,   40)
steam_sprites(10, fd20_v2, 32,  844)
steam_sprites(10, fd20_v3, 32, 1648)
tender_sprites(6, fd_tender_4o,  447,  40) 
tender_sprites(8, fd_tender_t17, 447, 107) 
tender_sprites(7, fd_tender_t6p, 447, 174) 
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, fd20_engine_sprites_start,
  (build_year >= 1932) +
  (build_year >= 1934))
{
  0: fd20_v1_sprites_start;
  1: fd20_v2_sprites_start;
     fd20_v3_sprites_start;
}

switch (FEAT_TRAINS, SELF, fd20_tender_sprites_start,
  (build_year >= 1932) +
  (build_year >= 1936))
{
  0: fd_tender_4o_sprites_start;
  1: fd_tender_t17_sprites_start;
     fd_tender_t6p_sprites_start;
}

steamer_direction_template(fd20, _sprites_start)
long_vehicle(fd20)

switch (FEAT_TRAINS, SELF, fd20_additional_text,
  current_year >= 1935)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
}

engine_tender(fd20)
engine_tender_length_change_build_after2(fd20, 8, 2, 5, 1,
                                               8, 2, 7, 1, 1932,
                                               8, 2, 6, 1, 1936)
engine_steam_1_effect(fd, -2, 13)
engine_weight_change_build_after2(fd20, 137+85, 
                                        137+123, 1932, 
                                        137+127, 1936)       
engine_TE_change_build_after2(fd20, get_tec_int(261.0, 137+85), 
                                    get_tec_int(261.0, 137+123), 1932, 
                                    get_tec_int(261.0, 137+127), 1936)       
engine_power_change_build_after(fd20, get_power_in_cb(2600), 
                                      get_power_in_cb(3140), 1936)       
engine_cost_change_build_after(fd20, 24, 
                                     28, 1932)
engine_RC_change_build_after2(fd20, 551, 
                                    646, 1932, 
                                    650, 1936)       
engine_speed_change_after2(fd20, 65, 
                                 85, 1936, 
                                 90, 1956)
engine_penalise_speed0(fd20)

item (FEAT_TRAINS, fd20, 283) {
  property {
    name: string(STR_NAME_FD20);
    vehicle_dates(1931, 1940, 25, 10)
    vehicle_cargo()
    vehicle_steam(2600, 137+85, 261.0)
    cost_factor: 24;
    length: 8;
  }
  graphics {
    default: fd20_sprites;
    purchase: fd20_purchase_sprites;
    articulated_part: fd20_articulated_part;
    length: fd20_length;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: fd20_additional_text;
    speed: fd20_speed;
    purchase_speed: fd20_purchase_speed;
    bitmask_vehicle_info: fd20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: fd20_weight;
    tractive_effort_coefficient: fd20_tractive_effort_coefficient;
    power: fd20_power;
    cost_factor: fd20_cost_factor;
    create_effect: fd_create_effect;
    running_cost_factor: fd20_running_cost_factor;
    colour_mapping: return ttdall_cc + 0xC9;
  }
}

long_name_template(fd20, STR_LONGNAME_FD20)
models_default_cargo_template(fd20)
allow_steamer(fd20)
