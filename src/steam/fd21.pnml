 // fd21

// Спрайты
// 1940-1942
// Товарные паровозы семейства ФД "Федя" (Феликс Дзержинский)
// - ФД20 (1931-1940) и ФД21 (1940-1942).

#define IMAGEFILE  "src/steam/fd.png"

// покупка
spriteset (fd21_purchase_spriteset_ico, IMAGEFILE)
{
  new_purchase_template(0, 80)
}

spriteset (fd21_purchase_spriteset_noico, IMAGEFILE)
{
  new_purchase_template_noico(0, 80)
}

switch (FEAT_TRAINS, SELF, fd21_purchase_spriteset, 
  enable_icons)
{
  1: fd21_purchase_spriteset_ico;
     fd21_purchase_spriteset_noico;
}

// паровоз
spriteset (fd21_1_spriteset, IMAGEFILE)
{
  s10_template(127, 909, 0)
} 

spriteset (fd21_2_spriteset, IMAGEFILE)
{
  s10_template(127, 976, 0)
} 

spriteset (fd21_3_spriteset, IMAGEFILE)
{
  s10_template(127, 1043, 0)
} 

spriteset (fd21_4_spriteset, IMAGEFILE)
{
  s10_template(127, 1110, 0)
} 

// реверс
spriteset (fd21_r_1_spriteset, IMAGEFILE)
{
  s10_template(587, 909, 0)
} 

spriteset (fd21_r_2_spriteset, IMAGEFILE)
{
  s10_template(587, 976, 0)
} 

spriteset (fd21_r_3_spriteset, IMAGEFILE)
{
  s10_template(587, 1043, 0)
} 

spriteset (fd21_r_4_spriteset, IMAGEFILE)
{
  s10_template(587, 1110, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, fd21_get_spriteset2,
  last_computed_result)
{
  0: fd21_1_spriteset;
  1: fd21_2_spriteset;
  2: fd21_3_spriteset;
  3: fd21_4_spriteset;
  align_10_spriteset;
}

switch (FEAT_TRAINS, PARENT, fd21_get_spriteset1,
  motion_counter & 3)
{
  fd21_get_spriteset2;
}

// Если рисуем на карте и следующий в цепочке вагон скрыт, то
// отдаём пустышку
switch (FEAT_TRAINS, SELF, fd21_get_spriteset,
  is_drawn_in_viewport() && is_hidden_at(1))
{
  1: dummy_spriteset;
  fd21_get_spriteset1;
}

// Макс. скорость: 85 км/ч, с 1956 90 км/ч
#define MAX_SPEED current_year >= 1956 ? 90 : 85

switch (FEAT_TRAINS, SELF, fd21_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, fd21_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

// не умеет СМЕ
switch (FEAT_TRAINS, SELF, fd21_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

switch (FEAT_TRAINS, SELF, fd21_articulated_part,
  extra_callback_info1)
{
  1: return dummy2;
  2: return unitb;
  3: return dummy1;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item (FEAT_TRAINS, fd21) 
{
  property
  {
    name: string(STR_FD21_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1940), 1, 1);
    model_life: get_model_life(1940,1942);
    retire_early: get_retire_early(1940,1942);
    vehicle_life: 25; // срок службы
    reliability_decay: 20;
    cost_factor: 43;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3100 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (135+127) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(225.0, 135+127);
    length: 8;
    visual_effect_and_powered: steam_visual_effect_and_powered(-2);
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
  }

  graphics {
    default: fd21_get_spriteset;
    purchase: fd21_purchase_spriteset;
    articulated_part: fd21_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_FD21_PURCHASE_HINT);
    speed: fd21_speed;
    purchase_speed: fd21_purchase_speed;
    bitmask_vehicle_info: fd21_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }

  livery_override(unitb)
  {
    length: return 6;
    fd20_t6p_spriteset;
  }

}
