 // fd_all

// 1931
// 1932-1933
// 1934-1940
// Товарные паровозы семейства ФД "Федя" (Феликс Дзержинский)
// - ФД20 (1931-1940) и ФД21 (1940-1942).

#define IMAGEFILE  "src/steam/fd.png"
purchase_sprites(fd_fd20_v1, 0, 0)
purchase_sprites(fd_fd20_v2, 110, 0)
purchase_sprites(fd_fd20_v3, 220, 0)
purchase_sprites(fd_fd20_v4, 330, 0)
purchase_sprites(fd_fd21, 440, 0)

steam_sprites(10, fd_fd20_v1, 32,   40)
steam_sprites(10, fd_fd20_v2, 32,  844)
steam_sprites(10, fd_fd20_v3, 32, 1648)
steam_sprites(10, fd_fd21, 32, 2452)

tender_sprites(6, fd_t_4o,  447,  40) // 1931
tender_sprites(8, fd_t_t17, 447, 107) // 1932-1935
tender_sprites(7, fd_t_t6p, 447, 174) // с 1936
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, fd_tender_sprites,
  (build_year >= 1932) +
  (build_year >= 1936))
{
  0: fd_t_4o_sprites_start;
  1: fd_t_t17_sprites_start;
     fd_t_t6p_sprites_start;
}

// fd20

switch (FEAT_TRAINS, SELF, fd_fd20_purchase_sprites,
  (current_year >= 1932) +
  (current_year >= 1934) +
  (current_year >= 1936))
{
  0: fd_fd20_v1_purchase_sprites;
  1: fd_fd20_v2_purchase_sprites;
  2: fd_fd20_v3_purchase_sprites;
     fd_fd20_v4_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, fd_fd20_all_sprites_start,
  (build_year >= 1932) +
  (build_year >= 1934))
{
  0: fd_fd20_v1_sprites_start;
  1: fd_fd20_v2_sprites_start;
     fd_fd20_v3_sprites_start;
}
  
switch (FEAT_TRAINS, SELF, fd_all_sprites_start,
  build_year >= 1940)
{
  0: fd_fd20_all_sprites_start;
     fd_fd21_sprites_start;
}

switch (FEAT_TRAINS, PARENT, fd_fd20_sprites_start, 
  models_details_level == 0)        
{
  0: fd_fd20_all_sprites_start;
     fd_all_sprites_start;
}

long_vehicle(fd_fd20)

switch (FEAT_TRAINS, SELF, fd_fd20_additional_text,
  current_year >= 1935)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
}

engine_steam_1_effect(fd, -2, 13)
engine_length_change_build_after2(fd_tender, 5, 7, 1932, 6, 1936)       
engine_weight_change_build_after2(fd_fd20, 137+85, 137+123, 1932, 137+127, 1936)       
engine_TE_change_build_after2(fd_fd20, get_tec_int(208.0, 137+85), get_tec_int(208.0, 137+123), 1932, get_tec_int(208.0, 137+127), 1936)       
engine_power_change_build_after(fd_fd20, 2600, 3000, 1936)       
engine_cost_change_build_after2(fd_fd20, 24, 27, 1932, 28, 1936)       
engine_RC_change_build_after2(fd_fd20, 551, 632, 1932, 636, 1936)       
engine_speed_change_after2(fd_fd20, 65, 85, 1936, 90, 1956)
engine_penalise_speed0(fd_fd20)

item (FEAT_TRAINS, fd_fd20) {
  property {
    name: string(STR_NAME_FD20);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1931), 1, 1);
    model_life: get_model_life(1931,1940);
    retire_early: get_retire_early(1931,1940);
    vehicle_life: 25;
    reliability_decay: 10;
    cost_factor: 24;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 2600 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (137+185) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(208.0, 137+85);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: fd_fd20_sprites;
    purchase: fd_fd20_purchase_sprites;
    articulated_part: articulated_part_tender2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: fd_fd20_additional_text;
    speed: fd_fd20_speed;
    purchase_speed: fd_fd20_purchase_speed;
    bitmask_vehicle_info: fd_fd20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: fd_fd20_weight;
    tractive_effort_coefficient: fd_fd20_tractive_effort_coefficient;
    power: fd_fd20_power;
    cost_factor: fd_fd20_cost_factor;
    create_effect: fd_create_effect;
    running_cost_factor: fd_fd20_running_cost_factor;
  }
  livery_override(unitb) {
    length: fd_tender_length;
            fd_tender_sprites;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, fd_fd20) {
    property { 
      name: string(STR_LONGNAME_FD20); }
  }
}

 // fd21

long_vehicle(fd_fd21)

general_running_cost(fd_fd21, 766)
engine_speed_change_after(fd_fd21, 85, 90, 1956)
engine_penalise_speed0(fd_fd21)

item (FEAT_TRAINS, fd_fd21) {
  property {
    name: string(STR_NAME_FD21);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1940), 1, 1);
    model_life: get_model_life(1940,1942);
    retire_early: get_retire_early(1940,1942);
    vehicle_life: 25;
    reliability_decay: 10;
    cost_factor: 33;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3100 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (135+127) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(225.0, 135+127);
    length: 8;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }

  graphics {
    default: fd_fd21_sprites;
    purchase: fd_fd21_purchase_sprites;
    articulated_part: articulated_part_tender2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1940, 1942), 
                                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
    speed: fd_fd21_speed;
    purchase_speed: fd_fd21_purchase_speed;
    bitmask_vehicle_info: fd_fd21_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: fd_create_effect;
    running_cost_factor: fd_fd21_running_cost_factor;
  }
  livery_override(unitb) {
    length: return 6;
    fd_t_t6p_sprites_start;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, fd_fd21) {
    property { name: string(STR_LONGNAME_FD21); }
  }
}

// FD all

switch (FEAT_TRAINS, SELF, fd_all_purchase_sprites,
  current_year >= 1940)
{
  0: fd_fd20_purchase_sprites;
     fd_fd21_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, fd_all_weight,
  build_year >= 1940)
{
  0: fd_fd20_weight;
     return (135+127);
}

switch (FEAT_TRAINS, SELF, fd_all_tractive_effort_coefficient,
  build_year >= 1940)
{
  0: fd_fd20_tractive_effort_coefficient;
     return get_tec_int(215.0, 135+127);
}

switch (FEAT_TRAINS, SELF, fd_all_power,
  build_year >= 1940)
{
  0: fd_fd20_power;
     return get_power_in_cb(3100);
}

switch (FEAT_TRAINS, SELF, fd_all_cost_factor,
  current_year >= 1940)
{
  0: fd_fd20_cost_factor;
     return 33;
}

switch (FEAT_TRAINS, SELF, fd_all_cargo_subtype_text,
  build_year >= 1940)
{
  0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_FD20), "");
  1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_FD21), "");
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, fd_all_running_cost_factor,
  build_year >= 1940)
{
  0: fd_fd20_running_cost_factor;
     fd_fd21_running_cost_factor;
}

switch (FEAT_TRAINS, SELF, fd_all_additional_text,
  (current_year >= 1935) +
  (current_year >= 1940))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_FD20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_FD20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1931, 1940), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_FD21)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1940, 1942), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
}

if (models_details_level == 0) {
  item (FEAT_TRAINS, fd_fd21) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, fd_fd20) {
    property {
      name: string(STR_NAME_FD_ALL);
      model_life: get_model_life(1931,1942);
      retire_early: get_retire_early(1931,1942);
    }
    graphics {
      default: fd_fd20_sprites;
      purchase: fd_all_purchase_sprites;
      articulated_part: articulated_part_tender2;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: any_start_stop;
      additional_text: fd_all_additional_text;
      speed: fd_fd20_speed;
      purchase_speed: fd_fd20_purchase_speed;
      bitmask_vehicle_info: fd_fd20_bitmask_vehicle_info;
      cargo_capacity: return 0;
      create_effect: fd_create_effect;
      running_cost_factor: fd_all_running_cost_factor;
      weight: fd_all_weight;
      tractive_effort_coefficient: fd_all_tractive_effort_coefficient;
      power: fd_all_power;
      cost_factor: fd_all_cost_factor;
      cargo_subtype_text: fd_all_cargo_subtype_text;
    }
  }
  if (enable_long_names) {
    item (FEAT_TRAINS, fd_fd20) {
      property { 
        name: string(STR_LONGNAME_FD_ALL); }
    }
  }
}
