 // is20

// Спрайты

#define IMAGEFILE  "src/steam/is.png"

// покупка
purchase_sprites(is20_1932, 0, 0)
purchase_sprites(is20_1936, 0, 20)

switch (FEAT_TRAINS, SELF, is20_purchase_sprites,
  (build_year >= 1936))
{
  1: is20_1936_purchase_sprites;
  is20_1932_purchase_sprites;
}

// паровоз, анимация из 4-х фреймов
spriteset (is20_1_spriteset, IMAGEFILE)
{
  s10_template(113, 61, 0)
} 

spriteset (is20_2_spriteset, IMAGEFILE)
{
  s10_template(113, 128, 0)
} 

spriteset (is20_3_spriteset, IMAGEFILE)
{
  s10_template(113, 195, 0)
} 

spriteset (is20_4_spriteset, IMAGEFILE)
{
  s10_template(113, 262, 0)
} 

//
spriteset (is20_1_v2_spriteset, IMAGEFILE)
{
  s10_template(113, 344, 0)
} 

spriteset (is20_2_v2_spriteset, IMAGEFILE)
{
  s10_template(113, 411, 0)
} 

spriteset (is20_3_v2_spriteset, IMAGEFILE)
{
  s10_template(113, 478, 0)
} 

spriteset (is20_4_v2_spriteset, IMAGEFILE)
{
  s10_template(113, 545, 0)
} 

//
spriteset (is20_1_v3_spriteset, IMAGEFILE)
{
  s10_template(113, 627, 0)
} 

spriteset (is20_2_v3_spriteset, IMAGEFILE)
{
  s10_template(113, 694, 0)
} 

spriteset (is20_3_v3_spriteset, IMAGEFILE)
{
  s10_template(113, 761, 0)
} 

spriteset (is20_4_v3_spriteset, IMAGEFILE)
{
  s10_template(113, 828, 0)
} 

// реверс : 553

// тендер
// 1932-1935
spriteset (is20_t4o_spriteset, IMAGEFILE)
{
  s6_template(113, 920, 0)
} 

spriteset (is20_t4o_v2_spriteset, IMAGEFILE)
{
  s6_template(113, 963, 0)
} 

spriteset (is20_t4o_v3_spriteset, IMAGEFILE)
{
  s6_template(113, 1006, 0)
} 

// с 1936
spriteset (is20_t6p_spriteset, IMAGEFILE)
{
  s7_template(113, 1074, 0)
} 

spriteset (is20_t6p_v2_spriteset, IMAGEFILE)
{
  s7_template(113, 1118, 0)
} 

spriteset (is20_t6p_v3_spriteset, IMAGEFILE)
{
  s7_template(113, 1162, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, is20_sprites_2,
  (cargo_subtype * 4) +
  LOAD_TEMP(3))
{
  0: is20_1_spriteset;
  1: is20_2_spriteset;
  2: is20_3_spriteset;
  3: is20_4_spriteset;
  4: is20_1_v2_spriteset;
  5: is20_2_v2_spriteset;
  6: is20_3_v2_spriteset;
  7: is20_4_v2_spriteset;
  8: is20_1_v3_spriteset;
  9: is20_2_v3_spriteset;
 10: is20_3_v3_spriteset;
 11: is20_4_v3_spriteset;
     align_10_sprites;
}

long_vehicle(is20)

engine_speed_change_after(is20, 110, 115, 1936)

engine_penalise_speed0(is20)

switch (FEAT_TRAINS, SELF, is20_articulated_part,
  extra_callback_info1)
{
  1: return dummy2;
  2: return unitb;
  3: return dummy1;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, is20_tender_sprites,
[
  set_offset_to(-2),
  prev_vehicle_cargo_subtype() + 
  ((build_year >= 1936) * 3) ])
{
  0: is20_t4o_spriteset;
  1: is20_t4o_v2_spriteset;
  2: is20_t4o_v3_spriteset;
  3: is20_t6p_spriteset;
  4: is20_t6p_v2_spriteset;
     is20_t6p_v3_spriteset;
}

switch (FEAT_TRAINS, SELF, is20_tender_length,
  build_year >= 1936)
{
  1: return 6;
     return 5;
}

switch (FEAT_TRAINS, SELF, is20_weight,
  build_year >= 1936)
{
  1: return (133+123);
     return (133+85);
}

// ТУ=ту/вес/9.8
switch (FEAT_TRAINS, SELF, is20_tractive_effort_coefficient,
  build_year >= 1936)
{
  0: return get_tec_int(151.0, 133+85);
     return get_tec_int(151.0, 137+127);
}

switch (FEAT_TRAINS, SELF, is20_cargo_subtype_text,
  cargo_subtype)
{
  0: string(STR_REFIT_TTD_53);
  1: string(STR_REFIT_TTD_59);
  2: string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, is20) 
{
  property
  {
    name: string(STR_IS20_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    model_life: get_model_life(1932,1941);
    retire_early: get_retire_early(1932,1941);
    vehicle_life: 30; // срок службы
    reliability_decay: 20;
    cost_factor: 23;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3200 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    engine_class: ENGINE_CLASS_STEAM;
    length: 8;
    visual_effect_and_powered: steam_visual_effect_and_powered(-2);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }

  graphics
  {
    default: is20_sprites;
    purchase: is20_purchase_sprites;
    articulated_part: is20_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_IS20_PURCHASE_HINT);
    speed: is20_speed;
    purchase_speed: is20_purchase_speed;
    bitmask_vehicle_info: is20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: is20_weight;
    cost_factor: return 44;
    cargo_subtype_text: is20_cargo_subtype_text;
    tractive_effort_coefficient: is20_tractive_effort_coefficient;
  }

  livery_override(unitb)
  {
    length: is20_tender_length;
    is20_tender_sprites;
  }

}

if (enable_long_names)
{
  item (FEAT_TRAINS, is20)
  {
    property
    { name: string(STR_IS20_NAME_LONG); }
  }
}

