 // is21

// Пассажирские паровозы семейства ИС (Иосиф Сталин): ИС21 (1941-1942).

engine_speed(is21, 115)

engine_penalise_speed0(is21)

switch (FEAT_TRAINS, SELF, is21_articulated_part,
  extra_callback_info1)
{
  1: return dummy2;
  2: return unitb;
  3: return dummy1;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, is21_tender_sprites,
[
  set_offset_to(-2),
  prev_vehicle_cargo_subtype() ])
{
  0: is_steamer_t6p_v1_sprites_left;
  1: is_steamer_t6p_v2_sprites_left;
  2: is_steamer_t6p_v3_sprites_left;
     align_7_sprites;
}

switch (FEAT_TRAINS, SELF, is21_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, is21) 
{
  property
  {
    name: string(STR_IS21_NAME);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1941), 1, 1);
    model_life: get_model_life(1941,1942);
    retire_early: get_retire_early(1941,1942);
    vehicle_life: 30;
    reliability_decay: 20;
    cost_factor: 56;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3300 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (133+127) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(155.0, 133+127);
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }

  graphics
  {
    default: is_steamer_sprites;
    purchase: is_steamer_purchase_sprites;
    articulated_part: is21_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_IS21_PURCHASE_HINT);
    speed: is21_speed;
    purchase_speed: is21_purchase_speed;
    bitmask_vehicle_info: is21_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: is21_cargo_subtype_text;
    create_effect: is_steamer_create_effect;
  }

  livery_override(unitb)
  {
    length: 6;
    is21_tender_sprites;
  }

}

if (enable_long_names)
{
  item (FEAT_TRAINS, is21)
  {
    property
    { name: string(STR_IS21_NAME_LONG); }
  }
}

