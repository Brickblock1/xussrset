 // is_all

// Пассажирские паровозы семейства ИС (Иосиф Сталин)

#define IMAGEFILE  "src/steam/is.png"
purchase_sprites(is_all_v1, 0, 0)
purchase_sprites(is_all_v2, 110, 0)

steam_sprites(10, is_all_v1, 32,   40)
steam_sprites(10, is_all_v2, 32,  844)
steam_sprites(10, is_all_v3, 32, 1648)

tender_sprites(6, is_all_t4o_v1, 447,  40)
tender_sprites(6, is_all_t4o_v2, 447, 107)
tender_sprites(6, is_all_t4o_v3, 447, 174)

tender_sprites(7, is_all_t6p_v1, 447, 308)
tender_sprites(7, is_all_t6p_v2, 447, 375)
tender_sprites(7, is_all_t6p_v3, 447, 442)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, is_all_tender_sprites,
[
  set_offset_to(-2),
  prev_vehicle_cargo_subtype() + 
  ((build_year >= 1936) * 3) ])
{
  0: is_all_t4o_v1_sprites_start;
  1: is_all_t4o_v2_sprites_start;
  2: is_all_t4o_v3_sprites_start;
  3: is_all_t6p_v1_sprites_start;
  4: is_all_t6p_v2_sprites_start;
     is_all_t6p_v3_sprites_start;
}

switch (FEAT_TRAINS, SELF, is_is21_tender_sprites,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype()])
{
  0: is_all_t6p_v1_sprites_start;
  1: is_all_t6p_v2_sprites_start;
     is_all_t6p_v3_sprites_start;
}

engine_length_change_build_after(is_all_tender, 5, 6, 1936)       

switch (FEAT_TRAINS, SELF, is_all_purchase_sprites,
  current_year >= 1936)
{
  0: is_all_v1_purchase_sprites;
     is_all_v2_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, is_is20_sprites_start,
  cargo_subtype)
{
  0: is_all_v1_sprites_start;
  1: is_all_v2_sprites_start;
  2: is_all_v3_sprites_start;
     align_10_sprites;
}

long_vehicle(is_is20)

 // is_is20

switch (FEAT_TRAINS, SELF, is_is20_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, is_is20_additional_text,
  current_year >= 1935)
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD));
}

engine_steam_1_effect(is_all, -2, 14)
engine_weight_change_build_after(is_is20, 133+85, 133+127, 1936)       
engine_TE_change_build_after(is_is20, get_tec_int(151.0, 133+85), get_tec_int(151.0, 133+127), 1936)       
engine_cost_change_build_after(is_is20, 46, 56, 1936)       
engine_speed_change_after(is_is20, 110, 115, 1936)
engine_penalise_speed0(is_is20)
engine_RC_change_build_after(is_is20, 672, 723, 1936)       

item (FEAT_TRAINS, is_is20) {
  property {
    name: string(STR_NAME_IS20);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1932), 1, 1);
    model_life: get_model_life(1932,1941);
    retire_early: get_retire_early(1932,1941);
    vehicle_life: 30;
    reliability_decay: 10;
    cost_factor: 46;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3200 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    engine_class: ENGINE_CLASS_STEAM;
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: is_is20_sprites;
    purchase: is_all_purchase_sprites;
    articulated_part: articulated_part_tender2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: is_is20_additional_text;
    speed: is_is20_speed;
    purchase_speed: is_is20_purchase_speed;
    bitmask_vehicle_info: is_is20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: is_is20_weight;
    cost_factor: is_is20_cost_factor;
    cargo_subtype_text: is_is20_cargo_subtype_text;
    tractive_effort_coefficient: is_is20_tractive_effort_coefficient;
    create_effect: is_all_create_effect;
    running_cost_factor: is_is20_running_cost_factor;
  }
  livery_override(unitb) {
    length: is_all_tender_length;
            is_all_tender_sprites;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, is_is20) {
    property { 
      name: string(STR_LONGNAME_IS20); }
  }
}

 // is21

switch (FEAT_TRAINS, SELF, is_is21_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_TTD_53);
  1: return string(STR_REFIT_TTD_59);
  2: return string(STR_REFIT_RED_ARROW);
     return CB_RESULT_NO_TEXT;
}

engine_speed(is_is21, 115)
engine_penalise_speed0(is_is21)
general_running_cost(is_is21, 761)

item (FEAT_TRAINS, is_is21) {
  property {
    name: string(STR_NAME_IS21);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1941), 1, 1);
    model_life: get_model_life(1941,1942);
    retire_early: get_retire_early(1941,1942);
    vehicle_life: 30;
    reliability_decay: 10;
    cost_factor: 56;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3300 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (133+127) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(155.0, 133+127);
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: is_is20_sprites;
    purchase: is_all_purchase_sprites;
    articulated_part: articulated_part_tender2;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1941, 1942), 
                                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD));
    speed: is_is21_speed;
    purchase_speed: is_is21_purchase_speed;
    bitmask_vehicle_info: is_is21_bitmask_vehicle_info;
    cargo_capacity: return 0;
    cargo_subtype_text: is_is21_cargo_subtype_text;
    create_effect: is_all_create_effect;
    running_cost_factor: is_is21_running_cost_factor;
  }

  livery_override(unitb) {
    length: 6;
    is_is21_tender_sprites;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, is_is21) {
    property { name: string(STR_LONGNAME_IS21); }
  }
}

if (models_details_level == 1) {
  item (FEAT_TRAINS, is_is21) {
    property { climates_available: get_climates_available(); }
  }
}

// is all

engine_power_change_build_after(is_all, 3200, 3300, 1941)       

switch (FEAT_TRAINS, SELF, is_all_weight,
  build_year >= 1941)
{
  0: is_is20_weight;
     return (137+127);
}

switch (FEAT_TRAINS, SELF, is_all_tractive_effort_coefficient,
  build_year >= 1941)
{
  0: is_is20_tractive_effort_coefficient;
     return get_tec_int(155.0, 137+127);
}

switch (FEAT_TRAINS, SELF, is_all_cost_factor,
  current_year >= 1941)
{
  0: is_is20_cost_factor;
     return 33;
}

switch (FEAT_TRAINS, SELF, is_all_running_cost_factor,
  build_year >= 1941)
{
  0: is_is20_running_cost_factor;
     is_is21_running_cost_factor;
}

switch (FEAT_TRAINS, SELF, is_all_cargo_subtype_text,
  ((build_year >= 1941) * 100) +
  cargo_subtype)
{
    0: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS20), string(STR_REFIT_TTD_53));
    1: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS20), string(STR_REFIT_TTD_59));
    2: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS20), string(STR_REFIT_RED_ARROW));
  100: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS21), string(STR_REFIT_TTD_53));
  101: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS21), string(STR_REFIT_TTD_59));
  102: return string(STR_REFIT_FACTORY_GENERAL_TYPE, string(STR_PURCHASE_HINT_IS21), string(STR_REFIT_RED_ARROW));
       return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, is_all_additional_text,
  (current_year >= 1935) +
  (current_year >= 1941))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS21)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1941, 1942), 
                   string(STR_PURCHASE_HINT_FACTORY_VOROSHILOVGRAD));
}

if (models_details_level == 0) {
  item (FEAT_TRAINS, is_is20) {
    property { 
      name: string(STR_NAME_IS_ALL);
      model_life: get_model_life(1932,1942);
      retire_early: get_retire_early(1932,1942);
    }
    graphics {
      default: is_is20_sprites;
      purchase: is_all_purchase_sprites;
      articulated_part: articulated_part_tender2;
      can_attach_wagon: any_can_attach_wagon;
      start_stop: any_start_stop;
      additional_text: is_all_additional_text;
      speed: is_is20_speed;
      purchase_speed: is_is20_purchase_speed;
      bitmask_vehicle_info: is_is20_bitmask_vehicle_info;
      cargo_capacity: return 0;
      weight: is_all_weight;
      tractive_effort_coefficient: is_all_tractive_effort_coefficient;
      create_effect: is_all_create_effect;
      running_cost_factor: is_all_running_cost_factor;
      cost_factor: is_all_cost_factor;
      cargo_subtype_text: is_all_cargo_subtype_text;
      power: is_all_power;
    }
  }
  if (enable_long_names) {
    item (FEAT_TRAINS, is_is20) {
      property { 
        name: string(STR_LONGNAME_IS_ALL); }
    }
  }
}
