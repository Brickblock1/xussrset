 // is_all
// 447

switch (FEAT_TRAINS, SELF, is_all_engine_sprites_start, 0)
{
  is20_engine_sprites_start;
}

switch (FEAT_TRAINS, SELF, is_all_tender_sprites_start, 0)
{
  is20_tender_sprites_start;
}

steamer_direction_template(is_all, _sprites_start)
long_vehicle(is_all)

engine_power_change_build_after(is_all, get_power_in_cb(3120), 
                                        get_power_in_cb(3200), 1941)       

switch (FEAT_TRAINS, SELF, is_all_weight,
  build_year >= 1941)
{
  0: is20_weight;
     return (133+127);
}

switch (FEAT_TRAINS, SELF, is_all_tractive_effort_coefficient,
  build_year >= 1941)
{
  0: is20_tractive_effort_coefficient;
     return get_tec_int(196.0, 133+127);
}

switch (FEAT_TRAINS, SELF, is_all_cost_factor,
  current_year >= 1941)
{
  0: is20_cost_factor;
     return 32;
}

switch (FEAT_TRAINS, SELF, is_all_running_cost_factor,
  build_year >= 1941)
{
  0: is20_running_cost_factor;
     is21_running_cost_factor;
}

switch (FEAT_TRAINS, SELF, is_all_additional_text,
  (current_year >= 1935) +
  (current_year >= 1941))
{
  0: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK));
  1: return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS20)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1932, 1941), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
     return string(STR_PURCHASE_HINT_ENGINE, 
                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM_WITH_SUBTYPE, string(STR_PURCHASE_HINT_IS21)), 
                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                   string(STR_PURCHASE_HINT_FROMTO, 1941, 1942), 
                   string(STR_PURCHASE_HINT_FACTORY_LUGANSK_VOROSHILOVGRAD));
}

engine_tender(is_all)
engine_tender_length_change_build_after(is_all, 8, 2, 5, 1,
                                                8, 2, 6, 1, 1936)

item (FEAT_TRAINS, is_all, 447) {
  property { 
    name: string(STR_NAME_IS_ALL);
    climates_available: NO_CLIMATE;
    introduction_date: date(get_loc_year(1932), 1, 1);
    model_life: get_model_life(1932,1942);
    retire_early: get_retire_early(1932,1942);
    vehicle_life: 30;
    reliability_decay: 10;
    cost_factor: 29;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 3120 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    engine_class: ENGINE_CLASS_STEAM;
    length: 8;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: is_all_sprites;
    purchase: is20_purchase_sprites;
    articulated_part: is_all_articulated_part;
    length: is_all_length;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: is_all_additional_text;
    speed: is20_speed;
    purchase_speed: is20_purchase_speed;
    bitmask_vehicle_info: is20_bitmask_vehicle_info;
    cargo_capacity: return 0;
    weight: is_all_weight;
    tractive_effort_coefficient: is_all_tractive_effort_coefficient;
    create_effect: is20_create_effect;
    running_cost_factor: is_all_running_cost_factor;
    cost_factor: is_all_cost_factor;
    cargo_subtype_text: is20_cargo_subtype_text;
    power: is_all_power;
    colour_mapping: is20_colour_mapping;
  }
}

long_name_template(is_all, STR_LONGNAME_IS_ALL)
models_default_cargo_template(is_all)

if (models_details_level == 0) {
  item (FEAT_TRAINS, is20) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, is21) {
    property { climates_available: NO_CLIMATE; }
  }
  item (FEAT_TRAINS, is_all) {
    property { climates_available: get_climates_available(); }
  }
}

allow_steamer(is_all)
