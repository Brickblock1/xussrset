 // l

// Спрайты

#define IMAGEFILE  "src/steam/l-lp.png"

// покупка
purchase_sprites(l_e, 315, 0)

// паровоз, анимация из 4-х фреймов
spriteset (l_e_1_spriteset, IMAGEFILE)
{
  s8_template(0, 0, 0)
} 

spriteset (l_e_2_spriteset, IMAGEFILE)
{
  s8_template(0, 38, 0)
} 

spriteset (l_e_3_spriteset, IMAGEFILE)
{
  s8_template(0, 76, 0)
} 

spriteset (l_e_4_spriteset, IMAGEFILE)
{
  s8_template(0, 114, 0)
} 

// тендер

spriteset (l_t_spriteset, IMAGEFILE)
{
  s5_template(0, 172, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, PARENT, l_e_sprites_start,
  LOAD_TEMP(3))
{
  0: l_e_1_spriteset;
  1: l_e_2_spriteset;
  2: l_e_3_spriteset;
     l_e_4_spriteset;
}

short_vehicle(l_e)

engine_speed_change_after(l_e, 105, 120, 1920)

engine_penalise_speed0(l_e)

switch (FEAT_TRAINS, SELF, l_e_articulated_part,
  extra_callback_info1)
{
  1: return dummy1;
  2: return unitb;
  3: return dummy1;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item (FEAT_TRAINS, l_e) 
{
  property
  {
    name: string(STR_L_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1914), 1, 1);
    model_life: get_model_life(1914,1918);
    retire_early: get_retire_early(1914,1918);
    vehicle_life: 40;
    reliability_decay: 20;
    cost_factor: 6;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1500 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (97+74) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(100.0, 97+74);
    length: 7;
    visual_effect_and_powered: steam_visual_effect_and_powered(-4);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }

  graphics {
    default: l_e_sprites;
    purchase: l_e_purchase_sprites;
    articulated_part: l_e_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_L_PURCHASE_HINT);
    speed: l_e_speed;
    purchase_speed: l_e_purchase_speed;
    bitmask_vehicle_info: l_e_bitmask_vehicle_info;
    cargo_capacity: return 0;
  }

  livery_override(unitb)
  {
    length: return 4;
    l_t_spriteset;
  }

}

if (enable_long_names)
{
  item (FEAT_TRAINS, l_e)
  {
    property
    { name: string(STR_L_NAME_LONG); }
  }
}

