 // l

// Спрайты

#define IMAGEFILE  "src/steam/l-lp.png"

// покупка
spriteset (l_e_purchase_spriteset, IMAGEFILE)
{
  new_purchase_template(315, 0)
}

// паровоз, анимация из 4-х фреймов
spriteset (l_e_1_spriteset, IMAGEFILE)
{
  s8_template(0, 0, 0)
} 

spriteset (l_e_2_spriteset, IMAGEFILE)
{
  s8_template(0, 38, 0)
} 

spriteset (l_e_3_spriteset, IMAGEFILE)
{
  s8_template(0, 76, 0)
} 

spriteset (l_e_4_spriteset, IMAGEFILE)
{
  s8_template(0, 114, 0)
} 

// тендер

spriteset (l_t_spriteset, IMAGEFILE)
{
  s5_template(0, 172, 0)
} 

#undef IMAGEFILE

switch (FEAT_TRAINS, PARENT, l_e_get_spriteset,
  motion_counter & 3)
{
  0: l_e_1_spriteset;
  1: l_e_2_spriteset;
  2: l_e_3_spriteset;
  l_e_4_spriteset;
}

// Присоединяем
switch (FEAT_TRAINS, SELF, l_e_can_attach_wagon,
  vehicle_type_id)
{
  return CB_RESULT_ATTACH_ALLOW;
}

// Макс. скорость: 105 км/ч до 1920, 120 км/ч после
#define MAX_SPEED current_year >= 1920 ? 120 : 105

switch (FEAT_TRAINS, SELF, l_e_purchase_speed,
  MAX_SPEED)
{
  return;
}

switch (FEAT_TRAINS, SELF, l_e_speed,
[
  STORE_TEMP(MAX_SPEED, 0),
  is_penalise_speed() ? get_penalised_speed(LOAD_TEMP(0)) : LOAD_TEMP(0)
])
{
  return;
}

#undef MAX_SPEED

switch (FEAT_TRAINS, SELF, l_e_bitmask_vehicle_info,
  position_in_consist == 0)
{
  1: return 0;
  return FLAG_PENALISE_SPEED;
}

item (FEAT_TRAINS, l_e) 
{
  property
  {
    name: string(STR_L_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1914), 1, 1);
    model_life: get_model_life(1914,1918);
    retire_early: get_retire_early(1914,1918);
    vehicle_life: 40; // срок службы
    reliability_decay: 20;
    cost_factor: 6;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1500 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (97+74) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(100.0, 97+74);
    length: 8;
    visual_effect_and_powered: steam_visual_effect_and_powered(-4);
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
  }

  graphics {
    default: l_e_get_spriteset;
    purchase: l_e_purchase_spriteset;
    articulated_part: articulated_part_tender;
    can_attach_wagon: l_e_can_attach_wagon;
    additional_text: return string(STR_L_PURCHASE_HINT);
    speed: l_e_speed;
    purchase_speed: l_e_purchase_speed;
    bitmask_vehicle_info: l_e_bitmask_vehicle_info;
    cargo_subtype_text: cargo_subtype_text_default;
    cargo_capacity: return 0;
  }

  livery_override(unitb)
  {
    length: return 5;
    l_t_spriteset;
  }

}
