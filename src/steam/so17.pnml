 // so17

#define IMAGEFILE  "src/steam/so17.png"
purchase_sprites(so17_v1, 0, 0)
purchase_sprites(so17_v2, 110, 0)
purchase_sprites(so17_v3, 220, 0)
                             
steam_sprites(7, so17_v1, 32,   40)
steam_sprites(7, so17_v2, 32,  844)
steam_sprites(7, so17_v3, 32, 1648)
#undef IMAGEFILE

switch (FEAT_TRAINS, PARENT, so17_purchase_sprites,
  (build_year >= 1946) +
  (build_year >= 1948))
{
  0: so17_v1_purchase_sprites;
  1: so17_v2_purchase_sprites;
     so17_v3_purchase_sprites;
} 

switch (FEAT_TRAINS, PARENT, so17_sprites_start,
  (build_year >= 1946) +
  (build_year >= 1948))
{
  0: so17_v1_sprites_left;
  1: so17_v2_sprites_left;
     so17_v3_sprites_left;
} 

short_vehicle(so17)

switch (FEAT_TRAINS, SELF, so17_create_effect, 
  steam_create_effect(-3, 12)) 
{ 
  return 1; 
}    

switch (FEAT_TRAINS, SELF, so17_weight,
  (build_year >= 1942) +
  (build_year >= 1948))
{
  0: return (97+68);
  1: return (103+68);
     return (103+82);
}

switch (FEAT_TRAINS, SELF, so17_tractive_effort_coefficient,
  (build_year >= 1942) +
  (build_year >= 1948))
{
  0: return get_tec_int(193.0, 97+68);
  1: return get_tec_int(203.0, 103+68);
     return get_tec_int(203.0, 103+82);
}

engine_speed_change_after2(so17, 65, 70, 1935, 75, 1936)

engine_penalise_speed0(so17)

switch (FEAT_TRAINS, SELF, so17_cost_factor,
  current_year >= 1942)
{
  0: return 18;
     return 19;
}

general_running_cost(so17_v1, 441)
general_running_cost(so17_v2, 427)
general_running_cost(so17_v3, 440)

switch (FEAT_TRAINS, SELF, so17_running_cost_factor,
  (current_year >= 1942) +
  (current_year >= 1948))
{
  0: so17_v1_running_cost_factor;
  1: so17_v2_running_cost_factor;
     so17_v3_running_cost_factor;
}

item (FEAT_TRAINS, so17) {
  property {
    name: string(STR_SO17_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1934), 1, 1);
    model_life: get_model_life(1934,1951);
    retire_early: get_retire_early(1934,1951);
    vehicle_life: 30;
    reliability_decay: 1;
    cost_factor: 18;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1970 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (97+68) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(193.0, 97+68);
    length: 6;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }

  graphics {
    default: so17_sprites;
    purchase: so17_purchase_sprites;
    articulated_part: articulated_part_tender1;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_SO17_PURCHASE_HINT);
    speed: so17_speed;
    purchase_speed: so17_purchase_speed;
    bitmask_vehicle_info: so17_bitmask_vehicle_info;
    cargo_capacity: return 0;
    tractive_effort_coefficient: so17_tractive_effort_coefficient;
    weight: so17_weight;
    create_effect: so17_create_effect;
    cost_factor: so17_cost_factor;
    running_cost_factor: so17_running_cost_factor;
  }

  livery_override(unitb) {
    length: e_all_tender_length;
            e_all_tender_sprites;
  }
}

if (enable_long_names) {
  item (FEAT_TRAINS, so17) {
    property { name: string(STR_SO17_NAME_LONG); }
  }
}

