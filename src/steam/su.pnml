 // su
// 437 (1B5h)

#define IMAGEFILE  "src/steam/su.png"
purchase_sprites(su_v1, 0, 0)
purchase_sprites(su_v2, 102, 0)
purchase_sprites(su_v3, 204, 0)
purchase_sprites(su_v4, 306, 0)
purchase_sprites_select3(su, 1932, 1935, 1947)
steam_sprites(8, su_v1, 32, 40)
steam_sprites(8, su_v2, 32, 844)
steam_sprites(8, su_v3, 32, 1648)
steam_sprites(8, su_v4, 32, 2452)
tender_sprites(5, su_tender_v1, 447, 40)
tender_sprites(5, su_tender_v2, 447, 107)
tender_sprites(6, su_tender_v3, 447, 174)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, su_engine_sprites_start,
  (build_year >= 1932) +
  (build_year >= 1935) +
  (build_year >= 1947))
{
  0: su_v1_sprites_start;
  1: su_v2_sprites_start;
  2: su_v3_sprites_start;
     su_v4_sprites_start;
}

switch (FEAT_TRAINS, SELF, su_tender_sprites_start,
  (build_year >= 1932) +
  (build_year >= 1935) +
  (build_year >= 1947))
{
  0: su_tender_v1_sprites_start;
  1: su_tender_v2_sprites_start;
  2: su_tender_v2_sprites_start;
     su_tender_v3_sprites_start;
}

steamer_direction_template(su, _sprites_start)
short_vehicle(su)

switch (FEAT_TRAINS, SELF, su_engine_colour_mapping,
  cargo_subtype + (enable_cc_default * 1024 * (cargo_subtype == 0)))
{
  0: return ttdall_cc + 0x53; 
  1: return ttdall_cc + 0x63;
  LV_RAL6002: return ttdall_cc + 0x53;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, su_tender_colour_mapping,
[ set_offset_to(-2),
  prev_vehicle_cargo_subtype() + (enable_cc_default * 1024 * (prev_vehicle_cargo_subtype() == 0))])
{
  0: return ttdall_cc + 0x53; 
  1: return ttdall_cc + 0x63;
  LV_RAL6002: return ttdall_cc + 0x53;
     return CB_FAILED;
}

steamer_direction_template(su, _colour_mapping)
livery_template(su, TTD_53, LV_RAL6002, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)
livery_template_alt(su, TTD_63)
engine_tender(su)
engine_tender_length_change_build_after(su, 7, 1, 4, 1,
                                            7, 1, 5, 1, 1947)
engine_steam_1_effect(su, -3, 11)
engine_speed_change_after2(su, 110, 
                               115, 1926, 
                               130, 1936)
engine_penalise_speed0(su)
engine_power_change_build_after2(su, get_power_in_cb(1476), 
                                     get_power_in_cb(1528), 1935,
                                     get_power_in_cb(1610), 1947)
engine_RC_change_build_after3(su, 311, 
                                  318, 1932, 
                                  322, 1935, 
                                  346, 1947)
engine_weight_change_build_after3(su, 84+64, 
                                      85+72, 1932, 
                                      85+70, 1935, 
                                      87+82, 1947)
engine_TE_change_build_after3(su, get_tec_int(129.0, 84+64),
                                  get_tec_int(129.0, 85+72), 1932,
                                  get_tec_int(129.0, 85+70), 1935,
                                  get_tec_int(130.0, 87+82), 1947)
engine_cost_change_build_after(su, 14, 
                                   15, 1947)

item (FEAT_TRAINS, su, 437) {
  property {
    name: string(STR_NAME_SU);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1925), 1, 1);
    model_life: get_model_life(1925,1951);
    retire_early: get_retire_early(1925,1951);
    vehicle_life: 40;
    reliability_decay: 10;
    cost_factor: 14;
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 1476 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    weight: (84+64) ton;
    engine_class: ENGINE_CLASS_STEAM;
    tractive_effort_coefficient: get_tec(129.0, 84+64);
    length: 7;
    cargo_allow_refit: [PASS];
    default_cargo_type: PASS;
    cargo_capacity: 1;
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }
  graphics {
    default: su_sprites;
    purchase: su_purchase_sprites;
    articulated_part: su_articulated_part;
    length: su_length;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_PURCHASE_HINT_ENGINE, 
                                   string(STR_PURCHASE_HINT_ENGINE_TYPE_PASSENGER_STEAM), 
                                   string(STR_PURCHASE_HINT_SECTIONS_1), 
                                   string(STR_PURCHASE_HINT_FROMTO, 1925, 1951), 
                                   string(STR_PURCHASE_HINT_LIST5, string(STR_PURCHASE_HINT_FACTORY_KOLOMNA), 
                                                                   string(STR_PURCHASE_HINT_FACTORY_BRYANSK), 
                                                                   string(STR_PURCHASE_HINT_FACTORY_SORMOVO), 
                                                                   string(STR_PURCHASE_HINT_FACTORY_NEVSKY), 
                                                                   string(STR_PURCHASE_HINT_FACTORY_KHARKIV)));
    speed: su_speed;
    power: su_power;
    weight: su_weight;
    cost_factor: su_cost_factor;
    tractive_effort_coefficient: su_tractive_effort_coefficient;    
    purchase_speed: su_purchase_speed;
    bitmask_vehicle_info: su_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: su_create_effect;
    running_cost_factor: su_running_cost_factor;
    colour_mapping: su_colour_mapping;
    cargo_subtype_text: su_cargo_subtype_text_pre;
  }
}

long_name_template(su, STR_LONGNAME_SU)
models_default_cargo_template(su)
