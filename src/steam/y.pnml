 // y

// Спрайты

#define IMAGEFILE  "src/steam/y.png"
purchase_sprites(y_all_v1, 0, 0)
purchase_sprites(y_all_v2, 110, 0)

steam_sprites(6y, y_all_v1, 32,  40)
steam_sprites(6y, y_all_v2, 32, 844)

tender_sprites(4, y_all_t_v1, 447,  40)
tender_sprites(4, y_all_t_v2, 447, 107)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, y_all_purchase_sprites,
  build_year >= 1914)
{
  1: y_all_v2_purchase_sprites;
     y_all_v1_purchase_sprites;
}

switch (FEAT_TRAINS, SELF, y_all_sprites_start,
  build_year >= 1914)
{
  0: y_all_v1_sprites_left;
     y_all_v2_sprites_left;
}

long_vehicle(y_all)

switch (FEAT_TRAINS, SELF, y_all_create_effect, 
  steam_create_effect(-3, 13)) 
{ 
  return 1; 
}    

engine_speed(y_all, 60)

engine_penalise_speed0(y_all)

switch (FEAT_TRAINS, SELF, y_all_articulated_part,
  extra_callback_info1)
{
  1: return dummy1;
  2: return unitb;
  3: return dummy1;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, y_all_tender_sprites,
  build_year >= 1914)
{
  0: y_all_t_v1_sprites_left;
     y_all_t_v2_sprites_left;
}

item (FEAT_TRAINS, y_all) 
{
  property
  {
    name: string(STR_Y_ALL_NAME);
    climates_available: get_climates_available();
    introduction_date: date(get_loc_year(1910), 1, 1);
    model_life: get_model_life(1910,1920);
    retire_early: get_retire_early(1910,1920);
    vehicle_life: 40;
    reliability_decay: 20;
    cost_factor: 2;
    running_cost_factor: get_steam_running_cost_factor();
    sprite_id: SPRITE_ID_NEW_TRAIN;
    misc_flags: bitmask(TRAIN_FLAG_TILT, TRAIN_FLAG_MU);
    track_type: get_normal_track_type();
    ai_special_flag: AI_FLAG_PASSENGER;
    power: 850 hpM;
    running_cost_base: RUNNING_COST_STEAM;
    engine_class: ENGINE_CLASS_STEAM;
    length: 5;
    cargo_allow_refit: [GOOD];
    default_cargo_type: GOOD;
    cargo_capacity: 1;
    weight: (60+47) ton;
    tractive_effort_coefficient: get_tec(118.0, 60+47);
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_STEAM;
  }

  graphics
  {
    default: y_all_sprites;
    purchase: y_all_purchase_sprites;
    articulated_part: y_all_articulated_part;
    can_attach_wagon: any_can_attach_wagon;
    start_stop: any_start_stop;
    additional_text: return string(STR_Y_ALL_PURCHASE_HINT);
    speed: y_all_speed;
    purchase_speed: y_all_purchase_speed;
    bitmask_vehicle_info: y_all_bitmask_vehicle_info;
    cargo_capacity: return 0;
    create_effect: y_all_create_effect;
  }

  livery_override(unitb)
  {
    length: 3;
    y_all_tender_sprites;
  }

}

if (enable_long_names)
{
  item (FEAT_TRAINS, y_all)
  {
    property
    { name: string(STR_Y_ALL_NAME_LONG); }
  }
}

