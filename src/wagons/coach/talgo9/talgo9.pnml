// talgo9

#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_A.png"
purchase_sprites(talgo9, 0, 0)
purchase_sprites(talgo9n, 102, 0)
MU_car_nd_sprites(7, talgo9_A, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_BC.png"
MU_car_sprites(8, talgo9_BC, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_E.png"
MU_car_nd_sprites(8, talgo9_E, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_D.png"
MU_car_nd_sprites(8, talgo9_D, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_F.png"
MU_car_sprites(8, talgo9_F, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_F2.png"
MU_car_sprites(8, talgo9_F2, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_G.png"
MU_car_sprites(8, talgo9_G, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_H.png"
MU_car_sprites(8, talgo9_H, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_H2.png"
MU_car_sprites(8, talgo9_H2, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/wagons/coach/talgo9/talgo_I.png"
MU_car_sprites(8, talgo9_I, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, talgo9_A_sprites_check,
  position_in_articulated_veh == 0)
{
  0: talgo9_A_sprites_left;
     talgo9_A_sprites_right;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_right,
   position_in_articulated_veh)
{
      0: talgo9_A_sprites_check;
  1..11: talgo9_BC_sprites_right;
     12: talgo9_D_sprites_right;
     13: talgo9_E_sprites_right;
     14: talgo9_H2_sprites_right;
     15: talgo9_H_sprites_right;
     16: talgo9_H2_sprites_right;
     17: talgo9_H_sprites_right;
     18: talgo9_H2_sprites_right;
     19: talgo9_A_sprites_check;
         align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_left,
   position_in_articulated_veh)
{
      0: talgo9_A_sprites_check;
      1: talgo9_H2_sprites_left;
      2: talgo9_H_sprites_left;
      3: talgo9_H2_sprites_left;
      4: talgo9_H_sprites_left;
      5: talgo9_H2_sprites_left;
      6: talgo9_E_sprites_left;
      7: talgo9_D_sprites_left;
  8..18: talgo9_BC_sprites_left;
     19: talgo9_A_sprites_check;
         align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9d_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01) ])
{
  1: talgo9_sprites_left;
     talgo9_sprites_right;
}

switch (FEAT_TRAINS, SELF, talgo9n_sprites_left,
   position_in_articulated_veh)
{
       0: talgo9_A_sprites_check;
       1: talgo9_I_sprites_left;
       2: talgo9_H_sprites_left;
       3: talgo9_I_sprites_left;
       4: talgo9_H_sprites_left;
       5: talgo9_I_sprites_left;
       6: talgo9_G_sprites_right;
       7: talgo9_G_sprites_left;
       8: talgo9_G_sprites_right;
       9: talgo9_G_sprites_left;
      10: talgo9_E_sprites_left;
      11: talgo9_D_sprites_left;
  12..13: talgo9_BC_sprites_left;
      14: talgo9_F_sprites_left;
      15: talgo9_F2_sprites_left;
      16: talgo9_F_sprites_left;
      17: talgo9_F2_sprites_left;
      18: talgo9_F_sprites_left;
      19: talgo9_A_sprites_check;
          align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9n_sprites_right,
   position_in_articulated_veh)
{
     0: talgo9_A_sprites_check;
     1: talgo9_F_sprites_right;
     2: talgo9_F2_sprites_right;
     3: talgo9_F_sprites_right;
     4: talgo9_F2_sprites_right;
     5: talgo9_F_sprites_right;
  6..7: talgo9_BC_sprites_right;
     8: talgo9_D_sprites_right;
     9: talgo9_E_sprites_right;
    10: talgo9_G_sprites_left;
    11: talgo9_G_sprites_right;
    12: talgo9_G_sprites_left;
    13: talgo9_G_sprites_right;
    14: talgo9_I_sprites_right;
    15: talgo9_H_sprites_right;
    16: talgo9_I_sprites_right;
    17: talgo9_H_sprites_right;
    18: talgo9_I_sprites_right;
    19: talgo9_A_sprites_check;
        align_8_sprites;
}

switch (FEAT_TRAINS, SELF, talgo9n_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (prev_vehicle_randombits() & 0x01) ])
{
  1: talgo9n_sprites_left;
     talgo9n_sprites_right;
}

switch (FEAT_TRAINS, SELF, talgo9_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   prev_vehicle_type_id() ])
{
  talgo9n: talgo9n_sprites_start;
           talgo9d_sprites_start;
}

long_vehicle(talgo9)

switch (FEAT_TRAINS, SELF, talgo9_length, position_in_articulated_veh)
{
   0: return 7;
  19: return 7;
      return 8;
}

switch (FEAT_TRAINS, SELF, talgo9_articulated_part,
  extra_callback_info1)
{
      1: return talgo9_part2;
  2..19: return talgo9_part3;
         return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, talgo9n_articulated_part,
  extra_callback_info1)
{
      1: return talgo9n_part2;
  2..19: return talgo9_part3;
         return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, talgo9_create_effect2,
  diesel_create_effect(0, 10)) { return 1; }


switch (FEAT_TRAINS, SELF, talgo9_create_effect,
  position_in_articulated_veh)
{
   0: talgo9_create_effect2;
  19: talgo9_create_effect2;
      return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, talgo9_running_cost_factor,
[  STORE_TEMP(0, 0),     // Моторы
   STORE_TEMP(1333, 1),  // Бригада
   STORE_TEMP(3618, 2),  // Износ
   STORE_TEMP(317, 3),   // Сопровождение
   STORE_TEMP(952, 4),   // ТО
   STORE_TEMP(127, 5),   // Сертификация

   STORE_TEMP(200, 6),                 // Скорость
   STORE_TEMP(359, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 80, 8)])  // Максимальная масса
{ all_running_cost_factor; }           // 6347

switch (FEAT_TRAINS, SELF, talgo9n_running_cost_factor,
[  STORE_TEMP(0, 0),     // Моторы
   STORE_TEMP(1531, 1),  // Бригада
   STORE_TEMP(4155, 2),  // Износ
   STORE_TEMP(364, 3),   // Сопровождение
   STORE_TEMP(1093, 4),  // ТО
   STORE_TEMP(146, 5),   // Сертификация

   STORE_TEMP(200, 6),                 // Скорость
   STORE_TEMP(365, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 18, 8)])  // Максимальная масса
{ all_running_cost_factor; }           // 7289

hint_wagon_coach_noto(talgo9,
   string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
   STR_PURCHASE_HINT_AGING_PERIOD_3_2,
   STR_PURCHASE_HINT_COACH_TYPE_C_WITH_COND,
   2014,
   string(STR_PURCHASE_HINT_FACTORY_PATENTES_TALGO))

item (FEAT_TRAINS, talgo9, 607) {
  property {
    name: string(STR_NAME_TALGO9);
    vehicle_dates(2014, 2050, 40, 10, 8, 10159)
    vehicle_wagon(359, 200)
    vehicle_wagon_pass(50, 23)  // 9 * 5 / 2
    vehicle_allow_tourists()
  }
  graphics {
        additional_text: talgo9_additional_text;
       articulated_part: talgo9_articulated_part;
       cargo_age_period: return 3000;
         colour_mapping: any_cc_colour;
            cost_factor: return 10159;
                default: talgo9_sprites;
                 length: talgo9_length;
               purchase: talgo9_purchase_sprites;
         purchase_speed: return 200;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    running_cost_factor: talgo9_running_cost_factor;
  }
}

item (FEAT_TRAINS, talgo9_part2, 608) {
  property {
    vehicle_no_dates(2014, 2050, 40, 10, 8, 0)
    vehicle_wagon(0, 200)
    vehicle_wagon_pass(364, 9*5)
  }
  graphics {
    cargo_age_period: return 300;
      cargo_capacity: return 364;
       create_effect: talgo9_create_effect;
             default: talgo9_sprites;
              length: talgo9_length;
  }
}

long_name_template(talgo9, STR_LONGNAME_TALGO9)

item (FEAT_TRAINS, talgo9n, 610) {
  property {
    name: string(STR_NAME_TALGO9);
    vehicle_dates(2014, 2050, 40, 10, 8, 11661)
    vehicle_wagon(365, 200)
    vehicle_wagon_pass(86, 16 * 5 / 2 )
    vehicle_allow_tourists()
  }
  graphics {
        additional_text: talgo9_additional_text;
       articulated_part: talgo9n_articulated_part;
       cargo_age_period: return 3000;
         colour_mapping: any_cc_colour;
            cost_factor: return 11661;
                default: talgo9_sprites;
                 length: talgo9_length;
               purchase: talgo9n_purchase_sprites;
         purchase_speed: return 200;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    running_cost_factor: talgo9n_running_cost_factor;
  }
}

long_name_template(talgo9n, STR_LONGNAME_TALGO9)

item (FEAT_TRAINS, talgo9n_part2, 616) {
  property {
    vehicle_no_dates(2014, 2050, 40, 10, 8, 0)
    vehicle_wagon(0, 200)
    vehicle_wagon_pass(142, 13)  // 5 * 5 / 2
  }
  graphics {
    cargo_age_period: return 1500;
       create_effect: talgo9_create_effect;
             default: talgo9_sprites;
              length: talgo9_length;
  }
}

item (FEAT_TRAINS, talgo9_part3, 609) {
  property {
    vehicle_no_dates(2014, 2050, 40, 10, 8, 0)
    vehicle_wagon(0, 200)
  }
  graphics {
    cargo_capacity: return 0;
     create_effect: talgo9_create_effect;
           default: talgo9_sprites;
            length: talgo9_length;
  }
}
