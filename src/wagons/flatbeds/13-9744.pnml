// flatbed_13_9744

#define IMAGEFILE  "src/wagons/flatbeds/13-9744.png"
purchase_sprites(flatbed_13_9744, 0, 0)
combo_layout_template(flatbed_13_9744, 9, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, flatbed_13_9744_wood_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: flatbed_13_9744_sprites_back;
  1: cargo9flatbed_sprites;
  2: flatbed_13_9744_sprites_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_container_sprites_start,
  getbits(extra_callback_info1, 8, 8))
{
  0: flatbed_13_9744_sprites_back;
  1: cargo9_container_2_20or40_sprites;
  2: flatbed_13_9744_sprites_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_sprites_start,
  cargo_type_in_veh)
{
  cargo_check_woodlist(flatbed_13_9744_wood_sprites_start)
  flatbed_13_9744_container_sprites_start;
}

long_vehicle(flatbed_13_9744)

switch (FEAT_TRAINS, SELF, flatbed_13_9744_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x0F])
{
   0..2: return ttd_cc2 + 0x3B;
   3..5: return ttd_cc2 + 0x3C;
   6..8: return ttd_cc2 + 0x3D;
  9..11: return ttd_cc2 + 0x3E;
         return ttd_cc2 + 0x3F;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_colour_mapping,
  cargo_subtype)
{
  0: flatbed_13_9744_random;
  1: return ttd_cc2 + 0x3B;
  2: return ttd_cc2 + 0x3C;
  3: return ttd_cc2 + 0x3D;
  4: return ttd_cc2 + 0x3E;
  5: return ttd_cc2 + 0x3F;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_cargo_subtype_text,
  cargo_subtype)
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_04);
  2: return string(STR_REFIT_TTD_06);
  3: return string(STR_REFIT_TTD_93);
  4: return string(STR_REFIT_TTD_96);
  5: return string(STR_REFIT_TTD_B9);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_weight,
[  STORE_TEMP(24, 7),
   cargo_type_in_veh])
{
  cargo_check_woodlist(return LOAD_TEMP(7))
  container_2_20or40_weight;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_loading_speed,
[  STORE_TEMP(0, 0),
   STORE_TEMP(0, 1),
   STORE_TEMP(0, 2),
   STORE_TEMP(0, 3),
   cargo_type_in_veh])
{
  cargo_check_woodlist(all_wagons_bulk_loading_speed)
  container_loading_speed;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_cargo_capacity,
  cargo_type_in_veh)
{
  cargo_check_woodlist(return 70)
  container_2_20or40_cargo_capacity;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_refit_cost,
  getbits(extra_callback_info1, 0, 8))
{
  cargo_check_woodlist(return 0 | CB_RESULT_AUTOREFIT)
  container_2_20or40_refit_cost;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_bitmask_vehicle_info,
  cargo_type_in_veh)
{
  cargo_check_woodlist(return 0)
  container_bitmask_vehicle_info;
}

switch (FEAT_TRAINS, SELF, flatbed_13_9744_running_cost_factor,
[  STORE_TEMP(0, 0),   // Моторы
   STORE_TEMP(0, 1),   // Бригада
   STORE_TEMP(16, 2),  // Износ
   STORE_TEMP(0, 3),   // Сопровождение
   STORE_TEMP(5, 4),   // ТО
   STORE_TEMP(0, 5),   // Сертификация

   STORE_TEMP(120, 6),                 // Скорость
   STORE_TEMP(24, 7),                  // Тара
   STORE_TEMP(LOAD_TEMP(7) + 36, 8)])  // Максимальная масса
{ all_running_cost_factor; }           // 15

hint_wagon_flat_noto(flatbed_13_9744,
                     70, 40,
                     string(STR_PURCHASE_HINT_LOADING_SPEED_5TIC),
                     STR_PURCHASE_HINT_AGING_PERIOD_3_1,
                     STR_PURCHASE_HINT_WAGON_TYPE_FLATBED,
                     2014,
                     string(STR_PURCHASE_HINT_FACTORY_ENGELS_TM))

item (FEAT_TRAINS, flatbed_13_9744, 597) {
  property {
    name: string(STR_MODEL_NUMBER2, 13, 9744);
    vehicle_dates(2014, 2050, 32, 10, 8, 72)
    vehicle_wagon(25.7, 120)
    default_cargo_type: WOOD;
     cargo_allow_refit: [cargo_all_woodlist(), cargo_all_containerlist()];
  }
  graphics {
         additional_text: flatbed_13_9744_additional_text;
        articulated_part: articulated_part_dummy1;
    bitmask_vehicle_info: flatbed_13_9744_bitmask_vehicle_info;
          cargo_capacity: flatbed_13_9744_cargo_capacity;
      cargo_subtype_text: flatbed_13_9744_cargo_subtype_text;
          colour_mapping: flatbed_13_9744_colour_mapping;
                 default: flatbed_13_9744_sprites;
           loading_speed: flatbed_13_9744_loading_speed;
                purchase: flatbed_13_9744_purchase_sprites;
         purchase_weight: return 26;
              refit_cost: flatbed_13_9744_refit_cost;
     running_cost_factor: flatbed_13_9744_running_cost_factor;
                  weight: flatbed_13_9744_weight;
  }
}

long_name_template(flatbed_13_9744, STR_LONGNAME_FLATBED_13_9744)
