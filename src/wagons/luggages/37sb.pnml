// luggage_37sb

#define IMAGEFILE  "src/wagons/luggages/37sb.png"
purchase_sprites(luggage_37sb, 0, 0)

pass_wagon_sprites(14, luggage_37sb_v1, 32, 40)
pass_wagon_sprites(14, luggage_37sb_v2, 32, 308)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, luggage_37sb_sprites_start_random,
[  set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: luggage_37sb_v1_sprites_start;
     luggage_37sb_v2_sprites_start;
}

switch (FEAT_TRAINS, SELF, luggage_37sb_sprites_start,
  cargo_subtype )
{
           0: luggage_37sb_sprites_start_random;
  LV_RAL6002: luggage_37sb_v1_sprites_start;
  LV_RAL6020: luggage_37sb_v2_sprites_start;
              align_14_sprites;
}

long_vehicle(luggage_37sb)

livery_template_base_list2(luggage_37sb, LV_RAL6020, LV_RAL6002)

switch (FEAT_TRAINS, SELF, luggage_37sb_cargo_capacity,
[  STORE_TEMP(26, 0),   // т
   STORE_TEMP(53, 1),   // м³
   STORE_TEMP(25, 2),   // м²
   STORE_TEMP(0, 3),    // палетомест
   STORE_TEMP(14, 4)])  // шаблон
{ box_cargo_capacity; }

switch (FEAT_TRAINS, SELF, luggage_37sb_running_cost_factor,
[  STORE_TEMP(0, 0),   // Моторы
   STORE_TEMP(8, 1),   // Бригада
   STORE_TEMP(54, 2),  // Износ
   STORE_TEMP(1, 3),   // Сопровождение
   STORE_TEMP(31, 4),  // ТО
   STORE_TEMP(1, 5),   // Сертификация

   STORE_TEMP(140, 6),                 // Скорость
   STORE_TEMP(43, 7),                  // Тара
   STORE_TEMP(LOAD_TEMP(7) + 26, 8)])  // Максимальная масса
{ all_running_cost_factor; }           // 93

switch (FEAT_TRAINS, SELF, mail_37sb_loading_speed,
  STORE_TEMP(7, 2))
{ all_wagons_box_loading_speed; }

hint_wagon_mail(luggage_37sb,
           26,
           string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
           STR_PURCHASE_HINT_AGING_PERIOD_3_1,
           STR_PURCHASE_HINT_WAGON_TYPE_LUGGAGE,
           1967, 1977,
           fact_leningradcw())

item (FEAT_TRAINS, luggage_37sb, 307) {
  property {
    name: string(STR_NAME_LUGGAGE_37SB);
    vehicle_dates(1967, 1977, 28, 10, 8, 149)
    vehicle_wagon(43, 140)
    cargo_allow_refit: [cargo_all_luggagelist()];
  }
  graphics {
        additional_text: luggage_37sb_additional_text;
       articulated_part: articulated_part_dummy6;
         cargo_capacity: luggage_37sb_cargo_capacity;
     cargo_subtype_text: luggage_37sb_cargo_subtype_text;
         colour_mapping: any_cc_colour;
                default: luggage_37sb_sprites;
          loading_speed: mail_37sb_loading_speed;
               purchase: luggage_37sb_purchase_sprites;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    running_cost_factor: luggage_37sb_running_cost_factor;
  }
}

long_name_template(luggage_37sb, STR_LONGNAME_LUGGAGE_37SB)
