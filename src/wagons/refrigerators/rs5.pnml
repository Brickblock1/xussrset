// rs5

#define PROP_rs5_CF  405 * 4 + 341
#define PROP_rs5_RC  741
#define PROP_rs5_SD  140
#define PROP_rs5_WT  233
#define PROP_rs5_TE  0
#define PROP_rs5_PR  0
#define PROP_rs5_CC  48
#define PROP_rs5_LC  47 * 4

#define IMAGEFILE  "src/wagons/refrigerators/rs5.png"
purchase_sprites(rs5, 0, 0)
combo_layout_refsection_template(rs5, 13, 32, 40)
ref_diesel_layout_template(rs5_diesel, 10, 32, 40 + 67*3)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, rs5_sprites_left,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs5_sprites_left_back;
  1: cargo13_frut_bc_sprites_5_wagons;
  2: rs5_sprites_left_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_right,
  getbits(extra_callback_info1, 8, 8))
{
  0: rs5_sprites_right_back;
  1: cargo13_frut_bc_sprites_5_wagons;
  2: rs5_sprites_right_front;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_left_all,
  position_in_articulated_veh)
{
    0: rs5_sprites_left;
    2: rs5_sprites_left;
    4: rs5_diesel_sprites_left;
    6: rs5_sprites_left;
    8: rs5_sprites_left;
  align_8_sprites;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_right_all,
  position_in_articulated_veh)
{
    0: rs5_sprites_right;
    2: rs5_sprites_right;
    4: rs5_diesel_sprites_right;
    6: rs5_sprites_right;
    8: rs5_sprites_right;
  align_8_sprites;
}

switch (FEAT_TRAINS, SELF, rs5_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   prev_vehicle_randombits() & 0x01])
{
  0: rs5_sprites_left_all;
     rs5_sprites_right_all;
}

long_vehicle(rs5)

engine_diesel_1r_effect(rs5int_create_effect_sprites, -2, 4, 11)

switch (FEAT_TRAINS, SELF, rs5int_create_effect2,
[  set_offset_to(-position_in_articulated_veh),
   prev_vehicle_randombits() & 0x01])
{
  0: rs5int_create_effect_sprites_left;
     rs5int_create_effect_sprites_right;
}

switch (FEAT_TRAINS, SELF, rs5int_create_effect1,
  position_in_articulated_veh)
{
  4: rs5int_create_effect2;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, rs5int_create_effect,
[  set_offset_to(-position_in_articulated_veh),
   prev_vehicle_cargo_load() > 0])
{
  1: rs5int_create_effect1;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, rs5_cargo_capacity,
[  STORE_TEMP(PROP_rs5_LC, 0),  // т
   STORE_TEMP(112 * 4, 1),      // м³
   STORE_TEMP(0, 2),            // м²
   STORE_TEMP(0, 3),            // палетомест
   STORE_TEMP(13 * 4, 4)])      // шаблон
{ ref_cargo_capacity; }

switch (FEAT_TRAINS, SELF, rs5_running_cost_factor,
[  STORE_TEMP(0, 0),    // Моторы
   STORE_TEMP(0, 1),    // Бригада
   STORE_TEMP(370, 2),  // Износ
   STORE_TEMP(123, 3),  // Сопровождение (охлаждение)
   STORE_TEMP(246, 4),  // ТО
   STORE_TEMP(2, 5),    // Сертификация

   STORE_TEMP(PROP_rs5_SD, 6),                        // Скорость
   STORE_TEMP(round(PROP_rs5_WT), 7),                 // Тара
   STORE_TEMP(round(PROP_rs5_WT + PROP_rs5_LC), 8)])  // Максимальная масса
{ all_running_cost_factor; }                          // 739

switch (FEAT_TRAINS, SELF, rs5_loading_speed,
  STORE_TEMP(12 * 4, 2))
{ all_wagons_box_loading_speed; }

switch (FEAT_TRAINS, SELF, rs5_articulated_part,
  extra_callback_info1)
{
  1: return dummy5;
  2: return rs5int;
  3: return dummy5;
  4: return rs5int;
  5: return dummy2;
  6: return rs5int;
  7: return dummy5;
  8: return rs5int;
  9: return dummy5;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

wagon_speed_change_on_load(rs5, 140, 120, 145)

name_in_group(rs5, string(STR_NAME_IN_GROUP, string(STR_NAME_REFRIGERATORS_SERIES), string(STR_MODEL_NUMBER2, 16, 3000)),
                   string(STR_NAME_IN_GROUP, string(STR_NAME_REFRIGERATORS_SERIES), string(STR_LONGNAME_RS5)))

hint_wagon_with_spectype_chande_speed_on_load(rs5,
     PROP_rs5_LC, 112*4,
     string(STR_PURCHASE_HINT_LOADING_SPEED_4TIC),
     STR_PURCHASE_HINT_AGING_PERIOD_8_1,
     string(STR_PURCHASE_HINT_WAGON_TYPE_REFRIGERATOR_4AND1, string(STR_MODEL_NUMBER2, 16, 3000), string(STR_MODEL_NUMBER2, 16, 3001)),
     1986, 2000,
     fact_bryansk(),
     120, 145)

item (FEAT_TRAINS, rs5, 266) {
  property {
    name: string(STR_NAME_RS5);
    vehicle_dates(1986, 2000, 25, 10, 8, PROP_rs5_CF)
    vehicle_wagon(PROP_rs5_WT, PROP_rs5_SD)
    vehicle_group(refrigerators_group)
    cargo_allow_refit: [cargo_all_reflist()];
  }
  graphics {
    purchase_menu(PROP_rs5_CF, PROP_rs5_RC, PROP_rs5_SD, PROP_rs5_WT, PROP_rs5_TE, PROP_rs5_PR, PROP_rs5_LC)
                   name: rs5_name;
        additional_text: rs5_additional_text;
       articulated_part: rs5_articulated_part;
       cargo_age_period: return 9000;
         cargo_capacity: rs5_cargo_capacity;
         colour_mapping: any_cc_colour;
                default: rs5_sprites;
          loading_speed: rs5_loading_speed;
               purchase: rs5_purchase_sprites;
             refit_cost: return 0 | CB_RESULT_AUTOREFIT;
    running_cost_factor: rs5_running_cost_factor;
                  speed: rs5_speed;
  }
}

long_name_template(rs5, STR_LONGNAME_RS5)

item (FEAT_TRAINS, rs5int, 265) {
  property {
    vehicle_no_dates(1986, 2050, 25, 10, 8, 0)
    vehicle_wagon(0, 140)
    effect_spawn_model_and_powered: EFFECT_SPAWN_MODEL_ELECTRIC;
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: rs5int_create_effect;
           default: rs5_sprites;
  }
}
