// tk8

#define PROP_tk8_CF  750
#define PROP_tk8_RC  999
#define PROP_tk8_SD  100
#define PROP_tk8_WT  170
#define PROP_tk8_TE  0
#define PROP_tk8_PR  0
#define PROP_tk8_CC  17
#define PROP_tk8_LC  17

#define IMAGEFILE  "src/wagons/special/tk8.png"
purchase_sprites(tk8, 0, 0)
tanker_layout_template(tk8, 16, 32, 40)
#undef IMAGEFILE
long_vehicle(tk8)

switch (FEAT_TRAINS, SELF, tk8_random,
[ set_offset_to(1),
   prev_vehicle_randombits() & 0x01])
{
  0: return ttd_cc2 + 0x00;
     return ttd_cc2 + 0x40;
}

switch (FEAT_TRAINS, SELF, tk8_colour_mapping,
  cargo_subtype)
{
  0: tk8_random;
  1: return ttd_cc2 + 0x00;
  2: return ttd_cc2 + 0x40;
     return CB_FAILED;
}

switch (FEAT_TRAINS, SELF, tk8_cargo_subtype_text,
    cargo_subtype )
{
  0: return string(STR_REFIT_FACTORY);
  1: return string(STR_REFIT_TTD_04);
  2: return string(STR_REFIT_TTD_4C);
     return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, tk8_loading_speed,
[  STORE_TEMP(0, 0),
   STORE_TEMP(0, 1),
   STORE_TEMP(1, 2),
   STORE_TEMP(1, 3)])
{ all_wagons_tanker_loading_speed; }

switch (FEAT_TRAINS, SELF, tk8_running_cost_factor,
[  STORE_TEMP(0, 0),    // Моторы
   STORE_TEMP(0, 1),    // Бригада
   STORE_TEMP(208, 2),  // Износ
   STORE_TEMP(125, 3),  // Сопровождение (сотрудник почты)
   STORE_TEMP(166, 4),  // ТО
   STORE_TEMP(500, 5),  // Сертификация

   STORE_TEMP(PROP_tk8_SD, 6),                        // Скорость
   STORE_TEMP(round(PROP_tk8_WT), 7),                 // Тара
   STORE_TEMP(round(PROP_tk8_WT + PROP_tk8_LC), 8)])  // Максимальная масса
{ all_running_cost_factor; }                          // 999 // 500

name_in_group(tk8, string(STR_NAME_IN_GROUP, string(STR_NAME_MAIL_SERIES), string(STR_NAME_TK8)),
                   string(STR_NAME_IN_GROUP, string(STR_NAME_MAIL_SERIES), string(STR_LONGNAME_TK8)))

hint_wagon_mail(tk8,
     17,
     string(STR_PURCHASE_HINT_LOADING_SPEED_6TIC),
     STR_PURCHASE_HINT_CAP_004_SLOW,
     STR_PURCHASE_HINT_WAGON_TYPE_SPECIAL,
     1963, 2007,
     fact_tver())

item (FEAT_TRAINS, tk8, 622) {
  property {
    name: string(STR_NAME_TK8);
    vehicle_dates(1963, 2007, 20, 10, 8, PROP_tk8_CF)
    vehicle_wagon(PROP_tk8_WT, PROP_tk8_SD)
    vehicle_wagon_cargo(17, 1)
//    vehicle_group(group_luggage)
    cargo_allow_refit: [URAN, NWST];
  }
  graphics {
    purchase_menu(PROP_tk8_CF, PROP_tk8_RC, PROP_tk8_SD, PROP_tk8_WT, PROP_tk8_TE, PROP_tk8_PR, PROP_tk8_LC)
        additional_text: tk8_additional_text;
       articulated_part: articulated_part_dummy8;
       cargo_age_period: return CAP_004_SLOW;
     cargo_subtype_text: tk8_cargo_subtype_text;
         colour_mapping: tk8_colour_mapping;
                default: tk8_sprites;
          loading_speed: tk8_loading_speed;
                   name: tk8_name;
               purchase: tk8_purchase_sprites;
    running_cost_factor: tk8_running_cost_factor;
                  speed: return 100;
  }
}

long_name_template(tk8, STR_LONGNAME_TK8)
